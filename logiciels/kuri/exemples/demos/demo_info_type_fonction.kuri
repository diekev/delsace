importe "info_type"
importe "flux"

struct Objet {
    nom := "objet"
}

fonc execute_noeud(obj1 : &Objet, objs : []&Objet) -> z32, *Objet
{
    retourne 0, nul
}

fonc imprime_type(info : *InfoType) -> rien
{
    discr info.id {
	    ENTIER {
		    info_arg := transtype(info : *InfoTypeEntier)

            si info_arg.est_signé {
			    si info_arg.taille_en_octet == 8 {
				    imprime_sans_format("z8")
				}
				sinon si info_arg.taille_en_octet == 16 {
				    imprime_sans_format("z16")
				}
				sinon si info_arg.taille_en_octet == 32 {
				    imprime_sans_format("z32")
				}
				sinon si info_arg.taille_en_octet == 64 {
				    imprime_sans_format("z64")
				}
			}
			sinon {
			    si info_arg.taille_en_octet == 8 {
				    imprime_sans_format("n8")
				}
				sinon si info_arg.taille_en_octet == 16 {
				    imprime_sans_format("n16")
				}
				sinon si info_arg.taille_en_octet == 32 {
				    imprime_sans_format("n32")
				}
				sinon si info_arg.taille_en_octet == 64 {
				    imprime_sans_format("n64")
				}
			}
		}
		RÉEL {
		    info_arg := transtype(info : *InfoTypeRéel)

            si info_arg.taille_en_octet == 16 {
			    imprime_sans_format("r16")
			}
			sinon si info_arg.taille_en_octet == 32 {
			    imprime_sans_format("r32")
			}
			sinon si info_arg.taille_en_octet == 64 {
			    imprime_sans_format("r64")
			}
		}
		BOOLÉEN {
			imprime_sans_format("bool")
		}
		CHAINE {
			imprime_sans_format("chaine")
		}
		STRUCTURE {
		    info_arg := transtype(info : *InfoTypeStructure)
			imprime_sans_format(info_arg.nom)
		}
		POINTEUR {
		    info_arg := transtype(info : *InfoTypePointeur)

		    si info_arg.est_référence {
			    imprime_sans_format('&')
		    }
		    sinon {
			    imprime_sans_format('*')
		    }

			imprime_type(info_arg.type_pointé)
		}
		FONCTION {
		    // À FAIRE : types paramètres
			imprime_sans_format("fonction()")
		}
		EINI {
			imprime_sans_format("eini")
		}
		RIEN {
			imprime_sans_format("rien")
		}
		TABLEAU {
		    info_arg := transtype(info : *InfoTypeTableau)
			imprime_sans_format("[]")
			imprime_type(info_arg.type_pointé)
		}
		ÉNUM {
		    info_arg := transtype(info : *InfoTypeÉnum)
			imprime_sans_format(info_arg.nom)
		}
	}
}

fonc enregistre_noeud(fonc_exec : eini) -> rien
{
    info := fonc_exec.info

    si info.id != id_info.FONCTION {
        imprime_sans_format("Obtenu autre chose qu'une fonction !", '\n')
        retourne
    }

    info_fonc := transtype(info : *InfoTypeFonction)

    imprime("Les types d'entrée sont : ", '\n')

    pour type dans info_fonc.types_entrée {
        imprime_sans_format("Type = ")
        imprime_type(type)
        imprime_sans_format('\n')
    }

    imprime("Les types de sortie sont : ", '\n')

    pour type dans info_fonc.types_sortie {
        imprime_sans_format("Type = ")
        imprime_type(type)
        imprime_sans_format('\n')
    }
}

fonc principale(args : []*z8) -> z32
{
    enregistre_noeud(execute_noeud)
    retourne 0
}
