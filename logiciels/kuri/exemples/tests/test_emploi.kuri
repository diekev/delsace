importe Fondation

principale :: fonc () -> z32
{
    test_modification_paramètre_empl()
    test_modification_paramètre_empl_indirection()
    test_modification_locale_via_empl()
    test_initialisation_membre_employé()
    test_initialisation_spéciale_membre_employé()
    test_initialisation_spéciale_membre_avec_indirection()
    test_initialisation_via_exécute()
    test_initialisation_via_exécute_indirection()
    test_transtypage_vers_bases_multiple()
    test_transtypage_vers_bases_multiple_avec_indirection()
    test_transtypage_référence_vers_bases_multiple()
    test_transtypage_référence_vers_bases_multiple_avec_indirection()
    test_emploi_variable_locale()
    test_emploi_énum()
    retourne 0
}

/* ------------------------------------------------------------------------- */
/** \nom Modification d'un paramètre via empl.
 * \{ */

test_modification_paramètre_empl :: fonc ()
{
    Vecteur :: struct {
        x := 0
        y := 0
        z := 0
    }

    modifie_vecteur :: fonc (empl v: *Vecteur)
    {
        x = 1
        y = 2
        z = 3
    }

    ne_modifie_pas_vecteur :: fonc (empl v: Vecteur)
    {
        x = 4
        y = 5
        z = 6
    }

    v: Vecteur
    assert(v.x == 0 && v.y == 0 && v.z == 0)

    modifie_vecteur(*v)
    assert(v.x == 1 && v.y == 2 && v.z == 3)

    ne_modifie_pas_vecteur(v)
    assert(v.x == 1 && v.y == 2 && v.z == 3)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Modification d'un paramètre via empl avec indirection.
 * \{ */

test_modification_paramètre_empl_indirection :: fonc ()
{
    Vecteur :: struct {
        x := 0
        y := 0
        z := 0
    }

    Point :: struct {
        empl base: Vecteur
    }

    modifie_point :: fonc (empl p: *Point)
    {
        x = 1
        y = 2
        z = 3
    }

    ne_modifie_pas_point :: fonc (empl p: Point)
    {
        x = 4
        y = 5
        z = 6
    }

    p: Point
    assert(p.x == 0 && p.y == 0 && p.z == 0)

    modifie_point(*p)
    assert(p.x == 1 && p.y == 2 && p.z == 3)

    ne_modifie_pas_point(p)
    assert(p.x == 1 && p.y == 2 && p.z == 3)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Modification d'une structure locale via empl.
 * \{ */

test_modification_locale_via_empl :: fonc ()
{
    Vecteur :: struct {
        x := 0
        y := 0
        z := 0
    }

    empl v: Vecteur
    assert(v.x == 0 && v.y == 0 && v.z == 0)

    x = 5
    y = 6
    z = 7
    assert(v.x == 5 && v.y == 6 && v.z == 7)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Initialisation de membre d'une structure de base dans dérivée.
 * \{ */

test_initialisation_membre_employé :: fonc ()
{
    Base :: struct {
        x := 1
    }

    Dérivée :: struct {
        empl base: Base
    }

    d: Dérivée
    assert(d.x == 1)
}

test_initialisation_spéciale_membre_employé :: fonc ()
{
    Base :: struct {
        x := 1
    }

    Dérivée :: struct {
        empl base: Base
        x = 2
    }

    d: Dérivée
    assert(d.x == 2)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Initialisation spéciale de membre employé.
 * \{ */

test_initialisation_spéciale_membre_avec_indirection :: fonc ()
{
    Base1 :: struct {
        x := 123
    }

    Base2 :: struct {
        empl base1: Base1
        x = 456
    }

    Base3 :: struct {
        empl base2: Base2
    }

    b3: Base3
    assert(b3.x == 456)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Initialisation via exécute.
 * \{ */

test_initialisation_via_exécute :: fonc ()
{
    Base :: struct {
        x := 123
    }

    construit_base :: fonc () -> Base
    {
        retourne Base(456)
    }

    b: Base
    assert(b.x == 123)

    b = #exécute construit_base()
    assert(b.x == 456)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Initialisation via exécute avec indirection.
 * \{ */

test_initialisation_via_exécute_indirection :: fonc ()
{
    Base :: struct {
        x := 123
    }

    Base2 :: struct {
        empl base: Base
    }

    construit_base :: fonc () -> Base2
    {
        retourne Base2(456)
    }

    b: Base2
    assert(b.x == 123)

    b = #exécute construit_base()
    assert(b.x == 456)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Transtypage vers type base.
 * \{ */

test_transtypage_vers_bases_multiple :: fonc ()
{
    Base1 :: struct {
        membre1: chaine
    }

    Base2 :: struct {
        membre2: chaine
    }

    Struct :: struct {
        empl base1: Base1
        empl base2: Base2
    }

    modifie_membre_base1 :: fonc (base: *Base1)
    {
        base.membre1 = "membre de base1"
    }

    modifie_membre_base2 :: fonc (base: *Base2)
    {
        base.membre2 = "membre de base2"
    }

    modifie_membre_base1_dérivé :: fonc (base: *Base1)
    {
        dérivé := base comme *Struct
        dérivé.base1.membre1 = "membre de base1"
    }

    modifie_membre_base2_dérivé :: fonc (base: *Base2)
    {
        dérivé := base comme *Struct
        dérivé.base2.membre2 = "membre de base2"
    }

    /* Dérivé vers base. */
    {
        s: Struct
        modifie_membre_base1(*s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(*s)
        assert(s.membre2 == "membre de base2")
    }

    /* Base vers dérivé. */
    {
        s: Struct
        modifie_membre_base1_dérivé(*s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2_dérivé(*s)
        assert(s.membre2 == "membre de base2")
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Transtypage vers type base un peu plus complexe.
 * \{ */

test_transtypage_vers_bases_multiple_avec_indirection :: fonc ()
{
    Base1 :: struct {
        membre1: chaine
    }

    Base2 :: struct {
        membre2: chaine
    }

    Base3 :: struct {
        un_réel: r32
        un_bool: bool
        empl base1: Base1
        un_tableau: [..]octet
        empl base2: Base2
    }

    Struct :: struct {
        un_entier: z32
        empl base3: Base3
    }

    modifie_membre_base1 :: fonc (base: *Base1)
    {
        base.membre1 = "membre de base1"
    }

    modifie_membre_base2 :: fonc (base: *Base2)
    {
        base.membre2 = "membre de base2"
    }

    modifie_membre_base1_dérivé :: fonc (base: *Base1)
    {
        dérivé := base comme *Struct
        dérivé.base1.membre1 = "membre de base1"
    }

    modifie_membre_base2_dérivé :: fonc (base: *Base2)
    {
        dérivé := base comme *Struct
        dérivé.base2.membre2 = "membre de base2"
    }

    /* Dérivé vers base. */
    {
        s: Struct
        modifie_membre_base1(*s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(*s)
        assert(s.membre2 == "membre de base2")
    }

    /* Base vers dérivé. */
    {
        s: Struct
        modifie_membre_base1_dérivé(*s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2_dérivé(*s)
        assert(s.membre2 == "membre de base2")
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Transtypage référence vers type base.
 * \{ */

test_transtypage_référence_vers_bases_multiple :: fonc ()
{
    Base1 :: struct {
        membre1: chaine
    }

    Base2 :: struct {
        membre2: chaine
    }

    Struct :: struct {
        empl base1: Base1
        empl base2: Base2
    }

    modifie_membre_base1 :: fonc (base: &Base1)
    {
        base.membre1 = "membre de base1"
    }

    modifie_membre_base2 :: fonc (base: &Base2)
    {
        base.membre2 = "membre de base2"
    }

    modifie_membre_base1_dérivé :: fonc (base: &Base1)
    {
        dérivé : &Struct = base comme &Struct
        dérivé.base1.membre1 = "membre de base1"
    }

    modifie_membre_base2_dérivé :: fonc (base: &Base2)
    {
        dérivé : &Struct = base comme &Struct
        dérivé.base2.membre2 = "membre de base2"
    }

    /* Dérivé vers base. */
    {
        s: Struct
        réf_s : &Struct = s
        modifie_membre_base1(réf_s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(réf_s)
        assert(s.membre2 == "membre de base2")
    }

    /* Dérivé sans référence vers base. */
    {
        s: Struct
        modifie_membre_base1(s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(s)
        assert(s.membre2 == "membre de base2")
    }

    /* Base vers dérivé. */
    {
        s: Struct
        réf_s : &Struct = s
        modifie_membre_base1_dérivé(réf_s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2_dérivé(réf_s)
        assert(s.membre2 == "membre de base2")
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Transtypage référence vers type base un peu plus complexe.
 * \{ */

test_transtypage_référence_vers_bases_multiple_avec_indirection :: fonc ()
{
    Base1 :: struct {
        membre1: chaine
    }

    Base2 :: struct {
        membre2: chaine
    }

    Base3 :: struct {
        un_réel: r32
        un_bool: bool
        empl base1: Base1
        un_tableau: [..]octet
        empl base2: Base2
    }

    Struct :: struct {
        un_entier: z32
        empl base3: Base3
    }

    modifie_membre_base1 :: fonc (base: &Base1)
    {
        base.membre1 = "membre de base1"
    }

    modifie_membre_base2 :: fonc (base: &Base2)
    {
        base.membre2 = "membre de base2"
    }

    modifie_membre_base1_dérivé :: fonc (base: &Base1)
    {
        dérivé : &Struct = base comme &Struct
        dérivé.base1.membre1 = "membre de base1"
    }

    modifie_membre_base2_dérivé :: fonc (base: &Base2)
    {
        dérivé : &Struct = base comme &Struct
        dérivé.base2.membre2 = "membre de base2"
    }

    /* Dérivé vers base. */
    {
        s: Struct
        réf_s : &Struct = s
        modifie_membre_base1(réf_s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(réf_s)
        assert(s.membre2 == "membre de base2")
    }

    /* Dérivé sans référence vers base. */
    {
        s: Struct
        modifie_membre_base1(s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2(s)
        assert(s.membre2 == "membre de base2")
    }

    /* Base vers dérivé. */
    {
        s: Struct
        réf_s : &Struct = s
        modifie_membre_base1_dérivé(réf_s)
        assert(s.membre1 == "membre de base1")
        modifie_membre_base2_dérivé(réf_s)
        assert(s.membre2 == "membre de base2")
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Emploi de variable locale.
 * \{ */

test_emploi_variable_locale :: fonc ()
{
    Structure :: struct {
        VALEUR :: 123
        x: z32
    }

    {
        empl s: Structure

        x = 123
        assert(s.x == 123)
    }

    {
        s: Structure
        empl s

        x = 456
        assert(s.x == 456)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Emploi de type de données.
 * \{ */

test_emploi_énum :: fonc ()
{
    Énum :: énum {
        VALEUR1
        VALEUR2
    }

    empl Énum

    x := VALEUR1
    assert(x == Énum.VALEUR1)
}

/** \} */
