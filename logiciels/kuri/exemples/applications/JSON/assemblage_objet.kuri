importe Tori

//##############################################################################

struct PileObjet {
    données : []*Objet
    curseur := 0
}

fonc détruit(dyn pile : &PileObjet) -> rien
{
    déloge pile.données
}

fonc empile(dyn pile : &PileObjet, noeud : *Objet) -> rien
{
    si pile.curseur == pile.données.taille {
        taille := pile.données.taille
        reloge pile.données : [taille + 1]*Objet
    }

    pile.données[pile.curseur] = noeud
    pile.curseur += 1
}

fonc dépile(dyn pile : &PileObjet) -> *Objet
{
    ret := pile.haut()
    pile.curseur -= 1
    retourne ret
}

fonc haut(pile : &PileObjet) -> *Objet
{
    retourne pile.données[pile.curseur - 1]
}

fonc taille(pile : &PileObjet) -> z32
{
    retourne pile.curseur
}

fonc est_vide(pile : &PileObjet) -> bool
{
    retourne pile.curseur == 0
}

//##############################################################################

struct AssembleuseObjet {
    gestionnaire : GestionnaireObjet
    pile : PileObjet
}

fonc détruit(assembleuse: &AssembleuseObjet) -> rien
{
    détruit(assembleuse.pile)
    détruit(@assembleuse.gestionnaire)
}

fonc empile_objet(assembleuse: &AssembleuseObjet, objet: *Objet) -> rien
{
    //assert(objet.type == TypeObjet.DICTIONNAIRE || objet.type == TypeObjet.LISTE)
    assembleuse.pile.empile(objet)
}

fonc dépile_objet(assembleuse: &AssembleuseObjet) -> rien
{
    assembleuse.pile.dépile()
}

fonc crée_objet(assembleuse: &AssembleuseObjet, nom_objet: chaine, type: TypeObjet) -> *Objet
{
    objet := crée_objet(assembleuse, type)

    si !assembleuse.pile.est_vide() {
		dyn objet_haut := assembleuse.pile.haut()

		si objet_haut.id == TypeObjet.DICTIONNAIRE {
			dyn dico := transtype(objet_haut: *ObjetDictionnaire)
			dico.insère(nom_objet, objet)
		}
		sinon {
			dyn tabl := transtype(objet_haut: *ObjetListe)
			tabl.valeur.pousse(objet)
		}
	}

	retourne objet
}

fonc crée_objet(assembleuse: &AssembleuseObjet, type: TypeObjet) -> *Objet
{
    discr type {
        NUL {
            obj := crée_nul(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
        DICTIONNAIRE {
            obj := crée_dictionnaire(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
        ENTIER {
            obj := crée_entier(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
        RÉEL {
            obj := crée_réel(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
        CHAINE {
            obj := crée_chaine(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
        LISTE {
            obj := crée_liste(@assembleuse.gestionnaire)
            retourne transtype(obj : *Objet)
        }
    }

    retourne nul
}
