importe Fondation
importe Tori

charge "assemblage_arbre"
charge "lexage"
charge "syntaxage"

fonc génère_page(
    noeud: *NoeudSyntaxique,
    dyn gestionnaire: *GestionnaireObjet,
    dyn dico: *ObjetDictionnaire,
    enchaineuse: &Enchaineuse) -> rien
{
    discr noeud.type {
        BLOC {
            pour enfant dans noeud.enfants {
                génère_page(enfant, gestionnaire, dico, enchaineuse)
            }
        }
        CHAINE_CARACTÈRE {
            enchaineuse.pousse(noeud.données.chn)
        }
        VARIABLE {
            variable := noeud.données.chn
            objet_variable := dico.trouve_objet(variable)

            si objet_variable == nul {
                retourne
            }

            discr objet_variable.id {
                NUL {
                    enchaineuse.pousse("nul")
                }
                CHAINE {
                    objet_chaine := transtype(objet_variable : *ObjetChaine)
                    enchaineuse.pousse(objet_chaine.valeur)
                }
                ENTIER {
                    objet_nombre := transtype(objet_variable : *ObjetEntier)
                    // À FAIRE
                    //tampon += std::to_string(nombre.valeur)
                    enchaineuse.pousse("nombre entier non supporté")
                }
                RÉEL {
                    objet_nombre := transtype(objet_variable : *ObjetRéel)
                    // À FAIRE
                    //tampon += std::to_string(nombre.valeur)
                    enchaineuse.pousse("nombre réel non supporté")
                }
                LISTE,
                DICTIONNAIRE {
                    // À FAIRE: imprime pointeur
                    enchaineuse.pousse("objet à ")
                }
            }
        }
        POUR {
            propriete := noeud.enfants[1].données.chn
            objet_iter := dico.trouve_objet(propriete)

            si objet_iter == nul {
                retourne
            }

            variable := noeud.enfants[0].données.chn

            discr objet_iter.id {
                NUL {
                    // À FAIRE: erreur
                    enchaineuse.pousse("Objet nul n'est pas itérable !")
                }
                CHAINE {
                    // À FAIRE: erreur
                    enchaineuse.pousse("Objet chaine n'est pas itérable !")
                }
                ENTIER {
                    // À FAIRE: erreur
                    enchaineuse.pousse("Objet nombre entier n'est pas itérable !")
                }
                RÉEL {
                    // À FAIRE: erreur
                    enchaineuse.pousse("Objet nombre réel n'est pas itérable !")
                }
                LISTE {
                    liste := transtype(objet_iter : *ObjetListe)

                    pour objet dans liste.valeur {
                        dico.insère(variable, objet)
                        génère_page(noeud.enfants[2], gestionnaire, dico, enchaineuse)
                    }
                }
                DICTIONNAIRE {
                    dictionnaire := transtype(objet_iter : *ObjetDictionnaire)

                    pour nom, idx dans dictionnaire.noms {
                        dyn objet_nom := gestionnaire.crée_chaine()
                        objet_nom.valeur = nom

                        dico.insère(variable, transtype(objet_nom : *Objet))
                        génère_page(noeud.enfants[2], gestionnaire, dico, enchaineuse)
                    }
                }
            }
        }
        SI {
            variable := noeud.enfants[0].données.chn
            objet_valeur := dico.trouve_objet(variable)

            si objet_valeur != nul {
                génère_page(noeud.enfants[1], gestionnaire, dico, enchaineuse)
            }
            sinon si noeud.enfants.taille > 2 {
                génère_page(noeud.enfants[2], gestionnaire, dico, enchaineuse)
            }
        }
    }
}

fonc principale(args : []*z8) -> z32
{
    diffère { imprime("mémoire utilisée : ", mémoire_utilisée(), "o\n") }

    chn := "{%pour x dans evenements%}<a href='{{lien}}'>{%si variable%}{{variable}} {{x}}{% sinon %}texte{% finsi %}</a>\n{% finpour %}"

    imprime("script :\n\n", chn, "\n\n")

    //######################################################
    // Lexage du script

    dyn lexeuse := construit_lexeuse(chn)
    diffère { détruit_lexeuse(lexeuse) }

    lexeuse.performe_lexage()

    imprime("lexèmes :\n\n")

    pour lexème dans lexeuse.lexèmes {
        imprime(lexème.type_lexème, " : ", lexème.chn, "\n")
    }

    imprime("\n")

    //######################################################
    // Syntaxage du script

    dyn syntaxeuse := construit_syntaxeuse(lexeuse.lexèmes)
    diffère { détruit_assembleuse(syntaxeuse.assembleuse) }

    syntaxeuse.performe_syntaxage()

	racine := syntaxeuse.assembleuse.pile.haut()

	imprime("arbre :\n\n")
	imprime_arbre(racine, 0)
	imprime("\n")

    //######################################################
    // Génération de la page

	dyn gestionnaire : GestionnaireObjet
	diffère { détruit(@gestionnaire) }

    dyn dico := crée_dictionnaire(@gestionnaire)

    // À FAIRE: simplifie la création d'objet en passant les paramètres
    dyn liste := crée_liste(@gestionnaire)
    dyn obj1 := crée_entier(@gestionnaire)
    obj1.valeur = 127
    dyn obj2 := crée_réel(@gestionnaire)
    obj2.valeur = 36.0
    dyn obj3 := crée_chaine(@gestionnaire)
    obj3.valeur = "chaine test"

    liste.valeur.pousse(transtype(obj1: *Objet))
    liste.valeur.pousse(transtype(obj2: *Objet))
    liste.valeur.pousse(transtype(obj3: *Objet))

	dico.insère("evenements", transtype(liste: *Objet))

	dyn chaine1 := crée_chaine(@gestionnaire)
    chaine1.valeur = "http://aaa"

    dico.insère("lien", transtype(chaine1: *Objet))

    chaine1 := crée_chaine(@gestionnaire)
    chaine1.valeur = "eh ouais..."

    dico.insère("variable", transtype(chaine1: *Objet))

    dyn enchaineuse : Enchaineuse
    diffère { détruit(enchaineuse) }

    génère_page(racine, @gestionnaire, dico, enchaineuse)

    imprime("Page :\n\n")
    imprime(enchaineuse.résultat, "\n")

    retourne 0
}
