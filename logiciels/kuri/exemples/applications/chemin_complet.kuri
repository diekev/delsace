/* Petite application pour imprimer le chemin complet depuis le nom d'un fichier.
 * Si le nom de fichier est absolu, il sera retouné.
 * S'il est relatif, un chemin absolu sera généré avec comme base le dossier
 * d'où est appelé le programme.
 */

importe Fondation
importe SysFichier

principale :: fonc ()
{
    arguments := arguments_ligne_commande()

    si arguments.taille != 2 {
        imprime("Utilisation : % FICHIER\n", arguments[0])
        exit(1)
    }

    chm := CheminFichier(arguments[1])

    si est_absolu(chm) {
        imprime("%\n", chm.chn)
        exit(0)
    }

    chemin_courant := tente donne_chemin_courant() piège err {
        imprime("Impossible de déterminer le chemin courant : %\n", err)
        exit(1)
    }
    diffère déloge(chemin_courant.chn)

    chm_abs := crée_chemin_absolu(chemin_courant, chm)
    diffère déloge(chm_abs.chn)

    imprime("%\n", chm_abs.chn)
}
