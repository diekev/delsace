importe Fondation
importe OpenGL

vérifie_compilation :: fonc (id_nuanceur: n32) -> bool
{
    résultat : z32 = 0
    longueur_info_log : z32 = 0

    glGetShaderiv(id_nuanceur, GL_COMPILE_STATUS, *résultat)
    glGetShaderiv(id_nuanceur, GL_INFO_LOG_LENGTH, *longueur_info_log)

    si longueur_info_log > 0 {
		message_erreur := loge_chaine(longueur_info_log + 1)
		glGetShaderInfoLog(id_nuanceur, longueur_info_log, nul, message_erreur.pointeur)

        imprime("!!!!! Erreur de compilation du nuanceur OpenGL :\n%\n", message_erreur)

        déloge(message_erreur)

        retourne faux
	}

    retourne vrai
}

vérifie_liaison :: fonc (id_programme: n32) -> bool
{
    résultat : z32 = 0
    longueur_info_log : z32 = 0

    glGetProgramiv(id_programme, GL_LINK_STATUS, *résultat)
	glGetProgramiv(id_programme, GL_INFO_LOG_LENGTH, *longueur_info_log)

    si longueur_info_log > 0 {
		message_erreur := loge_chaine(longueur_info_log + 1)
		glGetProgramInfoLog(id_programme, longueur_info_log, nul, message_erreur.pointeur)

        imprime("!!!!! Erreur de liaison du programme OpenGL :\n%\n", message_erreur)

        déloge(message_erreur)

        retourne faux
	}

    retourne vrai
}

détruit_nuanceur :: fonc (id_nuanceur: n32) -> rien
{
    si glIsShader(id_nuanceur) {
        glDeleteShader(id_nuanceur)
    }
}

charge_programme :: fonc (source_vertex: chaine, source_fragment: chaine) -> n32
{
    // -------------------
    // nuanceur vertex

	id_nuanceur_vertex := glCreateShader(GL_VERTEX_SHADER)
    diffère { détruit_nuanceur(id_nuanceur_vertex) }

    taille_src_vertex := source_vertex.taille comme z32
	glShaderSource(id_nuanceur_vertex, 1, *source_vertex.pointeur , *taille_src_vertex)
	glCompileShader(id_nuanceur_vertex)

    si !vérifie_compilation(id_nuanceur_vertex) {
        retourne 0
    }

    // -------------------
    // nuanceur fragment

	id_nuanceur_fragment := glCreateShader(GL_FRAGMENT_SHADER)
    diffère { détruit_nuanceur(id_nuanceur_fragment) }

    taille_src_fragment := source_fragment.taille comme z32
	glShaderSource(id_nuanceur_fragment, 1, *source_fragment.pointeur , *taille_src_fragment)
	glCompileShader(id_nuanceur_fragment)

    si !vérifie_compilation(id_nuanceur_fragment) {
        retourne 0
    }

    // -------------------
    // programme

    id_programme := glCreateProgram()

	glAttachShader(id_programme, id_nuanceur_vertex)
    diffère { glDetachShader(id_programme, id_nuanceur_vertex) }

    glAttachShader(id_programme, id_nuanceur_fragment)
    diffère { glDetachShader(id_programme, id_nuanceur_fragment) }

    glLinkProgram(id_programme)

    si !vérifie_liaison(id_programme) {
        glDeleteProgram(id_programme)
        retourne 0
    }

    retourne id_programme
}
