importe Compilatrice

donne_commande_pour_nom :: fonc (nom: chaine) -> fonc(*Anonyme)(rien)
#corps_texte {
    enchaineuse: Enchaineuse
    initialise_enchaineuse(*enchaineuse)
    diffère détruit_tampons(*enchaineuse)

    lexèmes := compilatrice_lèxe_fichier(#chemin_de_ce_fichier)

    sentinelle_trouvée := faux

    pour lexèmes {
        si it.genre != Compilatrice.GenreLexème.CHAINE_CARACTERE {
            continue
        }

        si it.texte == "donne_commande_pour_nom" {
            sentinelle_trouvée = vrai
            continue
        }

        saufsi sentinelle_trouvée {
            continue
        }

        saufsi it.texte.commence_par("commande_") {
            continue
        }

        nom_commande := it.texte.avance("commande_".taille)

        ajoute_au_tampon(*enchaineuse, "    si nom == \"", nom_commande, "\" { retourne ", it.texte, " }\n")
    }

    ajoute_au_tampon(*enchaineuse, "    retourne nul\n")

    retourne chaine_depuis_enchaineuse(*enchaineuse)
}

commande_défilage_bas :: fonc (anonyme: *Anonyme)
{
    anonyme.éditeur.décalage_ligne += 1
    si anonyme.éditeur.décalage_ligne >= anonyme.éditeur.lignes_texte.taille {
        anonyme.éditeur.décalage_ligne = anonyme.éditeur.lignes_texte.taille comme z32 - 1
    }
}

commande_déplace_curseur_bas :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.LigneSuivante)
}

commande_défilage_haut :: fonc (anonyme: *Anonyme)
{
    anonyme.éditeur.décalage_ligne -= 1
    si anonyme.éditeur.décalage_ligne < 0 {
        anonyme.éditeur.décalage_ligne = 0
    }
}

commande_déplace_curseur_haut :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.LignePrécédente)
}

commande_va_début_ligne :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.DébutLigne)
}

commande_va_fin_ligne :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.FinLigne)
}

commande_va_page_précédente :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.PagePrécédente)
}

commande_va_page_suivante :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.PageSuivante)
}

commande_va_au_mot_précédent :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.MotPrécédent)
}

commande_va_à_gauche :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.Recule)
}

commande_va_à_droite :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.Avance)
}

commande_va_au_mot_suivant :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.MotSuivant)
}

commande_ajoute_nouvelle_ligne :: fonc (anonyme: *Anonyme)
{
    éditeur_ajoute_nouvelle_ligne(*anonyme.éditeur)
}

commande_supprime_caractère :: fonc (anonyme: *Anonyme)
{
    éditeur_supprime_caractère(*anonyme.éditeur)
}

commande_supprime :: fonc (anonyme: *Anonyme)
{
    éditeur_déplace_curseur(*anonyme.éditeur, Mouvement.Avance)
    éditeur_supprime_caractère(*anonyme.éditeur)
}

commande_ouvrir_fichier :: fonc (anonyme: *Anonyme)
{
    anonyme.état = Anonyme.État.OUVERTURE_FICHIER
}

commande_sélectionne_fichier_suivant :: fonc (anonyme: *Anonyme)
{
    anonyme.fichier_sélectionné += 1
}

commande_sélectionne_fichier_précédent :: fonc (anonyme: *Anonyme)
{
    anonyme.fichier_sélectionné -= 1
}

commande_ouvre_fichier_sélectionné :: fonc (anonyme: *Anonyme)
{
    fichiers := donne_fichiers_pour_sélecteur(anonyme)

    si anonyme.fichier_sélectionné < 0 || anonyme.fichier_sélectionné >= fichiers.taille {
        retourne
    }

    fichier := fichiers[anonyme.fichier_sélectionné]
    éditeur_ouvre(*anonyme.éditeur, fichier.chemin)
    anonyme.état = Anonyme.État.ÉDITION
}
