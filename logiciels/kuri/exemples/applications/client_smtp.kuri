importe Fondation
importe GlibC
importe SysFichier

//https://tools.ietf.org/html/rfc5321#page-32
//https://fr.wikipedia.org/wiki/Simple_Mail_Transfer_Protocol
/*

S: 220 smtp.xxxx.xxxx SMTP Ready
C: HELO client
S: 250-smtp.xxxx.xxxx
S: 250-PIPELINING
S: 250 8BITMIME
C: MAIL FROM: <auteur@yyyy.yyyy>
S: 250 Sender ok
C: RCPT TO: <destinataire@xxxx.xxxx>
S: 250 Recipient ok.
C: DATA
S: 354 Enter mail, end with "." on a line by itself
C: Subject: Test
C:
C: Corps du texte
C: .
S: 250 Ok
C: QUIT
S: 221 Closing connection
Connection closed by foreign host.

*/

CodeRetour :: énum z32 {
	// Informationnel
	SERVEUR_NE_PEUT_SE_CONNECTER :: 101
	CONNEXION_REFUSÉE            :: 111

	// Succès
	MESSAGE :: 200
	RÉPONSE_COMMANDE_HELP :: 214
	SERVEUR_PRÊT :: 220
	FERMETURE_CANAL_TRANSMISSION :: 221
	ACTION_REQUISE_COMPLÉTÉE :: 250
	UTILISATION_NON_LOCAL_DEVRA_PROJETÉ :: 251
	NE_PEUT_VÉRIFIER_UTILISATEUR_MAIS_DELIVRE_QUAND_MÊME :: 252

	// Redirection
	COMMENCE_ENTRÉE_COURRIEL :: 354

	// Persistent transient failure
	PROBLÈME_CONNEXION_TIMEOUT :: 420
	SERVICE_NON_DISPONIBLE_À_CAUSE_CONNEXION :: 421
	BOÎTE_PLEINE :: 422
	MANQUE_ESPACE_DISQUE :: 431
	FILE_COURRIEL_DESTINATAIRE_EST_STOPPÉE :: 432
	SERVEUR_DESTINATAIRE_NE_RÉPOND_PAS :: 441
	CONNEXION_ABONDONNÉE_DURANT_TRANSMISSION :: 442
	COMPTE_HOP_EXCÉDÉ_POUR_LE_MESSAGE :: 446
	MESSAGE_TIMOUT :: 447
	ERREUR_ROUTAGE :: 449
	BOÎTE_COURRIEL_INDISPONIBLE :: 450
	ERREUR_LOCAL_LORS_DU_PROCÈS :: 451
	TROP_DE_COURRIELS_ENVOYÉS_OU_TROP_DE_DESTINATAIRES :: 452
	ERREUR_INTERNE :: 471

	// Erreurs permanentes
	ERREUR_DE_SYNTAXE :: 500
	ERREUR_DE_SYNTAXE_PARAMÈTRE_OU_ARGUMENT :: 501
	MAUVAISE_SÉQUENCE_DE_COMMANDE_OU_NON_AUTHENTIFIÉ :: 503
	PARAMÈTRE_DE_COMMANDE_NON_IMPLÉMENTÉ :: 504
	MAUVAISE_ADRESSE_COURRIEL :: 510
	MAUVAISE_ADRESSE_COURRIEL_ :: 511
	DNS_SERVEUR_DU_DESTINATAIRE_INTROUVABLE :: 512
	TYPE_ADRESSE_INCORRECT :: 513
	TAILE_DU_COURRIEL_TROP_GRANDE :: 523
	PROBLÈME_AUTHENTIFICATION :: 530
	DESTINATAIRE_REJETTE_LE_MESSAGE :: 541
	ADRESSE_COURRIEL_INEXISTANTE :: 550
	UTILISATEUR_NON_LOCAL_OU_ADRESSE_INVALIDE :: 551
	ALLOCATION_STOCKAGE_EXCÉDÉE :: 552
	NOM_BOITE_COURRIEL_INVALIDE :: 553
	TRANSACTION_ÉCHOUÉE :: 554
}

serveuse_envoie :: fonc (message: chaine) -> rien
{
	imprime("S > %", message)
}

principale :: fonc () -> z32
{
	nouveau_contexte := contexte
    nouveau_contexte.allocatrice = __stockage_temporaire

	serveuse_envoie("220 smtp.delsace.fr SMTP Ready\n")

	enchaineuse : Enchaineuse
	initialise_enchaineuse(@enchaineuse)
	diffère { détruit_tampons(@enchaineuse) }

	reçu_helo := faux
	reçu_envoyeur := faux
	reçu_destinataire := faux
	attente_message := faux

	es := entrée_standarde()

	boucle {
		imprime("C > ")

		entrée : chaine
    	pousse_contexte nouveau_contexte {
			entrée = es.lis_ligne()
    	}

		si entrée.commence_par("HELO") {
			reçu_helo = vrai
			serveuse_envoie("250 smtp.delsace.fr, heureux de vous rencontrer\r\n")
			//serveuse_envoie("250 PIPELINING\r\n")
			//serveuse_envoie("250 8BITMIME\r\n")
		}
		sinon si entrée.commence_par("MAIL FROM") {
			reçu_envoyeur = vrai
			serveuse_envoie("250 Sender Ok\r\n")
		}
		sinon si entrée.commence_par("RCPT TO:") {
			reçu_destinataire = vrai
			serveuse_envoie("250 Recipient ok\r\n")
		}
		sinon si entrée.commence_par("DATA") {
			si !reçu_envoyeur || !reçu_destinataire {
				serveuse_envoie("503 Mauvaise suite de commande\r\n")
			}
			sinon {
				serveuse_envoie("354 Entrez courriel, terminez par <CR><LF>.<CR><LF>\r\n")

				attente_message = vrai
			}
		}
		sinon si entrée.commence_par("QUIT") {
			serveuse_envoie("221 Au revoir\r\n")
			arrête
		}
		sinon {
			si attente_message {
				// accumule le mail
				si entrée == ".\r\n" || entrée == ".\n" {
					// fin du mail
					id := 5
					imprime("S > 250 Ok, enfiler comme %\r\n", id)
				}
				sinon {
					ajoute_au_tampon(@enchaineuse, entrée)
				}
			}
			sinon {
				serveuse_envoie("503 Mauvaise suite de commande\r\n")
			}
		}
	}

	courriel := chaine_depuis_enchaineuse(@enchaineuse)
	diffère { déloge courriel; }

	imprime("\nEnvoie du mail :\n%\n", courriel)

	retourne 0
}
