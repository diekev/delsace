importe GlibC

// ----------------------------------------------------------
// Toutes les erreurs possibles ; symétries de celles d'errno

ErreurFichier :: erreur {
    Inconnue

    ÉcriturePeutBloquer
    EntréeSortie
    EntréeSortieSpéciale
    EspaceDisqueInexistant
    EspaceDisqueInsuffisant
    FichierEstLectureSeul
    FichierEstPipe
    FichierExisteDéjà
    FichierInexistant
    FichierInvalide
    FichierTemporaireNonSupporté
    FichierTropGrand
    FichierTropPetit
    ImageEnLecture
    InterrompuParSignal
    LecturePeutBloquer
    LectureSurDossier
    LimiteOuvertureFichierAtteinte
    MauvaisEspaceAdressage
    MatérielInconnu
    MémoireInsuffisante
    NomTropGrand
    OuverturePeutBloquer
    OuvertureDossier
    PaireNonRenseigné
    PasDansUnDossier
    PermissionNonAccordé
    PipeFerméPourÉcriture
    SynchronisationImpossiblePourFichier
    TropDeLiensSymboliques
    ValeurInvalide
}

RésultatOpérationFichier :: union {
    e: ErreurFichier
    r: z32
}

système_ouvre :: fonc (chemin: *z8, drapeaux: z32, permissions: z32) -> RésultatOpérationFichier
{
    résultat := open(chemin, drapeaux, permissions)

    // À FAIRE : certaine valeur dépendent des drapeaux (création, écriture, ...)
    // il faudra atomiser l'interface, la préciser pour savoir quoi faire
    si résultat == -1 {
        err := lis_errno()

        discr err {
            EACCES       { retourne ErreurFichier.PermissionNonAccordé; }
            EDQUOT       { retourne ErreurFichier.EspaceDisqueInsuffisant; } // O_CREAT
            EEXIST       { retourne ErreurFichier.FichierExisteDéjà; } // O_CREAT & O_EXCL
            EFAULT       { retourne ErreurFichier.MauvaisEspaceAdressage; }
            EFBIG        { retourne ErreurFichier.FichierTropGrand; }
            EINTR        { retourne ErreurFichier.InterrompuParSignal; }
            EINVAL       { retourne ErreurFichier.ValeurInvalide; } // dépend du drapeau
            EISDIR       { retourne ErreurFichier.OuvertureDossier; } // Si O_WRONLY | O_RDWR ou O_TMPFILE and one of O_WRONLY or O_RDWR
            ELOOP        { retourne ErreurFichier.TropDeLiensSymboliques; } // dépend du drapeau
            EMFILE       { retourne ErreurFichier.LimiteOuvertureFichierAtteinte; }
            ENAMETOOLONG { retourne ErreurFichier.NomTropGrand; }
            ENFILE       { retourne ErreurFichier.LimiteOuvertureFichierAtteinte; }
            ENODEV       { retourne ErreurFichier.MatérielInconnu; }
            ENOENT       { retourne ErreurFichier.FichierInexistant; } // dépend du drapeau
            ENOMEM       { retourne ErreurFichier.MémoireInsuffisante; } // dépend du type de fichier
            ENOSPC       { retourne ErreurFichier.EspaceDisqueInsuffisant; }
            ENOTDIR      { retourne ErreurFichier.PasDansUnDossier; }
            ENXIO        { retourne ErreurFichier.EntréeSortieSpéciale; } // dépend du type de fichier
            EOPNOTSUPP   { retourne ErreurFichier.FichierTemporaireNonSupporté; }
            EOVERFLOW    { retourne ErreurFichier.FichierTropGrand; }
            EPERM        { retourne ErreurFichier.PermissionNonAccordé; } // dépend du drapeau
            EROFS        { retourne ErreurFichier.FichierEstLectureSeul; } // dépend du drapeau
            ETXTBSY      { retourne ErreurFichier.ImageEnLecture; } // dépend du drapeau
            EWOULDBLOCK  { retourne ErreurFichier.OuverturePeutBloquer; } // dépend du drapeau
        }
    }

    retourne résultat
}

système_ferme :: fonc (fd: z32) -> ErreurFichier
{
    résultat := close(fd)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EBADF        { retourne ErreurFichier.FichierInvalide; }
            EDQUOT       { retourne ErreurFichier.EspaceDisqueInsuffisant; }
            EINTR        { retourne ErreurFichier.InterrompuParSignal; }
            EIO          { retourne ErreurFichier.EntréeSortie; }
            ENOSPC       { retourne ErreurFichier.EspaceDisqueInexistant; }
            sinon        { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne 0 comme ErreurFichier
}

système_synchronise :: fonc (fd: z32) -> RésultatOpérationFichier
{
    résultat := fsync(fd)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EBADF        { retourne ErreurFichier.FichierInvalide; }
            EDQUOT       { retourne ErreurFichier.EspaceDisqueInsuffisant; }
            EINVAL       { retourne ErreurFichier.SynchronisationImpossiblePourFichier; }
            EIO          { retourne ErreurFichier.EntréeSortie; }
            ENOSPC       { retourne ErreurFichier.EspaceDisqueInexistant; }
            EROFS        { retourne ErreurFichier.SynchronisationImpossiblePourFichier; }
            sinon        { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne résultat
}

système_synchronise_données :: fonc (fd: z32) -> RésultatOpérationFichier
{
    résultat := fdatasync(fd)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EBADF        { retourne ErreurFichier.FichierInvalide; }
            EDQUOT       { retourne ErreurFichier.EspaceDisqueInsuffisant; }
            EINVAL       { retourne ErreurFichier.SynchronisationImpossiblePourFichier; }
            EIO          { retourne ErreurFichier.EntréeSortie; }
            ENOSPC       { retourne ErreurFichier.EspaceDisqueInexistant; }
            EROFS        { retourne ErreurFichier.SynchronisationImpossiblePourFichier; }
            sinon        { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne résultat
}

système_écris :: fonc (fd: z32, ptr:  *z8, taille: z64) -> RésultatOpérationFichier
{
    résultat := write(fd, ptr, taille)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EAGAIN       { retourne ErreurFichier.ÉcriturePeutBloquer; }
            EWOULDBLOCK  { retourne ErreurFichier.ÉcriturePeutBloquer; }
            EBADF        { retourne ErreurFichier.FichierInvalide; }
            EDESTADDRREQ { retourne ErreurFichier.PaireNonRenseigné; }
            EDQUOT       { retourne ErreurFichier.EspaceDisqueInsuffisant; }
            EFAULT       { retourne ErreurFichier.MauvaisEspaceAdressage; }
            EFBIG        { retourne ErreurFichier.FichierTropGrand; }
            EINTR        { retourne ErreurFichier.InterrompuParSignal; }
            EINVAL       { retourne ErreurFichier.ValeurInvalide; }
            EIO          { retourne ErreurFichier.EntréeSortie; }
            ENOSPC       { retourne ErreurFichier.EspaceDisqueInexistant; }
            EPERM        { retourne ErreurFichier.PermissionNonAccordé; }
            EPIPE        { retourne ErreurFichier.PipeFerméPourÉcriture; }
            sinon        { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne résultat
}

RésultatLecture :: union {
    e: ErreurFichier
    r: z64
}

système_lis :: fonc (fd: z32, ptr:  *z8, taille: z64) -> RésultatLecture
{
    résultat := read(fd, ptr, taille comme n64)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EAGAIN      { retourne ErreurFichier.LecturePeutBloquer; }
            EWOULDBLOCK { retourne ErreurFichier.LecturePeutBloquer; }
            EBADF       { retourne ErreurFichier.FichierInvalide; }
            EFAULT      { retourne ErreurFichier.MauvaisEspaceAdressage; }
            EINTR       { retourne ErreurFichier.InterrompuParSignal; }
            EINVAL      { retourne ErreurFichier.ValeurInvalide; }
            EIO         { retourne ErreurFichier.EntréeSortie; }
            EISDIR      { retourne ErreurFichier.LectureSurDossier; }
            sinon       { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne résultat
}

système_positionne_fichier :: fonc (fd: z32, décalage: z32, où: z32) -> RésultatOpérationFichier
{
    résultat := lseek(fd, décalage, où)

    si résultat == -1 {
        err := lis_errno()

        discr err {
            EBADF       { retourne ErreurFichier.FichierInvalide; }
            EINVAL      { retourne ErreurFichier.ValeurInvalide; } // où est invalide
            ENXIO       { retourne ErreurFichier.FichierTropPetit; } // où est SEEK_DATA ou SEEK_HOLE, et le décalage dépasse la taille du fichier
            EOVERFLOW   { retourne ErreurFichier.FichierTropPetit; }
            ESPIPE      { retourne ErreurFichier.FichierEstPipe; }
            sinon       { retourne ErreurFichier.Inconnue; }
        }
    }

    retourne résultat comme z32
}

// ----------------------------------------------------------

Fichier :: struct {
    desc : z32
}

RésultatFichier :: union {
    e: ErreurFichier
    f: Fichier
}

importe Fondation // pour chaine_c_temp

// ouvre_fichier_existant
// ouvre_fichier_existant_crée_sinon
ouvre_fichier :: fonc (chemin: chaine) -> RésultatFichier
{
   	ptr_chemin := chaine_c_temp(chemin)

    desc := tente système_ouvre(ptr_chemin, LECTURE_ÉCRITURE, 0o644) piège err {
        retourne err
    }

    fichier: Fichier
    fichier.desc = desc

    retourne fichier
}

ouvre_fichier_crée_si_non_existant :: fonc (chemin: chaine) -> RésultatFichier
{
    ptr_chemin := chaine_c_temp(chemin)

    desc := tente système_ouvre(ptr_chemin, LECTURE_ÉCRITURE | CREATION, 0o644) piège err {
        retourne err
    }

    fichier: Fichier
    fichier.desc = desc

    retourne fichier
}

ferme :: fonc (fichier: *Fichier) -> ErreurFichier
{
    si fichier.desc < 3 {
        retourne 0 comme ErreurFichier
    }

    retourne système_ferme(fichier.desc)
}

lis :: fonc (fichier: *Fichier, tampon: chaine) -> ErreurFichier
{
    si fichier.desc == -1 || fichier.desc == 1 || fichier.desc == 2 {
        retourne 0 comme ErreurFichier
    }

    retourne système_lis(fichier.desc, tampon.pointeur, tampon.taille)
}

CaractèreOuErreurFichier :: union {
    c: z8
    e: ErreurFichier
}

lis_caractère :: fonc (fichier: *Fichier) -> CaractèreOuErreurFichier
{
	c : z8

    octets_lus := tente système_lis(fichier.desc, @c, 1) piège err {
        retourne err
    }

    si octets_lus == 0 {
        retourne '\0'
    }

    retourne c
}

ChaineOuErreurFichier :: union {
    e: ErreurFichier
    c: chaine
}

lis_tout :: fonc (fichier: *Fichier) -> ChaineOuErreurFichier
{
    TAILLE_MAX :: 1024

    tampon : [TAILLE_MAX]z8
    résultat : chaine

    boucle {
        octets_lus := tente système_lis(fichier.desc, @tampon[0], TAILLE_MAX) piège err {
            déloge résultat
            retourne err
        }

        si octets_lus > 0 {
            ancienne_taille := résultat.taille
            reloge résultat : chaine(résultat.taille + octets_lus)
            copie_mem_nonsur(src=@tampon[0], dst=@résultat[ancienne_taille], taille=octets_lus)

            si octets_lus < TAILLE_MAX {
                arrête
            }
        }
        sinon si octets_lus == 0 {
            arrête
        }
    }

    retourne résultat
}

lis_ligne :: fonc (fichier: *Fichier) -> ChaineOuErreurFichier
{
	résultat : chaine
	c : z8

	boucle {
        octets_lus := tente système_lis(fichier.desc, @c, 1) piège err {
            déloge résultat
            retourne err
        }

        si octets_lus == 0 {
            arrête
        }

		chaine_ajoute(@résultat, c)

		si c == '\n' {
			arrête
		}
	}

    retourne résultat
}

// fonction spécialisée pour tout lire depuis un fichier sur le disque, où l'on
// calcule la taille du fichier avant de tout lire au lieu de faire une boucle
// pour accumuler les données dans un tampon
lis_tout_depuis_fichier :: fonc (fichier: *Fichier) -> ChaineOuErreurFichier
{
    taille_fichier := tente système_positionne_fichier(fichier.desc, 0, SEEK_END) piège err {
        retourne err
    }

    si taille_fichier == 0 {
        retourne ""
    }

    _ := tente système_positionne_fichier(fichier.desc, 0, SEEK_SET) piège err {
        retourne err
    }

    résultat := loge chaine(taille_fichier)

    octets_lus := tente système_lis(fichier.desc, résultat.pointeur, résultat.taille: n32) piège err {
        déloge résultat
        retourne err
    }

    retourne résultat
}

// écris_à_la_position
// écris_fin
écris :: fonc (fichier: *Fichier, chn: chaine) -> RésultatOpérationFichier
{
    retourne système_écris(fichier.desc, chn.pointeur, chn.taille)
}

fichier_entrée_standarde := Fichier( desc = 0 )
fichier_sortie_standarde := Fichier( desc = 1 )
fichier_sortie_erreur    := Fichier( desc = 2 )

entrée_standarde :: fonc () -> *Fichier
{
    retourne @fichier_entrée_standarde
}

sortie_standarde :: fonc () -> *Fichier
{
    retourne @fichier_sortie_standarde
}

sortie_erreur :: fonc () -> *Fichier
{
    retourne @fichier_sortie_erreur
}
