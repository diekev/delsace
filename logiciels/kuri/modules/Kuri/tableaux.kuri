importe Fondation

/* Fonctions basiques pour les tableaux */

loge_tableau :: fonc ($T: type_de_données, taille: z64, pos := PositionCodeSource()) -> [..]T
{
    taille_en_octets := taille * (taille_de(T) comme z64)
    ptr := loge_mémoire(taille_en_octets, info_de([..]T), pos)

    résultat : [..]T = ---
    résultat.pointeur = ptr
    résultat.taille = 0
    résultat.capacité = taille

    retourne résultat
}

reloge_tableau :: fonc (tableau: &[..]$T, nouvelle_taille: z64, pos := PositionCodeSource())
{
    ancienne_taille_en_octets := tableau.capacité * (taille_de(T) comme z64)
    nouvelle_taille_en_octets := nouvelle_taille * (taille_de(T) comme z64)

    ptr := reloge_mémoire(ancienne_taille_en_octets, nouvelle_taille_en_octets, tableau.pointeur, info_de([..]T), pos)

    tableau.pointeur = ptr
    tableau.capacité = nouvelle_taille
}

déloge :: fonc (tableau: &[..]$T, pos := PositionCodeSource())
{
    ancienne_taille_en_octets := tableau.capacité * (taille_de(T) comme z64)
    déloge_mémoire(ancienne_taille_en_octets, tableau.pointeur, info_de([..]T), pos)
    tableau.pointeur = nul
    tableau.capacité = 0
    tableau.taille = 0
}

tableau_ajoute :: fonc (tabl: *[..]$T, valeur: T) -> rien #enligne
{
    tmp := mémoire(tabl)

    si tmp.taille == tmp.capacité {
        capacité := tmp.capacité
        reloge_tableau(tmp, capacité + 1)
    }

    tmp.taille += 1
    tmp[tmp.taille - 1] = valeur

    mémoire(tabl) = tmp
}

tableau_ajoute_élément :: fonc (tableau: *[..]$T) -> *T
{
    valeur: T
    tableau_ajoute(tableau, valeur)
    /* Utilise ".pointeur" car [..] s'applique au pointeur du tableau. */
    retourne *tableau.pointeur[tableau.taille - 1]
}

élément_tableau_index_relatif :: fonc (tabl: [..]$T, index: z64) -> *T
{
    si index >= 0 {
        retourne *tabl[index]
    }

    index = tabl.taille + index
    retourne *tabl[index]
}

tableau_réserve :: fonc (tabl: *[..]$T, taille: z64) -> rien
{
    si taille <= tabl.capacité {
        retourne
    }

    tmp := mémoire(tabl)

    reloge_tableau(tmp, taille)

    mémoire(tabl) = tmp
}

tableau_redimensionne :: fonc (tabl: &[..]$T, taille: z64) -> rien
{
    si taille == tabl.capacité {
        tabl.taille = tabl.capacité
        retourne
    }

    si taille < tabl.capacité {
        tabl.taille = taille
        retourne
    }

    reloge_tableau(tabl, taille)
    tabl.taille = taille
}

tableau_copie :: fonc (tabl: [..]$T) -> [..]T
{
    tmp := loge_tableau(T, tabl.taille)
    //copie_mem_nonsur(src = tabl.pointeur, dst = tabl.pointeur, taille = tabl.taille * taille_de(z64) comme z64)

    pour tabl {
        tmp.pointeur[index_it] = it
    }

    tmp.taille = tabl.taille
    retourne tmp
}

tableau_copie :: fonc (tabl: []$T) -> [..]T
{
    tmp := loge_tableau(T, tabl.taille)

    pour tabl {
        tmp.pointeur[index_it] = it
    }

    tmp.taille = tabl.taille
    retourne tmp
}

tableau_copie :: fonc (tabl: [..]$T, fonction_de_copie: fonc(T)(T)) -> [..]T
{
    résultat: [..]T
    tableau_réserve(*résultat, tabl.taille)

    pour tabl {
        tableau_ajoute(*résultat, fonction_de_copie(it))
    }

    retourne résultat
}

tableau_insère_index :: fonc (tabl: *[..]$T, valeur: T, où: z64) -> rien
{
    tmp := mémoire(tabl)

    reloge_tableau(tmp, tmp.taille + 1)
    tmp.taille += 1

    pour > où + 1 ... tmp.taille - 1 {
        tmp[it] = tmp[it - 1]
    }

    tmp[où] = valeur

    mémoire(tabl) = tmp
}

tableau_supprime :: fonc (tabl: *[..]$T, index: z32) -> rien
{
    si index < 0 || index >= tabl.taille {
        retourne
    }

    tmp := mémoire(tabl)

    taille := tabl.taille

    pour i dans index comme z64 ... taille - 2 {
        tmp[i] = tmp[i + 1]
    }

    reloge_tableau(tmp, taille - 1)

    mémoire(tabl) = tmp
}

tableau_supprime_index :: fonc (tabl: *[..]$T, index: z64) -> rien
{
    tmp := mémoire(tabl)

    pour i dans index ... tmp.taille - 2 {
        tmp[i] = tmp[i + 1]
    }

    tmp.taille -= 1

    mémoire(tabl) = tmp
}

tableau_supprime_si :: fonc (tableau: *[..]$T, prédicat: fonc(T)(bool))
{
    temp := mémoire(tableau)

    nouveau_tableau: [..]T
    tableau_réserve(*nouveau_tableau, tableau.taille)

    pour temp {
        si prédicat(it) {
            continue
        }

        tableau_ajoute(*nouveau_tableau, it)
    }

    déloge(temp)

    mémoire(tableau) = nouveau_tableau
}

tableau_supprime_valeur :: fonc (tabl: *[..]$T, valeur: T)
{
    tmp := mémoire(tabl)

    pour tmp {
        si it == valeur {
            tableau_supprime_index(tabl, index_it)
            arrête
        }
    }
}

/**
 * Retourne un tableau ayant les mêmes valeurs que le tableau en paramètre, et réinitialise
 * ce dernier. Ceci est utile pour transférer la propriété d'un tableau.
 */
tableau_vole_mémoire :: fonc (tableau: &[..]$T) -> [..]T
{
    résultat := tableau
    tableau.pointeur = nul
    tableau.taille = 0
    tableau.capacité = 0
    retourne résultat
}

tableau_remplis_avec :: fonc (tableau: [..]$T, valeur: T)
{
    pour & v dans tableau {
        v = valeur
    }
}

est_adresse_borne_tableau :: fonc (tableau: [..]$T, adresse: *T) -> bool #enligne
{
    si adresse == tableau.pointeur {
        retourne vrai
    }

    si adresse == (tableau.pointeur + tableau.taille - 1) {
        retourne vrai
    }

    retourne faux
}

/* Retourne vrai si l'adresse est celle d'un élément se trouvant dans le tableau,
 * en incluant le premier et le dernier élément. */
est_adresse_élément_bornes_incluses :: fonc (tableau: [..]$T, adresse: *T) -> bool #enligne
{
    premier := tableau.pointeur
    dernier := premier + tableau.taille - 1
    retourne premier <= adresse <= dernier
}

/* Retourne vrai si l'adresse est celle d'un élément se trouvant dans le tableau,
 * en excluant le premier et le dernier élément. */
est_adresse_élément_bornes_excluses :: fonc (tableau: [..]$T, adresse: *T) -> bool #enligne
{
    premier := tableau.pointeur
    dernier := premier + tableau.taille
    retourne premier < adresse < dernier
}

index_élement :: fonc (tableau: [..]$T, adresse: *T) -> z64 #enligne
{
    retourne (adresse - tableau.pointeur)
}

tableau_détruit :: fonc (tableau: &[..]$T)
{
    déloge(tableau)
}

mémoire_utilisée_tableau :: fonc (tableau: [..]$T) -> z64
{
    retourne tableau.taille * (taille_de(T) comme z64)
}

déloge_tableau_et_ses_éléments :: fonc (tableau: &[..]$T)
{
    /* À FAIRE(langage) : déréférencement automatique */
    pour tableau.taille {
        déloge(tableau.pointeur[it])
    }
    déloge(tableau)
}

déloge_tableau_et_ses_éléments :: fonc (tableau: &[..]$T, rappel_déloge_élément: fonc(T)(rien))
{
    /* À FAIRE(langage) : déréférencement automatique */
    pour tableau.taille {
        rappel_déloge_élément(tableau.pointeur[it])
    }
    déloge(tableau)
}

tableau_trouve_index_élément :: fonc (tableau: [..]$T, valeur: T) -> Optionnel(z64)
{
    pour tableau {
        si it == valeur {
            retourne index_it
        }
    }
}

tableaux_sont_égaux :: fonc (tableau1: [..]$T, tableau2: [..]T) -> bool
{
    saufsi tableau1.taille == tableau2.taille {
        retourne faux
    }

    pour tableau1 {
        si tableau2[index_it] != it {
            retourne faux
        }
    }

    retourne vrai
}

tranche :: fonc (tableau: []$T, début: z64, fin: z64) -> [..]T
{
    résultat: [..]T

    si début > fin {
        retourne résultat
    }

    si début >= tableau.taille {
        retourne résultat
    }

    si fin >= tableau.taille {
        retourne résultat
    }

    résultat.pointeur = *tableau.pointeur[début]
    résultat.taille = fin - début + 1
    résultat.capacité = résultat.taille
    retourne résultat
}

dernier_élément :: fonc (tableau: [..]$T) -> *T
{
    si tableau.taille == 0 {
        retourne nul
    }
    retourne *tableau.pointeur[tableau.taille - 1]
}

tableau_iota :: fonc (tableau: []$T, valeur_initiale: T)
{
    pour & tableau {
        it = valeur_initiale
        valeur_initiale += (1 comme T)
    }
}

tableau_taille_mémoire :: fonc (tableau: []$T) -> z64
{
    retourne tableau.taille * taille_de(T) comme z64
}

tableau_avance :: fonc (tableau: []$T) -> []T
{
    si tableau.taille != 0 {
        tableau.pointeur += 1
        tableau.taille -= 1
    }
    retourne tableau
}

tableau_donne_index :: fonc (tableau: []$T, valeur: T) -> z64
{
    pour tableau {
        si it == valeur {
            retourne index_it
        }
    }
    retourne -1
}
