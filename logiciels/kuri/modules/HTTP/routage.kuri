importe Compilatrice
importe ConnexionTCP
importe Fondation
importe Internet
importe Introspection
importe SysFichier

TypeRappelRoute :: fonc(*Prise, &MessageClienteHTTP)(MessageServeuseHTTP)

/* Fonction pour parser une chaine de caractère selon les types trouvées lors
 * de la compilation du programme parent.
 */
parse_paramètre_routage_pour_type :: fonc (chn: chaine, valeur: eini) -> bool
{
    fonction := trouve_fonction_pour_type(valeur.info)
    saufsi fonction {
        retourne faux
    }
    retourne fonction.rappel(chn, tampon_pour_eini(valeur))
}

/* Type pour définir des erreurs. */
ErreurHTTP :: struct {
    code: CodeÉtatHTTP
    message: chaine
}

CodeEtContenuHTPP :: struct {
    code: CodeÉtatHTTP
    contenu: ContenuHTTP
}

message_depuis_erreur_http :: fonc (erreur_http: ErreurHTTP) -> MessageServeuseHTTP
{ 
    retourne crée_message_avec_contenu(erreur_http.code, contenu_html(erreur_http.message))
}

erreur_autorisation :: fonc (message := "") -> ErreurHTTP
{
    saufsi message {
        message = "autorisation requise"
    }

    retourne ErreurHTTP(CodeÉtatHTTP.UNAUTHORIZED, message)
}

erreur_ressource_non_trouvée :: fonc (message := "") -> ErreurHTTP
{
    saufsi message {
        message = "ressource non trouvée"
    }

    retourne ErreurHTTP(CodeÉtatHTTP.NOT_FOUND, message)
}

erreur_interne_serveur :: fonc (message := "") -> ErreurHTTP
{
    saufsi message {
        message = "erreur interne du serveur"
    }

    retourne ErreurHTTP(CodeÉtatHTTP.INTERNAL_SERVER_ERROR, message)
}

erreur_requête_malformée :: fonc (message := "") -> ErreurHTTP
{
    saufsi message {
        message = "requête malformée"
    }

    retourne ErreurHTTP(CodeÉtatHTTP.BAD_REQUEST, message)
}

/* Structure de base pour les paramètres des requêtes des URI.
 * Ces paramètres sont les paires nom-valeur suivant le point d'interrogation ('?')
 * dans un URI :
 * vidéos.fr/regarder/?vidéo=IDVIDÉO
 */
ParamètresRequête :: struct {
}

/* Structure de base pour les paramètres de routage. Ces paramètres peuvent être
 * définis dans les routes via un nom entourée par des chevrons :
 * "/<id>/"
 * défini un paramètre de entier nommé « id ».

 * Cette route sera appariée à tous les chemins ayant une chaine de caractère
 * représentant un entier comme première, et seule dans l'exemple, branche :
 * /123456/ prendra cette route, alors que /123def/ ne la prendra pas.
 */
ParamètresRoutage :: struct {
}

règles_ont_au_moins_un_paramètre :: fonc (règles: [..]RègleRoutage) -> bool
{
    pour règles {
        si it.action != ActionRègleRoutage.ApparieChaineLittérale {
            retourne vrai
        }
    }

    retourne faux
}

apparie_paramètres_à_la_structure :: fonc (règles: [..]RègleRoutage, infos: *InfoType) -> bool
{
    si infos.id != GenreInfoType.STRUCTURE {
        retourne faux
    }

    info_structure := infos comme *InfoTypeStructure

    nombre_paramètres := 0

    pour règles {
        saufsi it.action == ActionRègleRoutage.ApparieParamètre {
            continue
        }

        nombre_paramètres += 1

        si !possède_membre(info_structure, it.chn) {
            retourne faux
        }

        // À FAIRE : typage
    }

    si nombre_paramètres != donne_nombre_de_membres_effectif(info_structure) {
        retourne faux
    }

    retourne vrai
}

calcul_priorité :: fonc (règles: [..]RègleRoutage) -> z32
{
    priorité := 0

    pour règles {
        si it.action != ActionRègleRoutage.ApparieChaineLittérale {
            priorité += 1
        }
    }

    retourne priorité
}

NouvelleRouteuse :: struct {
    apparie_routes_get: fonc(chaine, *Prise, &MessageClienteHTTP)(MessageServeuseHTTP)
    apparie_routes_post: fonc(chaine, *Prise, &MessageClienteHTTP)(MessageServeuseHTTP)

    // À FAIRE
    rappel_page_non_trouvée: fonc(*Prise, &MessageClienteHTTP)(MessageServeuseHTTP)

    extension_fichiers: [..]chaine
}

apparie_fichiers :: fonc (routeuse: &NouvelleRouteuse, extension: chaine)
{
    pour routeuse.extension_fichiers {
        si it == extension {
            retourne
        }
    }

    tableau_ajoute(*routeuse.extension_fichiers, extension)
}

détruit_routeuse :: fonc (routeuse: &NouvelleRouteuse) -> rien
{
    déloge(routeuse.extension_fichiers)
}

apparie_route :: fonc (routeuse: &NouvelleRouteuse, prise: *Prise, message: &MessageClienteHTTP) -> MessageServeuseHTTP
{
    méthode := message.méthode
    // Utilise message.uri.chemin car message.requête peut contenir également les paramètres de requête et de fragment de l'URI
    chemin := message.uri.chemin

    si routeuse.apparie_routes_get && méthode == "GET" {
        si est_chemin_fichier_supporté(routeuse, chemin) {
            retourne apparie_fichier(routeuse, chemin, prise, message)
        }

        retourne routeuse.apparie_routes_get(chemin, prise, message)
    }

    si routeuse.apparie_routes_post && méthode == "POST" {
        retourne routeuse.apparie_routes_post(chemin, prise, message)
    }

    retourne méthode_non_autorisée()
}

chemin_pour_fichier :: fonc (chemin: chaine) -> CheminFichier
{
    // enlève le premier slash
    chemin_fichier := avance(chemin, 1)
    retourne CheminFichier(chemin_fichier)
}

est_chemin_fichier_supporté :: fonc (routeuse: &NouvelleRouteuse, chemin: chaine) -> bool
{
    chm := chemin_pour_fichier(chemin)

    si fichier_existe(chm) {
        extension := chm.extension()

        pour routeuse.extension_fichiers {
            si it == extension {
                retourne vrai
            }
        }
    }

    retourne faux
}

apparie_fichier :: fonc (routeuse: &NouvelleRouteuse @inutilisée, chemin: chaine, prise: *Prise, message: &MessageClienteHTTP) -> MessageServeuseHTTP
{
    chm := chemin_pour_fichier(chemin)

    si chm.extension() == ".css" {
        retourne sers_fichier(prise, message, "text/css; charset=utf-8")
    }

    si chm.extension() == ".png" {
        retourne sers_fichier(prise, message, "image/png")
    }

    si chm.extension() == ".js" || chm.extension() == ".jsx" {
        retourne sers_fichier(prise, message, "text/javascript; charset=utf-8")
    }

    retourne ressource_non_trouvée()
}

// --------------------------------
// #portée_fichier

divise_url :: fonc (ligne: chaine) -> [..]chaine
{
    taille_mot : z64 = 0
    début_mot : z64 = 0
    mots : [..]chaine

    premier_slash := vrai

    pour c, i dans ligne {
        si c == '/' {
            si premier_slash {
                premier_slash = faux

                mot := ligne.sous_chaine(0, 1)
                tableau_ajoute(*mots, mot)

                début_mot = i + 1
                taille_mot = 0
                continue
            }

            mot := ligne.sous_chaine(début_mot, début_mot + taille_mot)
            tableau_ajoute(*mots, mot)

            début_mot = i + 1
            taille_mot = 0
            continue
        }

        taille_mot += 1
    }

    si taille_mot != 0 {
        mot := ligne.sous_chaine(début_mot, début_mot + taille_mot)

        si mot {
            tableau_ajoute(*mots, mot)
        }
    }

    retourne mots
}

// Règles pour apparier les routes.

ActionRègleRoutage :: énum {
    Invalide
    /* Apparie la chaine telle que données dans le chemin, par exemple
     * /accueil/ devra apparier "accueil". */
    ApparieChaineLittérale
    /* Apparie un paramètre du nom données entre les chevrons, par exemple /<id>/
     * devra apparier un membre de la structure associée nommée "id". */
    ApparieParamètre
}

RègleRoutage :: struct {
    action: ActionRègleRoutage
    chn: chaine
}

ErreurParsage :: erreur {
    RègleDénuéeDeNom
    TypeActionInconnue
    NomRègleRedéfini
    ChevronFermantManquant
}

ErreurOuRèglesRoutage :: union {
    e: ErreurParsage
    r: [..]RègleRoutage
}

parse_règles :: fonc (route: chaine) -> ErreurOuRèglesRoutage
{
    éléments := divise_url(route)
    diffère { déloge(éléments) }

    résultat : [..]RègleRoutage

    pour éléments {
        règle : RègleRoutage

        si it[0] == '<' {
            si it[it.taille - 1] != '>' {
                déloge(résultat)
                retourne ErreurParsage.ChevronFermantManquant
            }

            nom_données := it.sous_chaine(1, it.taille - 1)

            pour r dans résultat {
                si r.chn == nom_données {
                    déloge(résultat)
                    retourne ErreurParsage.NomRègleRedéfini
                }
            }

            règle.chn = nom_données
            règle.action = ActionRègleRoutage.ApparieParamètre
        }
        sinon {
            // ceci est également utilisé pour la racine "/"
            règle.action = ActionRègleRoutage.ApparieChaineLittérale
            règle.chn = it
        }

        tableau_ajoute(*résultat, règle)
    }

    retourne résultat
}

// ---------------------------------------------------------------------
// Rassemblement des fonctions de parsage pour les paramètres de routage

__fonctions_parsage_routes : [..]FonctionParsage

trouve_fonction_parsage_pour_type :: fonc (info: *InfoType)
{
    retourne trouve_fonction_pour_type(__fonctions_parsage_routes, info)
}

fonction_routage_est_valide :: fonc (fonction: *NoeudCodeEntêteFonction) -> DonnéesFonctionPourTableFonction
{
    résultat : DonnéesFonctionPourTableFonction

    si fonction.params.taille != 1 {
        compilatrice_rapporte_erreur(fonction, "Impossible d'utiliser la fonction comme parseuse pour route HTTP, elle ne doit avoir qu'un seul paramètre")
        retourne résultat
    }

    type_premier_paramètre := fonction.params[0].type
    si type_premier_paramètre != info_de(chaine) {
        compilatrice_rapporte_erreur(fonction.params[0], "Fonction de parsage route HTTP invalide, le paramètre doit être de type « chaine ».")
        retourne résultat
    }

    si fonction.params_sorties.taille != 2 {
        compilatrice_rapporte_erreur(fonction, "Impossible d'utiliser la fonction comme parseuse pour route HTTP, elle doit avoir deux paramètres de sortie : TypeÀFormatter et bool")
        retourne résultat
    }

    type_première_sortie := fonction.params_sorties[0].type

    si type_première_sortie != info_de(bool) {
        compilatrice_rapporte_erreur(fonction.params_sorties[0], "Fonction de parsage route HTTP invalide, le premier paramètre de sortie doit être de type « bool ».")
        retourne résultat
    }

    résultat.fonction = fonction
    résultat.info_type = fonction.params_sorties[1].type
    retourne résultat
}

génère_fonction_enveloppe_routage :: fonc (enchaineuse: *Enchaineuse, données: DonnéesFonctionPourTableFonction) -> chaine
{
    nom_enveloppe := enchaine("parsage_routes_", données.fonction.nom)

    ajoute_au_tampon(enchaineuse, nom_enveloppe, " :: fonc (chn: chaine, tampon: [..]octet) -> bool\n")
    ajoute_au_tampon(enchaineuse, "{\n")
    ajoute_au_tampon(enchaineuse, "\tsuccès, valeur := ", données.fonction.nom, "(chn)\n")
    ajoute_au_tampon(enchaineuse, "\tsaufsi succès {\n\t\tretourne faux\n\t}\n")
    ajoute_au_tampon(enchaineuse, "\tcopie_mem_nonsur(src = *valeur, dst = tampon.pointeur, taille = tampon.taille)\n")
    ajoute_au_tampon(enchaineuse, "\tretourne vrai\n")
    ajoute_au_tampon(enchaineuse, "}\n")

    retourne nom_enveloppe
}

génère_initialisation_routes :: fonc ()
{
    créatrice_table_fonctions : CréatriceTableFonction
    diffère détruit_données_créatrice_table_fonctions(créatrice_table_fonctions)
    créatrice_table_fonctions.annotation_désirée = "ParsageRouteHTTP"
    créatrice_table_fonctions.nom_table_fonctions = "__fonctions_parsage_routes"
    créatrice_table_fonctions.données_pour_fonction = fonction_routage_est_valide
    créatrice_table_fonctions.génère_fonction_enveloppe = génère_fonction_enveloppe_routage
    créatrice_table_fonctions.initialise_types_de_bases = vrai
    créatrice_table_fonctions.ajoute_module("Introspection")
    créatrice_table_fonctions.ajoute_module("HTTP")

    crée_table_fonctions(créatrice_table_fonctions)
}
