// Interface pour les fonctions standardes définies par POSIX

importe Fondation

getenv :: fonc (nom: *z8) -> *z8 #externe
getuid :: fonc () -> z32 #externe
system :: fonc (command: *z8) -> z32 #externe
usleep :: fonc (µsec: n32) -> z32 #externe

chaine_environnement :: fonc (nom: chaine) -> chaine
{
    chn := getenv(nom.pointeur)

    saufsi chn {
        retourne ""
    }

    retourne converti_chaine_c(chn)
}

est_utilisateur_racine :: fonc() -> bool
{
    retourne getuid() == 0
}

exécute_commande_externe :: fonc (args: ...chaine) -> bool
{
    enchaineuse : Enchaineuse
    initialise_enchaineuse(*enchaineuse)

    pour args {
        ajoute_au_tampon(*enchaineuse, it)
        ajoute_au_tampon(*enchaineuse, " ")
    }

    // À FAIRE : ne peut pas utiliser "\0" dans la coulisse C
    ajoute_au_tampon(*enchaineuse, " ")

    commande := chaine_depuis_enchaineuse(*enchaineuse)
    diffère { déloge commande }

    commande[commande.taille - 1] = '\0'

    si system(commande.pointeur) != 0 {
        retourne faux
    }

    retourne vrai
}

dors_millisecondes :: fonc (millisecondes: z32)
{
    si millisecondes < 0 {
        retourne
    }

    si millisecondes == 0 {
        retourne
    }

    _ := usleep((millisecondes * 1000) comme n32)
}
