// Interface pour les fonctions standardes définies par POSIX

importe Fondation
importe GlibC

getenv :: fonc (nom: ChaineC) -> ChaineC #externe libc
getuid :: fonc () -> z32 #externe libc
system :: fonc (command: ChaineC) -> z32 #externe libc
usleep :: fonc (µsec: n32) -> z32 #externe libc
symlink :: fonc (target: ChaineC, linkpath: ChaineC) -> z32 #externe libc
mkdir :: fonc (chemin: ChaineC, mode: z32) -> z32 #externe libc

realpath :: fonc (path: ChaineC, resolved_path: ChaineC) -> ChaineC #externe libc

/* PATH_MAX, limits.h */
TAILLE_MAX_CHEMIN :: 4096

chaine_environnement :: fonc (nom: chaine) -> chaine
{
    chn_c_nom := crée_chaine_c(nom)
    diffère détruit_chaine_c(chn_c_nom)

    chn := getenv(chn_c_nom)

    saufsi (chn comme *z8) {
        retourne ""
    }

    retourne convertis_chaine_c(chn)
}

est_utilisateur_racine :: fonc() -> bool
{
    retourne getuid() == 0
}

exécute_commande_externe :: fonc (args: ...chaine) -> bool
{
    enchaineuse : Enchaineuse
    initialise_enchaineuse(*enchaineuse)

    pour args {
        ajoute_au_tampon(*enchaineuse, it)
        ajoute_au_tampon(*enchaineuse, " ")
    }

    ajoute_au_tampon(*enchaineuse, '\0')

    commande := chaine_depuis_enchaineuse(*enchaineuse)
    diffère { déloge(commande) }

    si system(ChaineC(commande.pointeur)) != 0 {
        retourne faux
    }

    retourne vrai
}

dors_millisecondes :: fonc (millisecondes: z32)
{
    si millisecondes < 0 {
        retourne
    }

    si millisecondes == 0 {
        retourne
    }

    _ := usleep((millisecondes * 1000) comme n32)
}
