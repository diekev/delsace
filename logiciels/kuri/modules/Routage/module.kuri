// Module de routage pour des services HTTP, répondant à méthodes GET ou POST

importe Fondation
importe Internet
importe Réseau

divise_url :: fonc (ligne: chaine) -> []chaine
{
    taille_mot : z64 = 0
	début_mot : z64 = 0
    mots : []chaine

    premier_slash := vrai

    pour c, i dans ligne {
	    si c == '/' {
            si premier_slash {
                premier_slash = faux

                mot := ligne.sous_chaine(0, 1)
                tableau_ajoute(*mots, mot)

		    	début_mot = i + 1
			    taille_mot = 0
                continue
            }

			mot := ligne.sous_chaine(début_mot, début_mot + taille_mot)
            tableau_ajoute(*mots, mot)

			début_mot = i + 1
			taille_mot = 0
			continue
		}

		taille_mot += 1
	}

    si taille_mot != 0 {
		mot := ligne.sous_chaine(début_mot, début_mot + taille_mot)

        si mot {
            tableau_ajoute(*mots, mot)
        }
	}

    retourne mots
}

// Règles pour apparier les routes.

ActionRègleRoutage :: énum {
    Invalide
    ApparieChaineLittérale
    ApparieChaine
    ApparieNombreEntier
}

RègleRoutage :: struct {
    action: ActionRègleRoutage
    chn: chaine
    nom_données: chaine
}

ErreurParsage :: erreur {
    RègleDénuéeDeNom
    TypeActionInconnue
    NomRègleRedéfini
    ChevronFermantManquant
}

ErreurOuRèglesRoutage :: union {
    e: ErreurParsage
    r: []RègleRoutage
}

parse_règles :: fonc (route: chaine) -> ErreurOuRèglesRoutage
{
    éléments := divise_url(route)
    diffère { déloge(éléments) }

    résultat : []RègleRoutage

    pour éléments {
        règle : RègleRoutage

        si it[0] == '<' {
            pos_double_points := trouve_caractère_depuis_le_début(it, ':')

            si pos_double_points == -1 {
                déloge(résultat)
                retourne ErreurParsage.RègleDénuéeDeNom
            }

            si it[it.taille - 1] != '>' {
                déloge(résultat)
                retourne ErreurParsage.ChevronFermantManquant
            }

            nom_données := it.sous_chaine(1, pos_double_points)
            type_action := it.sous_chaine(pos_double_points + 1, it.taille - 1)

            si type_action == "entier" {
                règle.action = ActionRègleRoutage.ApparieNombreEntier
            }
            sinon si type_action == "chaine" {
                règle.action = ActionRègleRoutage.ApparieChaine
            }
            sinon {
                déloge(résultat)
                retourne ErreurParsage.TypeActionInconnue
            }

            pour r dans résultat {
                si r.nom_données == nom_données {
                    déloge(résultat)
                    retourne ErreurParsage.NomRègleRedéfini
                }
            }

            règle.nom_données = nom_données
        }
        sinon {
            // ceci est également utilisé pour la racine "/"
            règle.action = ActionRègleRoutage.ApparieChaineLittérale
            règle.chn = it
        }

        tableau_ajoute(*résultat, règle)
    }

    retourne résultat
}

est_seulement_nombre :: fonc (chn: chaine) -> bool
{
    pour chn {
        si !est_nombre_décimal(it) {
            retourne faux
        }
    }

    retourne vrai
}

ErreurAppariement :: erreur {
    RègleInvalide
    ÉlémentDoitÊtreUnNombre
    ÉlémentDoitÊtreUneChaine
    RègleEtRouteOntTaillesDifférentes
}

DonnéesAppariement :: struct {
    nom: chaine
    données: chaine
}

DonnéesRouteOuErreur :: union {
    e: ErreurAppariement
    d: []DonnéesAppariement
}

apparie_règles_et_route :: fonc (règles: []RègleRoutage, route: chaine) -> DonnéesRouteOuErreur
{
    éléments := divise_url(route)
    diffère { déloge(éléments) }

    si règles.taille != éléments.taille {
        retourne ErreurAppariement.RègleEtRouteOntTaillesDifférentes
    }

    résultat : []DonnéesAppariement

    pour éléments {
        règle := règles[index_it]

        discr règle.action {
            Invalide {
                déloge(résultat)
                retourne ErreurAppariement.RègleInvalide
            }
            ApparieChaine {
                // RÀF, nous pourrions avoir une fonction de rappel
            }
            ApparieChaineLittérale {
                si it != règle.chn {
                    déloge(résultat)
                    retourne ErreurAppariement.ÉlémentDoitÊtreUneChaine
                }

                données : DonnéesAppariement
                données.nom = règle.nom_données
                données.données = it

                tableau_ajoute(*résultat, données)
            }
            ApparieNombreEntier {
                si !est_seulement_nombre(it) {
                    déloge(résultat)
                    retourne ErreurAppariement.ÉlémentDoitÊtreUnNombre
                }

                données : DonnéesAppariement
                données.nom = règle.nom_données
                données.données = it

                tableau_ajoute(*résultat, données)
            }
        }
    }

    retourne résultat
}

Entête :: struct {
    nom: chaine
    valeur: chaine
}

// Les données de la requête
Requête :: struct {
    uri : URI
    entêtes : []Entête
    cookies : []Cookie
    contenu : chaine
    type_contenu : chaine
    méthode : chaine
    adresse_ip : AdresseIPv4
}

// Le résultat d'une requête.
RésultatRequête :: struct {
    status : CodeÉtatHTTP
    contenu : chaine
    type_contenu : chaine

    // À FAIRE(langage) : erreur de compilation quand nous accédons à ceci via une expression d'appel
    // Entête :: struct {
    //     nom: chaine
    //     valeur: chaine
    // }

    entêtes : []Entête
    cookies : []Cookie
}

ajoute_entête :: fonc (résultat: &RésultatRequête, nom: chaine, valeur: chaine)
{
    entête := Entête(nom = nom, valeur = valeur)
    tableau_ajoute(*résultat.entêtes, entête)
}

ajoute_cookie :: fonc (résultat: &RésultatRequête, cookie: Cookie)
{
    tableau_ajoute(*résultat.cookies, cookie)
}

détruit_résultat :: fonc (résultat: &RésultatRequête)
{
    déloge(résultat.entêtes)
}

// La routeuse.

Route :: struct ($T: type_de_données) {
    chemin: chaine
    règles_routage: []RègleRoutage

    méthode_get : fonc(*Requête, *T)(RésultatRequête)
    méthode_post : fonc(*Requête, *T)(RésultatRequête)
}

Routeuse :: struct ($T: type_de_données) {
    routes : []Route(T = T)

    données_serveuse: *T

    rappel_page_non_trouvée: fonc(*Requête, *T)(RésultatRequête)
    rappel_méthode_non_autorisée: fonc(*Requête, *T)(RésultatRequête)
}

détruit_routeuse :: fonc (routeuse: *Routeuse(T = $T)) -> rien
{
    déloge(routeuse.routes)
}

ajoute_route :: fonc (routeuse: *Routeuse(T = $T), chemin: chaine, méthode_get: fonc(*Requête, *T)(RésultatRequête), méthode_post: fonc(*Requête,*T)(RésultatRequête)) -> rien
{
    route : Route(T = T)
    route.chemin = chemin
    route.méthode_get = méthode_get
    route.méthode_post = méthode_post

    route.règles_routage = tente parse_règles(chemin) piège err {
        retourne
    }

    tableau_ajoute(*routeuse.routes, route)
}

apparie_route :: fonc (routeuse: *Routeuse(T = $T), requête: *Requête) -> RésultatRequête
{
    pour routeuse.routes {
        données := tente apparie_règles_et_route(it.règles_routage, requête.uri.chemin) piège err {
            continue
        }

        si requête.méthode == "GET" {
            si it.méthode_get {
                retourne it.méthode_get(requête, routeuse.données_serveuse)
            }

            si routeuse.rappel_méthode_non_autorisée {
                retourne routeuse.rappel_méthode_non_autorisée(requête, routeuse.données_serveuse)
            }

            retourne méhode_non_autorisée()
        }

        si requête.méthode == "POST" {
            si it.méthode_post {
                retourne it.méthode_post(requête, routeuse.données_serveuse)
            }

            si routeuse.rappel_méthode_non_autorisée {
                retourne routeuse.rappel_méthode_non_autorisée(requête, routeuse.données_serveuse)
            }

            retourne méhode_non_autorisée()
        }

        si routeuse.rappel_méthode_non_autorisée {
            retourne routeuse.rappel_méthode_non_autorisée(requête, routeuse.données_serveuse)
        }
    }
    sansarrêt {
        si routeuse.rappel_page_non_trouvée {
            retourne routeuse.rappel_page_non_trouvée(requête, routeuse.données_serveuse)
        }
    }

    retourne page_non_trouvée()
}

page_non_trouvée :: fonc () -> RésultatRequête
{
    retourne RésultatRequête(status = CodeÉtatHTTP.NOT_FOUND)
}

méhode_non_autorisée :: fonc () -> RésultatRequête
{
    retourne RésultatRequête(status = CodeÉtatHTTP.METHOD_NOT_ALLOWED)
}

redirige :: fonc (chemin: chaine) -> RésultatRequête
{
    résultat := RésultatRequête(status = CodeÉtatHTTP.SEE_OTHER)
    résultat.ajoute_entête("Location", chemin)
    retourne résultat
}
