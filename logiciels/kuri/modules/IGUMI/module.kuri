importe Compilatrice
importe Chaine
importe Couleur
importe FTGL
importe Fondation
importe Image
importe Géométrie
importe Math
importe OpenGL
importe SysFichier
importe Typographie

/* ------------------------------------------------------------------------- */
/** \nom Initialisation et issitialisation.
 * \{ */

initialise_igumi :: fonc ()
{
    chemins := #exécute donne_chemins_nuanceurs()

    nuanceur_texte = crée_nuanceur_depuis_fichier(chemins.chemin_vertex_texte, chemins.chemin_fragment_texte)
    nuanceur_texte.nom = "texte"
    nuanceur_base = crée_nuanceur_depuis_fichier(chemins.chemin_vertex_base, chemins.chemin_fragment_base)
    nuanceur_base.nom = "base"
    nuanceur_image = crée_nuanceur_depuis_fichier(chemins.chemin_vertex_image, chemins.chemin_fragment_image)
    nuanceur_image.nom = "image"
    nuanceur_id = crée_nuanceur_depuis_fichier(chemins.chemin_vertex_id, chemins.chemin_fragment_id)
    nuanceur_id.nom = "id"

    chemin_fonte := CheminFichier("/usr/share/fonts/truetype/dejavu/DejaVuSansMono.ttf")
    fonte_globale = crée_fonte(chemin_fonte, 14.0)

    glGenVertexArrays(1, *va_immédiat)
    glGenBuffers(1, *vbo_immédiat)
    glGenBuffers(1, *vbo_immédiat_index)
    vérifie_erreur("création des VBO immédiats")

    glGenVertexArrays(1, *données_tampon_id.va_immédiat)
    glGenBuffers(1, *données_tampon_id.vbo_immédiat)
    glGenBuffers(1, *données_tampon_id.vbo_immédiat_index)
    vérifie_erreur("création des VBO immédiats (ID)")
}

issitialise_igumi :: fonc ()
{
    glDeleteVertexArrays(1, *va_immédiat)
    glDeleteBuffers(1, *vbo_immédiat)
    glDeleteBuffers(1, *vbo_immédiat_index)

    glDeleteVertexArrays(1, *données_tampon_id.va_immédiat)
    glDeleteBuffers(1, *données_tampon_id.vbo_immédiat)
    glDeleteBuffers(1, *données_tampon_id.vbo_immédiat_index)

    détruit_fonte(fonte_globale)

    détruit_nuanceur(nuanceur_base)
    détruit_nuanceur(nuanceur_texte)
    détruit_nuanceur(nuanceur_image)
    détruit_nuanceur(nuanceur_id)
}

définis_fenêtre :: fonc (largeur: z32, hauteur: z32)
{
    largeur_cannevas = largeur
    hauteur_cannevas = hauteur
    projection = crée_matrice_projection_orthographique(0, largeur comme r32, 0, hauteur comme r32, -1.0, 1.0)
}

donne_largeur_cannevas :: fonc () -> z32
{
    retourne largeur_cannevas
}

donne_hauteur_cannevas :: fonc () -> z32
{
    retourne hauteur_cannevas
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Nuanceur.
 * \{ */

NuanceurGL :: struct {
    nom: chaine
    programme: n32

    /* stocke les locations pour les attributes pour les nuanceurs de bases */
    loc_position : z32 = -1
    loc_normal : z32 = -1
    loc_couleur : z32 = -1
    loc_uv : z32 = -1
    loc_atlas : z32 = -1
    loc_projection : z32 = -1
    adresse_image : z32 = -1
    loc_id : z32 = -1
}

crée_nuanceur :: fonc (source_vertex: chaine, source_fragment: chaine) -> *NuanceurGL
{
    nuanceur := loge(NuanceurGL)
    recharge_nuanceur(nuanceur, source_vertex, source_fragment)
    retourne nuanceur
}

crée_nuanceur_depuis_fichier :: fonc (source_vertex: chaine, source_fragment: chaine) -> *NuanceurGL
{
    contenu_source_vertex := contenu_fichier_texte(source_vertex)
    diffère déloge(contenu_source_vertex)
    contenu_source_fragment := contenu_fichier_texte(source_fragment)
    diffère déloge(contenu_source_fragment)

    nuanceur := crée_nuanceur(contenu_source_vertex, contenu_source_fragment)

    glUseProgram(nuanceur.programme)

    nuanceur.loc_position = donne_adresse_attribut(nuanceur.programme, "position")
    nuanceur.loc_couleur = donne_adresse_attribut(nuanceur.programme, "couleur")
    nuanceur.loc_uv = donne_adresse_attribut(nuanceur.programme, "uv")
    nuanceur.loc_normal = donne_adresse_attribut(nuanceur.programme, "normal")
    nuanceur.loc_atlas = donne_adresse_uniforme(nuanceur.programme, "atlas")
    nuanceur.loc_projection = donne_adresse_uniforme(nuanceur.programme, "projection")
    nuanceur.adresse_image = donne_adresse_uniforme(nuanceur.programme, "image")
    nuanceur.loc_id = donne_adresse_attribut(nuanceur.programme, "id")

    glUseProgram(0)

    retourne nuanceur
}

recharge_nuanceur :: fonc (nuanceur: *NuanceurGL, source_vertex: chaine, source_fragment: chaine)
{
    détruit_données_nuanceur(nuanceur)
    opt_programme := compile_un_programme(source_vertex, source_fragment)
    saufsi opt_programme.possède_valeur() {
        retourne
    }
    nuanceur.programme = opt_programme.Quelque
}

détruit_données_nuanceur :: fonc (nuanceur: *NuanceurGL)
{
    détruit_programme(nuanceur.programme)
}

détruit_nuanceur :: fonc (nuanceur: *NuanceurGL)
{
    détruit_programme(nuanceur.programme)
    déloge(nuanceur)
}

CheminsNuanceurs :: struct {
    chemin_vertex_texte: chaine
    chemin_fragment_texte: chaine
    chemin_vertex_base: chaine
    chemin_fragment_base: chaine
    chemin_vertex_image: chaine
    chemin_fragment_image: chaine
    chemin_vertex_id: chaine
    chemin_fragment_id: chaine
}

donne_chemins_nuanceurs :: fonc () -> CheminsNuanceurs
{
    module := compilatrice_module_courant()
    chemin_module := CheminFichier(compilatrice_chemin_module(module))

    résultat: CheminsNuanceurs
    résultat.chemin_vertex_texte = (chemin_module / "nuanceurs/nuanceur_texte_vertex.glsl").chn
    résultat.chemin_fragment_texte = (chemin_module / "nuanceurs/nuanceur_texte_fragment.glsl").chn
    résultat.chemin_vertex_base = (chemin_module / "nuanceurs/nuanceur_base_vertex.glsl").chn
    résultat.chemin_fragment_base = (chemin_module / "nuanceurs/nuanceur_base_fragment.glsl").chn
    résultat.chemin_vertex_image = (chemin_module / "nuanceurs/nuanceur_image_vertex.glsl").chn
    résultat.chemin_fragment_image = (chemin_module / "nuanceurs/nuanceur_image_fragment.glsl").chn
    résultat.chemin_vertex_id = (chemin_module / "nuanceurs/nuanceur_id_vertex.glsl").chn
    résultat.chemin_fragment_id = (chemin_module / "nuanceurs/nuanceur_id_fragment.glsl").chn
    retourne résultat
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Données globales.
 * \{ */

FormatVertexXCNU :: struct {
    position: Vec3
    couleur: CouleurRVBA
    normal: Vec3
    uv: Vec2
}

DÉCALAGE_SOMMETS :: 0
DÉCALAGE_COULEUR :: 12
DÉCALAGE_NORMAL :: 28
DÉCALAGE_UV :: 40

projection: Mat4r
largeur_cannevas: z32
hauteur_cannevas: z32

MAX_SOMMETS_IMMÉDIAT :: 2400
sommets_immédiat : [MAX_SOMMETS_IMMÉDIAT]FormatVertexXCNU = ---
num_sommets_immédiat : z32 = 0
va_immédiat: n32
vbo_immédiat: n32
vbo_immédiat_index: n32
données_tampon_initialisées: bool

nuanceur_global: *NuanceurGL
nuanceur_base: *NuanceurGL
nuanceur_texte: *NuanceurGL
nuanceur_image: *NuanceurGL
nuanceur_id: *NuanceurGL

fonte_globale: *Fonte

FormatVertexXId :: struct {
    position: Vec3
    id: n32
}

DÉCALAGE_ID :: 12

DonnéesImmédiatesXId :: struct {
    sommets : [MAX_SOMMETS_IMMÉDIAT]FormatVertexXId = ---
    num_sommets : z32 = 0

    va_immédiat: n32
    vbo_immédiat: n32
    vbo_immédiat_index: n32
    données_tampon_initialisées: bool
}

données_tampon_id: DonnéesImmédiatesXId

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Dessin.
 * \{ */

dernier_mode: ModeImmédiat
dernier_nuanceur: *NuanceurGL
dernière_fonte: *Fonte

commence_immédiat :: fonc (mode: ModeImmédiat, nuanceur: *NuanceurGL, fonte : *Fonte = nul)
{
    si mode != dernier_mode || nuanceur != dernier_nuanceur || fonte != dernière_fonte || num_sommets_immédiat == MAX_SOMMETS_IMMÉDIAT {
        termine_immédiat()

        dernier_mode = mode
        dernier_nuanceur = nuanceur
        dernière_fonte = fonte
        utilise_nuanceur(nuanceur)
    }
}

ModeImmédiat :: énum {
    TRIANGLES
    LIGNES
}

termine_immédiat :: fonc ()
{
    si !num_sommets_immédiat {
        retourne
    }

    nuanceur := nuanceur_global

    si !nuanceur {
        si num_sommets_immédiat {
            panique("tentative d'utiliser termine_immédiat alors qu'aucun nuanceur n'est installé")
        }

        num_sommets_immédiat = 0
        retourne
    }

    s : []FormatVertexXCNU = sommets_immédiat
    compte := num_sommets_immédiat comme n64

    glUseProgram(nuanceur.programme)
    vérifie_erreur("termine_immédiat.glUseProgram")

    glBindVertexArray(va_immédiat)

    glBindBuffer(GL_ARRAY_BUFFER, vbo_immédiat)
    vérifie_erreur("termine_immédiat.glBindBuffer")

    définis_uniforme(nuanceur.loc_projection, projection)

    installe_format_xcnu(nuanceur)

    saufsi données_tampon_initialisées {
        glBufferData(GL_ARRAY_BUFFER, (MAX_SOMMETS_IMMÉDIAT comme n32) * taille_de(FormatVertexXCNU), s.pointeur, GL_DYNAMIC_DRAW)
        vérifie_erreur("termine_immédiat.glBufferData")
        données_tampon_initialisées = vrai
    }

    glBufferSubData(GL_ARRAY_BUFFER, 0, compte * taille_de(FormatVertexXCNU), s.pointeur)
    vérifie_erreur("termine_immédiat_id.glBufferSubData")

    si dernier_mode == ModeImmédiat.TRIANGLES {
        glDrawArrays(GL_TRIANGLES, 0, compte comme z64)
    }
    sinon {
        glDrawArrays(GL_LINES, 0, compte comme z64)
    }

    // nettoyage
    glUseProgram(0)
    glBindVertexArray(0)
    num_sommets_immédiat = 0

    vérifie_erreur("termine_immédiat")
}

utilise_nuanceur :: fonc (nuanceur: *NuanceurGL)
{
    nuanceur_global = nuanceur
}

installe_format_xcnu :: fonc (nuanceur: *NuanceurGL)
{
    stride := taille_de(FormatVertexXCNU)

    si nuanceur.loc_position != -1 {
        glVertexAttribPointer(nuanceur.loc_position comme GLuint, 3, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_SOMMETS comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_position")
        glEnableVertexAttribArray(nuanceur.loc_position comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_position")
    }

    si nuanceur.loc_couleur != -1 {
        glVertexAttribPointer(nuanceur.loc_couleur comme GLuint, 4, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_COULEUR comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_couleur")
        glEnableVertexAttribArray(nuanceur.loc_couleur comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_couleur")
    }

    si nuanceur.loc_normal != -1 {
        glEnableVertexAttribArray(nuanceur.loc_normal comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_normal")
        glVertexAttribPointer(nuanceur.loc_normal comme GLuint, 3, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_NORMAL comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_normal")
    }

    si nuanceur.loc_uv != -1 {
        glEnableVertexAttribArray(nuanceur.loc_uv comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_uv")
        glVertexAttribPointer(nuanceur.loc_uv comme GLuint, 2, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_UV comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_uv")
    }
}

ajoute_sommets :: fonc (pos: Vec3, couleur: CouleurRVBA)
{
    si num_sommets_immédiat >= MAX_SOMMETS_IMMÉDIAT {
        termine_immédiat()
    }

    sommets_immédiat[num_sommets_immédiat].position = pos
    sommets_immédiat[num_sommets_immédiat].couleur = couleur
    num_sommets_immédiat += 1
}

ajoute_sommets :: fonc (pos: Vec3, uv: Vec2, couleur: CouleurRVBA)
{
    si num_sommets_immédiat >= MAX_SOMMETS_IMMÉDIAT {
        termine_immédiat()
    }

    sommets_immédiat[num_sommets_immédiat].position = pos
    sommets_immédiat[num_sommets_immédiat].couleur = couleur
    sommets_immédiat[num_sommets_immédiat].uv = uv
    num_sommets_immédiat += 1
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Quad.
 * \{ */

quad_immédiat :: fonc (x: r32, y: r32, largeur: r32, hauteur: r32, couleur: CouleurRVBA)
{
    p0 := Vec3(x = x, y = y)
    p1 := Vec3(x = x + largeur, y = y)
    p2 := Vec3(x = x + largeur, y = y + hauteur)
    p3 := Vec3(x = x, y = y + hauteur)

    quad_immédiat(p0, p1, p2, p3, couleur)
}

quad_immédiat :: fonc (p0: Vec3, p1: Vec3, p2: Vec3, p3: Vec3, couleur: CouleurRVBA)
{
    ajoute_sommets(p0, couleur)
    ajoute_sommets(p1, couleur)
    ajoute_sommets(p2, couleur)

    ajoute_sommets(p0, couleur)
    ajoute_sommets(p2, couleur)
    ajoute_sommets(p3, couleur)
}

quad_immédiat :: fonc (p0: Vec3, p1: Vec3, p2: Vec3, p3: Vec3, uv0: Vec2, uv1: Vec2, uv2: Vec2, uv3: Vec2, couleur: CouleurRVBA)
{
    ajoute_sommets(p0, uv0, couleur)
    ajoute_sommets(p1, uv1, couleur)
    ajoute_sommets(p2, uv2, couleur)

    ajoute_sommets(p0, uv0, couleur)
    ajoute_sommets(p2, uv2, couleur)
    ajoute_sommets(p3, uv3, couleur)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Segment/Ligne.
 * \{ */

segment_immédiat :: fonc (x0: r32, y0: r32, x1: r32, y1: r32, couleur: CouleurRVBA)
{
    p0 := Vec3(x0, y0)
    p1 := Vec3(x1, y1)

    ajoute_sommets(p0, couleur)
    ajoute_sommets(p1, couleur)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Cercle.
 * \{ */

cercle_immédiat :: fonc (cx: r32, cy: r32, r: r32, couleur: CouleurRVBA, ajoute_centre: bool)
{
    segs := 32
    phi := 0.0
    cercle_immédiat_impl(cx, cy, r, segs, phi, couleur, ajoute_centre)
}

quadrant_cercle_immédiat :: fonc (cx: r32, cy: r32, r: r32, quadrant: z32, couleur: CouleurRVBA, ajoute_centre: bool)
{
    segs := 8
    phi := quadrant comme r32 * (Constantes(r32).PI / 2.0)
    cercle_immédiat_impl(cx, cy, r, segs, phi, couleur, ajoute_centre)
}

cercle_immédiat_impl :: fonc (cx: r32, cy: r32, r: r32, segs: z32, phi: r32, couleur: CouleurRVBA, ajoute_centre: bool)
{
    p0 := Vec3(cx, cy, 0.0)
    phi_delta := Constantes(r32).TAU / 32.0

    pour segs {
        ca0 := cx + cosinus(phi) * r
        sa0 := cy + sinus(phi) * r

        phi += phi_delta

        ca1 := cx + cosinus(phi) * r
        sa1 := cy + sinus(phi) * r

        p1 := Vec3(ca0, sa0, 0.0)
        p2 := Vec3(ca1, sa1, 0.0)

        si ajoute_centre {
            ajoute_sommets(p0, couleur)
        }

        ajoute_sommets(p1, couleur)
        ajoute_sommets(p2, couleur)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Quad arrondi.
 * \{ */

CoinRectangleArrondi :: énum {
    HautGauche
    HautDroite
    BasDroite
    BasGauche
}

quad_arrondi_immédiat :: fonc (pos_x: r32, pos_y: r32, largeur: r32, hauteur: r32, rayons: [4]r32, couleur: CouleurRVBA)
{
    /* Génération de point créer les différents rectangles à peindre.
     * Si le rayon correspondant à un coin est égal à zéro, un rectangle
     * sera dessiné, sinon, un quadrant de cercle.
     * 
     * Les index des points pour la grille sont :
     *
     * 12 --- 13 --- 14 --- 15
     *  |      |      |      |
     *  8 ---  9 --- 10 --- 11
     *  |      |      |      |
     *  4 ---  5 ---  6 ---  7
     *  |      |      |      |
     *  0 ---  1 ---  2 ---  3
     *   
     * Les points sont alors les couples (x,y) suivants :
     *
     *  4,3 --- 5,3 --- 6,7 --- 7,7
     *   |       |       |       |
     *  4,2 --- 5,2 --- 6,6 --- 7,6
     *   |       |       |       |
     *  0,1 --- 1,1 --- 2,5 --- 3,5
     *   |       |       |       |
     *  0,0 --- 1,0 --- 2,4 --- 3,4
     */

    points_x: [8]r32 = ---
    points_y: [8]r32 = ---

    /* Coordonnées x pour les deux lignes du bas. */
    points_x[0] = pos_x
    points_x[1] = pos_x + rayons[CoinRectangleArrondi.BasGauche]
    points_x[2] = pos_x + largeur - rayons[CoinRectangleArrondi.BasDroite]
    points_x[3] = pos_x + largeur

    /* Coordonnées x pour les deux lignes du haut */
    points_x[4] = pos_x
    points_x[5] = pos_x + rayons[CoinRectangleArrondi.HautGauche]
    points_x[6] = pos_x + largeur - rayons[CoinRectangleArrondi.HautDroite]
    points_x[7] = pos_x + largeur

    /* Coordonnées y pour les deux colonnes de gauche. */
    points_y[0] = pos_y
    points_y[1] = pos_y + rayons[CoinRectangleArrondi.BasGauche]
    points_y[2] = pos_y + hauteur - rayons[CoinRectangleArrondi.HautGauche]
    points_y[3] = pos_y + hauteur

    /* Coordonnées y pour les deux colonnes de droite. */
    points_y[4] = pos_y
    points_y[5] = pos_y + rayons[CoinRectangleArrondi.BasDroite]
    points_y[6] = pos_y + hauteur - rayons[CoinRectangleArrondi.HautDroite]
    points_y[7] = pos_y + hauteur

    points: [16]Vec3 = ---

    /* Ligne du bas */
    points[0] = Vec3(x = points_x[0], y = points_y[0])
    points[1] = Vec3(x = points_x[1], y = points_y[0])
    points[2] = Vec3(x = points_x[2], y = points_y[4])
    points[3] = Vec3(x = points_x[3], y = points_y[4])

    points[4] = Vec3(x = points_x[0], y = points_y[1])
    points[5] = Vec3(x = points_x[1], y = points_y[1])
    points[6] = Vec3(x = points_x[2], y = points_y[5])
    points[7] = Vec3(x = points_x[3], y = points_y[5])

    /* Ligne du haut */
    points[8] = Vec3(x = points_x[4], y = points_y[2])
    points[9] = Vec3(x = points_x[5], y = points_y[2])
    points[10] = Vec3(x = points_x[6], y = points_y[6])
    points[11] = Vec3(x = points_x[7], y = points_y[6])

    points[12] = Vec3(x = points_x[4], y = points_y[3])
    points[13] = Vec3(x = points_x[5], y = points_y[3])
    points[14] = Vec3(x = points_x[6], y = points_y[7])
    points[15] = Vec3(x = points_x[7], y = points_y[7])

    /* Peinture des rectangles et quandrants de cercle. */

    /* Centre. */
    quad_immédiat(points[5], points[6], points[10], points[9], couleur)
    /* Rectangle gauche. */
    quad_immédiat(points[4], points[5], points[9], points[8], couleur)
    /* Rectangle droite. */
    quad_immédiat(points[6], points[7], points[11], points[10], couleur)
    /* Rectangle haut. */
    quad_immédiat(points[9], points[10], points[14], points[13], couleur)
    /* Rectangle bas. */
    quad_immédiat(points[1], points[2], points[6], points[5], couleur)

    si rayons[CoinRectangleArrondi.HautGauche] > 0.0 {
        quadrant_cercle_immédiat(points[9].x, points[9].y, rayons[CoinRectangleArrondi.HautGauche], 1, couleur, vrai)
    }
    sinon {
        quad_immédiat(points[8], points[9], points[13], points[12], couleur)
    }

    si rayons[CoinRectangleArrondi.HautDroite] > 0.0 {
        quadrant_cercle_immédiat(points[10].x, points[10].y, rayons[CoinRectangleArrondi.HautDroite], 0, couleur, vrai)
    }
    sinon {
        quad_immédiat(points[10], points[11], points[15], points[14], couleur)
    }

    si rayons[CoinRectangleArrondi.BasGauche] > 0.0 {
        quadrant_cercle_immédiat(points[5].x, points[5].y, rayons[CoinRectangleArrondi.BasGauche], 2, couleur, vrai)
    }
    sinon {
        quad_immédiat(points[0], points[1], points[5], points[4], couleur)
    }

    si rayons[CoinRectangleArrondi.BasDroite] > 0.0 {
        quadrant_cercle_immédiat(points[6].x, points[6].y, rayons[CoinRectangleArrondi.BasDroite], 3, couleur, vrai)
    }
    sinon {
        quad_immédiat(points[2], points[3], points[7], points[6], couleur)
    }
}

contour_quad_arrondi_immédiat :: fonc (pos_x: r32, pos_y: r32, largeur: r32, hauteur: r32, rayon: r32, couleur: CouleurRVBA)
{
    px := pos_x + rayon
    py := pos_y + rayon
    tx := largeur - 2.0 * rayon
    ty := hauteur - 2.0 * rayon

    /* Haut. */
    segment_immédiat(px, pos_y, px + tx, pos_y, couleur)
    /* Bas. */
    segment_immédiat(px, pos_y + hauteur, px + tx, pos_y + hauteur, couleur)
    /* Gauche. */
    segment_immédiat(pos_x, py, pos_x, py + ty, couleur)
    /* Droite. */
    segment_immédiat(pos_x + largeur, py, pos_x + largeur, py + ty, couleur)

    /* Haut gauche. */
    quadrant_cercle_immédiat(px, py + ty, rayon, 1, couleur, faux)
    /* Haut droite. */
    quadrant_cercle_immédiat(px + tx, py + ty, rayon, 0, couleur, faux)
    /* Bas gauche. */
    quadrant_cercle_immédiat(px, py, rayon, 2, couleur, faux)
    /* Bas droite. */
    quadrant_cercle_immédiat(px + tx, py, rayon, 3, couleur, faux)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Texte.
 * \{ */

donne_paramètres_texture_atlas_fonte :: fonc (atlas: *texture_atlas_t) -> ParamètresTexture2D
{
    résultat: ParamètresTexture2D
    résultat.largeur = atlas.width comme z32
    résultat.hauteur = atlas.height comme z32
    résultat.données = atlas.data
    résultat.bordure = 0
    résultat.type = GL_UNSIGNED_BYTE
    résultat.format = GL_RED
    résultat.wrap_s = GL_CLAMP_TO_EDGE
    résultat.wrap_t = GL_CLAMP_TO_EDGE
    retourne résultat
}

/*
    EffetTexte :: énum {
        RIEN
    }

    dessine_texte :: fonc (fonte: &Fonte, x: r32, y: r32, texte: chaine, couleur: Couleur, effet: EffetTexte)
*/
dessine_texte :: fonc (fonte: *Fonte, texte: chaine, x: r32, y: r32, couleur: CouleurRVBA)
{
    commence_immédiat(ModeImmédiat.TRIANGLES, nuanceur_texte, fonte)

    // ancienne_fonction_mélange: z32
    // glGetIntegerv(GL_BLEND_SRC_ALPHA, *ancienne_fonction_mélange)
    // diffère glBlendFunc(GL_SRC_ALPHA, ancienne_fonction_mélange comme n32)
    glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)

    // gl_blend_est_actif := glIsEnabled(GL_BLEND) != 0
    // diffère saufsi gl_blend_est_actif { glDisable(GL_BLEND) }
    glEnable(GL_BLEND)

    // met en place la texture de la fonte sur le nuanceur
    glGenTextures(1, *fonte.atlas.id)
    diffère glDeleteTextures(1, *fonte.atlas.id)

    params := donne_paramètres_texture_atlas_fonte(fonte.atlas)
    définis_texture_2d(fonte.atlas.id, *params)

    glUseProgram(nuanceur_texte.programme)

    glBindTexture(GL_TEXTURE_2D, fonte.atlas.id)

    définis_uniforme(nuanceur_texte.loc_atlas, 0 comme z32)

    dernier_caractère : *texture_glyph_t = nul
    pour * c dans texte {
        glyph := fonte.donne_glyphe(c)

        si glyph == nul {
            continue
        }

        kerning := glyph.donne_crénage_avec(dernier_caractère)
        x += kerning

        glyphe_immédiat(glyph, x, y, couleur)

        x += glyph.advance_x

        dernier_caractère = glyph
    }
}

dessine_texte :: fonc (fonte: *Fonte, texte: ChaineUTF16, x: r32, y: r32, couleur: CouleurRVBA, effets: *EffetsTexte)
{
    commence_immédiat(ModeImmédiat.TRIANGLES, nuanceur_texte, fonte)

    // ancienne_fonction_mélange: z32
    // glGetIntegerv(GL_BLEND_SRC_ALPHA, *ancienne_fonction_mélange)
    // diffère glBlendFunc(GL_SRC_ALPHA, ancienne_fonction_mélange comme n32)
    glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)

    // gl_blend_est_actif := glIsEnabled(GL_BLEND) != 0
    // diffère saufsi gl_blend_est_actif { glDisable(GL_BLEND) }
    glEnable(GL_BLEND)

    // met en place la texture de la fonte sur le nuanceur
    saufsi glIsTexture(fonte.atlas.id) {
        glGenTextures(1, *fonte.atlas.id)
        // À FAIRE : libère la texture quand la police n'est plus requise
        // diffère glDeleteTextures(1, *fonte.atlas.id)

        params := donne_paramètres_texture_atlas_fonte(fonte.atlas)
        définis_texture_2d(fonte.atlas.id, *params)
    }

    glUseProgram(nuanceur_texte.programme)

    glBindTexture(GL_TEXTURE_2D, fonte.atlas.id)

    définis_uniforme(nuanceur_texte.loc_atlas, 0 comme z32)

    caractère_nul := fonte.donne_glyphe_nul()

    dernier_caractère : *texture_glyph_t = nul
    pour texte.unités {
        glyph := fonte.donne_glyphe(it)

        si glyph == nul {
            continue
        }

        kerning := glyph.donne_crénage_avec(dernier_caractère)
        x += kerning

        glyphe_immédiat(glyph, x, y, couleur)

        si effets {
            si effets.surlignage {
                surlignage_glyphe_immédiat(fonte, glyph, caractère_nul, x - kerning, y, effets.couleur_surlignage)
            }
            si effets.souslignage {
                souslignage_glyphe_immédiat(fonte, glyph, caractère_nul, x - kerning, y, effets.couleur_souslignage)
            }
            si effets.barrage {
                barrage_glyphe_immédiat(fonte, glyph, caractère_nul, x - kerning, y, effets.couleur_barrage)
            }
        }

        x += glyph.advance_x

        dernier_caractère = glyph
    }
}

glyphe_immédiat :: fonc (glyph: *texture_glyph_t, x: r32, y: r32, couleur: CouleurRVBA)
{
    /* Arrondis les valeurs pour éviter d'interpoler ou rendre des données nulles (l'espace entre les glyphes). */
    x0 := plancher(x + glyph.offset_x comme r32)
    y0 := plancher(y + glyph.offset_y comme r32)
    x1 := plancher(x0 + glyph.width comme r32)
    y1 := plancher(y0 - glyph.height comme r32)
    s0 := glyph.s0
    t0 := glyph.t0
    s1 := glyph.s1
    t1 := glyph.t1

    p0 := Vec3(x = x0, y = y0)
    p1 := Vec3(x = x0, y = y1)
    p2 := Vec3(x = x1, y = y1)
    p3 := Vec3(x = x1, y = y0)

    uv0 := Vec2(x = s0, y = t0)
    uv1 := Vec2(x = s0, y = t1)
    uv2 := Vec2(x = s1, y = t1)
    uv3 := Vec2(x = s1, y = t0)

    quad_immédiat(p0, p1, p2, p3, uv0, uv1, uv2, uv3, couleur)
}

souslignage_glyphe_immédiat :: fonc (fonte: *Fonte, glyph: *texture_glyph_t, caractère_nul: *texture_glyph_t, x: r32, y: r32, couleur: CouleurRVBA)
{
    ligne_glyphe_immédiate(fonte, glyph, caractère_nul, x, y + fonte.font.underline_position, couleur)
}

surlignage_glyphe_immédiat :: fonc (fonte: *Fonte, glyph: *texture_glyph_t, caractère_nul: *texture_glyph_t, x: r32, y: r32, couleur: CouleurRVBA)
{
    ligne_glyphe_immédiate(fonte, glyph, caractère_nul, x, y + fonte.font.ascender, couleur)
}

barrage_glyphe_immédiat :: fonc (fonte: *Fonte, glyph: *texture_glyph_t, caractère_nul: *texture_glyph_t, x: r32, y: r32, couleur: CouleurRVBA)
{
    ligne_glyphe_immédiate(fonte, glyph, caractère_nul, x, y + fonte.font.ascender * 0.33, couleur)
}

ligne_glyphe_immédiate :: fonc (fonte: *Fonte, glyph: *texture_glyph_t, caractère_nul: *texture_glyph_t, x: r32, y: r32, couleur: CouleurRVBA)
{
    x0 := x
    y0 := y
    x1 := x0 + glyph.advance_x
    y1 := y0 - fonte.font.underline_thickness
    s0 := caractère_nul.s0
    t0 := caractère_nul.t0
    s1 := caractère_nul.s1
    t1 := caractère_nul.t1

    p0 := Vec3(x = x0, y = y0)
    p1 := Vec3(x = x0, y = y1)
    p2 := Vec3(x = x1, y = y1)
    p3 := Vec3(x = x1, y = y0)

    uv0 := Vec2(x = s0, y = t0)
    uv1 := Vec2(x = s0, y = t1)
    uv2 := Vec2(x = s1, y = t1)
    uv3 := Vec2(x = s1, y = t0)

    quad_immédiat(p0, p1, p2, p3, uv0, uv1, uv2, uv3, couleur)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Image
 * \{ */

image_immédiat :: fonc (image: ImageIO, rect_image: RectanglePosDim(r32), rect: RectanglePosDim(r32), répétition: RépétitionImage)
{
    params := donne_paramètres_texture_pour_image(image, répétition)
    image_immédiat(*params, rect_image, rect)
}

image_immédiat :: fonc (image: TamponImageGrise8Bit, rect_image: RectanglePosDim(r32), rect: RectanglePosDim(r32), répétition: RépétitionImage)
{
    /* À FAIRE : dessine en noir et blanc. */
    params := donne_paramètres_texture_pour_image(image, répétition)
    image_immédiat(*params, rect_image, rect)
}

image_immédiat :: fonc (image: TamponImageRVBA8Bit, rect_image: RectanglePosDim(r32), rect: RectanglePosDim(r32), répétition: RépétitionImage)
{
    params := donne_paramètres_texture_pour_image(image, répétition)
    image_immédiat(*params, rect_image, rect)
}

image_immédiat :: fonc (image: TamponImageRVBA32Bit, rect_image: RectanglePosDim(r32), rect: RectanglePosDim(r32), répétition: RépétitionImage)
{
    params := donne_paramètres_texture_pour_image(image, répétition)
    image_immédiat(*params, rect_image, rect)
}

image_immédiat :: fonc (params: *ParamètresTexture2D, rect_image: RectanglePosDim(r32), rect: RectanglePosDim(r32))
{
    commence_immédiat(ModeImmédiat.TRIANGLES, nuanceur_image)

    // ancienne_fonction_mélange: z32
    // glGetIntegerv(GL_BLEND_SRC_ALPHA, *ancienne_fonction_mélange)
    // diffère glBlendFunc(GL_SRC_ALPHA, ancienne_fonction_mélange comme n32)
    glBlendFunc(GL_SRC_ALPHA, GL_ONE_MINUS_SRC_ALPHA)

    // gl_blend_est_actif := glIsEnabled(GL_BLEND) != 0
    // diffère saufsi gl_blend_est_actif { glDisable(GL_BLEND) }
    glEnable(GL_BLEND)

    code_texture: n32
    glGenTextures(1, *code_texture)
    diffère glDeleteTextures(1, *code_texture)
    définis_texture_2d(code_texture, params)

    glUseProgram(nuanceur_image.programme)
    diffère glUseProgram(0)

    glBindTexture(GL_TEXTURE_2D, code_texture)
    définis_uniforme(nuanceur_image.adresse_image, 0 comme z32)

    x := rect.x
    y := rect.y
    largeur := rect.largeur
    hauteur := rect.hauteur

    p0 := Vec3(x = x, y = y)
    p1 := Vec3(x = x + largeur, y = y)
    p2 := Vec3(x = x + largeur, y = y + hauteur)
    p3 := Vec3(x = x, y = y + hauteur)

    x_min_image := rect_image.x
    y_min_image := rect_image.y

    x_min_rect := rect.x
    x_max_rect := rect.x + rect.largeur
    y_min_rect := rect.y
    y_max_rect := rect.y + rect.hauteur

    ux_min := (x_min_rect - x_min_image) / rect_image.largeur
    ux_max := (x_max_rect - x_min_image) / rect_image.largeur

    uy_min := (y_min_rect - y_min_image) / rect_image.hauteur
    uy_max := (y_max_rect - y_min_image) / rect_image.hauteur

    u0 := Vec2(ux_min, uy_min)
    u1 := Vec2(ux_max, uy_min)
    u2 := Vec2(ux_max, uy_max)
    u3 := Vec2(ux_min, uy_max)

    quad_immédiat(p0, p1, p2, p3, u0, u1, u2, u3, CouleurRVBA())

    termine_immédiat()
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Tampon ID.
 * \{ */

dernier_mode_id: ModeImmédiat
dernier_nuanceur_id: *NuanceurGL

commence_immédiat_id :: fonc (mode: ModeImmédiat, nuanceur: *NuanceurGL)
{
    si mode != dernier_mode_id || nuanceur != dernier_nuanceur_id || données_tampon_id.num_sommets == MAX_SOMMETS_IMMÉDIAT {
        dernier_mode_id = mode
        dernier_nuanceur_id = nuanceur
        utilise_nuanceur(nuanceur)

        termine_immédiat_id()
    }
}

termine_immédiat_id :: fonc ()
{
    saufsi données_tampon_id.num_sommets {
        retourne
    }

    nuanceur := dernier_nuanceur_id

    saufsi nuanceur {
        si données_tampon_id.num_sommets {
            panique("tentative d'utiliser termine_immédiat alors qu'aucun nuanceur n'est installé")
        }

        données_tampon_id.num_sommets = 0
        retourne
    }

    s : []FormatVertexXId = données_tampon_id.sommets
    compte := données_tampon_id.num_sommets comme n64

    glUseProgram(nuanceur.programme)
    vérifie_erreur("termine_immédiat_id.glUseProgram")

    glBindVertexArray(données_tampon_id.va_immédiat)

    glBindBuffer(GL_ARRAY_BUFFER, données_tampon_id.vbo_immédiat)
    vérifie_erreur("termine_immédiat_id.glBindBuffer")

    définis_uniforme(nuanceur.loc_projection, projection)

    installe_format_xid(nuanceur)

    saufsi données_tampon_id.données_tampon_initialisées {
        glBufferData(GL_ARRAY_BUFFER, (MAX_SOMMETS_IMMÉDIAT comme n32) * taille_de(FormatVertexXId), s.pointeur, GL_DYNAMIC_DRAW)
        vérifie_erreur("termine_immédiat.glBufferData")
        données_tampon_id.données_tampon_initialisées = vrai
    }

    glBufferSubData(GL_ARRAY_BUFFER, 0, compte * taille_de(FormatVertexXId), s.pointeur)
    vérifie_erreur("termine_immédiat_id.glBufferSubData")

    si dernier_mode_id == ModeImmédiat.TRIANGLES {
        glDrawArrays(GL_TRIANGLES, 0, compte comme z64)
    }
    sinon {
        glDrawArrays(GL_LINES, 0, compte comme z64)
    }

    // nettoyage
    glUseProgram(0)
    glBindVertexArray(0)
    données_tampon_id.num_sommets = 0

    vérifie_erreur(#nom_de_cette_fonction)
}

installe_format_xid :: fonc (nuanceur: *NuanceurGL)
{
    stride := taille_de(FormatVertexXId)

    si nuanceur.loc_position != -1 {
        glEnableVertexAttribArray(nuanceur.loc_position comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_position")
        glVertexAttribPointer(nuanceur.loc_position comme GLuint, 3, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_SOMMETS comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_position")
    }

    si nuanceur.loc_id != -1 {
        glEnableVertexAttribArray(nuanceur.loc_id comme GLuint)
        vérifie_erreur("glEnableVertexAttribArray nuanceur.loc_id")
        glVertexAttribIPointer(nuanceur.loc_id comme GLuint, 1, GL_UNSIGNED_INT, stride, DÉCALAGE_ID comme *rien)
        // glVertexAttribPointer(nuanceur.loc_id comme GLuint, 1, GL_FLOAT, GL_FALSE, stride, DÉCALAGE_SOMMETS comme *rien)
        vérifie_erreur("glVertexAttribPointer nuanceur.loc_id")
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Quad ID.
 * \{ */

quad_immédiat_id :: fonc (x: r32, y: r32, largeur: r32, hauteur: r32, id: n32)
{
    p0 := Vec3(x = x, y = y)
    p1 := Vec3(x = x + largeur, y = y)
    p2 := Vec3(x = x + largeur, y = y + hauteur)
    p3 := Vec3(x = x, y = y + hauteur)

    quad_immédiat_id(p0, p1, p2, p3, id)
}

quad_immédiat_id :: fonc (p0: Vec3, p1: Vec3, p2: Vec3, p3: Vec3, id: n32)
{
    ajoute_sommets_id(p0, id)
    ajoute_sommets_id(p1, id)
    ajoute_sommets_id(p2, id)

    ajoute_sommets_id(p0, id)
    ajoute_sommets_id(p2, id)
    ajoute_sommets_id(p3, id)
}

ajoute_sommets_id :: fonc (pos: Vec3, id: n32)
{
    si données_tampon_id.num_sommets >= MAX_SOMMETS_IMMÉDIAT {
        termine_immédiat_id()
    }

    données_tampon_id.sommets[données_tampon_id.num_sommets].position = pos
    données_tampon_id.sommets[données_tampon_id.num_sommets].id = id
    données_tampon_id.num_sommets += 1
}

/** \} */
