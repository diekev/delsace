/* ------------------------------------------------------------------------- */
/** \nom ReferenceRecord
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-reference-record-specification-type
 * \{ */

TypeUnresolvable :: #opaque z32

/* Type pour ReferenceRecord.reference_name */
ReferencedName :: union {
    valeur: Valeur
    private_name: *PrivateName
}

imprime_refereced_name :: fonc (enchaineuse: *Enchaineuse, ref: *ReferencedName)
{
    saufsi ref {
        retourne
    }

    discr mémoire(ref) {
        valeur(v) {
            ajoute_au_tampon(enchaineuse, v)
        }
        private_name(p) {
            ajoute_au_tampon(enchaineuse, p.description)
        }
        sinon {
            ajoute_au_tampon(enchaineuse, "ReferencedName non initialisé")
        }
    }
} @FormattageImpression

is_array_key :: fonc (referenced_name: ReferencedName) -> bool
{
    discr referenced_name {
        valeur(v) {
            retourne is_array_index(v)
        }
        sinon {
            retourne faux
        }
    }
}

is_property_key :: fonc (referenced_name: ReferencedName) -> bool
{
    discr referenced_name {
        valeur(v) {
            retourne v.est_chaine() || v.est_symbol()
        }
        sinon {
            retourne faux
        }
    }
}

donne_property_key :: fonc (referenced_name: ReferencedName) -> PropertyKey
{
    discr referenced_name.valeur {
        String(s) {
            retourne s.texte
        }
        Symbol(s) {
            retourne s
        }
        sinon {
            panique("donne_property_key sur un referenced_name invalide")
        }
    }
}

/* Donne des informations sur où fut trouvée une référence, dans un DeclarativeEnvironment,
 * par rapport à l'environnement courant. */
CoordonnéeEnvironnement :: struct {
    /* L'index de la référence dans DeclarativeEnvironment.bindings. */
    index: z32
    /* Le nombre de sauts à effectuer pour aller de l'environnement courant à celui où se
     * trouve la référence. */
    sauts: z32
}

ReferenceRecord :: struct {
    Base :: union {
        V: Valeur
        E: *Environment
        UNRESOLVABLE: TypeUnresolvable
    }

    base: Base
    referenced_name: ReferencedName
    strict: bool
    valeur_this: Optionnel(Valeur)
    coordonnée: Optionnel(CoordonnéeEnvironnement)
}

est_référence_propriété :: fonc (référence: ReferenceRecord) -> bool
{
    discr référence.base {
        UNRESOLVABLE {
            retourne faux
        }
        E {
            retourne faux
        }
        sinon {
            retourne vrai
        }
    }
}

référence_non_résolvable :: fonc (nom: ReferencedName, strict: bool) -> ReferenceRecord
{
    résultat: ReferenceRecord
    résultat.base = TypeUnresolvable(0)
    résultat.strict = strict
    résultat.referenced_name = nom
    retourne résultat
}

est_référence_non_résolvable :: fonc (référence: ReferenceRecord) -> bool
{
    discr référence.base {
        UNRESOLVABLE {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ValeurCompletion
 * \{ */

ValeurCompletion :: union {
    V: Valeur
    R: ReferenceRecord
    P: PropertyDescriptor
}

est_reference_record :: fonc (valeur: ValeurCompletion) -> bool
{
    discr valeur {
        R {
            retourne vrai
        }
        V {
            retourne faux
        }
        sinon { retourne faux }
    }
}

est_undefined :: fonc (valeur: ValeurCompletion) -> bool
{
    discr valeur {
        V(v) {
            retourne est_undefined(v)
        }
        sinon {
            retourne faux
        }
    }
}

est_empty :: fonc (valeur: ValeurCompletion) -> bool
{
    discr valeur {
        R {
            retourne faux
        }
        V {
            retourne faux
        }
        P {
            retourne faux
        }
        sinon {
            retourne vrai
        }
    }
}

/* 6.2.5.3 IsSuperReference ( V )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-issuperreference */
is_super_reference :: fonc (reference: ReferenceRecord) -> bool
{
    /* 1. If V.[[ThisValue]] is not EMPTY, return true; otherwise return false. */
    discr reference.valeur_this {
        Quelque {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

/* 6.2.5.4 IsPrivateReference ( V )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-isprivatereference */
is_private_reference :: fonc (reference_record: &ReferenceRecord) -> bool
{
    // 1. If V.[[ReferencedName]] is a Private Name, return true; otherwise return false.
    discr reference_record.referenced_name {
        private_name {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

/* 6.2.5.5 GetValue
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-getvalue */
get_value :: fonc (interpréteuse: *Interpréteuse, valeur: ValeurCompletion) -> Valeur
{
    retourne get_value(interpréteuse.donne_realm(), valeur)
}

get_value :: fonc (realm: *Realm, valeur: ValeurCompletion) -> Valeur
{
    saufsi est_reference_record(valeur) {
        si est_empty(valeur) {
            retourne js_undefined
        }
        retourne valeur.V
    }

    retourne get_value(realm, valeur.R)
}

get_value :: fonc (interpréteuse: *Interpréteuse, valeur: ReferenceRecord) -> Valeur
{
    retourne get_value(interpréteuse.donne_realm(), valeur)
}

get_value :: fonc (realm: *Realm, V: ReferenceRecord) -> Valeur
{
    // 1. If V is not a Reference Record, return V.

    // 2. If IsUnresolvableReference(V) is true, throw a ReferenceError exception.
    si est_référence_non_résolvable(V) {
        message := imprime_chaine("\"%\" is not defined", V.referenced_name)
        retourne lance_reference_error(realm, message)
    }

    // 3. If IsPropertyReference(V) is true, then
    si est_référence_propriété(V) {
        // a. Let baseObj be ? ToObject(V.[[Base]]).
        base_object := to_object(realm, V.base.V)
        si realm.possède_exception() {
            retourne js_undefined
        }

        // b. If IsPrivateReference(V) is true, then
        si is_private_reference(V) {
            // i. Return ? PrivateGet(baseObj, V.[[ReferencedName]]).
            panique("get_value depuis private_reference non-implémenté")
        }

        si is_array_key(V.referenced_name) {
            index := to_uint32(V.referenced_name.valeur)
            retourne base_object.internal_get_by_index(index, get_this_value(V))
        }

        // c. If V.[[ReferencedName]] is not a property key, then
        saufsi is_property_key(V.referenced_name) {
            // i. Set V.[[ReferencedName]] to ? ToPropertyKey(V.[[ReferencedName]]).
            property_key := to_property_key(realm, V.referenced_name.valeur)
            si realm.possède_exception() {
                retourne js_undefined
            }
            V.referenced_name = property_key.donne_valeur(realm)
        }

        // d. Return ? baseObj.[[Get]](V.[[ReferencedName]], GetThisValue(V)).
        retourne base_object.internal_get(V.referenced_name.donne_property_key(), get_this_value(V))
    }

    // 4. Else,

    // a. Let base be V.[[Base]].
    base := V.base.E
    // b. Assert: base is an Environment Record.

    si V.coordonnée.possède_valeur() {
        coordonnée := V.coordonnée.Quelque
        declarative_environment := base.vérifie_comme(DeclarativeEnvironment)
        retourne declarative_environment.bindings[coordonnée.index].valeur
    }

    // c. Return ? base.GetBindingValue(V.[[ReferencedName]], V.[[Strict]]) (see 9.1).
    retourne base.get_binding_value(realm, V.referenced_name.donne_property_key(), V.strict)
}

/* 6.2.5.6 PutValue
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-putvalue */
put_value :: fonc (interpréteuse: *Interpréteuse, référence: ValeurCompletion, valeur: Valeur) -> Valeur
{
    // 1. If V is not a Reference Record, throw a ReferenceError exception.
    saufsi est_reference_record(référence) {
        message := imprime_chaine("'%' is not assignable", référence)
        retourne lance_reference_error(interpréteuse.donne_realm(), message)
    }

    record := référence.R

    // 2. If IsUnresolvableReference(V) is true, then
    si est_référence_non_résolvable(record) {
        // a. If V.[[Strict]] is true, throw a ReferenceError exception.
        si record.strict {
            message := imprime_chaine("assignment to undeclared variable \"%\"", record.referenced_name)
            retourne lance_reference_error(interpréteuse, message)
        }

        // b. Let globalObj be GetGlobalObject().
        global_obj := get_global_object(interpréteuse)

        // c. Perform ? Set(globalObj, V.[[ReferencedName]], W, false).
        set(global_obj, record.referenced_name.donne_property_key(), valeur, faux)
        si interpréteuse.possède_exception() {
            retourne js_undefined
        }

        // d. Return UNUSED.
        retourne js_undefined
    }

    // 3. If IsPropertyReference(V) is true, then
    si est_référence_propriété(record) {
        // a. Let baseObj be ? ToObject(V.[[Base]]).
        base_obj := to_object(interpréteuse.donne_realm(), record.base.V)
        si interpréteuse.possède_exception() {
            retourne js_undefined
        }

        // b. If IsPrivateReference(V) is true, then
        si is_private_reference(record) {
            // i. Return ? PrivateSet(baseObj, V.[[ReferencedName]], W).
            panique("put_valeur avec private_reference non-implémenté")
        }

        succeeded: bool

        si is_array_key(record.referenced_name) {
            index := to_uint32(record.referenced_name.valeur)
            succeeded = base_obj.internal_set_by_index(index, valeur, get_this_value(record))
        }
        sinon {
            // c. If V.[[ReferencedName]] is not a property key, then
            saufsi is_property_key(record.referenced_name) {
                // i. Set V.[[ReferencedName]] to ? ToPropertyKey(V.[[ReferencedName]]).
                property_key := to_property_key(interpréteuse.donne_realm(), record.referenced_name.valeur)
                si interpréteuse.possède_exception() {
                    retourne js_undefined
                }
                record.referenced_name = property_key.donne_valeur(interpréteuse.donne_realm())
            }

            succeeded = base_obj.internal_set(record.referenced_name.donne_property_key(), valeur, get_this_value(record))
        }

        // d. Let succeeded be ? baseObj.[[Set]](V.[[ReferencedName]], W, GetThisValue(V)).
        si interpréteuse.possède_exception() {
            retourne js_undefined
        }

        // e. If succeeded is false and V.[[Strict]] is true, throw a TypeError exception.
        si succeeded == faux && record.strict {
            retourne lance_type_error(interpréteuse.donne_realm(), "cannot add property on type")
        }
        // f. Return UNUSED.
        retourne js_undefined
    }

    // 4. Else,
    // a. Let base be V.[[Base]].
    base := record.base.E
    // b. Assert: base is an Environment Record.

    si record.coordonnée.possède_valeur() {
        coordonnée := record.coordonnée.Quelque
        declarative_environment := base.vérifie_comme(DeclarativeEnvironment)
        declarative_environment.bindings[coordonnée.index].valeur = valeur
        retourne vrai
    }

    // c. Return ? base.SetMutableBinding(V.[[ReferencedName]], W, V.[[Strict]]) (see 9.1).
    base.set_mutable_binding(interpréteuse.donne_realm(), record.referenced_name.donne_property_key(), valeur, record.strict)
    retourne vrai
}

/* 6.2.5.7 GetThisValue ( V )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-getthisvalue */
get_this_value :: fonc (reference: ReferenceRecord) -> Valeur
{
    // 1. Assert: IsPropertyReference(V) is true.
    assert(est_référence_propriété(reference))
    // 2. If IsSuperReference(V) is true, return V.[[ThisValue]]; otherwise return V.[[Base]].
    si is_super_reference(reference) {
        retourne reference.valeur_this.Quelque
    }
    retourne reference.base.V
}

/* 6.2.5.8 InitializeReferencedBinding ( V, W )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-initializereferencedbinding */
initialize_reference_binding :: fonc (reference: ReferenceRecord, valeur: Valeur)
{
    // 1. Assert: IsUnresolvableReference(V) is false.
    assert(!est_référence_non_résolvable(reference))

    // 2. Let base be V.[[Base]].
    base := reference.base.E

    // 3. Assert: base is an Environment Record.

    // 4. Return ? base.InitializeBinding(V.[[ReferencedName]], W).
    base.initialize_binding(reference.referenced_name.valeur.String.texte, valeur)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CompletionRecord
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-completion-record-specification-type
 * \{ */

CompletionRecord :: struct {
    Type :: énum {
        NORMAL
        BREAK
        CONTINUE
        RETURN
        THROW
    }

    type: Type
    valeur: ValeurCompletion
    cible: ECMAString
}

imprime_complétion :: fonc (enchaineuse: *Enchaineuse, completion: *CompletionRecord)
{
    saufsi completion {
        ajoute_au_tampon(enchaineuse, "nul")
        retourne
    }

    si completion.type == CompletionRecord.Type.THROW {
        ajoute_au_tampon(enchaineuse, completion.valeur.V)
        retourne
    }

    saufsi completion.type == CompletionRecord.Type.NORMAL || completion.type == CompletionRecord.Type.RETURN {
        ajoute_au_tampon(enchaineuse, "À FAIRE : impression des complétions anormales.")
        retourne
    }

    discr completion.valeur {
        V(v) {
            ajoute_au_tampon(enchaineuse, v)
        }
        R(r) {
            ajoute_au_tampon(enchaineuse, r)
        }
        P(p) {
            ajoute_au_tampon(enchaineuse, p)
        }
        sinon {}
    }
} @FormattageImpression

normal_completion :: fonc (valeur: Valeur) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.NORMAL, valeur)
}

empty_completion :: fonc () -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.NORMAL)
}

normal_completion :: fonc (valeur: ValeurCompletion) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.NORMAL, valeur)
}

return_completion :: fonc (valeur: Valeur) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.RETURN, valeur)
}

return_completion :: fonc (valeur: ValeurCompletion) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.RETURN, valeur)
}

continue_completion :: fonc (cible: ECMAString) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.CONTINUE, cible = cible)
}

break_completion :: fonc (cible: ECMAString) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.BREAK, cible = cible)
}

/* 6.2.4.3 UpdateEmpty ( completionRecord, value )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-updateempty */
update_empty :: fonc (completion: CompletionRecord, valeur: Valeur) -> CompletionRecord
{
    // 1. Assert: If completionRecord is either a return completion or a throw completion, then completionRecord.[[Value]] is not empty.

    // 2. If completionRecord.[[Value]] is not empty, return ? completionRecord.
    saufsi completion.valeur.est_empty() {
        retourne completion
    }

    // 3. Return Completion Record { [[Type]]: completionRecord.[[Type]], [[Value]]: value, [[Target]]: completionRecord.[[Target]] }.
    completion.valeur = valeur
    retourne completion
}

est_normal :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne completion.type == CompletionRecord.Type.NORMAL
}

est_undefined :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne est_normal(completion) && est_undefined(completion.valeur)
}

est_abrupt :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne !est_normal(completion)
}

est_return :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne completion.type == CompletionRecord.Type.RETURN
}

est_continue :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne completion.type == CompletionRecord.Type.CONTINUE
}

est_break :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne completion.type == CompletionRecord.Type.BREAK
}

est_throw :: fonc (completion: CompletionRecord) -> bool #enligne
{
    retourne completion.type == CompletionRecord.Type.THROW
}

throw_completion :: fonc (valeur: Valeur) -> CompletionRecord #enligne
{
    retourne CompletionRecord(CompletionRecord.Type.THROW, ValeurCompletion(valeur))
}

donne_object :: fonc (completion: &CompletionRecord) -> *Object #enligne
{
    retourne completion.valeur.V.donne_objet()
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Interpréteuse
 * \{ */

Interpréteuse :: struct {
    mv: *MachineVirtuelle
    sur_exécute: fonc(*Interpréteuse, *NoeudSyntaxique)(CompletionRecord)
    /* Appelé via la MachineVirtuelle quand le contexte change. */
    sur_execution_context: fonc (*Interpréteuse, *ExecutionContext)(rien)
    sur_destruction: fonc(*Interpréteuse)(rien)
}

initialise_interpréteuse :: fonc (interpréteuse: *Interpréteuse @inutilisée)
{
}

crée_interprète :: fonc (mv: *MachineVirtuelle) -> *Interpréteuse
{
    si mv.utilise_code_binaire {
        retourne crée_interprète_code_binaire(mv)
    }

    retourne crée_interprète_arbre(mv)
}

détruit :: fonc (interpréteuse: *Interpréteuse)
{
    interpréteuse.sur_destruction(interpréteuse)
}

interprète_script :: fonc (machine_virtuelle: *MachineVirtuelle, noeud: *NoeudScript) -> CompletionRecord
{
    interprète := crée_interprète(machine_virtuelle)
    diffère détruit(interprète)

    machine_virtuelle.interpréteuse = interprète
    diffère machine_virtuelle.interpréteuse = nul

    retourne exécute(interprète, noeud)
}

exécute :: fonc (interpréteuse: *Interpréteuse, noeud: *NoeudSyntaxique) -> CompletionRecord
{
    retourne interpréteuse.sur_exécute(interpréteuse, noeud)
}

donne_tas_de_mémoire :: fonc (interpréteuse: *Interpréteuse) -> *TasDeMémoire
{
    retourne interpréteuse.mv.donne_tas_de_mémoire()
}

/* https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#active-function-object */
donne_active_function_object :: fonc (interpréteuse: *Interpréteuse) -> *Function #enligne
{
    ctx := interpréteuse.donne_running_execution_context()
    retourne ctx.function
}

donne_running_execution_context :: fonc (interpréteuse: *Interpréteuse) -> *ExecutionContext #enligne
{
    retourne interpréteuse.mv.donne_running_execution_context()
}

donne_lexical_environement :: fonc (interpréteuse: *Interpréteuse) -> *Environment #enligne
{
    rec := interpréteuse.donne_running_execution_context()
    retourne rec.lexical_environment
}

donne_realm :: fonc (interpréteuse: *Interpréteuse) -> *Realm #enligne
{
    rec := interpréteuse.donne_running_execution_context()
    retourne rec.realm
}

donne_noeud_évalué_courant :: fonc (interpréteuse: *Interpréteuse) -> *NoeudSyntaxique
{
    retourne interpréteuse.donne_running_execution_context().noeud_exécuté_courant
}

crée_string_value_completion :: fonc (interpréteuse: *Interpréteuse, texte: chaine) -> CompletionRecord
{
    retourne crée_string_value_completion(interpréteuse, crée_chaine_utf16_unique(texte))
}

crée_string_value_completion :: fonc (interpréteuse: *Interpréteuse, texte: ECMAString) -> CompletionRecord
{
    string := crée_chaine(interpréteuse.donne_realm(), texte)
    value := Valeur(String = string)
    retourne normal_completion(value)
}

possède_exception :: fonc (interpréteuse: *Interpréteuse) -> bool #enligne
{
    retourne interpréteuse.mv.possède_exception()
}

définis_exception :: fonc (interpréteuse: *Interpréteuse, completion: CompletionRecord)
{
    définis_exception(interpréteuse.mv, completion)
}

définis_exception :: fonc (interpréteuse: *Interpréteuse, valeur: Valeur)
{
    définis_exception(interpréteuse.mv, valeur)
}

donne_exception :: fonc (interpréteuse: *Interpréteuse) -> Valeur
{
    retourne donne_exception(interpréteuse.mv)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom InterprèteArbre
 * \{ */

InterprèteArbre :: struct {
    empl base: Interpréteuse
    sur_exécute = exécute_arbre
    sur_execution_context = interprète_arbre_sur_execution_context
    sur_destruction = interprète_arbre_sur_destruction

    noeud_exécuté_courant: **NoeudSyntaxique @NeVisitePas
}

crée_interprète_arbre :: fonc (mv: *MachineVirtuelle) -> *InterprèteArbre
{
    résultat := loge(InterprèteArbre)
    résultat.mv = mv
    retourne résultat
}

interprète_arbre_sur_execution_context :: fonc (base: *Interpréteuse, ec: *ExecutionContext)
{
    interprète := base comme *InterprèteArbre
    si ec {
        interprète.noeud_exécuté_courant = *ec.noeud_exécuté_courant
    }
    sinon {
        interprète.noeud_exécuté_courant = nul
    }
}

interprète_arbre_sur_destruction :: fonc (base: *Interpréteuse)
{
    interprète := base comme *InterprèteArbre
    déloge(interprète)
}

exécute_arbre :: fonc (base: *Interpréteuse, noeud: *NoeudSyntaxique) -> CompletionRecord
{
    interpréteuse := base comme *InterprèteArbre

    si noeud.type == NoeudScript {
        script := noeud comme *NoeudScript
        retourne exécute(script, interpréteuse)
    }

    ctx := interpréteuse.donne_running_execution_context()
    interpréteuse.noeud_exécuté_courant = *ctx.noeud_exécuté_courant

    si noeud.type != NoeudScope && noeud.type != ScriptBody && noeud.type != BlockStatement {
        retourne exécute_noeud(noeud, interpréteuse)
    }

    résultat := normal_completion(js_undefined)

    scope := noeud comme *NoeudScope

    pour scope.enfants {
        résultat = exécute_noeud(it, interpréteuse)
        si résultat.est_normal() {
            continue
        }

        retourne résultat
    }

    retourne résultat
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Execution context.
 * \{ */

/* 9.4.2 Resolve Binding
 * https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#sec-resolvebinding */
resolve_binding :: fonc (interpréteuse: *Interpréteuse, nom: *PrimitiveChaine, environment : *Environment = nul) -> ReferenceRecord
{
    // 1. If env is not present or env is undefined, then
    // À FAIRE : undefined
    saufsi environment {
        // a. Set env to the running execution context's LexicalEnvironment.
        environment = interpréteuse.donne_lexical_environement()
    }

    // 2. Assert: env is an Environment Record.

    // 3. Let strict be IsStrict(the syntactic production that is being evaluated).
    strict := is_strict(interpréteuse.donne_noeud_évalué_courant())

    // 4. Return ? GetIdentifierReference(env, name, strict).
    retourne get_identifier_reference(interpréteuse, environment, nom, strict)
}

/* 9.4.3 GetThisEnvironment ( )
 * https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#sec-getthisenvironment */
get_this_environment :: fonc (interpréteuse: *Interpréteuse) -> *Environment
{
    // 1. Let env be the running execution context's LexicalEnvironment.
    env := interpréteuse.donne_running_execution_context().lexical_environment

    // 2. Repeat,
    boucle {
        // a. Let exists be env.HasThisBinding().
        exists := env.has_this_binding()

        // b. If exists is true, return env.
        si exists {
            arrête
        }

        // c. Let outer be env.[[OuterEnv]].
        outer := env.outer_environment

        // d. Assert: outer is not null.
        assert(outer != nul)
    
        // e. Set env to outer.
        env = outer
    }

    retourne env
}

/* 9.4.4 ResolveThisBinding ( )
 * https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#sec-resolvethisbinding */
resolve_this_binding :: fonc (interpréteuse: *Interpréteuse) -> Valeur
{
    // 1. Let envRec be GetThisEnvironment().
    env_rec := get_this_environment(interpréteuse)

    // 2. Return ? envRec.GetThisBinding().
    retourne env_rec.get_this_binding(interpréteuse.donne_realm())
}

/* 9.4.5 GetNewTarget ( )
 * https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#sec-getnewtarget */
get_new_target :: fonc (interpréteuse: *Interpréteuse) -> Valeur
{
    // 1. Let envRec be GetThisEnvironment().
    env_rec := get_this_environment(interpréteuse) comme *FunctionEnvironment

    // 2. Assert: envRec has a [[NewTarget]] field.
    assert(env_rec.table.classe == FunctionEnvironment)

    // 3. Return envRec.[[NewTarget]].
    retourne env_rec.new_target
}

/* 9.4.6 GetGlobalObject
 * https://tc39.es/ecma262/multipage/executable-code-and-execution-contexts.html#sec-getglobalobject */
get_global_object :: fonc (interpréteuse: *Interpréteuse) -> *Object
{
    // 1. Let currentRealm be the current Realm Record.
    current_realm := interpréteuse.donne_realm()
    // 2. Return currentRealm.[[GlobalObject]].
    retourne current_realm.global_object
}

/** \} */
