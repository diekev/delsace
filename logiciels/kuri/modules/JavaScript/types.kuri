importe Algorithmes
importe Chaine
importe Fondation
importe Math
importe Numérique

/* Ceux-là ne sont pas dans le standard, ce n'est que pour avoir des types pour l'union. */
UndefinedType :: #opaque z32
NullType :: #opaque z32

/* ------------------------------------------------------------------------- */
/** \nom The BigInt Type
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-ecmascript-language-types-symbol-type
 * \{ */

BigInt :: struct {
    // À FAIRE : implémente proprement
    valeur: z64
}

big_int_equal :: fonc (x: Valeur @inutilisée, y: Valeur @inutilisée) -> bool
{
    panique("big_int equal non-implémenté")
}

big_int_bitwise_not :: fonc (x: Valeur @inutilisée) -> Valeur
{
    panique("big_int_bitwise_not non-implémenté")
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom 6.1.5 The Symbol Type
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-ecmascript-language-types-symbol-type
 * \{ */

Symbol :: struct {
    /* Soit undefined ou une chaine. */
    description: Optionnel(ECMAString)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom 6.1.6.1 The Number Type
 * \{ */

/* 6.1.6.1.1 Number::unaryMinus ( x )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-unaryMinus */
number_unary_minus :: fonc (x: Valeur) -> Valeur
{
    // 1. If x is NaN, return NaN.
    si est_nan(x) {
        retourne Valeur(Number = NaN)
    }

    // 2. Return the negation of x; that is, compute a Number with the same magnitude but opposite sign.
    retourne Valeur(Number = -x.Number)
}

/* 6.1.6.1.2 Number::bitwiseNOT ( x )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-bitwiseNOT */
number_bitwise_not :: fonc (x: Valeur) -> Valeur
{
    // 1. Let oldValue be ! ToInt32(x).
    old_value := to_int32(x).Number comme z32

    // 2. Return the bitwise complement of oldValue.
    // The mathematical value of the result is exactly representable as a 32-bit two's complement bit string.
    retourne Valeur(Number = ~old_value comme r64)
}

/* 6.1.6.1.3 Number::exponentiate
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-exponentiate */
number_exponentiate :: fonc (b: Valeur, e: Valeur) -> Valeur
{
    // À FAIRE : standardise le reste
    base := b.Number
    exponent := e.Number

    // 1. If exponent is NaN, return NaN.
    si est_nan(exponent) {
        retourne Valeur(Number = NaN)
    }

    // 2. If exponent is either +0𝔽 or -0𝔽, return 1𝔽.
    si exponent == 0.0 || exponent == -0.0 {
        retourne Valeur(Number = 1.0)
    }

    // 3. If base is NaN, return NaN.
    si est_nan(base) {
        retourne Valeur(Number = NaN)
    }

    retourne Valeur(Number = puissance(b, e))
}

/* 6.1.6.1.4 Number::multiply ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-multiply */
number_multiply :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    // À FAIRE : standardise
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    retourne Valeur(Number = gauche.Number * droite.Number)
}

/* 6.1.6.1.5 Number::divide ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-divide */
number_divide :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    // À FAIRE : standardise
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    retourne Valeur(Number = gauche.Number / droite.Number)
}

/* 6.1.6.1.6 Number::remainder ( n, d )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-remainder */
number_remainder :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    retourne Valeur(Number = mod(gauche.Number, droite.Number))
}

/* 6.1.6.1.7 Number::add ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-add */
number_add :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    retourne Valeur(Number = gauche.Number + droite.Number)
}

/* 6.1.6.1.8 Number::subtract ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-subtract */
number_subtract :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    retourne Valeur(Number = gauche.Number - droite.Number)
}

/* 6.1.6.1.9 Number::leftShift ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-leftShift */
number_left_shift :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme z32
    d := droite.Number comme z32
    r := g << d
    retourne Valeur(Number = r comme r64)
}

/* 6.1.6.1.10 Number::signedRightShift ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-signedRightShift */
number_signed_right_shift :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme z32
    d := droite.Number comme z32
    r := g >> d
    retourne Valeur(Number = r comme r64)
}

/* 6.1.6.1.11 Number::unsignedRightShift ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-unsignedRightShift */
number_unsigned_right_shift :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme n32
    d := droite.Number comme n32
    r := g >> d
    retourne Valeur(Number = r comme r64)
}

/* 6.1.6.1.12 Number::lessThan ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-lessThan */
number_less_than :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    retourne Valeur(Boolean = gauche.Number < droite.Number)
}

/* 6.1.6.1.13 Number::equal ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-equal */
number_equal :: fonc (vx: Valeur, vy: Valeur) -> bool
{
    x := vx.Number
    y := vy.Number

    // 1. If x is NaN, return false.
    si est_nan(x) {
        retourne faux
    }

    // 2. If y is NaN, return false.
    si est_nan(y) {
        retourne faux
    }

    // 3. If x is y, return true.
    // NOTE : la comparaison x == y est pour les valeurs infinies.
    si abs(x - y) <= 1e-15 || (x == y) {
        retourne vrai
    }

    // 4. If x is +0𝔽 and y is -0𝔽, return true.
    si x == 0.0 && y == -0.0 {
        retourne vrai
    }

    // 5. If x is -0𝔽 and y is +0𝔽, return true.
    si x == -0.0 && y == 0.0 {
        retourne vrai
    }

    // 6. Return false.
    retourne faux
}

/* 6.1.6.1.14 Number::sameValue ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-sameValue */
number_same_value :: fonc (vx: Valeur, vy: Valeur) -> Valeur
{
    x := vx.Number
    y := vy.Number

    // 1. If x is NaN and y is NaN, return true.
    si est_nan(x) && est_nan(y) {
        retourne vrai
    }

    // 2. If x is +0𝔽 and y is -0𝔽, return false.
    si x == 0.0 && y == -0.0 {
        retourne faux
    }

    // 3. If x is -0𝔽 and y is +0𝔽, return false.
    si x == -0.0 && y == 0.0 {
        retourne faux
    }

    // 4. If x is y, return true.
    si x == y {
        retourne vrai
    }

    // 5. Return false.
    retourne faux
}

/* 6.1.6.1.15 Number::sameValueZero ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-sameValueZero */
number_same_value_zero :: fonc (vx: Valeur, vy: Valeur) -> Valeur
{
    x := vx.Number
    y := vy.Number

    // 1. If x is NaN and y is NaN, return true.
    si est_nan(x) && est_nan(y) {
        retourne vrai
    }

    // 2. If x is +0𝔽 and y is -0𝔽, return true.
    si x == 0.0 && y == -0.0 {
        retourne vrai
    }

    // 3. If x is -0𝔽 and y is +0𝔽, return true.
    si x == -0.0 && y == 0.0 {
        retourne vrai
    }

    // 4. If x is y, return true.
    si x == y {
        retourne vrai
    }

    // 5. Return false.
    retourne faux
}

/* 6.1.6.1.17 Number::bitwiseAND ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-bitwiseAND */
number_bitwise_and :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme z32
    d := droite.Number comme z32
    r := g & d
    retourne Valeur(Number = r comme r64)
}

/* 6.1.6.1.18 Number::bitwiseXOR ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-bitwiseXOR */
number_bitwise_xor :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme z32
    d := droite.Number comme z32
    r := g ^ d
    retourne Valeur(Number = r comme r64)
}

/* 6.1.6.1.19 Number::bitwiseOR ( x, y )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-bitwiseOR */
number_bitwise_or :: fonc (gauche: Valeur, droite: Valeur) -> Valeur
{
    assert(gauche.est_nombre())
    assert(droite.est_nombre())
    g := gauche.Number comme z32
    d := droite.Number comme z32
    r := g | d
    retourne Valeur(Number = r comme r64)
}

précision_max_pour_radix := n8.[
    0,  0,  52, 32, 26, 22, 20, 18, 17, 16,
    15, 15, 14, 14, 13, 13, 13, 12, 12, 12,
    12, 11, 11, 11, 11, 11, 11, 10, 10, 10,
    10, 10, 10, 10, 10, 10, 10]

/* 6.1.6.1.20 Number::toString ( x, radix )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-numeric-types-number-tostring */
number_to_string :: fonc (realm: *Realm, valeur: Valeur, radix_mv: n32) -> Valeur
{
    x := valeur.Number
    // 1. If x is NaN, return "NaN".
    si est_nan(x) {
        retourne crée_chaine(realm, STR_NaN)
    }

    // 2. If x is either +0𝔽 or -0𝔽, return "0".
    si x == 0.0 || x == -0.0 {
        retourne crée_chaine(realm, crée_chaine_utf16_unique("0"))
    }

    // 3. If x < -0𝔽, return the string-concatenation of "-" and Number::toString(-x, radix_mv).
    si x < -0.0 {
        chn_moins := crée_chaine(realm, crée_chaine_utf16_unique("-"))
        chn_x := number_to_string(realm, Valeur(Number = -x), radix_mv)
        retourne concatène(realm, chn_moins, chn_x.String)
    }

    // 4. If x is +∞𝔽, return "Infinity".
    si x == R64_INFINITÉ {
        retourne crée_chaine(realm, STR_Infinity)
    }

    // À FAIRE : implémente selon le standard.

    négatif := x < 0.0
    si négatif {
        x *= -1.0
    }

    part_entière := plancher(x)
    part_décimale := x - part_entière

    caractères: [..]z8
    diffère déloge(caractères)

    chiffres :: "0123456789abcdefghijklmnopqrstuvwxyz"

    radix := radix_mv comme r64

    si part_entière == 0.0 {
        tableau_ajoute(*caractères, '0')
    }
    sinon tantque part_entière > 0.0 {
        caractère := chiffres[plancher(mod(part_entière, radix)) comme n64]
        tableau_ajoute(*caractères, caractère)
        part_entière /= radix
        part_entière = plancher(part_entière)
    }

    si négatif {
        tableau_ajoute(*caractères, '-')
    }

    tableau_inverse(caractères)

    si part_décimale != 0.0 {
        tableau_ajoute(*caractères, '.')

        précision := précision_max_pour_radix[radix_mv]
        pour précision {
            part_décimale *= radix;
            intégrale := plancher(part_décimale) comme n64
            tableau_ajoute(*caractères, chiffres[intégrale])
            part_décimale -= intégrale comme r64
        }

        tantque caractères.taille > 0 && caractères[caractères.taille - 1] == '0' {
            caractères.taille -= 1
        }
    }

    chn := chaine(caractères.pointeur, caractères.taille)
    résultat := crée_chaine_utf16_unique(chn)
    retourne crée_chaine(realm, résultat)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Utilitaire
 * \{ */

est_intégral :: fonc (n: r64) -> bool
{
    retourne (n comme n64) comme r64 == n
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom PropertyDescriptor
 * 6.2.6 The Property Descriptor Specification Type
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-property-descriptor-specification-type
 * \{ */

PropertyDescriptor :: struct {
    value: Optionnel(Valeur)
    get: Optionnel(*Object) // ou undefined
    set: Optionnel(*Object) // ou undefined
    writable: Optionnel(bool)
    configurable: Optionnel(bool)
    enumerable: Optionnel(bool)
}

initialise_property_descriptor :: fonc (desc: *PropertyDescriptor, valeur: Valeur, drapeaux: DrapeauxPropriété)
{
    /* Pour les réutilisation de variable. */
    init_de(PropertyDescriptor)(desc)

    desc.value = valeur

    si drapeaux.CONFIGURABLE {
        desc.configurable = vrai
    }
    si drapeaux.ENUMERABLE {
        desc.enumerable = vrai
    }
    si drapeaux.WRITABLE {
        desc.writable = vrai
    }
}

/* 6.2.6.1 IsAccessorDescriptor ( Desc )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-isaccessordescriptor */
is_accessor_descriptor :: fonc (desc: &PropertyDescriptor) -> bool
{
    // À FAIRE 1. If Desc is undefined, return false.

    // 2. If Desc has a [[Get]] field, return true.
    si desc.get.possède_valeur() {
        retourne vrai
    }
    // 3. If Desc has a [[Set]] field, return true.
    si desc.set.possède_valeur() {
        retourne vrai
    }
    // 4. Return false.
    retourne faux
}

/* 6.2.6.2 IsDataDescriptor ( Desc )
 * https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#sec-isdatadescriptor */
is_data_descriptor :: fonc (desc: &PropertyDescriptor) -> bool
{
    // À FAIRE 1. If Desc is undefined, return false.

    // 2. If Desc has a [[Value]] field, return true.
    si desc.value.possède_valeur() {
        retourne vrai
    }
    // 3. If Desc has a [[Writable]] field, return true.
    si desc.writable.possède_valeur() {
        retourne vrai
    }
    // 4. Return false.
    retourne faux
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom PropertyKey
 * \{ */

PropertyKey :: union {
    String: ECMAString
    Symbol: *Symbol
}

formatte_property_key :: fonc (enchaineuse: *Enchaineuse, prop: *PropertyKey)
{
    discr mémoire(prop) {
        String(s) {
            ajoute_au_tampon(enchaineuse, s)
        }
        Symbol(s) {
            ajoute_au_tampon(enchaineuse, "%", s, "%")
        }
        sinon {}
    }
} @FormattageImpression

donne_empreinte :: fonc (key: PropertyKey) -> n64
{
    discr key {
        String(s) {
            retourne s.valeur
        }
        Symbol(s) {
            retourne s comme n64
        }
        sinon {}
    }

    retourne 0
}

est_chaine :: fonc (key: PropertyKey) -> bool
{
    discr key {
        String {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

est_symbol :: fonc (key: PropertyKey) -> bool
{
    discr key {
        Symbol {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

opérateur == :: fonc (p1: PropertyKey, p2: PropertyKey) -> bool
{
    discr p1 {
        String(s1) {
            discr p2 {
                String(s2) {
                    retourne s1 == s2
                }
                sinon {
                    retourne faux
                }
            }
        }
        Symbol(s1) {
            discr p2 {
                Symbol(s2) {
                    retourne s1 == s2
                }
                sinon {
                    retourne faux
                }
            }
        }
        sinon {
            retourne faux
        }
    }
}

opérateur != :: fonc (p1: PropertyKey, p2: PropertyKey) -> bool
{
    retourne !(p1 == p2)
}

/* https://tc39.es/ecma262/multipage/ecmascript-data-types-and-values.html#array-index */
is_array_index :: fonc (property_key: PropertyKey) -> bool
{
    saufsi property_key.est_chaine() {
        retourne faux
    }

    /* À FAIRE */
    texte := property_key.String.donne_chaine_utf16()

    texte_utf8 := converti_vers_chaine(texte)
    diffère déloge(texte_utf8)

    résultat := tente extrait_r64(texte_utf8) piège _ {
        retourne faux
    }

    retourne is_non_negative_integral_number(résultat)
}

to_uint32 :: fonc (property_key: PropertyKey) -> n32
{
    texte := property_key.String.donne_chaine_utf16()

    /* À FAIRE */
    texte_utf8 := converti_vers_chaine(texte)
    diffère déloge(texte_utf8)

    résultat := tente extrait_r64(texte_utf8) piège nonatteignable
    retourne résultat comme n32
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom PrimitiveChaine
 * \{ */

#portée_fichier

table_virtuelle_primitive_chaine := TableVirtuelleCellule(classe = PrimitiveChaine)

#portée_export

PrimitiveChaine :: struct {
    empl cellule: Cellule
    table = *table_virtuelle_primitive_chaine

    texte: ECMAString
    objet: *Object
}

crée_chaine :: fonc (realm: *Realm, texte: ECMAString) -> *PrimitiveChaine
{
    pour realm.chaines {
        si it.texte == texte {
            retourne it
        }
    }

    tas_de_mémoire := realm.donne_tas_de_mémoire()
    résultat := tas_de_mémoire.alloue(PrimitiveChaine)
    résultat.texte = texte
    tableau_ajoute(*realm.chaines, résultat)
    retourne résultat
}

crée_chaine :: fonc (tas_de_mémoire: *TasDeMémoire, texte: ECMAString) -> *PrimitiveChaine
{
    résultat := tas_de_mémoire.alloue(PrimitiveChaine)
    résultat.texte = texte
    retourne résultat
}

concatène :: fonc (realm: *Realm, str_gauche: *PrimitiveChaine, str_droite: *PrimitiveChaine) -> Valeur
{
    chn1 := str_gauche.texte.donne_chaine_utf16()
    chn2 := str_droite.texte.donne_chaine_utf16()

    enchaineuse: EnchaineuseUTF16
    initialise_enchaineuse(enchaineuse)
    diffère détruit_tampons(enchaineuse)

    ajoute(enchaineuse, chn1)
    ajoute(enchaineuse, chn2)

    chn := chaine_depuis_enchaineuse(enchaineuse)
    diffère détruit_chaine(chn)
    chn_résultat := crée_chaine_utf16_unique(chn)

    retourne Valeur(String = crée_chaine(realm, chn_résultat))
}

/** \} */
