importe Fondation

GenreLexème :: énum {
    NonInitialisé

    Identifiant
    ChaineLittérale
    NombreLittéral

    // opérateurs
    Plus
    Moins
    Étoile
    Slash
    Pourcent
    Inférieur
    Supérieur
    DécalageGauche
    DécalageDroite
    EtBinaire
    EtLogique
    OuBinaire
    OuLogique
    OuExclusif
    NonBinaire
    NonLogique
    Égal
    PlusÉgal
    MoinsÉgal
    ÉtoileÉgal
    SlashÉgal
    PourcentÉgal
    InférieurÉgal
    SupérieurÉgal
    DécalageGaucheÉgal
    DécalageDroiteÉgal
    EtBinaireÉgal
    OuBinaireÉgal
    OuExclusifÉgal
    EstÉgal
    NEspaPasÉgal
    EstÉgalStrict
    NEspaPasÉgalStrict
    PlusPlus
    MoinsMoins
    Interrogation
    DoublePoint
    Coalesce
    EtLogiqueÉgal
    OuLogiqueÉgal
    CoalesceÉgal
    DécalageDroiteNaturel
    DécalageDroiteNaturelÉgal

    // opérateurs unaires
    PlusUnaire
    MoinsUnaire

    // ponctuation
    Point
    Virgule
    PointVirgule
    ParenthèseOuvrante
    ParenthèseFermante
    AccoladeOuvrante
    AccoladeFermante
    CrochetOuvrant
    CrochetFermant

    // mots-clés
    As
    Async
    Await
    Break
    Case
    Catch
    Const
    Continue
    Debugger 
    Default 
    Delete 
    Do
    Else
    Enum
    Export
    Extends
    False
    Finally
    For
    From
    Function
    Get
    If
    Import
    In
    Instanceof
    Let
    Meta
    New
    Null
    Of
    Return
    Set
    Super
    Switch
    Target
    This
    Throw
    True
    Try
    Typeof
    Var
    Void
    While
    With
    Yield
}

rafine_genre_lexème_identifiant :: fonc (identifiant: chaine) -> GenreLexème
{
    si identifiant.taille == 2 {
        si identifiant == "as" { retourne GenreLexème.As }
        si identifiant == "do" { retourne GenreLexème.Do }
        si identifiant == "if" { retourne GenreLexème.If }
        si identifiant == "in" { retourne GenreLexème.In }
        si identifiant == "of" { retourne GenreLexème.Of }
    }
    sinon si identifiant.taille == 3 {
        si identifiant == "for" { retourne GenreLexème.For }
        si identifiant == "get" { retourne GenreLexème.Get }
        si identifiant == "let" { retourne GenreLexème.Let }
        si identifiant == "new" { retourne GenreLexème.New }
        si identifiant == "set" { retourne GenreLexème.Set }
        si identifiant == "try" { retourne GenreLexème.Try }
        si identifiant == "var" { retourne GenreLexème.Var }
    }
    sinon si identifiant.taille == 4 {
        si identifiant == "case" { retourne GenreLexème.Case }
        si identifiant == "else" { retourne GenreLexème.Else }
        si identifiant == "enum" { retourne GenreLexème.Enum }
        si identifiant == "from" { retourne GenreLexème.From }
        si identifiant == "meta" { retourne GenreLexème.Meta }
        si identifiant == "null" { retourne GenreLexème.Null }
        si identifiant == "this" { retourne GenreLexème.This }
        si identifiant == "true" { retourne GenreLexème.True }
        si identifiant == "void" { retourne GenreLexème.Void }
        si identifiant == "With" { retourne GenreLexème.With }
    }
    sinon si identifiant.taille == 5 {
        si identifiant == "async" { retourne GenreLexème.Async }
        si identifiant == "await" { retourne GenreLexème.Await }
        si identifiant == "break" { retourne GenreLexème.Break }
        si identifiant == "catch" { retourne GenreLexème.Catch }
        si identifiant == "const" { retourne GenreLexème.Const }
        si identifiant == "false" { retourne GenreLexème.False }
        si identifiant == "super" { retourne GenreLexème.Super }
        si identifiant == "throw" { retourne GenreLexème.Throw }
        si identifiant == "while" { retourne GenreLexème.While }
        si identifiant == "yield" { retourne GenreLexème.Yield }
    }
    sinon si identifiant.taille == 6 {
        si identifiant == "delete" { retourne GenreLexème.Delete }
        si identifiant == "export" { retourne GenreLexème.Export }
        si identifiant == "import" { retourne GenreLexème.Import }
        si identifiant == "return" { retourne GenreLexème.Return }
        si identifiant == "switch" { retourne GenreLexème.Switch }
        si identifiant == "target" { retourne GenreLexème.Target }
        si identifiant == "typeof" { retourne GenreLexème.Typeof }
    }
    sinon si identifiant.taille == 7 {
        si identifiant == "default" { retourne GenreLexème.Default }
        si identifiant == "extends" { retourne GenreLexème.Extends }
        si identifiant == "finally" { retourne GenreLexème.Finally }
    }
    sinon si identifiant.taille == 8 {
        si identifiant == "continue" { retourne GenreLexème.Continue }
        si identifiant == "debugger" { retourne GenreLexème.Debugger }
        si identifiant == "function" { retourne GenreLexème.Function }
    }
    sinon si identifiant.taille == 10 {
        si identifiant == "instanceof" { retourne GenreLexème.Instanceof }
    }

    retourne GenreLexème.Identifiant
}

rafine_genre_lexème_opérateur :: fonc (identifiant: chaine) -> Optionnel(GenreLexème)
{
    si identifiant.taille == 1 {
        c := identifiant[0]

        si c == '+' { retourne GenreLexème.Plus }
        si c == '-' { retourne GenreLexème.Moins }
        si c == '/' { retourne GenreLexème.Slash }
        si c == '*' { retourne GenreLexème.Étoile }
        si c == '%' { retourne GenreLexème.Pourcent }
        si c == '<' { retourne GenreLexème.Inférieur }
        si c == '>' { retourne GenreLexème.Supérieur }
        si c == '=' { retourne GenreLexème.Égal }
        si c == '&' { retourne GenreLexème.EtBinaire }
        si c == '|' { retourne GenreLexème.OuBinaire }
        si c == '^' { retourne GenreLexème.OuExclusif }
        si c == '~' { retourne GenreLexème.NonBinaire }
        si c == '!' { retourne GenreLexème.NonLogique }
        si c == '?' { retourne GenreLexème.Interrogation }
        si c == ':' { retourne GenreLexème.DoublePoint }
    }
    sinon si identifiant.taille == 2 {
        si identifiant == "+=" { retourne GenreLexème.PlusÉgal }
        si identifiant == "-=" { retourne GenreLexème.MoinsÉgal }
        si identifiant == "*=" { retourne GenreLexème.ÉtoileÉgal }
        si identifiant == "/=" { retourne GenreLexème.SlashÉgal }
        si identifiant == "%=" { retourne GenreLexème.PourcentÉgal }
        si identifiant == "<=" { retourne GenreLexème.InférieurÉgal }
        si identifiant == ">=" { retourne GenreLexème.SupérieurÉgal }
        si identifiant == "&=" { retourne GenreLexème.EtBinaireÉgal }
        si identifiant == "|=" { retourne GenreLexème.OuBinaireÉgal }
        si identifiant == "^=" { retourne GenreLexème.OuExclusifÉgal }
        si identifiant == "==" { retourne GenreLexème.EstÉgal }
        si identifiant == "!=" { retourne GenreLexème.NEspaPasÉgal }
        si identifiant == "++" { retourne GenreLexème.PlusPlus }
        si identifiant == "--" { retourne GenreLexème.MoinsMoins }
        si identifiant == "<<" { retourne GenreLexème.DécalageGauche }
        si identifiant == ">>" { retourne GenreLexème.DécalageDroite }
        si identifiant == "&&" { retourne GenreLexème.EtLogique }
        si identifiant == "||" { retourne GenreLexème.OuLogique }
        si identifiant == "??" { retourne GenreLexème.Coalesce }
    }
    sinon si identifiant.taille == 2 {
        si identifiant == "<<=" { retourne GenreLexème.DécalageGaucheÉgal }
        si identifiant == ">>=" { retourne GenreLexème.DécalageDroiteÉgal }
        si identifiant == ">>>" { retourne GenreLexème.DécalageDroiteNaturel }
        si identifiant == "===" { retourne GenreLexème.EstÉgalStrict }
        si identifiant == "!==" { retourne GenreLexème.NEspaPasÉgalStrict }
        si identifiant == "&&=" { retourne GenreLexème.EtLogiqueÉgal }
        si identifiant == "||=" { retourne GenreLexème.OuLogiqueÉgal }
        si identifiant == "??=" { retourne GenreLexème.CoalesceÉgal }
    }
    sinon si identifiant.taille == 4 {
        si identifiant == ">>>=" { retourne GenreLexème.DécalageDroiteNaturelÉgal }
    }
}

Lexème :: struct {
    texte: chaine
    genre: GenreLexème
}

Associativité :: énum {
    Gauche
    Droite
}

associativité_pour_opérateur :: fonc (genre: GenreLexème) -> Associativité
{
    discr genre {
        Plus,
        Moins,
        Étoile,
        Slash,
        Pourcent,
        Inférieur,
        Supérieur,
        DécalageGauche,
        DécalageDroite,
        EtBinaire,
        EtLogique,
        OuBinaire,
        OuLogique,
        OuExclusif,
        Égal,
        PlusÉgal,
        MoinsÉgal,
        ÉtoileÉgal,
        SlashÉgal,
        PourcentÉgal,
        InférieurÉgal,
        SupérieurÉgal,
        DécalageGaucheÉgal,
        DécalageDroiteÉgal,
        EtBinaireÉgal,
        OuBinaireÉgal,
        OuExclusifÉgal,
        EstÉgal,
        NEspaPasÉgal,
        EstÉgalStrict,
        NEspaPasÉgalStrict,
        Point,
        ParenthèseOuvrante,
        CrochetOuvrant {
            retourne Associativité.Gauche
        }
        MoinsUnaire,
        NonBinaire,
        NonLogique,
        PlusUnaire {
            retourne Associativité.Droite
        }
        PlusPlus,
        MoinsMoins {
            retourne Associativité.Gauche // N/A
        }
        sinon {
            retourne Associativité.Gauche
        }
    }
}

/* https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Operators/Operator_precedence */
précédence_pour_opérateur :: fonc (genre: GenreLexème) -> z32
{
    discr genre {
        Égal,
        PlusÉgal,
        MoinsÉgal,
        ÉtoileÉgal,
        SlashÉgal,
        PourcentÉgal,
        DécalageGaucheÉgal,
        DécalageDroiteÉgal,
        EtBinaireÉgal,
        OuBinaireÉgal,
        OuExclusifÉgal {
            retourne 2
        }
        OuLogique {
            retourne 3
        }
        EtLogique {
            retourne 4
        }
        OuBinaire {
            retourne 5
        }
        OuExclusif {
            retourne 6
        }
        EtBinaire {
            retourne 7
        }
        EstÉgal,
        NEspaPasÉgal,
        EstÉgalStrict,
        NEspaPasÉgalStrict {
            retourne 8
        }
        Inférieur,
        InférieurÉgal,
        Supérieur,
        SupérieurÉgal {
            retourne 9
        }
        DécalageGauche,
        DécalageDroite {
            retourne 10
        }
        Plus,
        Moins {
            retourne 11
        }
        Étoile,
        Slash,
        Pourcent {
            retourne 12
        }
        MoinsUnaire,
        NonBinaire,
        NonLogique,
        PlusUnaire {
            retourne 13
        }
        PlusPlus,
        MoinsMoins,
        Typeof {
            retourne 14
        }
        Point,
        ParenthèseOuvrante,
        CrochetOuvrant {
            retourne 15
        }
        sinon {
            retourne -1
        }
    }
}

est_lexème_expression :: fonc (genre: GenreLexème) -> bool
{
    discr genre {
        Var,
        Let,
        Const,
        Identifiant,
        ChaineLittérale,
        NombreLittéral,
        ParenthèseOuvrante,
        AccoladeOuvrante,
        True,
        False,
        Function,
        Null, 
        CrochetOuvrant {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

est_lexème_expression_unaire :: fonc (genre: GenreLexème) -> bool
{
    discr genre {
        PlusPlus,
        MoinsMoins,
        Plus,
        Moins,
        NonBinaire,
        NonLogique,
        New,
        Typeof  {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

est_lexème_expression_secondaire :: fonc (genre: GenreLexème) -> bool
{
    discr genre {
        Plus,
        PlusPlus,
        Moins,
        MoinsMoins,
        Étoile,
        Slash,
        Pourcent,
        Inférieur,
        Supérieur,
        DécalageGauche,
        DécalageDroite,
        EtBinaire,
        EtLogique,
        OuBinaire,
        OuLogique,
        OuExclusif,
        NonBinaire,
        NonLogique,
        Égal,
        PlusÉgal,
        MoinsÉgal,
        ÉtoileÉgal,
        SlashÉgal,
        PourcentÉgal,
        InférieurÉgal,
        SupérieurÉgal,
        DécalageGaucheÉgal,
        DécalageDroiteÉgal,
        EtBinaireÉgal,
        OuBinaireÉgal,
        OuExclusifÉgal,
        EstÉgal,
        NEspaPasÉgal,
        EstÉgalStrict,
        NEspaPasÉgalStrict,
        Point,
        ParenthèseOuvrante,
        CrochetOuvrant,
        Interrogation,
        DoublePoint,
        Coalesce,
        EtLogiqueÉgal,
        OuLogiqueÉgal,
        CoalesceÉgal,
        DécalageDroiteNaturel,
        DécalageDroiteNaturelÉgal {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

est_lexème_instruction :: fonc (genre: GenreLexème) -> bool
{
    discr genre {
        For,
        Function,
        Return,
        If {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

DrapeauxLexèmePourParsage :: énum_drapeau {
    EstExpression
    EstExpressionUnaire
    EstExpressionSecondaire
    EstInstruction
}

DonnéesLexèmePourParsage :: struct {
    associativité: Associativité
    précédence: z32
    drapeaux: DrapeauxLexèmePourParsage
}

données_lexèmes_pour_parsage: [GenreLexème.nombre_éléments]DonnéesLexèmePourParsage

initialise_données_lexèmes_pour_parsage :: fonc ()
{
    pour & données_lexèmes_pour_parsage {
        genre_lexème := (index_it comme z32) comme GenreLexème

        it.associativité = associativité_pour_opérateur(genre_lexème)
        it.précédence = précédence_pour_opérateur(genre_lexème)

        it.drapeaux.EstExpression = est_lexème_expression(genre_lexème)
        it.drapeaux.EstExpressionUnaire = est_lexème_expression_unaire(genre_lexème)
        it.drapeaux.EstExpressionSecondaire = est_lexème_expression_secondaire(genre_lexème)
        it.drapeaux.EstInstruction = est_lexème_instruction(genre_lexème)
    }
}

#ajoute_init initialise_données_lexèmes_pour_parsage()
