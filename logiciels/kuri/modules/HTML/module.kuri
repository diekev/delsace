// Module de parsage de code HTML
// Spec : https://html.spec.whatwg.org/multipage/parsing.html

importe Chaine
importe Fondation
importe Temps

charge "élément"
charge "attribut"
charge "browsing_context"
charge "comparaison"
charge "document"
charge "entités"
charge "entropisation"
charge "formattage"
charge "formulaire"
charge "html_collection"
charge "lexage"
charge "liste_éléments_actifs_formattage"
charge "noeud"
charge "parsage"
charge "pile_éléments_ouverts"
charge "tags_html"
charge "user_agent"

DOMString :: ChaineUTF16Unique

imprime_noeud :: fonc (noeud: *Noeud, tab: z32) -> rien
{
    imprime_tabulation(tab)

    si noeud.est_texte() {
        texte := noeud comme *NoeudTexte
        imprime("%\n", texte.données)
    }
    sinon si noeud.est_élément() {
        élément := noeud comme *Élément
        imprime("<%", élément.texte)

        pour élément.attributs {
            imprime(" %=%", it.nom, it.valeur)
        }

        imprime(">\n")
    }
    sinon si noeud.est_commentaire() {
        commentaire := noeud comme *NoeudCommentaire
        imprime("<!--%-->\n", commentaire.données)
    }
    sinon {
        imprime("%\n", noeud.genre)
    }

    pour noeud.enfants {
        imprime_noeud(it, tab + 1)
    }
}

imprime_document :: fonc (document: *Document)
{
    imprime("============== Document ==============\n")

    pour document.enfants {
        imprime_noeud(it, 0)
    }
}

/* ------------------------------------------------------------------------- */
/** \nom Traversée, visite, d'un noeud et de ses enfants.
 * \{ */

DécisionTraversée :: énum {
    /* Continue la traversée vers les enfants du noeuds. */
    CONTINUE
    /* Ignore les enfants de ce noeuds. */
    IGNORE_ENFANTS
}

traverse_noeud :: fonc (noeud: *Noeud, rappel_traverse: fonc(*Noeud, $T)(DécisionTraversée), données: T)
{
    pour noeud.enfants {
        décision := rappel_traverse(it, données)
        si décision == DécisionTraversée.IGNORE_ENFANTS {
            continue
        }

        traverse_noeud(it, rappel_traverse, données)
    }
}

/* Version de traverse_noeud sans données utilisateur spécifique. */
traverse_noeud :: fonc (noeud: *Noeud, rappel_traverse: fonc(*Noeud)(DécisionTraversée))
{
    pour noeud.enfants {
        décision := rappel_traverse(it)
        si décision == DécisionTraversée.IGNORE_ENFANTS {
            continue
        }

        traverse_noeud(it, rappel_traverse)
    }
}

/** \} */


/* Résultat du parsage (« parsat », de « parser » + « -at ») d'un document HTML. */
ParsatHTML :: struct {
    /* Le document qui fut parsé. */
    document: *Document
    /* Les différentes erreurs parsage. */
    erreurs: [..]chaine
    /* Le temps de parsage du document, en microsecondes. */
    temps: z64
}

détruit :: fonc (parsat: &ParsatHTML)
{
    si parsat.document {
        détruit_noeud(parsat.document)
    }
    déloge(parsat.erreurs)
}

/* Parse un document HTML depuis une chaine de caractère. */
parse_document :: fonc (document: chaine) -> ParsatHTML
{
    parseuse := initialise_parseuse(document)
    diffère détruit_parseuse(*parseuse)

    début := maintenant_précis()
    parse(*parseuse)
    temps := début.temps_écoulé_microsecondes()

    parsat: ParsatHTML
    parsat.document = parseuse.document
    parsat.temps = temps
    parsat.erreurs = tableau_vole_mémoire(parseuse.erreurs)
    retourne parsat
}
