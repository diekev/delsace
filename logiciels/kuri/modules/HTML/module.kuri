// Module de parsage de code HTML
// Spec : https://html.spec.whatwg.org/multipage/parsing.html

importe Chaine
importe Fondation
importe Temps

charge "élément"
charge "attribut"
charge "comparaison"
charge "document"
charge "entités"
charge "entropisation"
charge "formattage"
charge "formulaire"
charge "lexage"
charge "liste_éléments_actifs_formattage"
charge "noeud"
charge "parsage"
charge "pile_éléments_ouverts"
charge "tags_html"

imprime_noeud :: fonc (noeud: *Noeud, tab: z32) -> rien
{
    imprime_tabulation(tab)

    si noeud.genre == GenreNoeud.Texte {
        texte := noeud comme *NoeudTexte
        imprime("%\n", texte.données)
    }
    sinon si noeud.genre == GenreNoeud.Élément {
        élément := noeud comme *Élément
        imprime("<%", élément.texte)

        pour élément.attributs {
            imprime(" %=%", it.nom, it.valeur)
        }

        imprime(">\n")
    }
    sinon si noeud.genre == GenreNoeud.Commentaire {
        commentaire := noeud comme *NoeudCommentaire
        imprime("<!--%-->\n", commentaire.données)
    }
    sinon {
        imprime("%\n", noeud.genre)
    }

    pour noeud.enfants {
        imprime_noeud(it, tab + 1)
    }
}

imprime_document :: fonc (document: *Document)
{
    imprime("============== Document ==============\n")

    pour document.enfants {
        imprime_noeud(it, 0)
    }
}

rassemble_texte_noeud :: fonc (noeud: *Noeud, rappel_pour_texte: fonc(*NoeudTexte, eini)(rien), données: eini)
{
    pour noeud.enfants {
        si it.genre == GenreNoeud.Texte {
            texte := it comme *NoeudTexte
            rappel_pour_texte(texte, données)
        }
        sinon {
            rassemble_texte_noeud(it, rappel_pour_texte, données)
        }
    }
}

/* Fonction auxillaire pour extraire le texte d'un noeud et de ses enfants. */
extrait_texte_noeud :: fonc (noeud: *Noeud) -> chaine
{
    ExtractriceTexte :: struct {
        texte: ChaineUTF16
    }

    rappel_pour_extraction_texte :: fonc (noeud_texte: *NoeudTexte, données: eini)
    {
        extractrice := mémoire(données.pointeur comme **ExtractriceTexte)

        pour noeud_texte.données.unités {
            tableau_ajoute(*extractrice.texte.unités, it)
        }
    }

    extractrice: ExtractriceTexte
    diffère détruit_chaine(extractrice.texte)

    rassemble_texte_noeud(noeud, rappel_pour_extraction_texte, *extractrice)

    retourne imprime_chaine("%", extractrice.texte)
}

/* ------------------------------------------------------------------------- */
/** \nom Traversée, visite, d'un noeud et de ses enfants.
 * \{ */

DécisionTraversée :: énum {
    /* Continue la traversée vers les enfants du noeuds. */
    CONTINUE
    /* Ignore les enfants de ce noeuds. */
    IGNORE_ENFANTS
}

traverse_noeud :: fonc (noeud: *Noeud, rappel_traverse: fonc(*Noeud, $T)(DécisionTraversée), données: T)
{
    pour noeud.enfants {
        décision := rappel_traverse(it, données)
        si décision == DécisionTraversée.IGNORE_ENFANTS {
            continue
        }

        traverse_noeud(it, rappel_traverse, données)
    }
}

/* Version de traverse_noeud sans données utilisateur spécifique. */
traverse_noeud :: fonc (noeud: *Noeud, rappel_traverse: fonc(*Noeud)(DécisionTraversée))
{
    pour noeud.enfants {
        décision := rappel_traverse(it)
        si décision == DécisionTraversée.IGNORE_ENFANTS {
            continue
        }

        traverse_noeud(it, rappel_traverse)
    }
}

/** \} */

// ces fonctions devraient plutôt être dans noeud.kuri, mais nous avons des
// dépendances cycliques entre les modules que le langage ne sait pas encore gérer
détruit_noeud_selon_genre :: fonc (noeud: *Noeud)
{
    discr noeud.genre {
        Invalide {
            déloge(noeud)
        }
        Document {
            document := noeud comme *Document
            déloge(document)
        }
        Commentaire {
            commentaire := noeud comme *NoeudCommentaire
            détruit_chaine(commentaire.données)
            déloge(commentaire)
        }
        Élément {
            élément := noeud comme *Élément
            // Ne pas détruire le texte, c'est un pointeur partagé !
            déloge(élément)
        }
        Texte {
            texte := noeud comme *NoeudTexte
            détruit_chaine(texte.données)
            déloge(texte)
        }
        Doctype {
            type_document := noeud comme *DocumentType
            déloge(type_document)
        }
    }
}

détruit_noeud :: fonc (noeud: *Noeud)
{
    détruit_attributs(noeud.attributs)
    déloge_tableau_et_ses_éléments(noeud.enfants, détruit_noeud)
    détruit_noeud_selon_genre(noeud)
}

/* Résultat du parsage (« parsat », de « parser » + « -at ») d'un document HTML. */
ParsatHTML :: struct {
    /* Le document qui fut parsé. */
    document: *Document
    /* Les différentes erreurs parsage. */
    erreurs: [..]chaine
    /* Le temps de parsage du document, en microsecondes. */
    temps: z64
}

détruit :: fonc (parsat: &ParsatHTML)
{
    détruit_noeud(parsat.document)
    déloge(parsat.erreurs)
}

/* Parse un document HTML depuis une chaine de caractère. */
parse_document :: fonc (document: chaine) -> ParsatHTML
{
    parseuse := initialise_parseuse(document)
    diffère détruit_parseuse(*parseuse)

    début := maintenant_précis()
    parse(*parseuse)
    temps := début.temps_écoulé_microsecondes()

    parsat: ParsatHTML
    parsat.document = parseuse.document
    parsat.temps = temps
    parsat.erreurs = tableau_vole_mémoire(parseuse.erreurs)
    retourne parsat
}
