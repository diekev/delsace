importe Chaine
importe #inemployé CSS
importe Ensemble
importe Fondation
importe Image
importe Internet

/* ------------------------------------------------------------------------- */
/** \nom Document.
 * https://dom.spec.whatwg.org/#document
 * \{ */

Encodage :: énum {
    UTF8
    UTF8LE
    UTF8BE
}

ModeDocument :: énum {
    NO_QUIRKS
    QUIRKS
    LIMITED_QUIRKS
}

formatte_mode_document :: fonc (enchaineuse: *Enchaineuse, mode: *ModeDocument)
{
    saufsi mode {
        retourne
    }
    discr mémoire(mode) {
        NO_QUIRKS {
            ajoute_au_tampon(enchaineuse, "no-quirks")
        }
        QUIRKS {
            ajoute_au_tampon(enchaineuse, "quirks")
        }
        LIMITED_QUIRKS {
            ajoute_au_tampon(enchaineuse, "limited-quirks")
        }
    }
} @FormattageImpression

DOMHighResTimeStamp :: r64

/* https://html.spec.whatwg.org/multipage/dom.html#document-load-timing-info */
DocumentLoadTimingInfo :: struct {
    navigation_start_time: z64
    DOM_interactive_time: DOMHighResTimeStamp
    DOM_content_loaded_event_start_time: DOMHighResTimeStamp
    DOM_content_loaded_event_end_time: DOMHighResTimeStamp
    DOM_complete_time: DOMHighResTimeStamp
    load_event_start_time: DOMHighResTimeStamp
    load_event_end_time: DOMHighResTimeStamp
}

/* https://html.spec.whatwg.org/multipage/dom.html#document-unload-timing-info */
DocumentUnloadTimingInfo :: struct {
    unload_event_start_time: DOMHighResTimeStamp
    unload_event_end_time: DOMHighResTimeStamp
}

ÉtatRequêteImage :: énum {
    NonCommencé
    EnCours
    Échec
    Succès
}

ImageDisponible :: struct {
    données: ImageIO
    url: chaine
    état: ÉtatRequêteImage
}

Document :: struct {
    empl base: Noeud
    table = *table_virtuelle_document

    type_contenu: chaine
    type: chaine
    url: URI
    encodage: Encodage
    mode: ModeDocument
    permets_racines_shadow_déclaratives: bool

    pending_parsing_blocking_script: *HTMLScriptElement
    scripts_that_will_execute_as_soon_as_possible: [..]*HTMLScriptElement
    scripts_that_will_execute_in_order_as_soon_as_possible: [..]*HTMLScriptElement
    scripts_that_will_execute_when_the_document_has_finished_parsing: [..]*HTMLScriptElement

    la_parseuse_ne_peut_changer_le_mode: bool
    ready_for_post_load_tasks: bool
    page_showing: bool

    origin: Origin
    browsing_context: *BrowsingContext
    permissions_policy: PermissionsPolicy
    active_sandboxing_flags: SandboxingFlags
    load_timing_info: DocumentLoadTimingInfo
    about_base_url: Optionnel(URI)

    cross_origin_opener_policy: CrossOriginOpenerPolicy

    referrer: chaine /* Peut être vide. */

    current_document_readiness: chaine

    is_initial_about_blank: bool
    latest_entry: *SessionHistoryEntry

    unload_counter: z32

    /* CSS. */
    feuille_de_style_agent: *CSS.CSSStyleSheet
    feuilles_de_style: [..]*CSS.CSSStyleSheet
    arbre_de_disposition: *CSS.ArbreDeDisposition
    globales_dessin: *CSS.GlobalesDessin
    résolveuse_style: *CSS.RésolveuseStyle
    /* Nous accumulons les changements via les différentes fonctions d'ajournement/invalidation,
     * et remettons ceci à zéro lors des rendus via "CSS.donne_arbre_de_disposition_ajourné". */
    ajournement_arbre := CSS.AjournementArbre.Construction

    noeud_survolé: *Élément
    /* L'élément sous la souris lors des pressions et relâchement. */
    élément_en_cours_d_activation: *Élément

    images_disponibles: [..]ImageDisponible

    script_blocking_style_sheet_set: [..]*CSS.CSSStyleSheet
}

#portée_fichier

table_virtuelle_document := TableVirtuelleÉlément(
    classe = Document,
    sur_destruction = document_sur_destruction
)

document_sur_destruction :: fonc (base: *PlatformObject)
{
    document := base comme *Document
    CSS.détruit_arbre(document.arbre_de_disposition)
    CSS.détruit_globales_dessin(document.globales_dessin)
    CSS.détruit_résolveuse(document.résolveuse_style)
    déloge_tableau_et_ses_éléments(document.feuilles_de_style, CSS.détruit_feuille_de_style)

    pour document.images_disponibles {
        déloge(it.url)
        IMG_detruit_image(*it.données)
    }

    détruit_uri(document.url)
    déloge(document.images_disponibles)
    déloge(document)
}

#portée_export

crée_noeud_document :: fonc () -> *Document
{
    document := loge(Document)
    document.genre = GenreNoeud.Document
    document.encodage = Encodage.UTF8
    document.mode = ModeDocument.NO_QUIRKS
    document.type_contenu = "application/xml"
    document.type = "xml"
    document.url = URI("about:blank")
    document.document = document

    retourne document
}

est_en_mode_quirks :: fonc (document: *Document) -> bool
{
    retourne document.mode == ModeDocument.QUIRKS
}

/* https://html.spec.whatwg.org/multipage/iframe-embed-object.html#an-iframe-srcdoc-document */
est_iframe_srcdoc :: fonc (document: *Document) -> bool
{
    retourne document.url.uri == "about:srcdoc"
}

/* https://html.spec.whatwg.org/multipage/document-sequences.html#fully-active */
est_fully_active :: fonc (document: *Document @inutilisée) -> bool
{
    /* À FAIRE : A Document d is said to be fully active when d is the active document
     *           of a navigable navigable, and either navigable is a top-level traversable
     *           or navigable's container document is fully active. */
    retourne faux
}

/* https://html.spec.whatwg.org/multipage/urls-and-fetching.html#document-base-url */
donne_base_url :: fonc (document: *Document) -> URI
{
    // À FAIRE : implémente
    retourne document.url
}

donne_doctype :: fonc (document: *Document) -> *NoeudTypeDocument
{
    pour document.enfants {
        si it.genre == GenreNoeud.TypeDocument {
            retourne it comme *NoeudTypeDocument
        }
    }
    retourne nul
}

donne_title_element :: fonc (document: *Document) -> *Élément
{
    retourne donne_premier_élément_de_type(document, TAG_title)
}

donne_premier_élément_de_type :: fonc (noeud: *Noeud, tag: DOMString) -> *Élément
{
    si noeud.est_élément_type(tag) {
        retourne noeud comme *Élément
    }

    pour noeud.enfants {
        élément := donne_premier_élément_de_type(it, tag)
        si élément {
            retourne élément
        }
    }

    retourne nul
}

/* https://dom.spec.whatwg.org/#document-element */
donne_élément_document :: fonc (document: *Document) -> *Élément
{
    pour document.enfants {
        si it.est_élément() {
            retourne it comme *Élément
        }
    }
    retourne nul
}

/* https://html.spec.whatwg.org/multipage/dom.html#dom-tree-accessors */
donne_élément_html :: fonc (document: *Document) -> *HTMLHtmlElement
{
    élément := donne_élément_document(document)
    si élément && élément.local_name == TAG_html {
        retourne élément comme *HTMLHtmlElement
    }
    retourne nul
}

/* https://html.spec.whatwg.org/multipage/dom.html#the-body-element-2 */
donne_élément_body :: fonc (document: *Document) -> *Élément
{
    élément_html := document.donne_élément_html()
    si élément_html {
        pour élément_html.enfants {
            si it.est_élément_type(TAG_body) || it.est_élément_type(TAG_frameset) {
                retourne it comme *Élément
            }
        }
    }
    retourne nul
}

/* https://html.spec.whatwg.org/multipage/parsing.html#ready-for-post-load-tasks */
mark_as_ready_for_post_load_tasks :: fonc (document: *Document)
{
    document.ready_for_post_load_tasks = vrai
}

/* https://html.spec.whatwg.org/multipage/dom.html#update-the-current-document-readiness */
update_the_current_document_readiness :: fonc (document: *Document, readiness_value: chaine)
{
    // 1. If document's current document readiness equals readinessValue, then return.
    si document.current_document_readiness == readiness_value {
        retourne
    }

    // 2. Set document's current document readiness to readinessValue.
    document.current_document_readiness = readiness_value

    // À FAIRE : 3. If document is associated with an HTML parser, then:

        // 1. Let now be the current high resolution time given document's relevant global object.

        // 2. If readinessValue is "complete", and document's load timing info's DOM complete time is 0, then set document's load timing info's DOM complete time to now.

        // 3. Otherwise, if readinessValue is "interactive", and document's load timing info's DOM interactive time is 0, then set document's load timing info's DOM interactive time to now.

    // À FAIRE : 4. Fire an event named readystatechange at document.
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSS, Interface
 * \{ */

/* À FAIRE : standardise https://drafts.csswg.org/cssom/#add-a-css-style-sheet */
ajoute_feuille_de_style :: fonc (document: *Document, feuille_de_style: *CSS.CSSStyleSheet)
{
    tableau_ajoute(*document.feuilles_de_style, feuille_de_style)
}

supprime_feuille_de_style_associée :: fonc (document: *Document, élément: *Élément)
{
    pour document.feuilles_de_style {
        si it.owner_node != élément {
            continue
        }

        tableau_supprime_index(*document.feuilles_de_style, index_it)
        CSS.détruit_feuille_de_style(it)
        arrête
    }
}

définis_noeud_survolé :: fonc (document: *Document, noeud: *Élément)
{
    si document.noeud_survolé == noeud {
        retourne
    }

    si document.noeud_survolé {
        document.noeud_survolé.pseudo_classes.Hover = faux

        noeud_courant := document.noeud_survolé
        tantque noeud_courant != nul {
            noeud_courant.pseudo_classes.Hover = faux
            parent := noeud_courant.parent
            saufsi parent.est_élément() {
                arrête
            }
            noeud_courant = parent comme *Élément
        }
    }

    si noeud {
        noeud.pseudo_classes.Hover = vrai

        noeud_courant := noeud
        tantque noeud_courant != nul {
            noeud_courant.pseudo_classes.Hover = vrai
            parent := noeud_courant.parent
            saufsi parent.est_élément() {
                arrête
            }
            noeud_courant = parent comme *Élément
        }
    }

    ancien_noeud := document.noeud_survolé
    document.noeud_survolé = noeud

    ancêtre := donne_ancêtre_élément_commun(ancien_noeud, noeud)
    assert(ancêtre != nul)
    invalide_style_depuis_élément(document, ancêtre)

    user_agent := document.donne_user_agent()
    si user_agent {
        user_agent.notifie_élément_survolé()
    }
}

donne_lien_survolé :: fonc (document: *Document) -> *HTMLAnchorElement
{
    noeud := document.noeud_survolé

    tantque noeud != nul {
        si noeud.local_name == TAG_a {
            retourne noeud comme *HTMLAnchorElement
        }

        parent := noeud.parent
        saufsi parent.est_élément() {
            retourne nul
        }

        noeud = parent comme *Élément
    }

    retourne nul
}

donne_ancêtre_élément_commun :: fonc (a: *Élément, b: *Élément) -> *Élément
{
    si a == nul {
        retourne b
    }

    si b == nul {
        retourne a
    }

    ancêtres_de_a: [..]*Élément
    diffère déloge(ancêtres_de_a)

    noeud := a
    boucle {
        ensemble_ajoute(*ancêtres_de_a, noeud)
        saufsi noeud.parent.est_élément() {
            arrête
        }
        noeud = noeud.parent comme *Élément
    }

    noeud = b

    boucle {
        si ensemble_possède(*ancêtres_de_a, noeud) {
            retourne noeud
        }

        saufsi noeud.parent.est_élément() {
            arrête
        }
        noeud = noeud.parent comme *Élément
    }

    retourne nul
}

/* Invalide le style de manière générale. */
invalide_style :: fonc (document: *Document)
{
    document.définis_ajournement_arbre(CSS.AjournementArbre.Construction)
}

/* Invalide le style suite à la modification d'une pseudo-classe. */
invalide_style :: fonc (document: *Document, élément: *Élément, pseudo_classe: CSS.PseudoClasses)
{
    saufsi document.résolveuse_style {
        retourne
    }

    saufsi CSS.possède_règle_avec_pseudo_classe(document.résolveuse_style, pseudo_classe) {
        retourne
    }

    invalide_style_depuis_élément(document, élément)
}

/* Invalide le style suite au redimensionnement de la fenêtre. */
invalide_style_pour_redimensionnement :: fonc (document: *Document, largeur: z32, hauteur: z32)
{
    saufsi document.résolveuse_style {
        retourne
    }

    ajournement := CSS.donne_ajournement_pour_redimensionnement(document.résolveuse_style, largeur, hauteur)
    document.définis_ajournement_arbre(ajournement)
}

/* Invalide le style suite au changement de la valeur de l'attribut "id". */
invalide_style_pour_id :: fonc (document: *Document, élément: *Élément, vieil_id: *DOMString, nouvel_id: *DOMString)
{
    saufsi document.résolveuse_style {
        retourne
    }

    résolveuse := document.résolveuse_style
    possède_règle := faux
    si vieil_id && CSS.possède_règle_avec_id(résolveuse, mémoire(vieil_id)) {
        possède_règle = vrai
    }

    si nouvel_id && CSS.possède_règle_avec_id(résolveuse, mémoire(nouvel_id)) {
        possède_règle = vrai
    }

    saufsi possède_règle {
        retourne
    }

    invalide_style_depuis_élément(document, élément)
}

/* Invalide le style suite au changement de la valeur de l'attribut spécifié. */
invalide_style_pour_attribut :: fonc (document: *Document, élément: *Élément, attribut: DOMString)
{
    saufsi document.résolveuse_style {
        retourne
    }

    résolveuse := document.résolveuse_style
    saufsi CSS.possède_règle_avec_attribut(résolveuse, attribut) {
        retourne
    }

    invalide_style_depuis_élément(document, élément)
}

invalide_style_depuis_élément :: fonc (document: *Document, élément: *Élément)
{
    saufsi document.résolveuse_style {
        retourne
    }

    ajournement := donne_ajournement_depuis_élément(document.résolveuse_style, élément)
    document.définis_ajournement_arbre(ajournement)
}

invalide_style_pour_image :: fonc (document: *Document)
{
    saufsi document.résolveuse_style {
        retourne
    }
    document.définis_ajournement_arbre(CSS.AjournementArbre.Dessin)
}

donne_ajournement_depuis_élément :: fonc (résolveuse: *CSS.RésolveuseStyle, élément: *Élément) -> CSS.AjournementArbre
{
    résultat := CSS.donne_ajournement_pour_changement_style(résolveuse, élément)

    si résultat == CSS.AjournementArbre.zéro && élément.noeud_de_disposition == nul {
        /* Nous avons, par exemple, un noeud qui est "display: none", et reste "display: none". */
        retourne résultat
    }

    pour élément.enfants {
        saufsi it.est_élément() {
            continue
        }

        résultat |= donne_ajournement_depuis_élément(résolveuse, it comme *Élément)
    }

    retourne résultat
}

/* https://html.spec.whatwg.org/multipage/semantics.html#has-a-style-sheet-that-is-blocking-scripts */
has_a_style_sheet_that_is_blocking_scripts :: fonc (document: *Document) -> bool
{
    // 1. If document's script-blocking style sheet set is not empty, then return true.
    si document.script_blocking_style_sheet_set.taille != 0 {
        retourne vrai
    }

    // 2. If document's node navigable is null, then return false.
    navigable := document.donne_node_navigable()
    si navigable == nul {
        retourne faux
    }

    // 3. Let containerDocument be document's node navigable's container document.
    container_document := donne_container_document(navigable)

    // 4. If containerDocument is non-null and containerDocument's script-blocking
    //    style sheet set is not empty, then return true.
    si container_document != nul && container_document.script_blocking_style_sheet_set.taille != 0 {
        retourne vrai
    }

    // 5. Return false.
    retourne faux
}

/* https://html.spec.whatwg.org/multipage/semantics.html#has-no-style-sheet-that-is-blocking-scripts */
has_no_style_sheet_that_is_blocking_scripts :: fonc (document: *Document) -> bool
{
    retourne !has_a_style_sheet_that_is_blocking_scripts(document)
}

#portée_fichier

définis_ajournement_arbre :: fonc (document: *Document, ajournement: CSS.AjournementArbre)
{
    document.ajournement_arbre |= ajournement
    user_agent := document.donne_user_agent()
    si user_agent {
        user_agent.requiers_rendu()
    }
}

#portée_export

donne_user_agent :: fonc (document: *Document) -> *UserAgent
{
    browsing_context := document.browsing_context
    saufsi browsing_context {
        /* Permet d'appeler cette fonction en dehors d'un contexte de navigation. */
        retourne nul
    }

    assert(browsing_context.group != nul)
    assert(browsing_context.group.user_agent != nul)
    retourne browsing_context.group.user_agent
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Images.
 * \{ */

donne_image_pour_url :: fonc (document: *Document, url: chaine) -> ImageDisponible
{
    pour document.images_disponibles {
        si it.url == url {
            retourne it
        }
    }

    résultat: ImageDisponible
    résultat.url = copie_chaine(url)
    résultat.état = ÉtatRequêteImage.NonCommencé
    tableau_ajoute(*document.images_disponibles, résultat)
    retourne résultat
}

définis_état_requête_image :: fonc (document: *Document, url: chaine, état: ÉtatRequêteImage)
{
    pour & document.images_disponibles {
        si it.url == url {
            it.état = état
            retourne
        }
    }
}

définis_données_image :: fonc (document: *Document, url: chaine, données: ImageIO)
{
    pour & document.images_disponibles {
        si it.url == url {
            it.données = données
            it.état = ÉtatRequêteImage.Succès
            retourne
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudTypeDocument.
 * https://dom.spec.whatwg.org/#documenttype
 * \{ */

NoeudTypeDocument :: struct {
    empl base: Noeud
    table = *table_virtuelle_type_document

    nom: DOMString
    public_id: DOMString
    system_id: DOMString
}

crée_noeud_doctype :: fonc (document: *Document) -> *NoeudTypeDocument
{
    doctype := loge(NoeudTypeDocument)
    doctype.genre = GenreNoeud.TypeDocument
    doctype.document = document

    retourne doctype
}

#portée_fichier

table_virtuelle_type_document := TableVirtuelleÉlément(
    classe = NoeudTypeDocument,
    sur_destruction = type_document_sur_destruction
)

type_document_sur_destruction :: fonc (base: *PlatformObject)
{
    type_document := base comme *NoeudTypeDocument
    déloge(type_document)
}

#portée_export

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudFragmentDocument.
 * https://dom.spec.whatwg.org/#documentfragment
 * \{ */

NoeudFragmentDocument :: struct {
    empl base: Noeud
    table = *table_virtuelle_fragment_document
}

crée_noeud_fragment_document :: fonc (document: *Document) -> *NoeudFragmentDocument
{
    doctype := loge(NoeudFragmentDocument)
    doctype.genre = GenreNoeud.FragmentDocument
    doctype.document = document

    retourne doctype
}

#portée_fichier

table_virtuelle_fragment_document := TableVirtuelleÉlément(
    classe = NoeudFragmentDocument,
    sur_destruction = fragment_document_sur_destruction
)

fragment_document_sur_destruction :: fonc (base: *PlatformObject)
{
    fragment_document := base comme *NoeudFragmentDocument
    déloge(fragment_document)
}

#portée_export

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDonnéesCaractères.
 * Correspond à CharacterDataNode
 * https://dom.spec.whatwg.org/#characterdata
 * \{ */

NoeudDonnéesCaractères :: struct {
    empl base: Noeud

    données: ChaineUTF16
}

est_données_caractères :: fonc (noeud: Noeud)
{
    discr noeud.genre {
        Commentaire,
        Texte,
        ProcessingInstruction {
            retourne vrai
        }
        sinon {
            retourne faux
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudTexte.
 * https://dom.spec.whatwg.org/#text
 * \{ */

NoeudTexte :: struct {
    empl base_données: NoeudDonnéesCaractères
    table = *table_virtuelle_texte
}

crée_noeud_texte :: fonc (document: *Document) -> *NoeudTexte
{
    texte := loge(NoeudTexte)
    texte.genre = GenreNoeud.Texte
    texte.document = document

    retourne texte
}

/* https://html.spec.whatwg.org/multipage/dom.html#inter-element-whitespace */
est_espace_blanche_entre_élément :: fonc (texte: *NoeudTexte) -> bool
{
    retourne est_vide_ou_uniquement_espaces_blanches(texte.données)
}

#portée_fichier

table_virtuelle_texte := TableVirtuelleÉlément(
    classe = NoeudTexte,
    sur_destruction = texte_sur_destruction
)

texte_sur_destruction :: fonc (base: *PlatformObject)
{
    texte := base comme *NoeudTexte
    détruit_chaine(texte.données)
    déloge(texte)
}

#portée_export

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudSectionCDATA.
 * https://dom.spec.whatwg.org/#cdatasection
 * \{ */

NoeudSectionCDATA :: struct {
    empl base_texte: NoeudTexte
    table = *table_virtuelle_section_cdata
}

crée_noeud_section_cdata :: fonc (document: *Document) -> *NoeudTexte
{
    texte := loge(NoeudSectionCDATA)
    texte.genre = GenreNoeud.Section_CDATA
    texte.document = document

    retourne texte
}

#portée_fichier

table_virtuelle_section_cdata := TableVirtuelleÉlément(
    classe = NoeudSectionCDATA,
    sur_destruction = section_cdata_sur_destruction
)

section_cdata_sur_destruction :: fonc (base: *PlatformObject)
{
    section_cdata := base comme *NoeudSectionCDATA
    détruit_chaine(section_cdata.données)
    déloge(section_cdata)
}

#portée_export

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudCommentaire.
 * https://dom.spec.whatwg.org/#comment
 * \{ */

NoeudCommentaire :: struct {
    empl base_données: NoeudDonnéesCaractères
    table = *table_virtuelle_commentaire
}

crée_noeud_commentaire :: fonc (document: *Document, données: ChaineUTF16) -> *NoeudCommentaire
{
    commentaire := loge(NoeudCommentaire)
    commentaire.genre = GenreNoeud.Commentaire
    commentaire.document = document
    commentaire.données = copie_chaine(données)

    retourne commentaire
}

#portée_fichier

table_virtuelle_commentaire := TableVirtuelleÉlément(
    classe = NoeudCommentaire,
    sur_destruction = commentaire_sur_destruction
)

commentaire_sur_destruction :: fonc (base: *PlatformObject)
{
    commentaire := base comme *NoeudCommentaire
    détruit_chaine(commentaire.données)
    déloge(commentaire)
}

#portée_export

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudProcessingInstruction.
 * https://dom.spec.whatwg.org/#processinginstruction
 * \{ */

NoeudProcessingInstruction :: struct {
    empl base_données: NoeudDonnéesCaractères
    table = *table_virtuelle_processing_instruction

    cible: ChaineUTF16
}

crée_noeud_processing_instruction :: fonc (document: *Document, données: ChaineUTF16) -> *NoeudCommentaire
{
    résultat := loge(NoeudCommentaire)
    résultat.genre = GenreNoeud.Commentaire
    résultat.document = document
    résultat.données = copie_chaine(données)

    retourne résultat
}

#portée_fichier

table_virtuelle_processing_instruction := TableVirtuelleÉlément(
    classe = NoeudProcessingInstruction,
    sur_destruction = processing_instruction_sur_destruction
)

processing_instruction_sur_destruction :: fonc (base: *PlatformObject)
{
    processing_instruction := base comme *NoeudProcessingInstruction
    détruit_chaine(processing_instruction.données)
    détruit_chaine(processing_instruction.cible)
    déloge(processing_instruction)
}

#portée_export

/** \} */
