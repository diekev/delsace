importe Chaine
importe Compilatrice
importe Fondation
importe HTML
JS :: importe JavaScript
importe LigneCommande
importe SysFichier
importe Temps

// #exécute compile_pour_production()
// #exécute compile_pour_profilage()
// #exécute compile_pour_débogage()

principale :: fonc ()
{
    chemin := parse_argument_chemin()
    saufsi chemin {
        exit(1)
    }

    chm := CheminFichier(chemin)

    saufsi est_fichier_html(chm) {
        imprime("Le chemin '%' n'a pas l'air de pointer vers un fichier HTML !\n", args[1])
        exit(1)
    }

    diffère imprime("Mémoire utilisée : %o\n", mémoire_utilisée())
    diffère détruit_chaines_internées()

    _, contenu := contenu_fichier_texte(chm.chn)
    diffère déloge(contenu)

    imprime("%\n", contenu)

    taille_contenu := contenu.taille

    mv: JS.MachineVirtuelle
    JS.initialise_mv(nul, *mv)
    diffère JS.détruit_données_mv(*mv)

    début := maintenant_précis()
    parsat := parse_document(JS.donne_tas_de_mémoire(*mv), contenu)
    diffère détruit(parsat)

    contexte_formattage := initialise_contexte_formattage_minification()
    taille_minifiée := formatte_document(contexte_formattage, parsat.document, sortie_standarde())
    temps := début.temps_écoulé_microsecondes()

    imprime("\n")
    imprime("Taille originale : %o\n", FormatEntier(taille_contenu, séparation = 3))
    imprime("Taille minifiée  : %o\n", FormatEntier(taille_minifiée, séparation = 3))
    imprime("Durée de travail : %µ\n", FormatEntier(temps, séparation = 3))
}
