importe Chaine
importe Fondation
importe #inemployé HTML

/* CSS Object Model https://www.w3.org/TR/cssom-1/ */

/* ------------------------------------------------------------------------- */
/** \nom Style Sheets
 * https://www.w3.org/TR/cssom-1/#css-style-sheets
 * \{ */

/* https://www.w3.org/TR/cssom-1/#the-stylesheet-interface */
StyleSheet :: struct {
    /* "text/css" */
    type: CSSOMString
    location: *HTML.URL

    origine: OrigineCascade
}

CSSStyleSheet :: struct {
    empl base: StyleSheet

    owner_node: *HTML.Élément
    parent_style_sheet: *CSSStyleSheet
    owner_rule: *CSSRule
    rules: [..]*CSSRule
    alternate: bool
    origin_clean: bool
    disabled: bool
}

imprime_style_sheet :: fonc (stylesheet: *CSSStyleSheet)
{
    pour stylesheet.rules {
        si it.type == CSSRuleType.Style {
            style_rule := it comme *CSSStyleRule
            imprime_règle(style_rule)
        }
    }
}

détruit_feuille_de_style :: fonc (stylesheet: *CSSStyleSheet)
{
    détruit_rule_list(stylesheet.rules)
    HTML.détruit_url(stylesheet.location)
    déloge(stylesheet)
}

détruit_rule_list :: fonc (list: [..]*CSSRule)
{
    pour list {
        si it.type == CSSRuleType.Style {
            détruit_style_rule(it comme *CSSStyleRule)
        }
        sinon si it.type == CSSRuleType.Media {
            détruit_media_rule(it comme *CSSMediaRule)
        }
    }
    déloge(list)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSRule
 * https://www.w3.org/TR/cssom-1/#the-cssrule-interface
 * \{ */

// the following attribute and constants are historical
CSSRuleType :: énum n16 {
    Invalide :: 0
    Style :: 1
    Import :: 3
    Media :: 4
    FontFace :: 5
    Page :: 6
    Keyframes :: 7
    Keyframe :: 8
    Margin :: 9
    Namespace :: 10
    CounterStyle :: 11
    Supports :: 12
    FontFeatureValues :: 14
    Viewport :: 15
}

CSSRule :: struct {
    parent_rule: *CSSRule
    parent_style_sheet: *CSSStyleSheet

    type: CSSRuleType

    STYLE_RULE : n16 : 1
    CHARSET_RULE : n16 : 2
    IMPORT_RULE : n16 : 3
    MEDIA_RULE : n16 : 4
    FONT_FACE_RULE : n16 : 5
    PAGE_RULE : n16 : 6
    MARGIN_RULE : n16 : 9
    NAMESPACE_RULE : n16 : 10
}

donne_origine_cascade :: fonc (rule: *CSSRule) -> OrigineCascade
{
    saufsi rule.parent_style_sheet {
        retourne OrigineCascade.Author
    }
    retourne rule.parent_style_sheet.origine
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSStyleRule
 * \{ */

CSSStyleRule :: struct {
    empl base: CSSRule
    type = CSSRuleType.Style

    selector_list: SelectorList
    style: *CSSStyleDeclaration
}

détruit_style_rule :: fonc (rule: *CSSStyleRule)
{
    détruit_données_selector_list(*rule.selector_list)
    détruit_style_declaration(rule.style)
    déloge(rule)
}

imprime_règle :: fonc (règle: *CSSStyleRule)
{
    imprime("% {\n", règle.selector_list)
    imprime_style(règle.style)
    imprime("}\n")
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSStyleDeclaration
 * https://www.w3.org/TR/cssom-1/#css-declaration-blocks
 * https://www.w3.org/TR/cssom-1/#cssstyledeclaration
 * \{ */

CSSStyleDeclaration :: struct {
    déclarations: [..]*DéclarationPropriété
    /* The CSS rule that the CSS declaration block is associated with, if any, or null otherwise. */
    parent_css_rule: *CSSRule
    /* The Element that the CSS declaration block is associated with, if any, or null otherwise. */
    owner_node: *HTML.Élément
    /* Set if the object is a computed style declaration, rather than a specified style. */
    computed: bool
    /* Set when the CSS declaration block is updating the owner node’s style attribute. */
    updating: bool
}

détruit_style_declaration :: fonc (style: *CSSStyleDeclaration)
{
    saufsi style {
        retourne
    }

    pour style.déclarations {
        détruit_déclaration(it)
    }

    déloge(style.déclarations)
    déloge(style)
}

ajoute_déclaration :: fonc (style: *CSSStyleDeclaration, déclaration: *DéclarationPropriété)
{
    assert(déclaration.style_parent == nul)
    déclaration.style_parent = style
    tableau_ajoute(*style.déclarations, déclaration)
}

imprime_style :: fonc (style: *CSSStyleDeclaration)
{
    pour déclaration dans style.déclarations {
        imprime_déclaration(déclaration)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom DéclarationPropriété
 * https://www.w3.org/TR/cssom-1/#css-declarations
 * \{ */

DéclarationPropriété :: struct {
    info: *InfoTypePropriété
    style_parent: *CSSStyleDeclaration
    property_name: CSSOMString
    value: [..]ValeurDeStyle
    valeur_personnalisée: [..]ComponentValue
    important: bool
    case_sensitive: bool
    est_personnalisée: bool
}

détruit_déclaration :: fonc (déclaration: *DéclarationPropriété)
{
    pour * déclaration.value {
        détruit_valeur_de_style(it)
    }

    détruit_tableau_component_value(*déclaration.valeur_personnalisée)
    déloge(déclaration.value)
    déloge(déclaration)
}

imprime_déclaration :: fonc (déclaration: *DéclarationPropriété)
{
    imprime("    %:", déclaration.property_name)
    pour valeur dans déclaration.value {
        imprime(" %", valeur)
    }

    si déclaration.important {
        imprime(" !important")
    }

    imprime(";\n")
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSGroupingRule
 * https://www.w3.org/TR/cssom-1/#cssgroupingrule
 * \{ */

CSSGroupingRule :: struct {
    empl base_rule: CSSRule

    css_rules: [..]*CSSRule
}

détruit_données_grouping_rule :: fonc (rule: *CSSGroupingRule)
{
    détruit_rule_list(rule.css_rules)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSConditionalRule
 * https://www.w3.org/TR/css-conditional-3/#cssconditionrule
 * \{ */

CSSConditionalRule :: struct {
    empl grouping: CSSGroupingRule
}

détruit_données_conditional_rule :: fonc (rule: *CSSConditionalRule)
{
    détruit_données_grouping_rule(rule)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CSSMediaRule
 * https://www.w3.org/TR/css-conditional-3/#cssmediarule
 * \{ */

CSSMediaRule :: struct {
    empl cond: CSSConditionalRule
    type = CSSRuleType.Media

    media_list: *MediaList
}

détruit_données_media_rule :: fonc (rule: *CSSMediaRule)
{
    détruit_données_conditional_rule(rule)
}

détruit_media_rule :: fonc (rule: *CSSMediaRule)
{
    détruit_media_list(rule.media_list)
    détruit_données_media_rule(rule)
    déloge(rule)
}

/** \} */
