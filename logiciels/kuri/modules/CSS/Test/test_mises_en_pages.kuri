importe Chaine
importe CSS
importe Ensemble
importe Fondation
importe #inemployé HTML
importe #inemployé JavaScript
importe Processus
importe Structures
importe SysFichier
importe Temps

principale :: fonc ()
{
    logue_propriétés_non_supportées = faux
    logue_erreurs_de_parsage = faux

    args := arguments_ligne_commande()

    si args.taille == 2 {
        si args[1] != "--ajourne-fichiers" {
            imprime("Argument '%' inconnu\n", args[1])
            exit(1)
        }

        ajourne_fichiers()
        exit(0)
    }

    si args.taille == 3 {
        si args[1] != "--vis" {
            imprime("Argument '%' inconnu\n", args[1])
            exit(1)
        }

        chemin_navigateur := CheminFichier(args[2])

        saufsi est_un_fichier_régulier(chemin_navigateur) {
            imprime("\"%\" n'est pas un fichier\n", args[2])
            exit(1)
        }

        visualise_fichiers(chemin_navigateur)
        exit(0)
    }

    si args.taille > 3 {
        imprime("Erreur : trop d'arguments.\n")
        imprime("Utilisation : % [--ajourne-fichiers | --vis NAVIGATEUR]\n", args[0])
        exit(1)
    }

    exit(test_mises_en_pages() comme z32)
}

test_mises_en_pages :: fonc () -> bool
{
    début := maintenant_précis()

    chemins_sources := donne_chemins_fichiers_sources()
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    chemins_sorties := donne_chemins_fichiers_sorties(chemins_sources)
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    échecs: [..]CheminFichier
    diffère déloge(échecs)

    nombre_de_tests_passants: z32

    pour chemins_sources {
        imprime(".")

        impression := donne_texte_pour_sortie(it)
        diffère déloge(impression)

        chemin_sortie := chemins_sorties[index_it]

        sortie := contenu_fichier_texte(chemin_sortie)
        diffère déloge(sortie)

        si impression != sortie {
            tableau_ajoute(*échecs, it)
        }
        sinon {
            nombre_de_tests_passants += 1
        }
    }

    imprime("\n")

    fin := début.temps_écoulé_millisecondes()

    si échecs.taille != 0 {
        pour échecs {
            imprime("[ \x1b[31mÉCHEC\x1b[37m ] : %\n", it)
        }
    }
    sinon {
        imprime("[ \x1b[32mPASSE\x1b[37m ]\n")
    }

    imprime("\n")
    imprime("Tests    : % réussis, % en tout\n", nombre_de_tests_passants, chemins_sources.taille)
    imprime("Temps    : %ms\n", fin)
    imprime("\n")

    retourne échecs.taille != 0
}

ajourne_fichiers :: fonc ()
{
    chemins_sources := donne_chemins_fichiers_sources()
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    chemins_sorties := donne_chemins_fichiers_sorties(chemins_sources)
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    pour chemins_sources {
        impression := donne_texte_pour_sortie(it)
        diffère déloge(impression)

        chemin_sortie := chemins_sorties[index_it]

        original := contenu_fichier_texte(chemin_sortie)
        diffère déloge(original)
    
        si impression == original {
            continue
        }

        fichier := tente ouvre_fichier_crée_si_non_existant(chemin_sortie, pour_écriture_tronquée) piège err {
            imprime("impossible de créer le fichier '%' %\n", chemin_sortie, err)
            retourne
        }

        _ := écris(*fichier, impression)
    }
}

visualise_fichiers :: fonc (chemin_navigateur: CheminFichier)
{
    chemins_sources := donne_chemins_fichiers_sources()
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    chemins_sorties := donne_chemins_fichiers_sorties(chemins_sources)
    diffère déloge_tableau_et_ses_éléments(chemins_sources, détruit_chemin)

    chemins_échouants: [..]CheminFichier
    diffère déloge(chemins_échouants)

    chemin_courant := tente chemin_courant() piège err {
        imprime("Impossible de déterminer le chemin courant : %\n", err)
        retourne
    }

    pour chemins_sources {
        impression := donne_texte_pour_sortie(it)
        // diffère déloge(impression)

        chemin_sortie := chemins_sorties[index_it]

        original := contenu_fichier_texte(chemin_sortie)
        // diffère déloge(original)

        si impression == original {
            continue
        }

        chm_abs := crée_chemin_absolu(chemin_courant, it)
        // diffère déloge(chm_abs.chn)

        commande := imprime_chaine("% %", chemin_navigateur, chm_abs)
        // diffère déloge(commande)

        imprime("\x1b[32m[% / %] Exécution de %\x1b[37m\n", index_it + 1, chemins_sources.taille, commande)

        saufsi exécute_commande_externe(commande) {
            imprime("Erreur lors de l'exécution de la commande\n")
            arrête
        }

        boucle {
            imprime("Appuyez sur entrée si le test passe, le fichier de sortie sera ajourné, sinon tapez \"non\" ou \"n\" pour passer au suivant\n")
            imprime("> ")

            entrée := tente entrée_standarde().lis_tout() piège nonatteignable
            diffère déloge(entrée)

            réponse := recule(entrée, 1)

            si réponse == "" {
                imprime("Ajournement du fichier de sortie...\n")
                fichier := tente ouvre_fichier_crée_si_non_existant(chemin_sortie, pour_écriture_tronquée) piège err {
                    imprime("impossible de créer le fichier '%' %\n", chemin_sortie, err)
                    retourne
                }

                _ := écris(*fichier, impression)
                continue it
            }

            si réponse == "non" || réponse == "n" {
                tableau_ajoute(*chemins_échouants, it)
                continue it
            }
        }
    }

    si chemins_échouants {
        imprime("Les fichiers suivants échous toujours\n")
        pour chemins_échouants {
            imprime("    %\n", it)
        }
    }
}

donne_chemins_fichiers_sources :: fonc () -> [..]CheminFichier
{
    résultat: [..]CheminFichier

    dossier_parent := CheminFichier(#chemin_de_ce_fichier).chemin_parent()
    diffère détruit_chemin(dossier_parent)
    dossier := dossier_parent / "fichiers/sources"

    dossiers_à_visiter: Pile(CheminFichier)
    dossiers_à_visiter.empile(dossier) 

    tantque !dossiers_à_visiter.est_vide() {
        dossier_courant := dossiers_à_visiter.dépile()
        diffère détruit_chemin(dossier_courant)

        pour ExploratriceFichier(dossier_courant) {
            si it.est_un_dossier() {
                dossiers_à_visiter.empile(copie_chemin(it))
                continue
            }

            si it.extension() != ".html" {
                continue
            }

            tableau_ajoute(*résultat, copie_chemin(it))
        }
    }

    retourne résultat
}

donne_chemins_fichiers_sorties :: fonc (sources: []CheminFichier) -> [..]CheminFichier
{
    résultat: [..]CheminFichier
    tableau_réserve(*résultat, sources.taille)

    pour sources {
        tableau_ajoute(*résultat, donne_chemin_pour_fichier_sortie(it))
    }

    retourne résultat
}

donne_chemin_pour_fichier_sortie :: fonc (chemin: CheminFichier) -> CheminFichier
{
    dossier := chemin_parent(chemin)
    diffère détruit_chemin(dossier)

    nom_fichier_texte := enchaine(chemin.nom_fichier(), ".txt")

    tmp := dossier / nom_fichier_texte
    diffère détruit_chemin(tmp)

    résultat: CheminFichier
    résultat.chn = remplace(tmp.chn, "fichiers/sources", "fichiers/sorties")
    retourne résultat
}

donne_texte_pour_sortie :: fonc (chemin: CheminFichier) -> chaine
{
    contenu := contenu_fichier_texte(chemin)
    diffère déloge(contenu)

    mv: JavaScript.MachineVirtuelle
    JavaScript.initialise_mv(nul, *mv)
    diffère JavaScript.détruit_données_mv(*mv)

    parsat := HTML.parse_document(JavaScript.donne_tas_de_mémoire(*mv), contenu)
    diffère HTML.détruit(parsat)

    largeur := 1920
    hauteur := 1080
    arbre := donne_arbre_de_disposition_ajourné(parsat.document, largeur, hauteur)
    saufsi arbre && arbre.racine {
        imprime("Impossible de crée un arbre de disposition")
        retourne ""
    }

    résultat: chaine

    ctx := contexte()
    ctx.format_réel.précision = 2
    pousse_contexte ctx {
        résultat = donne_chaine_impression_arbre(arbre)
    }

    retourne résultat
}
