importe Chaine
importe CSS
importe Ensemble
importe Fondation
HTML :: importe HTML
importe SysFichier

principale :: fonc ()
{
    // test_collection_règles()
    // test_fragmentation_texte()
    test_parsage_propriété()
}

CasParsage :: struct {
    entrée: []chaine
    sortie: []chaine
}

DonnéesTestParsage :: struct {
    contenu: chaine
    lignes: [..]chaine
    cas_parsage: [..]CasParsage
}

charge_cas_parsage :: fonc () -> DonnéesTestParsage
{
    chemin := "fichiers/parsage.css"
    succès, contenu := contenu_fichier_texte(chemin)
    saufsi succès {
        retourne DonnéesTestParsage()
    }

    lignes := divise(contenu, '\n')

    cas_parsage: [..]CasParsage

    indice := 0
    tantque indice < lignes.taille {
        si lignes[indice] == "// entrée" {
            indice += 1

            cas := tableau_ajoute_élément(*cas_parsage)
            cas.entrée.pointeur = lignes.pointeur + indice
            cas.entrée.taille = 1
            indice += 1

            tantque indice < lignes.taille {
                si lignes[indice] == "// sortie" {
                    indice += 1
                    arrête
                }

                cas.entrée.taille += 1
                indice += 1
            }

            cas.sortie.pointeur = lignes.pointeur + indice
            cas.sortie.taille = 1
            indice += 1

            tantque indice < lignes.taille {
                si lignes[indice] == "// entrée" {
                    arrête
                }

                cas.sortie.taille += 1
                indice += 1
            }

            continue
        }

        indice += 1
    }

    résultat: DonnéesTestParsage
    résultat.contenu = contenu
    résultat.lignes = lignes
    résultat.cas_parsage = cas_parsage
    retourne résultat
}

test_parsage_propriété :: fonc ()
{
    données_test := charge_cas_parsage()

    pour données_test.cas_parsage {
        entrée := enchaine(...it.entrée)
        sortie := enchaine(...it.sortie)

        style_entrée := parse_css_style_declaration(entrée)
        style_sortie := parse_css_style_declaration(sortie)

        saufsi sont_équivalents(style_entrée, style_sortie) {
            imprimeln("Les styles ne sont pas équivalents :\n")

            imprimeln("Le style en entrée est :")
            imprime_style(style_entrée)
            imprimeln("")

            imprimeln("Le style en sortie est :")
            imprime_style(style_sortie)
            imprimeln("")
        }
    }
}

sont_équivalents :: fonc (style1: *CSSStyleDeclaration, style2: *CSSStyleDeclaration) -> bool
{
    si style1.déclarations.taille != style2.déclarations.taille {
        retourne faux
    }

    pour décl1 dans style1.déclarations {
        décl2 := donne_déclaration(style2, décl1.property_name)
        si décl2 == nul {
            retourne faux
        }

        saufsi sont_équivalents(décl1, décl2) {
            imprime_déclaration(décl1)
            imprime_déclaration(décl2)
            retourne faux
        }
    }

    retourne vrai
}

sont_équivalents :: fonc (décl1: *DéclarationPropriété, décl2: *DéclarationPropriété) -> bool
{
    si décl1.est_personnalisée || décl2.est_personnalisée {
        // À FAIRE
        retourne faux
    }

    si décl1.value.taille != décl2.value.taille {
        retourne faux
    }

    pour valeur1, indice dans décl1.value {
        saufsi sont_mêmes_valeurs(valeur1, décl2.value[indice]) {
            retourne faux
        }
    }

    retourne vrai
}

parse_css_style_declaration :: fonc (texte: chaine) -> *CSSStyleDeclaration
{
    lexèmes := lèxe_style(texte)
    diffère détruit_lexèmes(*lexèmes)

    components := parse_a_list_of_component_values(lexèmes)
    diffère déloge(components)

    parseuse_decl := initialise_une_parseuse(components)

    content := consomme_une_liste_de_déclarations(parseuse_decl)
    si content.decls.taille == 0 {
        imprimeln("Impossible de préparser la déclaration : %", texte)
        exit(1)
    }

    ctx: ContexteParsageFeuilleDeStyle
    diffère détruit_données_contexte(*ctx)

    retourne parse_css_style_declaration(*ctx, content.decls)
}

test_collection_règles :: fonc ()
{
    stylesheet := HTML.donne_feuille_de_style_défaut()

    élément := crée_chaine_utf16("p")

    propriétés: PropriétésDeStyle

    règles := collecte_les_règles_appropriées(stylesheet, élément)

    imprime("% règle(s) pour %\n", règles.taille, élément)
    // pour règles {
    //     imprime_règle(it)
    // }

    pour règles {
        pour déclaration dans it.déclarations {
            ajoute_déclaration(*propriétés, déclaration)
        }
    }

    imprime("% déclaration(s)\n", propriétés.déclarations.taille)
}

test_fragmentation_texte :: fonc ()
{
    // texte := "début   test  \t   test   \t   \n  \n   \n \t   test \t  fin"
    texte := "The     \n Effects of CRTs"
    texte_utf16 := crée_chaine_utf16(texte)

    fragments: [..]FragmentTexte
    fragmente_texte_avec_espaces_blanches_ascii(texte_utf16, *fragments)
    imprime_fragments(fragments, "fragmente_texte_avec_espaces_blanches_ascii")

    collapse_séquences_d_espaces_blanches_autour_bris_segments(fragments)
    imprime_fragments(fragments, "collapse_séquences_d_espaces_blanches_autour_bris_segments")

    collapse_bris_segments(fragments, PROP_normal)
    imprime_fragments(fragments, "collapse_bris_segments")

    convertis_tabulations_en_espaces(fragments)
    imprime_fragments(fragments, "convertis_tabulations_en_espaces")

    collapse_espaces_consécutives(fragments)
    imprime_fragments(fragments, "collapse_espaces_consécutives")
}

imprime_fragments :: fonc (fragments: []FragmentTexte, message: chaine)
{
    imprime("%\n", message)
    pour fragments {
        si it.ignore_mais_préserve_soft_wrap_opportunity {
            continue
        }

        si it.est_espace_blanche && it.texte.unités[0] == 0x0A {
            imprime("'\\n'\n")
            continue
        }
        si it.est_espace_blanche && it.texte.unités[0] == '\t' comme n32 {
            imprime("'\\t'\n")
            continue
        }
        
        imprime("'%'\n", it.texte)
    }
}
