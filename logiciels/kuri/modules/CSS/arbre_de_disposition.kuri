importe Aléa
importe Chaine
importe Couleur
importe Ensemble
importe Fondation
importe Géométrie
importe #inemployé HTML
importe IGUMI
importe Image
importe Internet
importe Math
importe OpenGL
importe PeintureInterface
importe Triage
importe Typographie

/* ------------------------------------------------------------------------- */
/** \nom Arbre de disposition
 * \{ */

ArbreDeDisposition :: struct {
    racine: *NoeudDeDisposition

    noeuds: [..]*NoeudDeDisposition
    tampon_id: [..]n32
    largeur_vue: z32
    hauteur_vue: z32
}

détruit_arbre :: fonc (arbre: *ArbreDeDisposition)
{
    saufsi arbre {
        retourne
    }

    pour arbre.noeuds {
        déloge(it.enfants)
        si it.propriétés {
            détruit_propriétés(it.propriétés)
        }

        élément := it.donne_élément()
        si élément {
            élément.noeud_de_disposition = nul
        }

        si it.type == NoeudDeDispositionBloc {
            bloc := it comme *NoeudDeDispositionBloc
            détruit_lignes(bloc)
            déloge(bloc)
        }
        sinon si it.type == NoeudDeDispositionInline {
            noeud := it comme *NoeudDeDispositionInline
            détruit_lignes(noeud)
            déloge(noeud)
        }
        sinon si it.type == NoeudDeDispositionTexte {
            texte := it comme *NoeudDeDispositionTexte
            détruit_chaine(texte.texte)
            déloge(texte.morceaux)
            déloge(texte)
        }
        sinon si it.type == NoeudDeDispositionNouvelleLigne {
            bris := it comme *NoeudDeDispositionNouvelleLigne
            déloge(bris)
        }
        sinon si it.type == NoeudDeDispositionRemplacement {
            remplacement := it comme *NoeudDeDispositionRemplacement
            déloge(remplacement)
        }
        sinon {
            imprime("noeud inconnu !")
            déloge(it)
        }
    }

    déloge(arbre.tampon_id)
    déloge(arbre.noeuds)
    déloge(arbre)
}

crée_noeud :: fonc (arbre: *ArbreDeDisposition, $TypeNoeud: type_de_données) -> *TypeNoeud
{
    résultat := loge(TypeNoeud)
    résultat.id = arbre.noeuds.taille comme n32
    tableau_ajoute(*arbre.noeuds, résultat)
    retourne résultat
}

donne_élément_pour_position :: fonc (arbre: *ArbreDeDisposition, x: z32, y: z32) -> *HTML.Élément
{
    saufsi 0 <= x < arbre.largeur_vue && 0 <= y < arbre.hauteur_vue {
        retourne nul
    }

    tampon_id := arbre.tampon_id
    /* OpenGL est de bas en haut, donc inverse l'axe des y. */
    index := (arbre.hauteur_vue - y - 1) * arbre.largeur_vue + x

    index_noeud := tampon_id[index]

    noeud := arbre.noeuds[index_noeud]
    saufsi noeud {
        retourne nul
    }
    retourne noeud.donne_élément()
}

imprime_arbre :: fonc (arbre: *ArbreDeDisposition)
{
    saufsi arbre.racine {
        retourne
    }

    enchaineuse: Enchaineuse
    initialise_enchaineuse(*enchaineuse)
    diffère détruit_tampons(*enchaineuse)

    imprime_arbre(*enchaineuse, arbre.racine, 0)

    résultat := chaine_depuis_enchaineuse(*enchaineuse)
    diffère déloge(résultat)

    imprime("%\n", résultat)
}

imprime_arbre :: fonc (enchaineuse: *Enchaineuse, racine: *NoeudDeDisposition, profondeur: n32)
{
    indentation := donne_chaine_indentation(profondeur comme z32)

    imprime_dans_enchaineuse(enchaineuse, "%%", indentation, racine.donne_nom_classe())
    si racine.type == NoeudDeDispositionBloc {
        bloc := racine comme *NoeudDeDispositionBloc
        imprime_dans_enchaineuse(enchaineuse, " %", bloc.élément.local_name)
    }

    imprime_dans_enchaineuse(enchaineuse, " % % %x%\n", racine.modèle_de_boite.marge.x, racine.modèle_de_boite.marge.y, racine.modèle_de_boite.marge.largeur, racine.modèle_de_boite.marge.hauteur)

    pour racine.enfants {
        imprime_arbre(enchaineuse, it, profondeur + 1)
    }
}

dessine :: fonc (arbre: *ArbreDeDisposition, ctx: *ContexteDessin)
{
    arbre.hauteur_vue = ctx.hauteur_vue
    arbre.largeur_vue = ctx.largeur_vue

    dessine(arbre.racine, ctx)
    ctx.peintre.termine_dessin()

    /* Tampon ID. */

    fbo: GLuint
    glGenFramebuffers(1, *fbo)
    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, fbo)

    render_buf: GLuint
    glGenRenderbuffers(1, *render_buf)
    glBindRenderbuffer(GL_RENDERBUFFER, render_buf)
    glRenderbufferStorage(GL_RENDERBUFFER, GL_R32UI, ctx.largeur_vue, ctx.hauteur_vue)
    vérifie_erreur("glRenderbufferStorage")
    glFramebufferRenderbuffer(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, GL_RENDERBUFFER, render_buf)
    vérifie_erreur("glFramebufferRenderbuffer")

    dessine_tampon_id(arbre.racine, ctx)
    vérifie_erreur("dessine_tampon_id")
    IGUMI.termine_immédiat_id()

    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, 0)
    vérifie_erreur("glBindFramebuffer")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, fbo)
    vérifie_erreur("glBindFramebuffer")

    tableau_redimensionne(arbre.tampon_id, ctx.largeur_vue * ctx.hauteur_vue)

    glReadBuffer(GL_COLOR_ATTACHMENT0)
    vérifie_erreur("glReadBuffer")
    glReadPixels(0, 0, ctx.largeur_vue, ctx.hauteur_vue, GL_RED_INTEGER, GL_UNSIGNED_INT, arbre.tampon_id.pointeur)
    vérifie_erreur("glReadPixels")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, 0)

    glBindRenderbuffer(GL_RENDERBUFFER, 0)

    glDeleteFramebuffers(1, *fbo)
    glDeleteRenderbuffers(1, *render_buf)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom RésolveuseStyle.
 * \{ */

RègleAppariée :: struct {
    règle: *CSSStyleRule
    index_feuille_de_style: z64
    index_règle: z64
    origine: OrigineCascade
}

opérateur == :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle == b.règle
}

opérateur != :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle != b.règle
}

opérateur < :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle < b.règle
}

RèglesParIdentifiant :: struct {
    ident: n32
    règles: [..]RègleAppariée
}

opérateur == :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident == b.ident
}

opérateur != :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident != b.ident
}

opérateur < :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident < b.ident
}

RésolveuseStyle :: struct {
    globales: *GlobalesDessin
    taille_fonte_racine: r64
    feuilles_de_style: []*CSSStyleSheet

    /* Tampon réutilisé pour les cascades de propriétés. */
    déclarations_cascade: [..]DéclarationCascadée

    /* Style dont les propriétés sont toujours à leurs valeurs initiales.
     * Utilisé lors du calcul des valeurs. */
    valeurs_initiales: Style

    /* Cache. */
    pseudo_classes: PseudoClasses
    /* Les valeurs des ChaineUTF16Uniques des sélecteurs d'id.
     * Nous utilisons les valeurs et non les chaines car nous stockons les
     * données sous d'ensemble et il est plus rapide de comparer des nombres
     * que des chaines pour le stockage ordonné. */
    ids_connus: [..]n32
    attrs_connus: [..]n32
    classes_connues: [..]n32

    /* Ségrégation des règles pour accélérer leurs appariements. */
    règles_par_classe: [..]RèglesParIdentifiant
    autres_règles: [..]RègleAppariée
}

crée_résolveuse_style :: fonc (document: *HTML.Document) -> *RésolveuseStyle
{
    résultat := loge(RésolveuseStyle)
    initialise_résolveuse(résultat)
    résultat.taille_fonte_racine = document.globales_dessin.taille_fonte_défaut
    résultat.globales = document.globales_dessin
    résultat.feuilles_de_style = document.feuilles_de_style
    initialise_cache(résultat)
    retourne résultat
}

initialise_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    initialise_style(*résolveuse.valeurs_initiales)
}

initialise_cache :: fonc (résolveuse: *RésolveuseStyle)
{
    pseudo_classes: PseudoClasses
    résolveuse.ids_connus.taille = 0
    résolveuse.attrs_connus.taille = 0
    résolveuse.classes_connues.taille = 0
    résolveuse.autres_règles.taille = 0
    résolveuse.règles_par_classe.taille = 0

    pour feuille, index_feuille dans résolveuse.feuilles_de_style {
        pour règle, index_règle dans feuille.rules {
            si règle.type != CSSRuleType.Style {
                continue
            }

            règle_de_style := règle comme *CSSStyleRule
            règle_ajoutée := faux

            règle_appariée: RègleAppariée
            règle_appariée.règle = règle_de_style
            règle_appariée.index_feuille_de_style = index_feuille
            règle_appariée.index_règle = index_règle

            pour complex dans règle_de_style.selector_list.complex_selectors {

                dernière_classe: Optionnel(CSSOMString)

                pour compound dans complex.compound_selectors {
                    init_de(Optionnel(CSSOMString))(*dernière_classe)
                    pour simple dans compound.simple_selectors {
                        si simple.type == SimpleSelector.Type.PseudoClass {
                            pseudo_classes |= simple.pseudo_classe
                        }
                        sinon si simple.type == SimpleSelector.Type.ID {
                            ensemble_ajoute(*résolveuse.ids_connus, simple.ident.valeur)
                        }
                        sinon si simple.type == SimpleSelector.Type.Attrib {
                            ensemble_ajoute(*résolveuse.attrs_connus, simple.ident.valeur)
                        }
                        sinon si simple.type == SimpleSelector.Type.Class {
                            ensemble_ajoute(*résolveuse.classes_connues, simple.ident.valeur)
                            dernière_classe = simple.ident
                        }
                    }
                }

                si dernière_classe.possède_valeur() {
                    règle_ajoutée = vrai
                    ajoute_règle_pour_classe(résolveuse, dernière_classe.Quelque, règle_appariée)
                }
            }

            saufsi règle_ajoutée {
                tableau_ajoute(*résolveuse.autres_règles, règle_appariée)
            }
        }
    }

    résolveuse.pseudo_classes = pseudo_classes
}

ajoute_règle_pour_classe :: fonc (résolveuse: *RésolveuseStyle, classe: CSSOMString, règle: RègleAppariée)
{
    règle_par_identifiant: RèglesParIdentifiant
    règle_par_identifiant.ident = classe.valeur

    index := ensemble_possède_retourne_index(*résolveuse.règles_par_classe, règle_par_identifiant)
    si index != -1 {
        règles_par_identifant := *résolveuse.règles_par_classe[index]
        tableau_ajoute(*règles_par_identifant.règles, règle)
        retourne
    }

    tableau_ajoute(*règle_par_identifiant.règles, règle)
    ensemble_ajoute(*résolveuse.règles_par_classe, règle_par_identifiant)
}

donne_règles_pour_classe :: fonc (résolveuse: *RésolveuseStyle, classe: CSSOMString) -> [..]RègleAppariée
{
    règle_par_identifiant: RèglesParIdentifiant
    règle_par_identifiant.ident = classe.valeur

    index := ensemble_possède_retourne_index(*résolveuse.règles_par_classe, règle_par_identifiant)
    si index != -1 {
        retourne résolveuse.règles_par_classe[index].règles
    }

    résultat: [..]RègleAppariée
    retourne résultat
}

détruit_données_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    déloge(résolveuse.déclarations_cascade)
    déloge(résolveuse.ids_connus)
    déloge(résolveuse.attrs_connus)
    déloge(résolveuse.classes_connues)
    déloge(résolveuse.autres_règles)
    pour résolveuse.règles_par_classe {
        déloge(it.règles)
    }
    déloge(résolveuse.règles_par_classe)
}

détruit_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    saufsi résolveuse {
        retourne
    }
    détruit_données_résolveuse(résolveuse)
    déloge(résolveuse)
}

/* https://html.spec.whatwg.org/multipage/rendering.html#the-css-user-agent-style-sheet-and-presentational-hints */
ajoute_presentational_hint :: fonc (résolveuse: *RésolveuseStyle, déclaration: *DéclarationPropriété)
{
    tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(déclaration, Spécificité(0, 0, 0), OrigineCascade.Author))
}

possède_règle_avec_pseudo_classe :: fonc (résolveuse: *RésolveuseStyle, pseudo_classe: PseudoClasses) -> bool
{
    retourne (résolveuse.pseudo_classes & pseudo_classe) != PseudoClasses.zéro
}

possède_règle_avec_id :: fonc (résolveuse: *RésolveuseStyle, id: CSSOMString) -> bool
{
    si id.est_vide() {
        /* Si l'id est la chaine vide, nous devons vérifier que toutes les règles
         * s'appliquent, donc pas de court-circuitage. */
        retourne vrai
    }

    retourne ensemble_possède(*résolveuse.ids_connus, id.valeur)
}

possède_règle_avec_attribut :: fonc (résolveuse: *RésolveuseStyle, attr: CSSOMString) -> bool
{
    si attr.est_vide() {
        /* Si l'attribut est la chaine vide, nous devons vérifier que toutes les règles
         * s'appliquent, donc pas de court-circuitage. */
        retourne vrai
    }

    retourne ensemble_possède(*résolveuse.attrs_connus, attr.valeur)
}

donne_ajournement_pour_redimensionnement :: fonc (résolveuse: *RésolveuseStyle @inutilisée, largeur: z32 @inutilisée, hauteur: z32 @inutilisée) -> AjournementArbre
{
    /* À FAIRE : règles média, sauvegarde de l'ancienne taille */
    retourne AjournementArbre.Disposition
}

RègleEtSpécificité :: struct {
    règle: RègleAppariée
    /* La spécifité du sélecteur complexe ayant la hautissime spécifité ayant apparié l'élément. */
    spécificité: Spécificité
}

collecte_les_règles_appropriées :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément) -> [..]RègleEtSpécificité
{
    temp: [..]RègleAppariée
    diffère déloge(temp)

    résultat: [..]RègleEtSpécificité

    classes := HTML.donne_liste_classe(élément)
    pour classes {
        règles_pour_classe := donne_règles_pour_classe(résolveuse, it)

        pour règle dans règles_pour_classe {
            ensemble_ajoute(*temp, règle)
        }
    }

    pour temp {
        apparie_règle(it, élément, *résultat)
    }

    pour résolveuse.autres_règles {
        apparie_règle(it, élément, *résultat)
    }

    retourne résultat
}

apparie_règle :: fonc (règle_de_style: RègleAppariée, élément: *HTML.Élément, résultat: *[..]RègleEtSpécificité)
{
    règle_appariée := faux
    spécificité: Spécificité

    pour complex dans règle_de_style.règle.selector_list.complex_selectors {
        saufsi apparie_sélecteur(complex.compound_selectors, complex.compound_selectors.taille - 1, élément) {
            continue
        }

        spécificité_sélecteur := calcule_spécificité(*complex)
        si spécificité_sélecteur > spécificité {
            spécificité = spécificité_sélecteur
        }

        règle_appariée = vrai
    }

    si règle_appariée {
        tableau_ajoute(résultat, RègleEtSpécificité(règle_de_style, spécificité))
    }
}

résoud_style :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément, propriétés_parent: *PropriétésDeStyle) -> *PropriétésDeStyle
{
    règles := collecte_les_règles_appropriées(résolveuse, élément)
    diffère déloge(règles)
    // imprime("% règle(s) pour %\n", règles.taille, élément.local_name)

    résolveuse.déclarations_cascade.taille = 0

    pour règles {
        // imprime_règle(it.règle)
        règle := it.règle.règle
        déclarations := règle.style.déclarations
        tableau_réserve(*résolveuse.déclarations_cascade, résolveuse.déclarations_cascade.taille + déclarations.taille)
        spécificité := it.spécificité
        origine := donne_origine_cascade(règle)

        index_règle := (it.règle.index_feuille_de_style << 32) | it.règle.index_règle

        pour déclaration dans déclarations {
            tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(déclaration, spécificité, origine, index_règle))
        }
    }

    HTML.applique_presentational_hints(élément, résolveuse)

    si élément.style {
        /* https://drafts.csswg.org/css-style-attr/#style-attribute
         * "In the cascade, these declarations are considered to have author origin and a specificity higher than any selector." */
        tableau_réserve(*résolveuse.déclarations_cascade, résolveuse.déclarations_cascade.taille + élément.style.déclarations.taille)
        spécificité := donne_spécificité_max()
        pour élément.style.déclarations {
            tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(it, spécificité, OrigineCascade.Author))
        }
    }
    tri_rapide(résolveuse.déclarations_cascade, compare_déclarations_pour_cascade)

    résultat := crée_propriétés_de_style(propriétés_parent)

    pour > résolveuse.déclarations_cascade {
        ajoute_déclaration(résultat, it.déclaration)
    }

    calcule_style(résolveuse, résultat, propriétés_parent)

    retourne résultat
}

AjournementArbre :: énum_drapeau {
    /* L'arbre doit être redessiné. */
    Dessin
    /* L'arbre doit être disposé. */
    Disposition
    /* L'arbre doit être construit. */
    Construction
}

donne_ajournement_pour_changement_style :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément) -> AjournementArbre
{
    noeud_de_disposition := élément.noeud_de_disposition
    anciennes_propriétés: *PropriétésDeStyle
    propriétés_parent: *PropriétésDeStyle

    si noeud_de_disposition {
        anciennes_propriétés = noeud_de_disposition.propriétés
        assert(anciennes_propriétés != nul)
        propriétés_parent = anciennes_propriétés.parent
    }

    nouvelles_propriétés := résoud_style(résolveuse, élément, propriétés_parent)
    diffère détruit_propriétés(nouvelles_propriétés)

    /* Vérifie si "display: none" reste "display: none". */
    display := nouvelles_propriétés.style.valeurs[IDPropriété.Display].display
    si display.outer_type == Display.Type.None {
        si noeud_de_disposition == nul {
            /* Nous restons sur "display: none". */
            retourne AjournementArbre.zéro
        }
        /* Nous sommes passé de "display: none" à autre chose, reconstruisons. */
        retourne AjournementArbre.Construction
    }

    si noeud_de_disposition == nul {
        /* Nous sommes passé de "display: none" à autre chose, reconstruisons. */
        retourne AjournementArbre.Construction
    }

    assert(anciennes_propriétés != nul)
    résultat: AjournementArbre

    pour * infos_types_propriétés_shorthand {
        résultat |= donne_ajournement_pour_différence_propriétés(nouvelles_propriétés, anciennes_propriétés, it)
    }

    pour * infos_types_propriétés {
        résultat |= donne_ajournement_pour_différence_propriétés(nouvelles_propriétés, anciennes_propriétés, it)
    }

    si résultat.Construction {
        retourne résultat
    }

    si résultat == AjournementArbre.zéro {
        retourne résultat
    }

    /* Sauvegarde les propriétés. */
    permute(nouvelles_propriétés, anciennes_propriétés)

    retourne résultat
}

donne_ajournement_pour_différence_propriétés :: fonc (nouvelles_propriétés: *PropriétésDeStyle, anciennes_propriétés: *PropriétésDeStyle, info: *InfoTypePropriété) -> AjournementArbre
{
    nouvelle := nouvelles_propriétés.donne_déclaration(info.nom)
    ancienne := anciennes_propriétés.donne_déclaration(info.nom)

    si nouvelle == nul && ancienne == nul {
        retourne AjournementArbre.zéro
    }

    si nouvelle == nul || ancienne == nul {
        retourne donne_ajournement_arbre(info)
    }

    si nouvelle.value.taille != ancienne.value.taille {
        retourne donne_ajournement_arbre(info)
    }

    pour nouvelle.value {
        si ancienne.value[index_it] == it {
            continue
        }

        retourne donne_ajournement_arbre(info)
    }

    si nouvelle.important != ancienne.important {
        retourne donne_ajournement_arbre(info)
    }

    retourne AjournementArbre.zéro
}

donne_ajournement_arbre :: fonc (info: *InfoTypePropriété) -> AjournementArbre
{
    si info.drapeaux.AffecteDisposition {
        retourne AjournementArbre.Disposition
    }
    si info.drapeaux.AffecteConstructionArbre {
        retourne AjournementArbre.Construction
    }
    retourne AjournementArbre.Dessin
}

/* https://www.w3.org/TR/css-cascade/#origin */
OrigineCascade :: énum {
    Author
    User
    UserAgent
}

DéclarationCascadée :: struct {
    déclaration: *DéclarationPropriété
    spécificité: Spécificité
    origine: OrigineCascade
    index_règle: z64
}

/* https://www.w3.org/TR/css-cascade/#cascade-sort */
compare_déclarations_pour_cascade :: fonc (a: DéclarationCascadée, b: DéclarationCascadée) -> z32
{
    /* Tri par importance. */
    si a.déclaration.important && !b.déclaration.important {
        retourne -1
    }
    si b.déclaration.important && !a.déclaration.important {
        retourne 1
    }

    /* Même importance -> tri par origine. */
    si a.origine != b.origine {
        retourne a.origine comme z32 - b.origine comme z32
    }

    /* Même importance et origine -> tri par spécifité. */
    si a.spécificité > b.spécificité {
        retourne -1
    }
    si b.spécificité > a.spécificité {
        retourne 1
    }

    /* Même importance, origine, et spécifité -> tri par position. */
    si a.index_règle < b.index_règle {
        retourne 1
    }
    si b.index_règle < a.index_règle {
        retourne -1
    }

    /* À FAIRE : contextes. */

    retourne 0
}

crée_style_pour_bloc_anonyme :: fonc (résolveuse: *RésolveuseStyle, bloc: *NoeudDeDispositionBloc, bloc_parent: *NoeudDeDispositionBloc)
{
    résultat := crée_propriétés_de_style(bloc_parent.propriétés)
    calcule_style(résolveuse, résultat, bloc_parent.propriétés)

    bloc.propriétés = résultat
}

calcule_style :: fonc (résolveuse: *RésolveuseStyle, propriétés: *PropriétésDeStyle, propriétés_parent: *PropriétésDeStyle)
{
    /* https://www.w3.org/TR/2011/REC-CSS2-20110607/cascade.html#computed-value */
    ctx_calcul_style: ContexteCalculStyle
    ctx_calcul_style.taille_fonte = résolveuse.globales.taille_fonte_défaut
    ctx_calcul_style.taille_fonte_racine = résolveuse.taille_fonte_racine
    ctx_calcul_style.propriétés = propriétés
    ctx_calcul_style.valeurs_initiales = *résolveuse.valeurs_initiales

    si propriétés_parent {
        ctx_calcul_style.taille_fonte = propriétés_parent.style.valeurs[IDPropriété.FontSize].number
    }

    pour propriétés.déclarations {
        si it.est_personnalisée {
            continue
        }
        info := it.info

        si info.sur_calcul_style {
            info.sur_calcul_style(it, *propriétés.style, *ctx_calcul_style)
        }
        sinon {
            calcule_valeur_propriété_générique(it, *propriétés.style, *ctx_calcul_style)
        }

        si info.id == IDPropriété.FontSize {
            ctx_calcul_style.taille_fonte = propriétés.style.valeurs[IDPropriété.FontSize].number
        }
    }

    si propriétés.style.valeurs[IDPropriété.BorderStyleLeft].line_style == LineStyle.None {
        propriétés.style.valeurs[IDPropriété.BorderWidthLeft] = ValeurDeStyle(number = 0.0)
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleTop].line_style == LineStyle.None {
        propriétés.style.valeurs[IDPropriété.BorderWidthTop] = ValeurDeStyle(number = 0.0)
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleRight].line_style == LineStyle.None {
        propriétés.style.valeurs[IDPropriété.BorderWidthRight] = ValeurDeStyle(number = 0.0)
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleBottom].line_style == LineStyle.None {
        propriétés.style.valeurs[IDPropriété.BorderWidthBottom] = ValeurDeStyle(number = 0.0)
    }
    saufsi propriétés.style.valeurs[IDPropriété.FontSize].est_nombre() {
        propriétés.style.valeurs[IDPropriété.FontSize] = calcule_valeur_propriété_générique(propriétés.style.valeurs[IDPropriété.FontSize], GenreValeurCalculée.LongueurAbsolue | GenreValeurCalculée.FontSize, *propriétés.style, *ctx_calcul_style)
    }
    saufsi propriétés.style.valeurs[IDPropriété.FontWeight].est_nombre() {
        propriétés.style.valeurs[IDPropriété.FontWeight] = calcule_valeur_propriété_générique(propriétés.style.valeurs[IDPropriété.FontWeight], GenreValeurCalculée.FontWeight, *propriétés.style, *ctx_calcul_style)
    }

    calcule_display_final(propriétés)
}

/* Corrige display en prenant en compte position et float.
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#dis-pos-flo */
calcule_display_final :: fonc (propriétés: *PropriétésDeStyle)
{
    display := propriétés.donne_valeur(IDPropriété.Display).display
    position := propriétés.donne_valeur(IDPropriété.Position)
    float := propriétés.donne_valeur(IDPropriété.Float)

    // 1. If 'display' has the value 'none', then 'position' and 'float' do not apply. In this case,
    // the element generates no box.
    si display.outer_type == Display.Type.None {
        retourne
    }

    // 2. Otherwise, if 'position' has the value 'absolute' or 'fixed', the box is absolutely positioned,
    // the computed value of 'float' is 'none', and display is set according to the table below.
    // The position of the box will be determined by the 'top', 'right', 'bottom' and 'left' properties
    // and the box's containing block.
    si position.est_mot_clé(PROP_absolute) || position.est_mot_clé(PROP_fixed) {
        propriétés.style.valeurs[IDPropriété.Float] = ValeurDeStyle(mot_clé = PROP_none)
        propriétés.style.valeurs[IDPropriété.Display] = convertis_display_pour_position_ou_float(display)
        retourne
    }

    // 3. Otherwise, if 'float' has a value other than 'none', the box is floated and 'display' is set
    // according to the table below.
    saufsi float.est_none() {
        propriétés.style.valeurs[IDPropriété.Display] = convertis_display_pour_position_ou_float(display)
        retourne
    }

    // 4. Otherwise, if the element is the root element, 'display' is set according to the table below,
    // except that it is undefined in CSS 2.1 whether a specified value of 'list-item' becomes a computed
    // value of 'block' or 'list-item'.
    // À FAIRE : test plus robuste pour définir l'élément racine.
    // À FAIRE : list-item.
    saufsi propriétés.parent {
        propriétés.style.valeurs[IDPropriété.Display] = convertis_display_pour_position_ou_float(display)
        retourne
    }

    // 5. Otherwise, the remaining 'display' property values apply as specified.
}

convertis_display_pour_position_ou_float :: fonc (spécifié: Display) -> ValeurDeStyle
{
    // À FAIRE : display-internal
    résultat := spécifié

    si spécifié.outer_type == Display.Type.Inline {
        résultat.outer_type = Display.Type.Block

        si spécifié.inner_type != Display.Type.Table {
            résultat.inner_type = Display.Type.FlowRoot
        }
    }

    retourne ValeurDeStyle(display = résultat)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Génération d'arbres de disposition.
 * \{ */

donne_arbre_de_disposition_ajourné :: fonc (document: *HTML.Document, largeur: z32, hauteur: z32) -> *ArbreDeDisposition
{
    ajournement := document.ajournement_arbre

    si document.arbre_de_disposition == nul || ajournement.Construction {
        si document.arbre_de_disposition {
            détruit_arbre(document.arbre_de_disposition)
            document.arbre_de_disposition = nul
        }
        crée_arbre_de_disposition(document, largeur, hauteur)
    }
    sinon si ajournement.Disposition {
        dispose_arbre(document.globales_dessin, document.arbre_de_disposition, largeur, hauteur)
    }

    document.ajournement_arbre = AjournementArbre.zéro

    retourne document.arbre_de_disposition
}

crée_arbre_de_disposition :: fonc (document: *HTML.Document, largeur: z32, hauteur: z32)
{
    html := HTML.donne_élément_html(document)
    saufsi html {
        retourne
    }

    saufsi document.globales_dessin {
        document.globales_dessin = crée_globales_dessin()
    }

    saufsi document.résolveuse_style {
        document.résolveuse_style = crée_résolveuse_style(document)
    }

    résultat := loge(ArbreDeDisposition)

    résolveuse := document.résolveuse_style

    /* À FAIRE : ceci peut changer mais comment est encore inconnue. */
    html.pseudo_classes.Root = vrai

    crée_noeud_pour_élément(résolveuse, résultat, nul, html)
    résultat.racine = html.noeud_de_disposition

    // imprime("Créé % noeud(s)\n", résultat.noeuds.taille)

    dispose_arbre(document.globales_dessin, résultat, largeur, hauteur)

    // imprime_arbre(résultat)

    si document.arbre_de_disposition {
        détruit_arbre(document.arbre_de_disposition)
        document.arbre_de_disposition = nul
    }

    document.arbre_de_disposition = résultat
}

dispose_arbre :: fonc (globales: *GlobalesDessin, arbre: *ArbreDeDisposition, largeur: z32, hauteur: z32)
{
    initial_containing_block: ContainingBlock
    initial_containing_block.rect.hauteur = hauteur comme r32
    initial_containing_block.rect.largeur = largeur comme r32
    initial_containing_block.noeud = arbre.racine

    ctx: ContexteDeFormattage
    ctx.globales = globales
    ctx.initial_containing_block = *initial_containing_block

    mesure(arbre.racine, *ctx)

    // imprime("taille arbre : %x%\n", résultat.racine.largeur, résultat.racine.hauteur)
}

crée_noeud_pour_élément :: fonc (résolveuse: *RésolveuseStyle, arbre: *ArbreDeDisposition, parent: *NoeudDeDisposition, noeud: *HTML.Noeud)
{
    si HTML.est_texte(noeud) {
        /* Les textes doivent une boite inline anonyme autour d'eux. */
        résultat := arbre.crée_noeud(NoeudDeDispositionInline)
        crée_style_pour_bloc_anonyme(résolveuse, résultat, parent comme *NoeudDeDispositionBloc)
        parent.ajoute_enfant(résultat)

        texte := arbre.crée_noeud(NoeudDeDispositionTexte)
        texte.noeud_texte = noeud comme *HTML.NoeudTexte
        résultat.ajoute_enfant(texte)
        retourne
    }

    saufsi HTML.est_élément(noeud) {
        retourne
    }

    élément := noeud comme *HTML.Élément

    propriétés_parent: *PropriétésDeStyle
    si parent {
        propriétés_parent = parent.propriétés
    }

    propriétés := résoud_style(résolveuse, élément, propriétés_parent)

    display := propriétés.style.valeurs[IDPropriété.Display].display
    si display.outer_type == Display.Type.None {
        détruit_propriétés(propriétés)
        retourne
    }

    résultat: *NoeudDeDisposition
    si élément.local_name == HTML.TAG_img {
        remplacement := arbre.crée_noeud(NoeudDeDispositionRemplacement)
        remplacement.élément = élément
        résultat = remplacement
    }
    sinon si display.outer_type == Display.Type.Block {
        bloc := arbre.crée_noeud(NoeudDeDispositionBloc)
        bloc.élément = élément
        résultat = bloc
    }
    sinon si élément.local_name == HTML.TAG_br {
        bris := arbre.crée_noeud(NoeudDeDispositionNouvelleLigne)
        résultat = bris
    }
    sinon {
        inline := arbre.crée_noeud(NoeudDeDispositionInline)
        inline.élément = élément
        résultat = inline
    }

    si parent == nul {
        résolveuse.taille_fonte_racine = propriétés.style.valeurs[IDPropriété.FontSize].number
    }

    élément.noeud_de_disposition = résultat
    résultat.propriétés = propriétés

    si parent {
        parent.ajoute_enfant(résultat)
    }

    pour noeud.enfants {
        crée_noeud_pour_élément(résolveuse, arbre, résultat, it)
    }

    si résultat.type != NoeudDeDispositionBloc && résultat.type != NoeudDeDispositionInline {
        retourne
    }

    noeud_disposition := résultat comme *NoeudDeDispositionBloc

    /* Crée des blocs anonymes pour envelopper les blocs inlines si au moins un enfant est un bloc.
     * Voir définition de "block container box" :
     * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#block-boxes */
    doit_formatter_en_blocs := faux

    pour noeud_disposition.enfants {
        si it.est_block_level() {
            doit_formatter_en_blocs = vrai
            arrête
        }
    }

    noeud_disposition.est_disposition_inline = doit_formatter_en_blocs == faux

    saufsi noeud_disposition.est_disposition_inline {
        si noeud_disposition.type == NoeudDeDispositionInline {
            assert(parent != nul)

            /* When an inline box contains an in-flow block-level box, the inline box (and its
             * inline ancestors within the same line box) are broken around the block-level
             * box (and any block-level siblings that are consecutive or separated only by
             * collapsible whitespace and/or out-of-flow elements), splitting the inline box
             * into two boxes (even if either side is empty), one on each side of the
             * block-level box(es). The line boxes before the break and after the break are
             * enclosed in anonymous block boxes, and the block-level box becomes a sibling of
             * those anonymous boxes. When such an inline box is affected by relative
             * positioning, any resulting translation also affects the block-level box
             * contained in the inline box.
             *
             * À FAIRE : récursion
             *
             * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#anonymous-block-level */
            anciens_enfants := noeud_disposition.enfants
            diffère déloge(anciens_enfants)

            noeud_inline_courant := noeud_disposition
            noeud_inline_courant.enfants.pointeur = nul
            noeud_inline_courant.enfants.taille = 0
            noeud_inline_courant.enfants.capacité = 0

            pour anciens_enfants {
                si it.est_block_level() {
                    parent.ajoute_enfant(it)
                    noeud_inline_courant = nul
                    continue
                }

                si noeud_inline_courant == nul {
                    noeud_inline_courant = arbre.crée_noeud(NoeudDeDispositionInline)
                    noeud_inline_courant.parent = noeud_disposition
                    noeud_inline_courant.est_disposition_inline = vrai
                    crée_style_pour_bloc_anonyme(résolveuse, noeud_inline_courant, noeud_disposition)
                    noeud_inline_courant.propriétés = copie_propriétés(noeud_disposition.propriétés)
                    noeud_inline_courant.propriétés.style.valeurs[IDPropriété.Display] = Display(outer_type = Display.Type.Inline, inner_type = Display.Type.Flow)
                    parent.ajoute_enfant(noeud_inline_courant)
                }

                noeud_inline_courant.ajoute_enfant(it)
            }
        }
        sinon {
            nouveaux_enfants: [..]*NoeudDeDisposition

            bloc_anonyme_courant: *NoeudDeDispositionBloc
            pour noeud_disposition.enfants {
                si it.est_block_level() {
                    tableau_ajoute(*nouveaux_enfants, it)
                    bloc_anonyme_courant = nul
                    continue
                }

                si bloc_anonyme_courant == nul {
                    bloc_anonyme_courant = arbre.crée_noeud(NoeudDeDispositionBloc)
                    bloc_anonyme_courant.parent = noeud_disposition
                    bloc_anonyme_courant.est_disposition_inline = vrai
                    crée_style_pour_bloc_anonyme(résolveuse, bloc_anonyme_courant, noeud_disposition)
                    bloc_anonyme_courant.propriétés.style.valeurs[IDPropriété.Display] = Display(outer_type = Display.Type.Block, inner_type = Display.Type.Flow)
                    tableau_ajoute(*nouveaux_enfants, bloc_anonyme_courant)
                }

                bloc_anonyme_courant.ajoute_enfant(it)
            }

            déloge(noeud_disposition.enfants)
            noeud_disposition.enfants = nouveaux_enfants
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ContexteDessin
 * \{ */

ContexteDessin :: struct {
    document: *HTML.Document
    globales: *GlobalesDessin
    peintre: *Peintre
    fragment: *Fragment

    hauteur_vue: z32
    largeur_vue: z32

    décalage_y: r32
}

donne_rectangle_corrigé :: fonc (ctx: *ContexteDessin, rect: RectanglePosDim(r32)) -> RectanglePosDim(r32)
{
    /* Nous disposons les noeuds comme si leur origine était leur coin du haut
     * gauche mais nous devons les dessiner comme si leur origine était leur
     * coin du bas gauche.
     * De plus, nous devons nous assurer que l'origine du document est le bas
     * gauche de la page et non le haut gauche. */
    rect.y = ctx.hauteur_vue comme r32 + ctx.décalage_y - rect.y - rect.hauteur
    retourne rect
}

remplis_rectangle_id :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDisposition)
{
    // peintre := ctx.peintre

    // entropie := graine_aléatoire(noeud.id + 1)

    // couleur: CouleurRVBA
    // couleur.r = aléatoire_unilatérale(entropie)
    // couleur.v = aléatoire_unilatérale(entropie)
    // couleur.b = aléatoire_unilatérale(entropie)
    // couleur.a = 1.0

    rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())
    // peintre.remplis_rectangle(rect, couleur)

    IGUMI.commence_immédiat_id(IGUMI.ModeImmédiat.TRIANGLES, IGUMI.nuanceur_id)

    // imprime("[%] (% % % %) : %\n", #nom_de_cette_fonction, rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)

    IGUMI.quad_immédiat_id(rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)
}

remplis_rectangle_id :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDisposition, rect: RectanglePosDim(r32))
{
    // peintre := ctx.peintre

    // entropie := graine_aléatoire(noeud.id + 1)

    // couleur: CouleurRVBA
    // couleur.r = aléatoire_unilatérale(entropie)
    // couleur.v = aléatoire_unilatérale(entropie)
    // couleur.b = aléatoire_unilatérale(entropie)
    // couleur.a = 1.0

    rect = ctx.donne_rectangle_corrigé(rect)
    // peintre.remplis_rectangle(rect, couleur)

    IGUMI.commence_immédiat_id(IGUMI.ModeImmédiat.TRIANGLES, IGUMI.nuanceur_id)

    // imprime("[%] (% % % %) : %\n", #nom_de_cette_fonction, rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)

    IGUMI.quad_immédiat_id(rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)
}

donne_image_pour_url :: fonc (ctx: *ContexteDessin, url: CSSOMString, base_url: Optionnel(URI)) -> Optionnel(ImageIO)
{
    saufsi base_url.possède_valeur() {
        base_url = HTML.donne_base_url(ctx.document)
    }

    uri_de_base := base_url.Quelque
    url_de_base := HTML.convertis_uri_url(*uri_de_base)
    diffère HTML.détruit_url(url_de_base)

    url_src := HTML.basic_url_parser(url.donne_chaine_utf16(), base = url_de_base)
    saufsi url_src {
        retourne
    }

    url_string := HTML.url_serializer(url_src)

    données_image := HTML.donne_image_pour_url(ctx.document, url_string)

    si données_image.état == HTML.ÉtatRequêteImage.Succès {
        retourne données_image.données
    }

    si données_image.état == HTML.ÉtatRequêteImage.Échec {
        retourne
    }

    si données_image.état == HTML.ÉtatRequêteImage.EnCours {
        retourne
    }

    HTML.définis_état_requête_image(ctx.document, url_string, HTML.ÉtatRequêteImage.EnCours)

    RappelProcessResponse :: struct {
        empl base: HTML.BaseProcessResponse

        document: *HTML.Document
        url_src: *HTML.URL
        url_string: chaine
    }

    sur_rappel_process_response :: fonc (base: *HTML.BaseProcessResponse, réponse: *HTML.Response)
    {
        diffère HTML.détruit_response(réponse)

        rappel := base comme *RappelProcessResponse

        saufsi réponse.body_info {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        saufsi est_type_mime_image(réponse.body_info.content_type) {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        contenu := réponse.body.Quelque

        proxy_image := IMG_cree_proxy_memoire(contenu.pointeur, contenu.taille comme n64)
        diffère IMG_detruit_proxy(proxy_image)

        chn_c_chemin := crée_chaine_c(rappel.url_src.path.Path[rappel.url_src.path.Path.taille - 1])
        diffère détruit_chaine_c(chn_c_chemin)

        image_data: ImageIO
        résultat_lecture_image := IMG_ouvre_image_avec_proxy(chn_c_chemin, *image_data, proxy_image)
        si résultat_lecture_image != ResultatOperation.OK {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        HTML.définis_données_image(rappel.document, rappel.url_string, image_data)
        HTML.invalide_style_pour_image(rappel.document)
    }

    sur_destruction_process_response :: fonc (base: *HTML.BaseProcessResponse)
    {
        rappel := base comme *RappelProcessResponse
        HTML.détruit_url(rappel.url_src)
        déloge(rappel.url_string)
        déloge(rappel)
    }

    process_response := loge(RappelProcessResponse)
    process_response.document = ctx.document
    process_response.url_src = url_src
    process_response.url_string = url_string
    process_response.sur_rappel = sur_rappel_process_response
    process_response.sur_destruction = sur_destruction_process_response

    request := loge(HTML.Request)
    request.destination = "image"
    request.mode = "no-cors"
    request.redirect_mode = "follow"
    request.url = tente parse_uri_standard(url_string) piège nonatteignable

    user_agent := HTML.donne_user_agent(ctx.document)
    fetch_controller := HTML.fetch(user_agent, request, process_response = process_response)
    diffère HTML.détruit_fetch_controller(fetch_controller)
}

est_type_mime_image :: fonc (content_type: chaine) -> bool
{
    retourne content_type == "image/jpeg" || content_type == "image/png"
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDisposition
 * \{ */

ModèleDeBoite :: struct {
    marge: RectanglePosDim(r32)
    bordure: RectanglePosDim(r32)
    rembourrage: RectanglePosDim(r32)
    contenu: RectanglePosDim(r32)
}

définis_largeur :: fonc (modèle: &ModèleDeBoite, largeur: r32)
{
    modèle.contenu.largeur = largeur
}

définis_hauteur :: fonc (modèle: &ModèleDeBoite, hauteur: r32)
{
    modèle.contenu.hauteur = hauteur
}

NoeudDeDisposition :: struct {
    type := #type_de_cette_structure

    id: n32
    propriétés: *PropriétésDeStyle

    parent: *NoeudDeDisposition
    enfants: [..]*NoeudDeDisposition

    modèle_de_boite: ModèleDeBoite

    sur_mesure: fonc (*NoeudDeDisposition, *ContexteDeFormattage)(rien)
    sur_dessin: fonc (*NoeudDeDisposition, *ContexteDessin)(rien)
    sur_dessin_tampon_id: fonc (*NoeudDeDisposition, *ContexteDessin)(rien)
    sur_divise_en_lignes: fonc (*NoeudDeDisposition, *NoeudDeDispositionBloc, *ContexteDeFormattage)(rien)
}

donne_propriétés :: fonc (noeud: *NoeudDeDisposition) -> *PropriétésDeStyle
{
    si noeud.type == NoeudDeDispositionTexte {
        retourne donne_propriété_parent(noeud comme *NoeudDeDispositionTexte)
    }
    retourne noeud.propriétés
}

donne_rectangle_bordure :: fonc (noeud: *NoeudDeDisposition) -> RectanglePosDim(r32)
{
    retourne noeud.modèle_de_boite.bordure
}

donne_rectangle_contenu :: fonc (noeud: *NoeudDeDisposition) -> RectanglePosDim(r32)
{
    retourne noeud.modèle_de_boite.contenu
}

ajourne_boite_pour_calcul_largeur :: fonc (noeud: *NoeudDeDisposition)
{
    style := *noeud.propriétés.style

    margin_left := style.valeurs[IDPropriété.MarginLeft].number comme r32
    border_left_width := style.valeurs[IDPropriété.BorderWidthLeft].number comme r32
    padding_left := style.valeurs[IDPropriété.PaddingLeft].number comme r32
    width := style.valeurs[IDPropriété.Width].number comme r32
    padding_right := style.valeurs[IDPropriété.PaddingRight].number comme r32
    border_right_width := style.valeurs[IDPropriété.BorderWidthRight].number comme r32
    margin_right := style.valeurs[IDPropriété.MarginRight].number comme r32

    noeud.modèle_de_boite.marge.x = 0.0
    noeud.modèle_de_boite.marge.largeur = margin_left + border_left_width + padding_left + width + padding_right + border_right_width + margin_right

    noeud.modèle_de_boite.bordure.x = margin_left
    noeud.modèle_de_boite.bordure.largeur = border_left_width + padding_left + width + padding_right + border_right_width

    noeud.modèle_de_boite.rembourrage.x = margin_left + border_left_width
    noeud.modèle_de_boite.rembourrage.largeur =  padding_left + width + padding_right

    noeud.modèle_de_boite.contenu.x = margin_left + border_left_width + padding_left
    noeud.modèle_de_boite.contenu.largeur = width
}

ajourne_boite_pour_calcul_hauteur :: fonc (noeud: *NoeudDeDisposition)
{
    style := *noeud.propriétés.style

    margin_top := style.valeurs[IDPropriété.MarginTop].number comme r32
    border_top_height := style.valeurs[IDPropriété.BorderWidthTop].number comme r32
    padding_top := style.valeurs[IDPropriété.PaddingTop].number comme r32
    height := style.valeurs[IDPropriété.Height].number comme r32
    padding_bottom := style.valeurs[IDPropriété.PaddingBottom].number comme r32
    border_bottom_height := style.valeurs[IDPropriété.BorderWidthBottom].number comme r32
    margin_bottom := style.valeurs[IDPropriété.MarginBottom].number comme r32

    noeud.modèle_de_boite.marge.y = 0.0
    noeud.modèle_de_boite.marge.hauteur = margin_top + border_top_height + padding_top + height + padding_bottom + border_bottom_height + margin_bottom

    noeud.modèle_de_boite.bordure.y = margin_top
    noeud.modèle_de_boite.bordure.hauteur = border_top_height + padding_top + height + padding_bottom + border_bottom_height

    noeud.modèle_de_boite.rembourrage.y = margin_top + border_top_height
    noeud.modèle_de_boite.rembourrage.hauteur =  padding_top + height + padding_bottom

    noeud.modèle_de_boite.contenu.y = margin_top + border_top_height + padding_top
    noeud.modèle_de_boite.contenu.hauteur = height
}

donne_propriété_parent :: fonc (noeud_texte: *NoeudDeDispositionTexte) -> *PropriétésDeStyle
{
    parent := noeud_texte.parent
    tantque est_noeud_anonyme(parent) {
        parent = parent.parent
    }
    retourne parent.propriétés
}

est_noeud_anonyme :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    si noeud.type != NoeudDeDispositionBloc && noeud.type != NoeudDeDispositionInline {
        retourne faux
    }

    noeud_bloc := noeud comme *NoeudDeDispositionBloc
    retourne noeud_bloc.élément == nul
}

est_noeud_non_anonyme :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    retourne !est_noeud_anonyme(noeud)
}

est_block_level :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    assert(noeud.propriétés != nul)
    retourne donne_display(noeud).outer_type == Display.Type.Block
}

est_inline_level :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    assert(noeud.propriétés != nul)
    retourne donne_display(noeud).outer_type == Display.Type.Inline
}

donne_display :: fonc (noeud: *NoeudDeDisposition) -> Display
{
    assert(noeud.propriétés != nul)
    retourne noeud.propriétés.donne_valeur(IDPropriété.Display).display
}

donne_élément :: fonc (noeud: *NoeudDeDisposition) -> *HTML.Élément
{
    noeud_courant := noeud

    tantque noeud_courant != nul {
        élément: *HTML.Élément
        si noeud_courant.type == NoeudDeDispositionInline {
            élément = (noeud_courant comme *NoeudDeDispositionBloc).élément
        }
        sinon si noeud_courant.type == NoeudDeDispositionRemplacement {
            élément = (noeud_courant comme *NoeudDeDispositionBloc).élément
        }
        sinon si noeud_courant.type == NoeudDeDispositionBloc {
            élément = (noeud_courant comme *NoeudDeDispositionBloc).élément
        }

        si élément {
            retourne élément
        }

        noeud_courant = noeud_courant.parent
    }

    retourne nul
}

décale :: fonc (noeud: *NoeudDeDisposition, x: r32, y: r32)
{
    noeud.modèle_de_boite.marge.x += x
    noeud.modèle_de_boite.bordure.x += x
    noeud.modèle_de_boite.rembourrage.x += x
    noeud.modèle_de_boite.contenu.x += x

    noeud.modèle_de_boite.marge.y += y
    noeud.modèle_de_boite.bordure.y += y
    noeud.modèle_de_boite.rembourrage.y += y
    noeud.modèle_de_boite.contenu.y += y

    pour noeud.enfants {
        it.décale(x, y)
    }

    si noeud.type == NoeudDeDispositionBloc || noeud.type == NoeudDeDispositionInline {
        bloc := noeud comme *NoeudDeDispositionBloc
        pour bloc.lignes {
            pour * fragment dans it.fragments {
                fragment.x += x
                fragment.y += y
            }
        }
    }
}

donne_nom_classe :: fonc (noeud: *NoeudDeDisposition) -> chaine
{
    infos := __table_des_types[noeud.type] comme *InfoTypeStructure
    retourne infos.nom
}

mesure :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage)
{
    si noeud.sur_mesure {
        noeud.sur_mesure(noeud, ctx)
    }
}

dessine :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    si noeud.sur_dessin {
        noeud.sur_dessin(noeud, ctx)
    }
}

dessine_tampon_id :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    si noeud.sur_dessin_tampon_id {
        noeud.sur_dessin_tampon_id(noeud, ctx)
    }
}

ajoute_enfant :: fonc (parent: *NoeudDeDisposition, enfant: *NoeudDeDisposition)
{
    enfant.parent = parent
    tableau_ajoute(*parent.enfants, enfant)
}

donne_curseur :: fonc (noeud: *NoeudDeDisposition) -> CursorType
{
    saufsi noeud.propriétés {
        retourne CursorType.auto
    }
    retourne noeud.propriétés.style.valeurs[IDPropriété.Cursor].cursor
}

divise_en_lignes :: fonc (noeud: *NoeudDeDisposition, bloc_contenant: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage)
{
    si noeud.sur_divise_en_lignes {
        noeud.sur_divise_en_lignes(noeud, bloc_contenant, ctx)
        retourne
    }

    pour noeud.enfants {
        divise_en_lignes(it, bloc_contenant, ctx)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ContainingBlock
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#containing-block-details
 * https://drafts.csswg.org/css-display/#containing-block
 * https://www.w3.org/TR/css-position-3/#def-cb
 * \{ */

ContainingBlock :: struct {
    rect: RectanglePosDim(r32)
    noeud: *NoeudDeDisposition
}

crée_containing_block_pour_contenu :: fonc (noeud: *NoeudDeDisposition) -> ContainingBlock
{
    résultat: ContainingBlock
    résultat.rect = noeud.modèle_de_boite.contenu
    résultat.noeud = noeud
    retourne résultat
}

crée_containing_block_pour_rembourrage :: fonc (noeud: *NoeudDeDisposition) -> ContainingBlock
{
    résultat: ContainingBlock
    résultat.rect = noeud.modèle_de_boite.rembourrage
    résultat.noeud = noeud
    retourne résultat
}

donne_x :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.x
}

donne_y :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.y
}

donne_largeur :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.largeur
}

donne_hauteur :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.hauteur
}

donne_containing_block :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage) -> ContainingBlock
{
    // 1. The containing block in which the root element lives is a rectangle called the initial containing block.
    // For continuous media, it has the dimensions of the viewport and is anchored at the canvas origin;
    // it is the page area for paged media.
    // The 'direction' property of the initial containing block is the same as for the root element.
    si noeud.parent == nul {
        retourne ctx.donne_initial_containing_block()
    }

    // 2. For other elements, if the element's position is 'relative' or 'static',
    // the containing block is formed by the content edge of the nearest block
    // container ancestor box.
    position := noeud.propriétés.donne_valeur(IDPropriété.Position)
    si position.est_mot_clé(PROP_relative) || position.est_mot_clé(PROP_static) {
        parent := noeud.parent

        tantque parent != nul {
            si parent.type == NoeudDeDispositionBloc {
                retourne crée_containing_block_pour_contenu(parent)
            }

            parent = parent.parent
        }
    }
    // 3. If the element has 'position: fixed', the containing block is established by the viewport
    // in the case of continuous media or the page area in the case of paged media.
    sinon si position.est_mot_clé(PROP_fixed) {
        retourne ctx.donne_initial_containing_block()
    }
    // 4. If the element has 'position: absolute', the containing block is established by the nearest
    // ancestor with a 'position' of 'absolute', 'relative' or 'fixed', in the following way:
    sinon si position.est_mot_clé(PROP_absolute) {
        ancêtre := noeud.parent

        tantque ancêtre != nul {
            position_parent := ancêtre.propriétés.donne_valeur(IDPropriété.Position)
            si position_parent.est_mot_clé(PROP_relative) || position_parent.est_mot_clé(PROP_absolute) || position_parent.est_mot_clé(PROP_fixed) {
                arrête
            }

            ancêtre = ancêtre.parent
        }

        si ancêtre {
            // À FAIRE : 1. In the case that the ancestor is an inline element, the containing block is the bounding
            // box around the padding boxes of the first and the last inline boxes generated for that element.
            // In CSS 2.1, if the inline element is split across multiple lines, the containing block is undefined.

            // À FAIRE : 2. Otherwise, the containing block is formed by the padding edge of the ancestor.
            retourne crée_containing_block_pour_rembourrage(ancêtre)
        }
    }

    // If there is no such ancestor, the containing block is the initial containing block.
    retourne ctx.donne_initial_containing_block()
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom BoiteDeLigne
 * Boite pour tenir les fragments de chaque ligne pour les dispositions
 * « inline ».
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#line-box
 * \{ */

Fragment :: struct {
    noeud: *NoeudDeDisposition
    index_début: z64
    index_fin: z64

    x: r32
    y: r32
    hauteur: r32
    largeur: r32
}

donne_rectangle :: fonc (fragment: *Fragment) -> RectanglePosDim(r32)
{
    rect: RectanglePosDim(r32)
    rect.x = fragment.x
    rect.y = fragment.y
    rect.largeur = fragment.largeur
    rect.hauteur = fragment.hauteur
    retourne rect
}

BoiteDeLigne :: struct {
    bloc: *NoeudDeDispositionBloc

    fragments: [..]Fragment
    largeur_courante: r32
}

détruit_données_boite_de_ligne :: fonc (boite: *BoiteDeLigne)
{
    déloge(boite.fragments)
}

peut_ajouter_fragment :: fonc (boite: *BoiteDeLigne, largeur: r32) -> bool
{
    retourne (boite.largeur_courante + largeur) < boite.bloc.propriétés.style.valeurs[IDPropriété.Width].number comme r32
}

ajoute_fragment :: fonc (boite: *BoiteDeLigne, noeud: *NoeudDeDisposition, index_début: z64, index_fin: z64, hauteur: r32, largeur: r32)
{
    fragment: Fragment
    fragment.noeud = noeud
    fragment.index_début = index_début
    fragment.index_fin = index_fin
    fragment.hauteur = hauteur
    fragment.largeur = largeur
    fragment.x = boite.largeur_courante

    boite.largeur_courante += largeur

    tableau_ajoute(*boite.fragments, fragment)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionBloc
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionBloc :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_bloc_sur_mesure
    sur_dessin = noeud_disposition_bloc_sur_dessin
    sur_dessin_tampon_id = noeud_disposition_bloc_sur_dessin_tampon_id

    élément: *HTML.Élément

    lignes: [..]BoiteDeLigne

    est_disposition_inline: bool
}

détruit_lignes :: fonc (noeud: *NoeudDeDispositionBloc)
{
    réinitialise_lignes(noeud)
    déloge(noeud.lignes)
}

réinitialise_lignes :: fonc (noeud: *NoeudDeDispositionBloc)
{
    pour * noeud.lignes {
        détruit_données_boite_de_ligne(it)
    }
    noeud.lignes.taille = 0
}

formatte_intérieur :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage)
{
    si (noeud.type == NoeudDeDispositionBloc && noeud.est_disposition_inline) || noeud.type == NoeudDeDispositionInline {
        ctx_local: ContexteDeFormattageInline
        ctx_local.globales = ctx.globales
        ctx_local.doit_aligner_les_descendants = ctx.doit_aligner_les_descendants
        ctx_local.initial_containing_block = ctx.initial_containing_block

        formatte(*ctx_local, noeud)
    }
    sinon {
        si noeud.requiers_contexte_de_formattage_indépendant() {
            ctx_local: ContexteDeFormattageBloc
            ctx_local.globales = ctx.globales
            ctx_local.doit_aligner_les_descendants = ctx.doit_aligner_les_descendants
            ctx_local.initial_containing_block = ctx.initial_containing_block

            formatte(*ctx_local, noeud)
            retourne
        }

        formatte(ctx, noeud)
    }
}

noeud_disposition_bloc_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage)
{
    noeud := base comme *NoeudDeDispositionBloc

    containing_block := donne_containing_block(base, ctx)

    // Ceci ne fonctionne pas si "width" est auto. Il nous faudra peut-être forcer "width: fit-content"
    // si ctx.doit_aligner_les_descendants {
    //     propriétés := noeud.propriétés
    //     margin_left := propriétés.donne_valeur(IDPropriété.MarginLeft)
    //     margin_right := propriétés.donne_valeur(IDPropriété.MarginRight)

    //     si !margin_left.est_auto() && !margin_right.est_auto() {
    //         propriétés.style.valeurs[IDPropriété.MarginLeft] = ValeurDeStyle(mot_clé = PROP_auto)
    //         propriétés.style.valeurs[IDPropriété.MarginRight] = ValeurDeStyle(mot_clé = PROP_auto)
    //     }
    // }

    /* À FAIRE : utilise les bonnes formules.
     * https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#Computing_widths_and_margins */
    calcule_largeur_et_marges(noeud, containing_block.donne_largeur())
    calcule_hauteur_et_marges(noeud)

    // si noeud.élément && noeud.élément.local_name == HTML.TAG_div {
    //     attribut_align := HTML.donne_attribut(noeud.élément, HTML.ATTR_align)
    //     si attribut_align.possède_valeur() {
    //         alignement := attribut_align.Quelque
    //         /* À FAIRE : "middle" */
    //         si compare_ascii_insensible_à_la_casse(alignement, HTML.TAG_center) {
    //             ctx.doit_aligner_les_descendants |= vrai
    //         }
    //     }
    // }

    formatte_intérieur(noeud, ctx)

    saufsi noeud.est_disposition_inline {
        /* À FAIRE : une disposition inline interfère avec la hauteur du bloc.
         * Il faudra réviser l'interaction entre la disposition inline et le type de noeud (block ou inline). */
        calcule_hauteur_et_marges(noeud)
    }
}

noeud_disposition_bloc_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionBloc

    peintre := ctx.peintre

    si noeud.est_noeud_non_anonyme() {
        background_color := noeud.propriétés.style.valeurs[IDPropriété.BackgroundColor].couleur
        si background_color.a != 0 {
            rect_noeud: RectanglePosDim(r32)
            si base.parent == nul {
                /* L'arrière plan de l'élément racine doit s'appliquer au cannevas. */
                rect_noeud.hauteur = ctx.hauteur_vue comme r32
                rect_noeud.largeur = ctx.largeur_vue comme r32
            }
            sinon {
                rect_noeud = ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())
            }

            peintre.remplis_rectangle(rect_noeud, background_color)
        }
        sinon si noeud.parent == nul && noeud.élément && noeud.élément.local_name == HTML.TAG_html {
            /* Hérite de l'arrière-plan du "body".
             * https://www.w3.org/TR/CSS21/colors.html#background */
            body := HTML.donne_élément_body(noeud.élément.document)
            si body && body.noeud_de_disposition {
                background_color = body.noeud_de_disposition.propriétés.style.valeurs[IDPropriété.BackgroundColor].couleur
                si background_color.a != 0 {
                    /* À FAIRE : ne peint pas l'arrière-plan du "body" dans ce cas. */
                    rect_noeud: RectanglePosDim(r32)
                    rect_noeud.hauteur = ctx.hauteur_vue comme r32
                    rect_noeud.largeur = ctx.largeur_vue comme r32
                    peintre.remplis_rectangle(rect_noeud, background_color)
                }
            }
        }

        image := noeud.propriétés.donne_valeur(IDPropriété.BackgroundImage)
        saufsi image.est_none() {
            base_url := donne_url_base_pour_background_image(noeud.propriétés)
            opt_image_data := ctx.donne_image_pour_url(image.url, base_url)
            si opt_image_data.possède_valeur() {
                image_data := opt_image_data.Quelque
                /* À FAIRE : background-clip */
                rect := noeud.donne_rectangle_bordure()

                rect_image := rect
                rect_image.largeur = image_data.largeur comme r32
                rect_image.hauteur = image_data.hauteur comme r32

                rect = ctx.donne_rectangle_corrigé(rect)
                rect_image = ctx.donne_rectangle_corrigé(rect_image)
                peintre.dessine_image(image_data, rect_image, rect)
            }
        }

        bordure := donne_bordure(*noeud.propriétés.style)

        pour bordure.style {
            si it == LineStyle.None || it == LineStyle.Hidden {
                continue
            }

            coté := index_it comme z32 comme BorderEdge

            couleur: CouleurRVBAN8

            si it == LineStyle.Inset {
                si coté == BorderEdge.Top || coté == BorderEdge.Left {
                    couleur = CouleurRVBAN8(154, 154, 154, 255)
                }
                sinon {
                    couleur = CouleurRVBAN8(238, 238, 238, 255)
                }
            }
            sinon si it == LineStyle.Outset {
                si coté == BorderEdge.Top || coté == BorderEdge.Left {
                    couleur = CouleurRVBAN8(238, 238, 238, 255)
                }
                sinon {
                    couleur = CouleurRVBAN8(154, 154, 154, 255)
                }
            }
            sinon {
                border_color := bordure.color[coté]
                /* À FAIRE : calcule la valeur même s'il n'y a pas de propriété. */
                si border_color.est_mot_clé(PROP_currentColor) {
                    couleur = noeud.propriétés.style.valeurs[IDPropriété.Color]
                }
                sinon {
                    couleur = bordure.color[coté].couleur
                }
            }

            largeur := bordure.width[coté].number comme r32

            rect: RectanglePosDim(r32)
            rect_bordure := noeud.donne_rectangle_bordure()

            si coté == BorderEdge.Top {
                rect.x = rect_bordure.x
                rect.y = rect_bordure.y
                rect.largeur = rect_bordure.largeur
                rect.hauteur = largeur
            }
            sinon si coté == BorderEdge.Bottom {
                rect.x = rect_bordure.x
                rect.y = rect_bordure.y + rect_bordure.hauteur - largeur
                rect.largeur = rect_bordure.largeur
                rect.hauteur = largeur
            }
            sinon si coté == BorderEdge.Left {
                rect.x = rect_bordure.x
                rect.y = rect_bordure.y
                rect.largeur = largeur
                rect.hauteur = rect_bordure.hauteur
            }
            sinon /* si coté == BorderEdge.Right */ {
                rect.x = rect_bordure.x + rect_bordure.largeur - largeur
                rect.y = rect_bordure.y
                rect.largeur = largeur
                rect.hauteur = rect_bordure.hauteur
            }

            rect = ctx.donne_rectangle_corrigé(rect)
            peintre.remplis_rectangle(rect, couleur)
        }
    }

    saufsi noeud.est_disposition_inline {
        dessine_enfants_block(noeud, ctx)
    }
    sinon {
        dessine_enfants_inline(noeud, ctx)
    }
}

noeud_disposition_bloc_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionBloc

    ctx.remplis_rectangle_id(noeud)

    saufsi noeud.est_disposition_inline {
        dessine_tampon_id_enfants_block(noeud, ctx)
    }
    sinon {
        dessine_tampon_id_enfants_inline(noeud, ctx)
    }
}

ajoute_ligne :: fonc (bloc: *NoeudDeDispositionBloc) -> *BoiteDeLigne
{
    ligne := tableau_ajoute_élément(*bloc.lignes)
    ligne.bloc = bloc
    retourne ligne
}

donne_ligne_courante :: fonc (bloc: *NoeudDeDispositionBloc) -> *BoiteDeLigne
{
    si bloc.lignes.taille == 0 {
        retourne ajoute_ligne(bloc)
    }

    retourne *bloc.lignes[bloc.lignes.taille - 1]
}

calcule_largeur_et_marges :: fonc (noeud: *NoeudDeDispositionBloc, width_of_containing_block: r64)
{
    résoud_pourcentage_largeurs(noeud, width_of_containing_block)

    // À FAIRE : ne modifie pas les valeurs calculées
    sauvegarde_style := noeud.propriétés.style
    calcule_largeur_et_marges_block_level_normal_flow(noeud, width_of_containing_block)

    width := noeud.propriétés.style.valeurs[IDPropriété.Width]
    max_width := noeud.propriétés.style.valeurs[IDPropriété.MaxWidth]
    si max_width.est_nombre() && (width.number > max_width.number) {
        noeud.propriétés.style = sauvegarde_style
        noeud.propriétés.style.valeurs[IDPropriété.Width] = max_width
        calcule_largeur_et_marges_block_level_normal_flow(noeud, width_of_containing_block)
    }

    width = noeud.propriétés.style.valeurs[IDPropriété.Width]
    min_width := noeud.propriétés.style.valeurs[IDPropriété.MinWidth]
    si min_width.est_nombre() && (width.number < min_width.number) {
        noeud.propriétés.style = sauvegarde_style
        noeud.propriétés.style.valeurs[IDPropriété.Width] = min_width
        calcule_largeur_et_marges_block_level_normal_flow(noeud, width_of_containing_block)
    }

    ajourne_boite_pour_calcul_largeur(noeud)
}

calcule_pourcentage :: fonc (valeur: ValeurDeStyle, référence: r64) -> ValeurDeStyle
{
    saufsi valeur.est_percentage() {
        retourne valeur
    }

    retourne ValeurDeStyle(number = valeur.percentage * référence / 100.0)
}

résoud_pourcentage_largeurs :: fonc (noeud: *NoeudDeDispositionBloc, width_of_containing_block: r64)
{
    style := *noeud.propriétés.style

    margin_left := style.valeurs[IDPropriété.MarginLeft]
    border_left_width := style.valeurs[IDPropriété.BorderWidthLeft]
    padding_left := style.valeurs[IDPropriété.PaddingLeft]
    width := style.valeurs[IDPropriété.Width]
    padding_right := style.valeurs[IDPropriété.PaddingRight]
    border_right_width := style.valeurs[IDPropriété.BorderWidthRight]
    margin_right := style.valeurs[IDPropriété.MarginRight]

    style.valeurs[IDPropriété.MarginLeft] = calcule_pourcentage(margin_left, width_of_containing_block)
    style.valeurs[IDPropriété.BorderWidthLeft] = calcule_pourcentage(border_left_width, width_of_containing_block)
    style.valeurs[IDPropriété.PaddingLeft] = calcule_pourcentage(padding_left, width_of_containing_block)
    style.valeurs[IDPropriété.Width] = calcule_pourcentage(width, width_of_containing_block)
    style.valeurs[IDPropriété.PaddingRight] = calcule_pourcentage(padding_right, width_of_containing_block)
    style.valeurs[IDPropriété.BorderWidthRight] = calcule_pourcentage(border_right_width, width_of_containing_block)
    style.valeurs[IDPropriété.MarginRight] = calcule_pourcentage(margin_right, width_of_containing_block)
}

résoud_pourcentage_hauteur :: fonc (noeud: *NoeudDeDispositionBloc, height_of_containing_block: r64)
{
    style := *noeud.propriétés.style

    margin_top := style.valeurs[IDPropriété.MarginTop]
    border_top_width := style.valeurs[IDPropriété.BorderWidthTop]
    padding_top := style.valeurs[IDPropriété.PaddingTop]
    height := style.valeurs[IDPropriété.Height]
    padding_bottom := style.valeurs[IDPropriété.PaddingBottom]
    border_bottom_width := style.valeurs[IDPropriété.BorderWidthBottom]
    margin_bottom := style.valeurs[IDPropriété.MarginBottom]

    style.valeurs[IDPropriété.MarginTop] = calcule_pourcentage(margin_top, height_of_containing_block)
    style.valeurs[IDPropriété.BorderWidthTop] = calcule_pourcentage(border_top_width, height_of_containing_block)
    style.valeurs[IDPropriété.PaddingTop] = calcule_pourcentage(padding_top, height_of_containing_block)
    style.valeurs[IDPropriété.Height] = calcule_pourcentage(height, height_of_containing_block)
    style.valeurs[IDPropriété.PaddingBottom] = calcule_pourcentage(padding_bottom, height_of_containing_block)
    style.valeurs[IDPropriété.BorderWidthBottom] = calcule_pourcentage(border_bottom_width, height_of_containing_block)
    style.valeurs[IDPropriété.MarginBottom] = calcule_pourcentage(margin_bottom, height_of_containing_block)
}

calcule_hauteur_et_marges :: fonc (noeud: *NoeudDeDispositionBloc)
{
    // résoud_pourcentage_hauteurs(noeud)

    // À FAIRE : ne modifie pas les valeurs calculées
    sauvegarde_style := noeud.propriétés.style
    calcule_hauteur_et_marges_block_level_normal_flow(noeud)

    height := noeud.propriétés.style.valeurs[IDPropriété.Height]
    max_height := noeud.propriétés.style.valeurs[IDPropriété.MaxHeight]
    si max_height.est_nombre() && (height.number > max_height.number) {
        noeud.propriétés.style = sauvegarde_style
        noeud.propriétés.style.valeurs[IDPropriété.Height] = max_height
        calcule_hauteur_et_marges_block_level_normal_flow(noeud)
    }

    height = noeud.propriétés.style.valeurs[IDPropriété.Height]
    min_height := noeud.propriétés.style.valeurs[IDPropriété.MinHeight]
    si min_height.est_nombre() && (height.number < min_height.number) {
        noeud.propriétés.style = sauvegarde_style
        noeud.propriétés.style.valeurs[IDPropriété.Height] = min_height
        calcule_hauteur_et_marges_block_level_normal_flow(noeud)
    }

    ajourne_boite_pour_calcul_hauteur(noeud)
}

calcule_hauteur_et_marges_block_level_normal_flow :: fonc (noeud: *NoeudDeDispositionBloc)
{
    style := *noeud.propriétés.style

    /* À FAIRE */
    margin_top := style.valeurs[IDPropriété.MarginTop]
    border_top_height := style.valeurs[IDPropriété.BorderWidthTop]
    padding_top := style.valeurs[IDPropriété.PaddingTop]
    height := style.valeurs[IDPropriété.Height]
    padding_bottom := style.valeurs[IDPropriété.PaddingBottom]
    border_bottom_height := style.valeurs[IDPropriété.BorderWidthBottom]
    margin_bottom := style.valeurs[IDPropriété.MarginBottom]

    saufsi margin_top.est_nombre() {
        margin_top = ValeurDeStyle(number = 0)
    }
    saufsi border_top_height.est_nombre() {
        border_top_height = ValeurDeStyle(number = 0)
    }
    saufsi padding_top.est_nombre() {
        padding_top = ValeurDeStyle(number = 0)
    }
    saufsi height.est_nombre() {
        height = ValeurDeStyle(number = 0)
    }
    saufsi padding_bottom.est_nombre() {
        padding_bottom = ValeurDeStyle(number = 0)
    }
    saufsi border_bottom_height.est_nombre() {
        border_bottom_height = ValeurDeStyle(number = 0)
    }
    saufsi margin_bottom.est_nombre() {
        margin_bottom = ValeurDeStyle(number = 0)
    }

    style.valeurs[IDPropriété.MarginTop] = margin_top
    style.valeurs[IDPropriété.BorderWidthTop] = border_top_height
    style.valeurs[IDPropriété.PaddingTop] = padding_top
    style.valeurs[IDPropriété.Height] = height
    style.valeurs[IDPropriété.PaddingBottom] = padding_bottom
    style.valeurs[IDPropriété.BorderWidthBottom] = border_bottom_height
    style.valeurs[IDPropriété.MarginBottom] = margin_bottom
}

/* https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#blockwidth */
calcule_largeur_et_marges_block_level_normal_flow :: fonc (noeud: *NoeudDeDispositionBloc, width_of_containing_block: r64)
{
    style := *noeud.propriétés.style

    margin_left := style.valeurs[IDPropriété.MarginLeft]
    border_left_width := style.valeurs[IDPropriété.BorderWidthLeft]
    padding_left := style.valeurs[IDPropriété.PaddingLeft]
    width := style.valeurs[IDPropriété.Width]
    padding_right := style.valeurs[IDPropriété.PaddingRight]
    border_right_width := style.valeurs[IDPropriété.BorderWidthRight]
    margin_right := style.valeurs[IDPropriété.MarginRight]

    // If 'width' is not 'auto' and 'border-left-width' + 'padding-left' + 'width' + 'padding-right'
    // + 'border-right-width' (plus any of 'margin-left' or 'margin-right' that are not 'auto') is
    // larger than the width of the containing block, then any 'auto' values for 'margin-left' or
    // 'margin-right' are, for the following rules, treated as zero.
    saufsi width.est_auto() {
        tmp : r64 = 0.0
        tmp += border_left_width.number
        tmp += padding_left.number
        tmp += width.number
        tmp += padding_right.number
        tmp += border_right_width.number

        saufsi margin_left.est_auto() {
            tmp += margin_left.number
        }
        saufsi margin_right.est_auto() {
            tmp += margin_right.number
        }

        si tmp > width_of_containing_block {
            si margin_left.est_auto() {
                margin_left = ValeurDeStyle(number = 0.0)
            }
            si margin_right.est_auto() {
                margin_right = ValeurDeStyle(number = 0.0)
            }
        }
    }

    // If all of the above have a computed value other than 'auto', the values are said to be "over-constrained"
    // and one of the used values will have to be different from its computed value.
    // If the 'direction' property of the containing block has the value 'ltr', the specified value of
    // 'margin-right' is ignored and the value is calculated so as to make the equality true.
    // If the value of 'direction' is 'rtl', this happens to 'margin-left' instead.
    surcontraint := 0
    surcontraint += margin_left.est_auto() comme z32
    surcontraint += border_left_width.est_auto() comme z32
    surcontraint += padding_left.est_auto() comme z32
    surcontraint += width.est_auto() comme z32
    surcontraint += padding_right.est_auto() comme z32
    surcontraint += border_right_width.est_auto() comme z32
    surcontraint += margin_right.est_auto() comme z32

    si surcontraint == 0 {
        // À FAIRE : direction
        margin_right = ValeurDeStyle(number = 0.0)
    }

    // If there is exactly one value specified as 'auto', its used value follows from the equality.
    si surcontraint == 1 {
        si margin_left.est_auto() {
            résultat := width_of_containing_block - (border_left_width.number + padding_left.number + width.number + padding_right.number + border_right_width.number + margin_right.number)
            margin_left = ValeurDeStyle(number = résultat)
        }
        sinon si border_left_width.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + padding_left.number + width.number + padding_right.number + border_right_width.number + margin_right.number)
            border_left_width = ValeurDeStyle(number = résultat)
        }
        sinon si padding_left.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + border_left_width.number + width.number + padding_right.number + border_right_width.number + margin_right.number)
            padding_left = ValeurDeStyle(number = résultat)
        }
        sinon si width.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + border_left_width.number + padding_left.number + padding_right.number + border_right_width.number + margin_right.number)
            width = ValeurDeStyle(number = résultat)
        }
        sinon si padding_right.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + border_left_width.number + padding_left.number + width.number + border_right_width.number + margin_right.number)
            padding_right = ValeurDeStyle(number = résultat)
        }
        sinon si border_right_width.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + border_left_width.number + padding_left.number + width.number + padding_right.number + margin_right.number)
            border_right_width = ValeurDeStyle(number = résultat)
        }
        sinon si margin_right.est_auto() {
            résultat := width_of_containing_block - (margin_left.number + border_left_width.number + padding_left.number + width.number + padding_right.number + border_right_width.number)
            margin_right = ValeurDeStyle(number = résultat)
        }
    }

    // If 'width' is set to 'auto', any other 'auto' values become '0' and 'width' follows from the resulting equality.
    si width.est_auto() {
        si margin_left.est_auto() {
            margin_left = ValeurDeStyle(number = 0.0)
        }
        si border_left_width.est_auto() {
            border_left_width = ValeurDeStyle(number = 0.0)
        }
        si padding_left.est_auto() {
            padding_left = ValeurDeStyle(number = 0.0)
        }
        si padding_right.est_auto() {
            padding_right = ValeurDeStyle(number = 0.0)
        }
        si border_right_width.est_auto() {
            border_right_width = ValeurDeStyle(number = 0.0)
        }
        si margin_right.est_auto() {
            margin_right = ValeurDeStyle(number = 0.0)
        }

        résultat := width_of_containing_block - (margin_left.number + border_left_width.number + padding_left.number + padding_right.number + border_right_width.number + margin_right.number)
        width = ValeurDeStyle(number = résultat)
    }

    // If both 'margin-left' and 'margin-right' are 'auto', their used values are equal.
    // This horizontally centers the element with respect to the edges of the containing block.
    si margin_left.est_auto() && margin_right.est_auto() {
        résultat := width_of_containing_block - (border_left_width.number + padding_left.number + width.number + padding_right.number + border_right_width.number)
        résultat *= 0.5

        margin_left = ValeurDeStyle(number = résultat)
        margin_right = ValeurDeStyle(number = résultat)
    }

    style.valeurs[IDPropriété.MarginLeft] = margin_left
    style.valeurs[IDPropriété.BorderWidthLeft] = border_left_width
    style.valeurs[IDPropriété.PaddingLeft] = padding_left
    style.valeurs[IDPropriété.Width] = width
    style.valeurs[IDPropriété.PaddingRight] = padding_right
    style.valeurs[IDPropriété.BorderWidthRight] = border_right_width
    style.valeurs[IDPropriété.MarginRight] = margin_right
}

dessine_enfants_block :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    // rect := ctx.donne_rectangle_corrigé(noeud)

    // couleur := CouleurRVBA(0.5, 0.0, 0.5, 1.0)

    // peintre := ctx.peintre
    // peintre.dessine_rectangle(rect, couleur)

    pour noeud.enfants {
        position := it.propriétés.style.valeurs[IDPropriété.Position]
        saufsi position.est_mot_clé(PROP_static) {
            continue
        }
        dessine(it, ctx)
    }

    pour noeud.enfants {
        position := it.propriétés.style.valeurs[IDPropriété.Position]
        si position.est_mot_clé(PROP_static) {
            continue
        }
        dessine(it, ctx)
    }
}

dessine_tampon_id_enfants_block :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    pour noeud.enfants {
        dessine_tampon_id(it, ctx)
    }
}

AlignementTexte :: énum {
    Gauche
    Droite
    Centre
    Justifié
}

donne_alignement_texte :: fonc (noeud: *NoeudDeDispositionBloc) -> AlignementTexte
{
    style := *noeud.propriétés.style
    text_align := style.valeurs[IDPropriété.TextAlign]

    si text_align.est_mot_clé(PROP_start) {
        retourne AlignementTexte.Gauche
    }

    si text_align.est_mot_clé(PROP_end) {
        retourne AlignementTexte.Droite
    }

    si text_align.est_mot_clé(PROP_left) {
        retourne AlignementTexte.Gauche
    }

    si text_align.est_mot_clé(PROP_right) {
        retourne AlignementTexte.Droite
    }

    si text_align.est_mot_clé(PROP_center) {
        retourne AlignementTexte.Centre
    }

    si text_align.est_mot_clé(PROP_justify) {
        retourne AlignementTexte.Justifié
    }

    retourne AlignementTexte.Gauche
}

dessine_enfants_inline :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    ctx_locale := mémoire(ctx)
    pour noeud.lignes {
        pour * fragment dans it.fragments {
            ctx_locale.fragment = fragment
            dessine(fragment.noeud, *ctx_locale)
        }
    }
}

dessine_tampon_id_enfants_inline :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    ctx_locale := mémoire(ctx)
    pour noeud.lignes {
        pour * fragment dans it.fragments {
            ctx_locale.fragment = fragment
            dessine_tampon_id(fragment.noeud, *ctx_locale)
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionInline
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionInline :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_inline_sur_mesure
    sur_dessin = noeud_disposition_inline_sur_dessin
    sur_dessin_tampon_id = noeud_disposition_inline_sur_dessin_tampon_id
}

noeud_disposition_inline_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage)
{
    noeud := base comme *NoeudDeDispositionInline
    containing_block := donne_containing_block(base, ctx)
    calcule_largeur_et_marges(noeud, containing_block.donne_largeur())
    calcule_hauteur_et_marges(noeud) // pour résoudre les %
    formatte_intérieur(noeud, ctx)
    calcule_hauteur_et_marges(noeud)
}

noeud_disposition_inline_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionInline
    dessine_enfants_inline(noeud, ctx)
}

noeud_disposition_inline_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionInline
    ctx.remplis_rectangle_id(noeud)
    dessine_tampon_id_enfants_inline(noeud, ctx)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionNouvelleLigne
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionNouvelleLigne :: struct {
    empl base_bloc: NoeudDeDisposition
    type = #type_de_cette_structure
    sur_divise_en_lignes = noeud_disposition_nouvelle_ligne_sur_divise_en_lignes
}

noeud_disposition_nouvelle_ligne_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition @inutilisée, bloc_contenant: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage @inutilisée)
{
    _ := bloc_contenant.ajoute_ligne()
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionTexte
 * \{ */

/* Créé pour chaque élément contenant du texte. */
NoeudDeDispositionTexte :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_texte_sur_mesure
    sur_dessin = noeud_disposition_texte_sur_dessin
    sur_divise_en_lignes = noeud_disposition_texte_sur_divise_en_lignes
    sur_dessin_tampon_id = noeud_disposition_texte_sur_dessin_tampon_id

    noeud_texte: *HTML.NoeudTexte
    texte: ChaineUTF16
    fonte: *Fonte

    morceaux: [..]ChaineUTF16
}

détermine_fonte :: fonc (noeud_texte: *NoeudDeDispositionTexte, ctx: *ContexteDeFormattage) -> *Fonte
{
    globales := ctx.globales
    assert(globales != nul)
    famille_fonte := "sérif"
    style := "normal"
    graisse := GraisseFonte.Normal
    propriétés := donne_propriété_parent(noeud_texte)
    taille_fonte := propriétés.style.valeurs[IDPropriété.FontSize].number comme r32

    font_family := propriétés.style.valeurs[IDPropriété.FontFamily].mot_clé
    si font_family == PROP_monospace {
        famille_fonte = "monospace"
    }
    sinon si font_family == PROP_sans_serif {
        famille_fonte = "sans-serif"
    }

    font_weight := propriétés.style.valeurs[IDPropriété.FontWeight].number
    si font_weight >= 700.0 {
        graisse = GraisseFonte.Gras
    }

    font_style := propriétés.style.valeurs[IDPropriété.FontStyle].mot_clé
    si font_style == PROP_italic {
        style = "italic"
    }

    fonte := globales.donne_fonte(famille_fonte, style, graisse, taille_fonte)
    noeud_texte.fonte = fonte
    retourne fonte
}

noeud_disposition_texte_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage)
{
    noeud := base comme *NoeudDeDispositionTexte

    si est_vide_ou_uniquement_espaces_blanches(noeud.noeud_texte.données) {
        noeud.sur_dessin = nul
        retourne
    }

    fonte := détermine_fonte(noeud, ctx)
    métriques := fonte.donne_métriques_fonte()

    /* À FAIRE : meilleur algorithme, respect du standard. */
    morceaux := divise_sur_espaces_blanches_ascii(noeud.noeud_texte.données)
    diffère déloge(morceaux)

    enchaineuse: EnchaineuseUTF16
    initialise_enchaineuse(enchaineuse)
    diffère détruit_tampons(enchaineuse)

    virgule := ChaineUTF16Unique(0x20).donne_chaine_utf16()
    pour morceaux {
        si index_it > 0 {
            ajoute(enchaineuse, virgule)
        }
        ajoute(enchaineuse, it)
    }

    texte := chaine_depuis_enchaineuse(enchaineuse)
    noeud.texte = texte

    /* XXX */
    noeud.modèle_de_boite.marge.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.bordure.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.contenu.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.rembourrage.hauteur = métriques.donne_hauteur_ligne()

    largeur_texte := fonte.donne_largeur_texte(texte) comme r32
    noeud.modèle_de_boite.marge.largeur = largeur_texte
    noeud.modèle_de_boite.bordure.largeur = largeur_texte
    noeud.modèle_de_boite.contenu.largeur = largeur_texte
    noeud.modèle_de_boite.rembourrage.largeur = largeur_texte
}

noeud_disposition_texte_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, bloc_contenant: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage)
{
    noeud := base comme *NoeudDeDispositionTexte

    si est_vide_ou_uniquement_espaces_blanches(noeud.noeud_texte.données) {
        noeud.sur_dessin = nul
        retourne
    }

    fonte := détermine_fonte(noeud, ctx)
    métriques := fonte.donne_métriques_fonte()

    noeud.modèle_de_boite.marge.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.bordure.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.contenu.hauteur = métriques.donne_hauteur_ligne()
    noeud.modèle_de_boite.rembourrage.hauteur = métriques.donne_hauteur_ligne()

    /* À FAIRE : meilleur algorithme, respect du standard. */
    morceaux := divise_sur_espaces_blanches_ascii(noeud.noeud_texte.données)

    espace := ChaineUTF16Unique(' ' comme n32)
    index_espace := morceaux.taille
    largeur_espace := fonte.donne_largeur_texte(espace.donne_chaine_utf16())

    tableau_ajoute(*morceaux, espace.donne_chaine_utf16())

    ligne_courante := bloc_contenant.donne_ligne_courante()
    noeud.morceaux = morceaux

    hauteur_texte := métriques.donne_hauteur_ligne()

    propriétés := donne_propriété_parent(noeud)
    line_height := propriétés.donne_valeur(IDPropriété.LineHeight)

    si line_height.est_nombre() {
        /* line-height définis la hauteur minimale. */
        hauteur_texte = max(hauteur_texte, line_height.number comme r32)
    }

    pour morceaux {
        largeur_morceau := fonte.donne_largeur_texte(it)

        saufsi ligne_courante.peut_ajouter_fragment(largeur_morceau comme r32) {
            ligne_courante = bloc_contenant.ajoute_ligne()
        }

        ligne_courante.ajoute_fragment(noeud, index_it, index_it + 1, hauteur_texte, largeur_morceau comme r32)

        si index_it < index_espace - 1 {
            si ligne_courante.peut_ajouter_fragment(largeur_espace comme r32) {
                ligne_courante.ajoute_fragment(noeud, index_espace, index_espace + 1, hauteur_texte, largeur_espace comme r32)
            }
        }
    }
}

noeud_disposition_texte_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionTexte

    propriétés := donne_propriété_parent(noeud)

    effets: EffetsTexte

    line := propriétés.style.valeurs[IDPropriété.TextDecorationLine]
    saufsi line.est_mot_clé(PROP_none) {
        decoration := line.text_decoration_line
        couleur := vers_couleur_rvba(propriétés.style.valeurs[IDPropriété.TextDecorationColor].couleur)

        si decoration.UNDERLINE {
            effets.souslignage = vrai
            effets.couleur_souslignage = couleur
        }
        si decoration.OVERLINE {
            effets.surlignage = vrai
            effets.couleur_surlignage = couleur
        }
        si decoration.LINE_THROUGH {
            effets.barrage = vrai
            effets.couleur_barrage = couleur
        }
    }

    peintre := ctx.peintre
    fonte := noeud.fonte
    assert(fonte != nul)

    métriques := donne_métriques_fonte(fonte)
    couleur := propriétés.style.valeurs[IDPropriété.Color].couleur

    background_color := propriétés.style.valeurs[IDPropriété.BackgroundColor].couleur
    si background_color.a != 0 {
        rect_noeud: RectanglePosDim(r32)
        si ctx.fragment {
            fragment := ctx.fragment
            rect_noeud = ctx.donne_rectangle_corrigé(fragment.donne_rectangle())
        }
        sinon {
            rect_noeud = ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
        }
        peintre.remplis_rectangle(rect_noeud, background_color)
    }

    si ctx.fragment {
        fragment := ctx.fragment
        rect := ctx.donne_rectangle_corrigé(fragment.donne_rectangle())
        rect.y -= métriques.jambage_inférieur

        // peintre.dessine_rectangle(rect, couleur)

        texte := noeud.morceaux[fragment.index_début]

        peintre.dessine_texte(fonte, texte, rect, couleur, *effets)
    }
    sinon {
        texte := noeud.texte
        rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
        rect.y -= métriques.jambage_inférieur
        peintre.dessine_texte(fonte, texte, rect, couleur, *effets)
    }
}

noeud_disposition_texte_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionTexte

    si ctx.fragment {
        fragment := ctx.fragment
        ctx.remplis_rectangle_id(noeud, fragment.donne_rectangle())
    }
    sinon {
        ctx.remplis_rectangle_id(noeud)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionRemplacement :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_remplacement_sur_mesure
    sur_dessin = noeud_disposition_remplacement_sur_dessin
    sur_divise_en_lignes = noeud_disposition_remplacement_sur_divise_en_lignes
    sur_dessin_tampon_id = noeud_disposition_remplacement_sur_dessin_tampon_id
}

possède_ratio_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    saufsi noeud.élément {
        retourne faux
    }

    élément := noeud.élément comme *HTML.HTMLImageElement
    assert(élément.local_name == HTML.TAG_img)

    retourne élément.image_data.donnees != nul
}

possède_largeur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    retourne possède_ratio_intrinsèque(noeud)
}

possède_hauteur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    retourne possède_ratio_intrinsèque(noeud)
}

donne_ratio_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    élément := noeud.élément comme *HTML.HTMLImageElement
    assert(élément.local_name == HTML.TAG_img)
    retourne HTML.donne_ratio_intrinsèque(élément)
}

donne_largeur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    élément := noeud.élément comme *HTML.HTMLImageElement
    assert(élément.local_name == HTML.TAG_img)
    retourne HTML.donne_largeur_intrinsèque(élément) comme r32
}

donne_hauteur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    élément := noeud.élément comme *HTML.HTMLImageElement
    assert(élément.local_name == HTML.TAG_img)
    retourne HTML.donne_hauteur_intrinsèque(élément) comme r32
}

noeud_disposition_remplacement_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage)
{
    noeud := base comme *NoeudDeDispositionRemplacement

    containing_block := donne_containing_block(noeud, ctx)

    résoud_pourcentage_largeurs(noeud, containing_block.donne_largeur())
    résoud_pourcentage_hauteur(noeud, containing_block.donne_hauteur())

    display := noeud.propriétés.donne_valeur(IDPropriété.Display).display
    si display.outer_type == Display.Type.Inline {
        calcule_largeur_et_marges_inline_replaced_element(noeud)
        calcule_hauteur_et_marges_inline_replaced_element(noeud)
    }
    sinon {
        calcule_largeur_et_marges_block_level_replaced_element(noeud, containing_block.donne_largeur())
        /* La hauteur à la même règle que pour le cas inline. */
        calcule_hauteur_et_marges_inline_replaced_element(noeud)
    }
}

/* https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#inline-replaced-width */
calcule_largeur_et_marges_inline_replaced_element :: fonc (noeud: *NoeudDeDispositionRemplacement)
{
    style := *noeud.propriétés.style

    margin_left := style.valeurs[IDPropriété.MarginLeft]
    border_left_width := style.valeurs[IDPropriété.BorderWidthLeft]
    padding_left := style.valeurs[IDPropriété.PaddingLeft]
    width := style.valeurs[IDPropriété.Width]
    height := style.valeurs[IDPropriété.Height]
    padding_right := style.valeurs[IDPropriété.PaddingRight]
    border_right_width := style.valeurs[IDPropriété.BorderWidthRight]
    margin_right := style.valeurs[IDPropriété.MarginRight]

    // A computed value of 'auto' for 'margin-left' or 'margin-right' becomes a used value of '0'.
    si margin_left.est_auto() {
        margin_left = ValeurDeStyle(number = 0.0)
    }
    si margin_right.est_auto() {
        margin_right = ValeurDeStyle(number = 0.0)
    }

    // If 'height' and 'width' both have computed values of 'auto' and the element also has
    // an intrinsic width, then that intrinsic width is the used value of 'width'.
    si width.est_auto() && height.est_auto() && noeud.possède_largeur_intrinsèque() {
        width = ValeurDeStyle(number = noeud.donne_largeur_intrinsèque())
    }
    // If 'height' and 'width' both have computed values of 'auto' and the element has no
    // intrinsic width, but does have an intrinsic height and intrinsic ratio;
    // or if 'width' has a computed value of 'auto', 'height' has some other computed value,
    // and the element does have an intrinsic ratio; then the used value of 'width' is:
    //     (used height) * (intrinsic ratio)
    sinon si (width.est_auto() && height.est_auto() && !noeud.possède_largeur_intrinsèque()) ||
             (width.est_auto() && !height.est_auto() && noeud.possède_ratio_intrinsèque()) {
        si height.est_auto() {
            width = ValeurDeStyle(number = noeud.donne_hauteur_intrinsèque() * noeud.donne_ratio_intrinsèque())
        }
        sinon {
            width = ValeurDeStyle(number = height.number * noeud.donne_ratio_intrinsèque())
        }
    }

    // If 'height' and 'width' both have computed values of 'auto' and the element has
    // an intrinsic ratio but no intrinsic height or width, then the used value of 'width'
    // is undefined in CSS 2.1.
    // However, it is suggested that, if the containing block's width does not itself
    // depend on the replaced element's width, then the used value of 'width' is calculated
    // from the constraint equation used for block-level, non-replaced elements in normal flow.

    // Otherwise, if 'width' has a computed value of 'auto', and the element has an intrinsic width,
    // then that intrinsic width is the used value of 'width'.
    sinon si width.est_auto() && noeud.possède_largeur_intrinsèque() {
        width = ValeurDeStyle(number = noeud.donne_largeur_intrinsèque())
    }
    // Otherwise, if 'width' has a computed value of 'auto', but none of the conditions above are met,
    // then the used value of 'width' becomes 300px.
    // If 300px is too wide to fit the device, UAs should use the width of the largest rectangle that
    // has a 2:1 ratio and fits the device instead.
    sinon si width.est_auto() {
        width = ValeurDeStyle(number = 300.0)
    }

    style.valeurs[IDPropriété.MarginLeft] = margin_left
    style.valeurs[IDPropriété.BorderWidthLeft] = border_left_width
    style.valeurs[IDPropriété.PaddingLeft] = padding_left
    style.valeurs[IDPropriété.Width] = width
    style.valeurs[IDPropriété.PaddingRight] = padding_right
    style.valeurs[IDPropriété.BorderWidthRight] = border_right_width
    style.valeurs[IDPropriété.MarginRight] = margin_right

    ajourne_boite_pour_calcul_largeur(noeud)
}

/* https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#inline-replaced-height */
calcule_hauteur_et_marges_inline_replaced_element :: fonc (noeud: *NoeudDeDispositionRemplacement)
{
    style := *noeud.propriétés.style

    margin_top := style.valeurs[IDPropriété.MarginTop]
    border_top_height := style.valeurs[IDPropriété.BorderWidthTop]
    padding_top := style.valeurs[IDPropriété.PaddingTop]
    height := style.valeurs[IDPropriété.Height]
    width := style.valeurs[IDPropriété.Width]
    padding_bottom := style.valeurs[IDPropriété.PaddingBottom]
    border_bottom_height := style.valeurs[IDPropriété.BorderWidthBottom]
    margin_bottom := style.valeurs[IDPropriété.MarginBottom]

    // If 'margin-top', or 'margin-bottom' are 'auto', their used value is 0.
    si margin_top.est_auto() {
        margin_top = ValeurDeStyle(number = 0.0)
    }
    si margin_bottom.est_auto() {
        margin_bottom = ValeurDeStyle(number = 0.0)
    }

    // If 'height' and 'width' both have computed values of 'auto' and the element also has
    // an intrinsic height, then that intrinsic height is the used value of 'height'.
    si width.est_auto() && height.est_auto() {
        panique("non-implémenté") // il faut préserver les valeurs calculées !
    }

    // Otherwise, if 'height' has a computed value of 'auto', and the element has an intrinsic
    // ratio then the used value of 'height' is:
    //      (used width) / (intrinsic ratio)
    si height.est_auto() && noeud.possède_ratio_intrinsèque() {
        height = ValeurDeStyle(number = width.number / noeud.donne_ratio_intrinsèque())
    }

    // Otherwise, if 'height' has a computed value of 'auto', and the element has an intrinsic height,
    // then that intrinsic height is the used value of 'height'.

    // Otherwise, if 'height' has a computed value of 'auto', but none of the conditions above are met,
    // then the used value of 'height' must be set to the height of the largest rectangle that has a
    // 2:1 ratio, has a height not greater than 150px, and has a width not greater than the device width.
    si height.est_auto() {
        height = ValeurDeStyle(number = width.number * 0.5)
    }

    style.valeurs[IDPropriété.MarginTop] = margin_top
    style.valeurs[IDPropriété.BorderWidthTop] = border_top_height
    style.valeurs[IDPropriété.PaddingTop] = padding_top
    style.valeurs[IDPropriété.Height] = height
    style.valeurs[IDPropriété.PaddingBottom] = padding_bottom
    style.valeurs[IDPropriété.BorderWidthBottom] = border_bottom_height
    style.valeurs[IDPropriété.MarginBottom] = margin_bottom

    ajourne_boite_pour_calcul_hauteur(noeud)
}

/* https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#block-replaced-width */
calcule_largeur_et_marges_block_level_replaced_element :: fonc (noeud: *NoeudDeDispositionRemplacement, width_of_containing_block: r64)
{
    sauvegarde_style := noeud.propriétés.style

    // The used value of 'width' is determined as for inline replaced elements.
    calcule_largeur_et_marges_inline_replaced_element(noeud)
    width := noeud.propriétés.donne_valeur(IDPropriété.Width)

    // Then the rules for non-replaced block-level elements are applied to determine the margins.
    noeud.propriétés.style = sauvegarde_style
    noeud.propriétés.style.valeurs[IDPropriété.Width] = width
    calcule_largeur_et_marges_block_level_normal_flow(noeud, width_of_containing_block)
}

noeud_disposition_remplacement_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, bloc_contenant: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage)
{
    noeud_disposition_remplacement_sur_mesure(base, ctx)

    noeud := base comme *NoeudDeDispositionRemplacement
    largeur := noeud.propriétés.style.valeurs[IDPropriété.Width].number comme r32
    hauteur := noeud.propriétés.style.valeurs[IDPropriété.Height].number comme r32

    ligne_courante := bloc_contenant.donne_ligne_courante()
    saufsi ligne_courante.peut_ajouter_fragment(largeur) {
        ligne_courante = bloc_contenant.ajoute_ligne()
    }

    ligne_courante.ajoute_fragment(noeud, 0, 0, hauteur, largeur)
}

noeud_disposition_remplacement_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    
    rect: RectanglePosDim(r32)
    si ctx.fragment {
        rect = ctx.fragment.donne_rectangle()
    }
    sinon {
        rect = noeud.donne_rectangle_bordure()
    }

    rect = ctx.donne_rectangle_corrigé(rect)
    peintre := ctx.peintre

    assert(noeud.élément.local_name == HTML.TAG_img)
    image := noeud.élément comme *HTML.HTMLImageElement

    si image.image_data.donnees {
        peintre.dessine_image(image.image_data, rect)
    }
    sinon {
        /* À FAIRE : image nulle */
        peintre.remplis_rectangle(rect, CouleurRVBA(1.0, 0.0, 1.0, 1.0))
    }
}

noeud_disposition_remplacement_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement

    si ctx.fragment {
        fragment := ctx.fragment
        ctx.remplis_rectangle_id(noeud, fragment.donne_rectangle())
    }
    sinon {
        ctx.remplis_rectangle_id(noeud)
    }
}

/** \} */
