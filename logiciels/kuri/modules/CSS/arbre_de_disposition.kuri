importe Aléa
importe Chaine
importe Couleur
importe Ensemble
importe Fondation
importe Géométrie
importe #inemployé HTML
importe IGUMI
importe Image
importe Internet
importe Math
importe MIME
importe OpenGL
importe PeintureInterface
importe Structures
importe Triage
importe Typographie

/* ------------------------------------------------------------------------- */
/** \nom Arbre de disposition
 * \{ */

ArbreDeDisposition :: struct {
    racine: *NoeudDeDisposition
    stacking_context: *StackingContext

    noeuds: [..]*NoeudDeDisposition
    tampon_id_fut_dessiné: bool
    tampon_id: [..]n32
    largeur_vue: z32
    hauteur_vue: z32
}

détruit_arbre :: fonc (arbre: *ArbreDeDisposition)
{
    saufsi arbre {
        retourne
    }

    pour arbre.noeuds {
        déloge(it.enfants)
        si it.propriétés {
            détruit_propriétés(it.propriétés)
        }

        élément := it.donne_élément()
        si élément {
            élément.noeud_de_disposition = nul
        }

        si it.type == NoeudDeDispositionBloc {
            bloc := it comme *NoeudDeDispositionBloc
            détruit_lignes(bloc)
            déloge(bloc)
        }
        sinon si it.type == NoeudDeDispositionInline {
            noeud := it comme *NoeudDeDispositionInline
            détruit_lignes(noeud)
            déloge(noeud)
        }
        sinon si it.type == NoeudDeDispositionTexte {
            texte := it comme *NoeudDeDispositionTexte
            détruit_chaine(texte.texte)
            déloge(texte.fragments)
            déloge(texte)
        }
        sinon si it.type == NoeudDeDispositionNouvelleLigne {
            bris := it comme *NoeudDeDispositionNouvelleLigne
            déloge(bris)
        }
        sinon si it.type == NoeudDeDispositionRemplacement {
            remplacement := it comme *NoeudDeDispositionRemplacement
            déloge(remplacement)
        }
        sinon si it.type == NoeudDeDispositionSelect {
            select := it comme *NoeudDeDispositionSelect
            détruit_chaine(select.texte)
            déloge(select.fragments)
            déloge(select)
        }
        sinon si it.type == TableWrapperBox {
            box := it comme *TableWrapperBox
            déloge(box)
        }
        sinon si it.type == TableBox {
            box := it comme *TableBox
            déloge(box)
        }
        sinon si it.type == TableColumnGroupBox {
            box := it comme *TableColumnGroupBox
            déloge(box)
        }
        sinon si it.type == TableColumnBox {
            box := it comme *TableColumnBox
            déloge(box)
        }
        sinon si it.type == TableFooterGroupBox {
            box := it comme *TableFooterGroupBox
            déloge(box)
        }
        sinon si it.type == TableHeaderGroupBox {
            box := it comme *TableHeaderGroupBox
            déloge(box)
        }
        sinon si it.type == TableRowGroupBox {
            box := it comme *TableRowGroupBox
            déloge(box)
        }
        sinon si it.type == TableRowBox {
            box := it comme *TableRowBox
            déloge(box)
        }
        sinon si it.type == TableCellBox {
            box := it comme *TableCellBox
            déloge(box)
        }
        sinon si it.type == CaptionBox {
            box := it comme *CaptionBox
            déloge(box)
        }
        sinon {
            imprime("noeud inconnu !")
            déloge(it)
        }
    }

    détruit_stacking_context(arbre.stacking_context)
    déloge(arbre.tampon_id)
    déloge(arbre.noeuds)
    déloge(arbre)
}

crée_noeud :: fonc (arbre: *ArbreDeDisposition, $TypeNoeud: type_de_données) -> *TypeNoeud
{
    résultat := loge(TypeNoeud)
    résultat.id = arbre.noeuds.taille comme n32
    tableau_ajoute(*arbre.noeuds, résultat)
    retourne résultat
}

donne_élément_pour_position :: fonc (arbre: *ArbreDeDisposition, x: z32, y: z32) -> *HTML.Élément
{
    saufsi arbre.tampon_id_fut_dessiné {
        retourne nul
    }

    saufsi 0 <= x < arbre.largeur_vue && 0 <= y < arbre.hauteur_vue {
        retourne nul
    }

    tampon_id := arbre.tampon_id
    /* OpenGL est de bas en haut, donc inverse l'axe des y. */
    index := (arbre.hauteur_vue - y - 1) * arbre.largeur_vue + x

    index_noeud := tampon_id[index]

    noeud := arbre.noeuds[index_noeud]
    saufsi noeud {
        retourne nul
    }
    retourne noeud.donne_élément_récursif()
}

imprime_arbre :: fonc (arbre: *ArbreDeDisposition)
{
    résultat := donne_chaine_impression_arbre(arbre)
    diffère déloge(résultat)

    imprime("%\n", résultat)
}

donne_chaine_impression_arbre :: fonc (arbre: *ArbreDeDisposition) -> chaine
{
    saufsi arbre.racine {
        retourne ""
    }

    enchaineuse: Enchaineuse
    initialise_enchaineuse(*enchaineuse)
    diffère détruit_tampons(*enchaineuse)

    imprime_arbre(*enchaineuse, arbre.racine, 0)

    ajoute_au_tampon(*enchaineuse, "\n")

    imprime_arbre_stacking_context(*enchaineuse, arbre.stacking_context, 0)

    retourne chaine_depuis_enchaineuse(*enchaineuse)
}

imprime_arbre :: fonc (enchaineuse: *Enchaineuse, racine: *NoeudDeDisposition, profondeur: n32)
{
    indentation := donne_chaine_indentation(profondeur comme z32)

    nom_classe := racine.donne_nom_classe_sans_préfixe()

    imprime_dans_enchaineuse(enchaineuse, "%%", indentation, nom_classe)

    élément := donne_élément(racine)
    si élément {
        imprime_dans_enchaineuse(enchaineuse, " <%", élément.local_name)

        saufsi élément.id.est_vide() {
            ajoute_au_tampon(enchaineuse, "#", élément.id)
        }

        classe := HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_class)
        saufsi classe.est_vide() {
            ajoute_au_tampon(enchaineuse, ".", classe)
        }

        ajoute_au_tampon(enchaineuse, ">")
    }
    sinon {
        ajoute_au_tampon(enchaineuse, " <(anonyme)>")
    }

    modèle := racine.modèle_de_boite

    /* Position. */
    imprime_dans_enchaineuse(enchaineuse, " <%, %>", modèle.bordure.x, modèle.bordure.y)

    /* Cotations horizontales. */
    cotations := donne_cotations_horizontales(modèle)
    ajoute_au_tampon(enchaineuse, " CH ")
    imprime_cotation(enchaineuse, *cotations)

    /* Cotations verticales. */
    cotations = donne_cotations_verticales(modèle)
    ajoute_au_tampon(enchaineuse, " CV ")
    imprime_cotation(enchaineuse, *cotations)
    ajoute_au_tampon(enchaineuse, "\n")

    imprime_fragments(enchaineuse, racine, profondeur + 1)

    pour racine.enfants {
        imprime_arbre(enchaineuse, it, profondeur + 1)
    }
}

imprime_fragments :: fonc (enchaineuse: *Enchaineuse, noeud: *NoeudDeDisposition, profondeur: n32)
{
    si noeud.type != NoeudDeDispositionBloc && noeud.type != NoeudDeDispositionInline {
        retourne
    }

    indentation := donne_chaine_indentation(profondeur comme z32)
    indentation_texte := donne_chaine_indentation(profondeur comme z32 + 1)

    bloc := noeud comme *NoeudDeDispositionBloc

    pour bloc.lignes_tenante.fragments {
        ajoute_au_tampon(enchaineuse, indentation)
        nom_classe := it.noeud.donne_nom_classe_sans_préfixe()
        imprime_dans_enchaineuse(enchaineuse, "frag % % de % début % fin % rect (%,% %x%)", index_it, it.type, nom_classe, it.index_début, it.index_fin, it.x, it.y, it.largeur, it.hauteur)

        si it.noeud.type == NoeudDeDispositionTexte {
            noeud_texte := it.noeud comme *NoeudDeDispositionTexte
            fragment_texte := noeud_texte.fragments[it.index_début]
            ajoute_au_tampon(enchaineuse, "\n", indentation_texte, "\"", fragment_texte.texte, "\"")
        }

        ajoute_au_tampon(enchaineuse, "\n")
    }
}

dessine :: fonc (arbre: *ArbreDeDisposition, ctx: *ContexteDessin)
{
    arbre.hauteur_vue = ctx.hauteur_vue
    arbre.largeur_vue = ctx.largeur_vue

    IGUMI.réinitialise_données()
    dessine_stacking_context(arbre.stacking_context, ctx)
    ctx.peintre.termine_dessin()
}

dessine_tampon_id_couleur :: fonc (arbre: *ArbreDeDisposition, ctx: *ContexteDessin)
{
    arbre.hauteur_vue = ctx.hauteur_vue
    arbre.largeur_vue = ctx.largeur_vue

    ctx.tampon_id_couleur = vrai

    IGUMI.réinitialise_données()
    dessine_stacking_context_tampon_id(arbre.stacking_context, ctx)
    ctx.peintre.termine_dessin()

    ctx.tampon_id_couleur = faux
}

dessine_tampon_id :: fonc (arbre: *ArbreDeDisposition, ctx: *ContexteDessin)
{
    assert(ctx.tampon_id_couleur == faux)
    /* Tampon ID. */

    IGUMI.réinitialise_données_id()
    fbo: GLuint
    glGenFramebuffers(1, *fbo)
    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, fbo)

    render_buf: GLuint
    glGenRenderbuffers(1, *render_buf)
    glBindRenderbuffer(GL_RENDERBUFFER, render_buf)
    glRenderbufferStorage(GL_RENDERBUFFER, GL_R32UI, ctx.largeur_vue, ctx.hauteur_vue)
    vérifie_erreur("glRenderbufferStorage")
    glFramebufferRenderbuffer(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, GL_RENDERBUFFER, render_buf)
    vérifie_erreur("glFramebufferRenderbuffer")

    dessine_stacking_context_tampon_id(arbre.stacking_context, ctx)
    vérifie_erreur("dessine_tampon_id")
    IGUMI.termine_immédiat_id()

    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, 0)
    vérifie_erreur("glBindFramebuffer")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, fbo)
    vérifie_erreur("glBindFramebuffer")

    tableau_redimensionne(arbre.tampon_id, ctx.largeur_vue * ctx.hauteur_vue)

    glReadBuffer(GL_COLOR_ATTACHMENT0)
    vérifie_erreur("glReadBuffer")
    glReadPixels(0, 0, ctx.largeur_vue, ctx.hauteur_vue, GL_RED_INTEGER, GL_UNSIGNED_INT, arbre.tampon_id.pointeur)
    vérifie_erreur("glReadPixels")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, 0)

    glBindRenderbuffer(GL_RENDERBUFFER, 0)

    glDeleteFramebuffers(1, *fbo)
    glDeleteRenderbuffers(1, *render_buf)

    arbre.tampon_id_fut_dessiné = vrai
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom RésolveuseStyle.
 * \{ */

RègleAppariée :: struct {
    règle: *CSSStyleRule
    index_feuille_de_style: z64
    index_règle: z64
    origine: OrigineCascade
}

opérateur == :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle == b.règle
}

opérateur != :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle != b.règle
}

opérateur < :: fonc (a: RègleAppariée, b: RègleAppariée) -> bool
{
    retourne a.règle < b.règle
}

RèglesParIdentifiant :: struct {
    ident: n32
    règles: [..]RègleAppariée
}

opérateur == :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident == b.ident
}

opérateur != :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident != b.ident
}

opérateur < :: fonc (a: RèglesParIdentifiant, b: RèglesParIdentifiant) -> bool
{
    retourne a.ident < b.ident
}

RésolveuseStyle :: struct {
    globales: *GlobalesDessin
    document: *HTML.Document
    /* Pour MediaQuery et ContexteCalculStyle. */
    largeur_écran: r64
    hauteur_écran: r64
    taille_fonte_racine: r64
    feuilles_de_style: []*CSSStyleSheet

    /* Tampon réutilisé pour les cascades de propriétés. */
    déclarations_cascade: [..]DéclarationCascadée

    /* Style dont les propriétés sont toujours à leurs valeurs initiales.
     * Utilisé lors du calcul des valeurs. */
    valeurs_initiales: Style

    /* Cache. */
    pseudo_classes: PseudoClasses
    /* Les valeurs des ChaineUTF16Uniques des sélecteurs d'id.
     * Nous utilisons les valeurs et non les chaines car nous stockons les
     * données sous d'ensemble et il est plus rapide de comparer des nombres
     * que des chaines pour le stockage ordonné. */
    ids_connus: [..]n32
    attrs_connus: [..]n32
    classes_connues: [..]n32

    /* Ségrégation des règles pour accélérer leurs appariements. */
    règles_par_classe: [..]RèglesParIdentifiant
    autres_règles: [..]RègleAppariée

    /* Cache pour les hints de présentations. */
    presentational_hints: [..]*DéclarationPropriété
}

crée_résolveuse_style :: fonc (document: *HTML.Document, largeur: z32, hauteur: z32) -> *RésolveuseStyle
{
    résultat := loge(RésolveuseStyle)
    initialise_résolveuse(résultat)
    résultat.document = document
    résultat.taille_fonte_racine = document.globales_dessin.taille_fonte_défaut
    résultat.globales = document.globales_dessin
    résultat.feuilles_de_style = document.feuilles_de_style
    résultat.largeur_écran = largeur comme r64
    résultat.hauteur_écran = hauteur comme r64
    initialise_cache(résultat)
    retourne résultat
}

initialise_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    initialise_style(*résolveuse.valeurs_initiales)
}

initialise_cache :: fonc (résolveuse: *RésolveuseStyle)
{
    résolveuse.pseudo_classes = PseudoClasses.zéro
    résolveuse.ids_connus.taille = 0
    résolveuse.attrs_connus.taille = 0
    résolveuse.classes_connues.taille = 0
    résolveuse.autres_règles.taille = 0
    résolveuse.règles_par_classe.taille = 0

    user_agent := HTML.donne_user_agent(résolveuse.document)

    ctx: ContexteÉvaluationMediaQuery
    /* user_agent peut être nul pour l'exécution des tests. */
    ctx.supporte_scripting = user_agent != nul && user_agent.supporte_scripting
    ctx.largeur = résolveuse.largeur_écran
    ctx.taille_fonte = résolveuse.globales.taille_fonte_défaut

    pour feuille, index_feuille dans résolveuse.feuilles_de_style {
        index_règle : z64 = 0
        pour règle dans feuille.rules {
            si règle.type == CSSRuleType.Style {
                règle_de_style := règle comme *CSSStyleRule
                ajoute_règle_aux_caches(résolveuse, règle_de_style, index_feuille, index_règle)
                index_règle += 1
                continue
            }

            si règle.type == CSSRuleType.Media {
                règle_de_media := règle comme *CSSMediaRule
                saufsi évalue_media_list(*ctx, règle_de_media) {
                    continue
                }

                pour règle_média dans règle_de_media.css_rules {
                    si règle_média.type == CSSRuleType.Style {
                        règle_de_style := règle_média comme *CSSStyleRule
                        ajoute_règle_aux_caches(résolveuse, règle_de_style, index_feuille, index_règle)
                        index_règle += 1
                    }
                }

                continue
            }
        }
    }
}

évalue_media_list :: fonc (ctx: *ContexteÉvaluationMediaQuery, règle_de_media: *CSSMediaRule) -> bool
{
    saufsi règle_de_media.media_list {
        retourne faux
    }

    pour règle_de_media.media_list.media_queries {
        saufsi évalue_media_query(ctx, it) {
            retourne faux
        }
    }

    retourne vrai
}

ajoute_règle_aux_caches :: fonc (résolveuse: *RésolveuseStyle, règle_de_style: *CSSStyleRule, index_feuille: z64, index_règle: z64)
{
    règle_ajoutée := faux

    règle_appariée: RègleAppariée
    règle_appariée.règle = règle_de_style
    règle_appariée.index_feuille_de_style = index_feuille
    règle_appariée.index_règle = index_règle

    pour complex dans règle_de_style.selector_list.complex_selectors {
        dernière_classe: Optionnel(CSSOMString)

        pour compound dans complex.compound_selectors {
            init_de(Optionnel(CSSOMString))(*dernière_classe)
            pour simple dans compound.simple_selectors {
                si simple.type == SimpleSelector.Type.PseudoClass {
                    résolveuse.pseudo_classes |= simple.pseudo_classe
                }
                sinon si simple.type == SimpleSelector.Type.ID {
                    ensemble_ajoute(*résolveuse.ids_connus, simple.ident.valeur)
                }
                sinon si simple.type == SimpleSelector.Type.Attrib {
                    ensemble_ajoute(*résolveuse.attrs_connus, simple.ident.valeur)
                }
                sinon si simple.type == SimpleSelector.Type.Class {
                    ensemble_ajoute(*résolveuse.classes_connues, simple.ident.valeur)
                    dernière_classe = simple.ident
                }
            }
        }

        si dernière_classe.possède_valeur() {
            ajoute_règle_pour_classe(résolveuse, dernière_classe.Quelque, règle_appariée)
        }
        sinon saufsi règle_ajoutée {
            règle_ajoutée = vrai
            tableau_ajoute(*résolveuse.autres_règles, règle_appariée)
        }
    }
}

ajoute_règle_pour_classe :: fonc (résolveuse: *RésolveuseStyle, classe: CSSOMString, règle: RègleAppariée)
{
    règle_par_identifiant: RèglesParIdentifiant
    règle_par_identifiant.ident = classe.valeur

    index := ensemble_possède_retourne_index(*résolveuse.règles_par_classe, règle_par_identifiant)
    si index != -1 {
        règles_par_identifant := *résolveuse.règles_par_classe[index]
        tableau_ajoute(*règles_par_identifant.règles, règle)
        retourne
    }

    tableau_ajoute(*règle_par_identifiant.règles, règle)
    ensemble_ajoute(*résolveuse.règles_par_classe, règle_par_identifiant)
}

donne_règles_pour_classe :: fonc (résolveuse: *RésolveuseStyle, classe: CSSOMString) -> [..]RègleAppariée
{
    règle_par_identifiant: RèglesParIdentifiant
    règle_par_identifiant.ident = classe.valeur

    index := ensemble_possède_retourne_index(*résolveuse.règles_par_classe, règle_par_identifiant)
    si index != -1 {
        retourne résolveuse.règles_par_classe[index].règles
    }

    résultat: [..]RègleAppariée
    retourne résultat
}

détruit_données_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    déloge(résolveuse.déclarations_cascade)
    déloge(résolveuse.ids_connus)
    déloge(résolveuse.attrs_connus)
    déloge(résolveuse.classes_connues)
    déloge(résolveuse.autres_règles)
    pour résolveuse.règles_par_classe {
        déloge(it.règles)
    }
    déloge(résolveuse.règles_par_classe)
    pour résolveuse.presentational_hints {
        détruit_déclaration(it)
    }
    déloge(résolveuse.presentational_hints)
}

détruit_résolveuse :: fonc (résolveuse: *RésolveuseStyle)
{
    saufsi résolveuse {
        retourne
    }
    détruit_données_résolveuse(résolveuse)
    déloge(résolveuse)
}

/* https://html.spec.whatwg.org/multipage/rendering.html#the-css-user-agent-style-sheet-and-presentational-hints */
ajoute_presentational_hint :: fonc (résolveuse: *RésolveuseStyle, nom_propriété: CSSOMString, value: ValeurDeStyle)
{
    pour résolveuse.presentational_hints {
        si it.property_name != nom_propriété {
            continue
        }

        si it.value.taille != 1 {
            continue
        }

        si it.value[0] != value {
            continue
        }

        tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(it, Spécificité(0, 0, 0), OrigineCascade.Author))
        retourne
    }

    déclaration := loge(DéclarationPropriété)
    déclaration.info = donne_info_type_pour_nom(nom_propriété)
    déclaration.property_name = nom_propriété
    tableau_ajoute(*déclaration.value, value)

    tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(déclaration, Spécificité(0, 0, 0), OrigineCascade.Author))

    tableau_ajoute(*résolveuse.presentational_hints, déclaration)
}

possède_règle_avec_pseudo_classe :: fonc (résolveuse: *RésolveuseStyle, pseudo_classe: PseudoClasses) -> bool
{
    retourne (résolveuse.pseudo_classes & pseudo_classe) != PseudoClasses.zéro
}

possède_règle_avec_id :: fonc (résolveuse: *RésolveuseStyle, id: CSSOMString) -> bool
{
    si id.est_vide() {
        /* Si l'id est la chaine vide, nous devons vérifier que toutes les règles
         * s'appliquent, donc pas de court-circuitage. */
        retourne vrai
    }

    retourne ensemble_possède(*résolveuse.ids_connus, id.valeur)
}

possède_règle_avec_attribut :: fonc (résolveuse: *RésolveuseStyle, attr: CSSOMString) -> bool
{
    si attr.est_vide() {
        /* Si l'attribut est la chaine vide, nous devons vérifier que toutes les règles
         * s'appliquent, donc pas de court-circuitage. */
        retourne vrai
    }

    retourne ensemble_possède(*résolveuse.attrs_connus, attr.valeur)
}

donne_ajournement_pour_redimensionnement :: fonc (résolveuse: *RésolveuseStyle @inutilisée, largeur: z32 @inutilisée, hauteur: z32 @inutilisée) -> AjournementArbre
{
    /* À FAIRE : règles média, sauvegarde de l'ancienne taille */
    retourne AjournementArbre.Disposition
}

RègleEtSpécificité :: struct {
    règle: RègleAppariée
    /* La spécifité du sélecteur complexe ayant la hautissime spécifité ayant apparié l'élément. */
    spécificité: Spécificité
}

collecte_les_règles_appropriées :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément, pseudo_element: *PseudoElement) -> [..]RègleEtSpécificité
{
    temp: [..]RègleAppariée
    diffère déloge(temp)

    résultat: [..]RègleEtSpécificité

    classes := HTML.donne_liste_classe(élément)
    pour classes {
        règles_pour_classe := donne_règles_pour_classe(résolveuse, it)

        pour règle dans règles_pour_classe {
            ensemble_ajoute(*temp, règle)
        }
    }

    pour temp {
        apparie_règle(it, élément, pseudo_element, *résultat)
    }

    pour résolveuse.autres_règles {
        apparie_règle(it, élément, pseudo_element, *résultat)
    }

    retourne résultat
}

apparie_règle :: fonc (règle_de_style: RègleAppariée, élément: *HTML.Élément, pseudo_element: *PseudoElement, résultat: *[..]RègleEtSpécificité)
{
    règle_appariée := faux
    spécificité: Spécificité

    pour complex dans règle_de_style.règle.selector_list.complex_selectors {
        saufsi apparie_sélecteur(complex.compound_selectors, complex.compound_selectors.taille - 1, élément, pseudo_element) {
            continue
        }

        spécificité_sélecteur := calcule_spécificité(*complex)
        si spécificité_sélecteur > spécificité {
            spécificité = spécificité_sélecteur
        }

        règle_appariée = vrai
    }

    si règle_appariée {
        tableau_ajoute(résultat, RègleEtSpécificité(règle_de_style, spécificité))
    }
}

résoud_style :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément, pseudo_element: *PseudoElement, propriétés_parent: *PropriétésDeStyle) -> *PropriétésDeStyle
{
    règles := collecte_les_règles_appropriées(résolveuse, élément, pseudo_element)
    diffère déloge(règles)
    // imprime("% règle(s) pour %\n", règles.taille, élément.local_name)

    si pseudo_element != nul && règles.taille == 0 {
        retourne nul
    }

    résolveuse.déclarations_cascade.taille = 0

    pour règles {
        // imprime_règle(it.règle)
        règle := it.règle.règle
        déclarations := règle.style.déclarations
        tableau_réserve(*résolveuse.déclarations_cascade, résolveuse.déclarations_cascade.taille + déclarations.taille)
        spécificité := it.spécificité
        origine := donne_origine_cascade(règle)

        position: PositionDéclaration
        position.index_feuille = it.règle.index_feuille_de_style
        position.index_règle = it.règle.index_règle

        pour déclaration, index_déclaration dans déclarations {
            position.index_déclaration = index_déclaration
            tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(déclaration, spécificité, origine, position))
        }
    }

    HTML.applique_presentational_hints(élément, résolveuse)

    si élément.style {
        /* https://drafts.csswg.org/css-style-attr/#style-attribute
         * "In the cascade, these declarations are considered to have author origin and a specificity higher than any selector." */
        tableau_réserve(*résolveuse.déclarations_cascade, résolveuse.déclarations_cascade.taille + élément.style.déclarations.taille)
        spécificité := donne_spécificité_max()
        pour élément.style.déclarations {
            tableau_ajoute(*résolveuse.déclarations_cascade, DéclarationCascadée(it, spécificité, OrigineCascade.Author))
        }
    }
    tri_rapide(résolveuse.déclarations_cascade, compare_déclarations_pour_cascade)

    résultat := crée_propriétés_de_style(propriétés_parent)

    pour > résolveuse.déclarations_cascade {
        ajoute_déclaration(résultat, it.déclaration)
    }

    calcule_style(résolveuse, résultat, propriétés_parent)

    retourne résultat
}

AjournementArbre :: énum_drapeau {
    /* L'arbre doit être redessiné. */
    Dessin
    /* L'arbre doit être disposé. */
    Disposition
    /* L'arbre doit être construit. */
    Construction
    /* Les stacking contexts doivent être construit. */
    StackingContext
}

donne_ajournement_pour_changement_style :: fonc (résolveuse: *RésolveuseStyle, élément: *HTML.Élément) -> AjournementArbre
{
    noeud_de_disposition := élément.noeud_de_disposition
    anciennes_propriétés: *PropriétésDeStyle
    propriétés_parent: *PropriétésDeStyle

    si noeud_de_disposition {
        anciennes_propriétés = noeud_de_disposition.propriétés
        assert(anciennes_propriétés != nul)
        propriétés_parent = anciennes_propriétés.parent
    }

    nouvelles_propriétés := résoud_style(résolveuse, élément, nul, propriétés_parent)
    diffère détruit_propriétés(nouvelles_propriétés)

    /* Vérifie si "display: none" reste "display: none". */
    display := nouvelles_propriétés.donne_valeur_calculée(IDPropriété.Display).display
    si display.outer_type == Display.Type.None {
        si noeud_de_disposition == nul {
            /* Nous restons sur "display: none". */
            retourne AjournementArbre.zéro
        }
        /* Nous sommes passé de "display: none" à autre chose, reconstruisons. */
        retourne AjournementArbre.Construction
    }

    si noeud_de_disposition == nul {
        /* Nous sommes passé de "display: none" à autre chose, reconstruisons. */
        retourne AjournementArbre.Construction
    }

    assert(anciennes_propriétés != nul)
    résultat: AjournementArbre

    pour * infos_types_propriétés_shorthand {
        résultat |= donne_ajournement_pour_différence_propriétés(nouvelles_propriétés, anciennes_propriétés, it)
    }

    pour * infos_types_propriétés {
        résultat |= donne_ajournement_pour_différence_propriétés(nouvelles_propriétés, anciennes_propriétés, it)
    }

    si résultat.Construction {
        retourne résultat
    }

    si résultat == AjournementArbre.zéro {
        retourne résultat
    }

    /* Sauvegarde les propriétés. */
    permute(nouvelles_propriétés, anciennes_propriétés)

    retourne résultat
}

donne_ajournement_pour_différence_propriétés :: fonc (nouvelles_propriétés: *PropriétésDeStyle, anciennes_propriétés: *PropriétésDeStyle, info: *InfoTypePropriété) -> AjournementArbre
{
    nouvelle := nouvelles_propriétés.donne_déclaration(info.nom)
    ancienne := anciennes_propriétés.donne_déclaration(info.nom)

    si nouvelle == nul && ancienne == nul {
        retourne AjournementArbre.zéro
    }

    si nouvelle == nul || ancienne == nul {
        retourne donne_ajournement_arbre(info)
    }

    si nouvelle.value.taille != ancienne.value.taille {
        retourne donne_ajournement_arbre(info)
    }

    pour nouvelle.value {
        si ancienne.value[index_it] == it {
            continue
        }

        retourne donne_ajournement_arbre(info)
    }

    si nouvelle.important != ancienne.important {
        retourne donne_ajournement_arbre(info)
    }

    retourne AjournementArbre.zéro
}

donne_ajournement_arbre :: fonc (info: *InfoTypePropriété) -> AjournementArbre
{
    si info.drapeaux.AffecteDisposition {
        retourne AjournementArbre.Disposition
    }
    si info.drapeaux.AffecteConstructionArbre {
        retourne AjournementArbre.Construction
    }
    si info.drapeaux.AffecteStackingContext {
        retourne AjournementArbre.StackingContext
    }
    retourne AjournementArbre.Dessin
}

/* https://www.w3.org/TR/css-cascade/#origin */
OrigineCascade :: énum {
    Author
    User
    UserAgent
}

PositionDéclaration :: struct {
    index_feuille: z64
    index_règle: z64
    index_déclaration: z64
}

opérateur < :: fonc (a: PositionDéclaration, b: PositionDéclaration) -> bool
{
    si a.index_feuille < b.index_feuille {
        retourne vrai
    }

    si a.index_feuille > b.index_feuille {
        retourne faux
    }

    si a.index_règle < b.index_règle {
        retourne vrai
    }

    si a.index_règle > b.index_règle {
        retourne faux
    }

    retourne a.index_déclaration < b.index_déclaration
}

DéclarationCascadée :: struct {
    déclaration: *DéclarationPropriété
    spécificité: Spécificité
    origine: OrigineCascade
    position: PositionDéclaration
}

/* https://www.w3.org/TR/css-cascade/#cascade-sort */
compare_déclarations_pour_cascade :: fonc (a: DéclarationCascadée, b: DéclarationCascadée) -> z32
{
    /* Tri par importance. */
    si a.déclaration.important && !b.déclaration.important {
        retourne -1
    }
    si b.déclaration.important && !a.déclaration.important {
        retourne 1
    }

    /* Même importance -> tri par origine. */
    si a.origine != b.origine {
        retourne a.origine comme z32 - b.origine comme z32
    }

    /* Même importance et origine -> tri par spécifité. */
    si a.spécificité > b.spécificité {
        retourne -1
    }
    si b.spécificité > a.spécificité {
        retourne 1
    }

    /* Même importance, origine, et spécifité -> tri par position. */
    si a.position < b.position {
        retourne 1
    }
    si b.position < a.position {
        retourne -1
    }

    /* À FAIRE : contextes. */

    retourne 0
}

crée_style_pour_bloc_anonyme :: fonc (résolveuse: *RésolveuseStyle, bloc: *NoeudDeDispositionBloc, bloc_parent: *NoeudDeDispositionBloc, outer_display: Display.Type)
{
    résultat := crée_propriétés_de_style(bloc_parent.propriétés)
    calcule_style(résolveuse, résultat, bloc_parent.propriétés)
    résultat.définis_valeur_calculée(IDPropriété.Display, Display(outer_type = outer_display, inner_type = Display.Type.Flow))

    bloc.propriétés = résultat
}

ContexteParsageShorthandVariable :: struct {
    empl base: ContexteParsageShorthand

    ctx_calcul_style: *ContexteCalculStyle
}

contexte_parsage_shorthand_variable_ajoute_propriété :: fonc (ctx: *ContexteParsageShorthandVariable, nom: CSSOMString, valeur: ValeurDeStyle)
{
    info := donne_info_type_pour_nom(nom)
    saufsi info {
        retourne
    }

    déclaration: DéclarationPropriété
    déclaration.info = info
    déclaration.property_name = nom
    déclaration.value.pointeur = *valeur
    déclaration.value.taille = 1

    propriétés := ctx.ctx_calcul_style.propriétés

    si info.sur_calcul_style {
        info.sur_calcul_style(*déclaration, *propriétés.style, ctx.ctx_calcul_style)
    }
    sinon {
        calcule_valeur_propriété_générique(*déclaration, *propriétés.style, ctx.ctx_calcul_style)
    }

    si info.id == IDPropriété.FontSize {
        ctx.ctx_calcul_style.taille_fonte = propriétés.donne_valeur_calculée(IDPropriété.FontSize).pixels
    }
}

développe_variables :: fonc (propriétés: *PropriétésDeStyle, value: []ComponentValue) -> [..]ComponentValue
{
    résultat: [..]ComponentValue
    pour value {
        saufsi it.est_fonction(PROP_var) {
            tableau_ajoute(*résultat, it)
            continue
        }

        parseuse := initialise_une_parseuse(it.function.value)
        opt_variable := parse_variables_depuis_arguments(parseuse)
        saufsi opt_variable.possède_valeur() {
            continue
        }
        variable := opt_variable.Quelque.variable

        déclaration := donne_déclaration_propriété_personnalisée(propriétés, variable.nom)
        saufsi déclaration {
            continue
        }

        pour valeur_var dans déclaration.valeur_personnalisée {
            tableau_ajoute(*résultat, valeur_var)
        }

        // À FAIRE : fallback
        // À FAIRE : variables récursives
    }
    retourne résultat
}

calcule_style :: fonc (résolveuse: *RésolveuseStyle, propriétés: *PropriétésDeStyle, propriétés_parent: *PropriétésDeStyle)
{
    /* https://www.w3.org/TR/2011/REC-CSS2-20110607/cascade.html#computed-value */
    ctx_calcul_style: ContexteCalculStyle
    ctx_calcul_style.document = résolveuse.document
    ctx_calcul_style.taille_fonte = résolveuse.globales.taille_fonte_défaut
    ctx_calcul_style.taille_fonte_racine = résolveuse.taille_fonte_racine
    ctx_calcul_style.propriétés = propriétés
    ctx_calcul_style.valeurs_initiales = *résolveuse.valeurs_initiales
    ctx_calcul_style.largeur_canevas = résolveuse.largeur_écran
    ctx_calcul_style.hauteur_canevas = résolveuse.hauteur_écran

    si propriétés_parent {
        ctx_calcul_style.taille_fonte = propriétés_parent.donne_valeur_calculée(IDPropriété.FontSize).pixels
    }

    pour propriétés.déclarations {
        si it.est_personnalisée {
            continue
        }
        info := it.info

        si it.est_shorthand_avec_variable {
            valeurs := développe_variables(propriétés, it.valeur_personnalisée)

            ctx_parsage: ContexteParsageShorthandVariable
            ctx_parsage.sur_ajoute_propriété = contexte_parsage_shorthand_variable_ajoute_propriété
            ctx_parsage.ctx_calcul_style = *ctx_calcul_style

            déclaration_parsée: ParsedDeclaration
            déclaration_parsée.name = info.nom
            ctx_parsage.déclaration_parsée = *déclaration_parsée

            _ := info.sur_parsage_shorthand(*ctx_parsage, valeurs)

            déloge(valeurs)
            continue
        }

        si info.sur_calcul_style {
            info.sur_calcul_style(it, *propriétés.style, *ctx_calcul_style)
        }
        sinon {
            calcule_valeur_propriété_générique(it, *propriétés.style, *ctx_calcul_style)
        }

        si info.id == IDPropriété.FontSize {
            ctx_calcul_style.taille_fonte = propriétés.donne_valeur_calculée(IDPropriété.FontSize).pixels
        }
    }

    si propriétés.style.valeurs[IDPropriété.BorderStyleLeft].line_style == LineStyle.None {
        propriétés.définis_valeur_calculée(IDPropriété.BorderWidthLeft, ValeurDeStyle(pixels = 0.0))
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleTop].line_style == LineStyle.None {
        propriétés.définis_valeur_calculée(IDPropriété.BorderWidthTop, ValeurDeStyle(pixels = 0.0))
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleRight].line_style == LineStyle.None {
        propriétés.définis_valeur_calculée(IDPropriété.BorderWidthRight, ValeurDeStyle(pixels = 0.0))
    }
    si propriétés.style.valeurs[IDPropriété.BorderStyleBottom].line_style == LineStyle.None {
        propriétés.définis_valeur_calculée(IDPropriété.BorderWidthBottom, ValeurDeStyle(pixels = 0.0))
    }
    saufsi propriétés.style.valeurs[IDPropriété.FontSize].est_pixels() {
        propriétés.définis_valeur_calculée(IDPropriété.FontSize, calcule_valeur_propriété_générique(propriétés.style.valeurs[IDPropriété.FontSize], GenreValeurCalculée.LongueurAbsolue | GenreValeurCalculée.FontSize, *propriétés.style, *ctx_calcul_style))
    }
    saufsi propriétés.style.valeurs[IDPropriété.FontWeight].est_nombre() {
        propriétés.définis_valeur_calculée(IDPropriété.FontWeight, calcule_valeur_propriété_générique(propriétés.style.valeurs[IDPropriété.FontWeight], GenreValeurCalculée.FontWeight, *propriétés.style, *ctx_calcul_style))
    }

    calcule_display_final(propriétés)

    /* Transfert les valeurs calculées aux valeurs utilisées. */
    propriétés.style_utilisée = propriétés.style
}

/* Corrige display en prenant en compte position et float.
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#dis-pos-flo */
calcule_display_final :: fonc (propriétés: *PropriétésDeStyle)
{
    display := propriétés.donne_valeur_calculée(IDPropriété.Display).display
    position := propriétés.donne_valeur_calculée(IDPropriété.Position)
    float := propriétés.donne_valeur_calculée(IDPropriété.Float)

    // 1. If 'display' has the value 'none', then 'position' and 'float' do not apply. In this case,
    // the element generates no box.
    si display.outer_type == Display.Type.None {
        retourne
    }

    // 2. Otherwise, if 'position' has the value 'absolute' or 'fixed', the box is absolutely positioned,
    // the computed value of 'float' is 'none', and display is set according to the table below.
    // The position of the box will be determined by the 'top', 'right', 'bottom' and 'left' properties
    // and the box's containing block.
    si position.est_mot_clé(PROP_absolute) || position.est_mot_clé(PROP_fixed) {
        propriétés.définis_valeur_calculée(IDPropriété.Float, ValeurDeStyle(mot_clé = PROP_none))
        propriétés.définis_valeur_calculée(IDPropriété.Display, convertis_display_pour_position_ou_float(display))
        retourne
    }

    // 3. Otherwise, if 'float' has a value other than 'none', the box is floated and 'display' is set
    // according to the table below.
    saufsi float.est_none() {
        propriétés.définis_valeur_calculée(IDPropriété.Display, convertis_display_pour_position_ou_float(display))
        retourne
    }

    // 4. Otherwise, if the element is the root element, 'display' is set according to the table below,
    // except that it is undefined in CSS 2.1 whether a specified value of 'list-item' becomes a computed
    // value of 'block' or 'list-item'.
    // À FAIRE : test plus robuste pour définir l'élément racine.
    // À FAIRE : list-item.
    saufsi propriétés.parent {
        propriétés.définis_valeur_calculée(IDPropriété.Display, convertis_display_pour_position_ou_float(display))
        retourne
    }

    // 5. Otherwise, the remaining 'display' property values apply as specified.
}

convertis_display_pour_position_ou_float :: fonc (spécifié: Display) -> ValeurDeStyle
{
    // À FAIRE : display-internal
    résultat := spécifié

    si spécifié.outer_type == Display.Type.Inline {
        résultat.outer_type = Display.Type.Block

        si spécifié.inner_type != Display.Type.Table {
            résultat.inner_type = Display.Type.FlowRoot
        }
    }

    retourne ValeurDeStyle(display = résultat)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Génération d'arbres de disposition.
 * \{ */

donne_arbre_de_disposition_ajourné :: fonc (document: *HTML.Document, largeur: z32, hauteur: z32) -> *ArbreDeDisposition
{
    ajournement := document.ajournement_arbre

    si document.arbre_de_disposition == nul || ajournement.Construction {
        si document.arbre_de_disposition {
            détruit_arbre(document.arbre_de_disposition)
            document.arbre_de_disposition = nul
        }
        crée_arbre_de_disposition(document, largeur, hauteur)
    }
    sinon si ajournement.Disposition {
        dispose_arbre(document.globales_dessin, document.arbre_de_disposition, largeur, hauteur)
    }
    sinon si ajournement.StackingContext {
        détruit_stacking_context(document.arbre_de_disposition.stacking_context)
        document.arbre_de_disposition.stacking_context = construit_arbre_de_stacking_contexts(document.arbre_de_disposition.racine)
    }

    document.ajournement_arbre = AjournementArbre.zéro

    retourne document.arbre_de_disposition
}

crée_arbre_de_disposition :: fonc (document: *HTML.Document, largeur: z32, hauteur: z32)
{
    html := HTML.donne_élément_html(document)
    saufsi html {
        retourne
    }

    saufsi document.feuille_de_style_agent {
        document.feuille_de_style_agent = HTML.donne_feuille_de_style_défaut()
        tableau_insère_index(*document.feuilles_de_style, document.feuille_de_style_agent, 0)
    }

    saufsi document.globales_dessin {
        document.globales_dessin = crée_globales_dessin()
    }

    saufsi document.résolveuse_style {
        document.résolveuse_style = crée_résolveuse_style(document, largeur, hauteur)
    }

    résultat := loge(ArbreDeDisposition)

    résolveuse := document.résolveuse_style

    /* À FAIRE : ceci peut changer mais comment est encore inconnue. */
    html.pseudo_classes.Root = vrai

    créatrice: CréatriceArbre
    créatrice.arbre = résultat
    créatrice.résolveuse = résolveuse

    crée_noeud_pour_élément(*créatrice, nul, html)
    résultat.racine = html.noeud_de_disposition

    // imprime("Créé % noeud(s)\n", résultat.noeuds.taille)

    dispose_arbre(document.globales_dessin, résultat, largeur, hauteur)

    // imprime_arbre(résultat)

    si document.arbre_de_disposition {
        détruit_arbre(document.arbre_de_disposition)
        document.arbre_de_disposition = nul
    }

    document.arbre_de_disposition = résultat

    résultat.stacking_context = construit_arbre_de_stacking_contexts(résultat.racine)
}

dispose_arbre :: fonc (globales: *GlobalesDessin, arbre: *ArbreDeDisposition, largeur: z32, hauteur: z32)
{
    initial_containing_block: ContainingBlock
    initial_containing_block.rect.hauteur = hauteur comme r32
    initial_containing_block.rect.largeur = largeur comme r32
    initial_containing_block.noeud = arbre.racine

    ctx: ContexteDeFormattage
    ctx.globales = globales
    ctx.initial_containing_block = *initial_containing_block

    boites_absolues: GardeBoitesAbsolues
    diffère déloge(boites_absolues.boites)

    ctx.boites_absolues = *boites_absolues

    mesure(arbre.racine, *ctx, ModeDeFormattage.Normal, nul)

    // imprime("taille arbre : %x%\n", résultat.racine.largeur, résultat.racine.hauteur)
}

CréatriceArbre :: struct {
    résolveuse: *RésolveuseStyle
    arbre: *ArbreDeDisposition
}

crée_noeud_pour_élément :: fonc (empl créatrice: *CréatriceArbre, parent: *NoeudDeDisposition, noeud: *HTML.Noeud)
{
    si HTML.est_texte(noeud) {
        /* Les textes doivent une boite inline anonyme autour d'eux. */
        résultat := arbre.crée_noeud(NoeudDeDispositionInline)
        crée_style_pour_bloc_anonyme(résolveuse, résultat, parent comme *NoeudDeDispositionBloc, Display.Type.Inline)
        parent.ajoute_enfant(résultat)

        texte := arbre.crée_noeud(NoeudDeDispositionTexte)
        texte.noeud_texte = noeud comme *HTML.NoeudTexte
        résultat.ajoute_enfant(texte)
        retourne
    }

    saufsi HTML.est_élément(noeud) {
        retourne
    }

    élément := noeud comme *HTML.Élément

    propriétés_parent: *PropriétésDeStyle
    si parent {
        propriétés_parent = parent.propriétés
    }

    propriétés := résoud_style(résolveuse, élément, nul, propriétés_parent)

    display := propriétés.donne_valeur_calculée(IDPropriété.Display).display
    si display.outer_type == Display.Type.None {
        détruit_propriétés(propriétés)
        retourne
    }

    ignore_enfants := faux

    résultat: *NoeudDeDisposition
    si élément.local_name == HTML.TAG_img {
        résultat = crée_noeud_de_remplacement_image(arbre, élément comme *HTML.HTMLImageElement)
    }
    sinon si élément.local_name == HTML.TAG_input && HTML.is_text_entry_widget(élément comme *HTML.HTMLInputElement) {
        résultat = crée_noeud_de_remplacement_text_input(arbre, élément comme *HTML.HTMLInputElement)
    }
    sinon si élément.local_name == HTML.TAG_textarea {
        résultat = crée_noeud_de_remplacement_textarea(arbre, élément comme *HTML.HTMLTextAreaElement)
    }
    sinon si élément.local_name == HTML.TAG_canvas {
        résultat = crée_noeud_de_remplacement_canvas(arbre, élément comme *HTML.HTMLCanvasElement)
    }
    sinon si élément.local_name == HTML.TAG_select {
        résultat = crée_noeud_de_remplacement_select(arbre, élément comme *HTML.HTMLSelectElement)
        ignore_enfants = vrai
    }
    sinon {
        résultat = crée_noeud_pour_display(arbre, display, élément)
    }

    si parent == nul {
        résolveuse.taille_fonte_racine = propriétés.donne_valeur_calculée(IDPropriété.FontSize).pixels
    }

    table: *TableBox
    si résultat.type == TableWrapperBox {
        table = (résultat comme *TableWrapperBox).table
    }

    élément.noeud_de_disposition = résultat
    résultat.propriétés = propriétés
    si table {
        résultat.propriétés = résoud_style(résolveuse, élément, nul, propriétés_parent)
        table.propriétés = propriétés
    }

    si parent {
        parent.ajoute_enfant(résultat)
    }

    si ignore_enfants {
        retourne
    }

    nouveau_parent := résultat
    si table {
        nouveau_parent = table
    }

    pour noeud.enfants {
        crée_noeud_pour_élément(créatrice, nouveau_parent, it)
    }

    crée_noeud_pour_pseudo_élément(créatrice, résultat, élément, PROP_after)
    
    corrige_hiérarchie(résultat, créatrice)
}

crée_noeud_pour_display :: fonc (arbre: *ArbreDeDisposition, display: Display, élément: *HTML.Élément) -> *NoeudDeDisposition
{
    si display.inner_type == Display.Type.Table {
        wrapper_box := arbre.crée_noeud(TableWrapperBox)
        wrapper_box.élément = élément

        box := arbre.crée_noeud(TableBox)
        wrapper_box.table = box

        wrapper_box.ajoute_enfant(box)
        retourne wrapper_box
    }
    si display.inner_type == Display.Type.TableCaption {
        box := arbre.crée_noeud(CaptionBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableColumn {
        box := arbre.crée_noeud(TableColumnBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableColumnGroup {
        box := arbre.crée_noeud(TableColumnGroupBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableCell {
        box := arbre.crée_noeud(TableCellBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableRow {
        box := arbre.crée_noeud(TableRowBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableFooterGroup {
        box := arbre.crée_noeud(TableFooterGroupBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableHeaderGroup {
        box := arbre.crée_noeud(TableHeaderGroupBox)
        box.élément = élément
        retourne box
    }
    si display.inner_type == Display.Type.TableRowGroup {
        box := arbre.crée_noeud(TableRowGroupBox)
        box.élément = élément
        retourne box
    }
    si display.outer_type == Display.Type.Block || display.inner_type == Display.Type.Flex {
        bloc := arbre.crée_noeud(NoeudDeDispositionBloc)
        bloc.élément = élément
        retourne bloc
    }
    si élément && élément.local_name == HTML.TAG_br {
        bris := arbre.crée_noeud(NoeudDeDispositionNouvelleLigne)
        bris.élément = élément
        retourne bris
    }

    inline := arbre.crée_noeud(NoeudDeDispositionInline)
    inline.élément = élément
    retourne inline
}

crée_noeud_pour_pseudo_élément :: fonc (empl créatrice: *CréatriceArbre, parent: *NoeudDeDisposition, élément: *HTML.Élément, ident_pseudo_element: CSSOMString)
{
    pseudo_element := PseudoElement(ident_pseudo_element)
    propriétés_pseudo := résoud_style(résolveuse, élément, *pseudo_element, parent.propriétés)
    saufsi propriétés_pseudo {
        retourne
    }

    display := propriétés_pseudo.donne_valeur_calculée(IDPropriété.Display).display
    si display.outer_type == Display.Type.None {
        détruit_propriétés(propriétés_pseudo)
        retourne
    }

    // À FAIRE : content

    noeud := crée_noeud_pour_display(arbre, display, nul)
    noeud.propriétés = propriétés_pseudo
    noeud.est_pseudo_element = vrai

    si noeud.type == TableWrapperBox {
        table := (noeud comme *TableWrapperBox).table
        table.propriétés = résoud_style(résolveuse, élément, *pseudo_element, parent.propriétés)
    }

    ajoute_enfant(parent, noeud)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ContexteDessin
 * \{ */

ContexteDessin :: struct {
    document: *HTML.Document
    globales: *GlobalesDessin
    peintre: *Peintre

    hauteur_vue: z32
    largeur_vue: z32

    décalage_y: r32
    id_stacking_context: n32

    tampon_id_couleur: bool
    est_fixé: bool
}

donne_rectangle_corrigé :: fonc (ctx: *ContexteDessin, rect: RectanglePosDim(r32)) -> RectanglePosDim(r32)
{
    /* Nous disposons les noeuds comme si leur origine était leur coin du haut
     * gauche mais nous devons les dessiner comme si leur origine était leur
     * coin du bas gauche.
     * De plus, nous devons nous assurer que l'origine du document est le bas
     * gauche de la page et non le haut gauche. */
    rect.y = ctx.hauteur_vue comme r32 - rect.y - rect.hauteur
    saufsi ctx.est_fixé {
        rect.y += ctx.décalage_y
    }
    retourne rect
}

remplis_rectangle_id :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDisposition)
{
    rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())

    si ctx.tampon_id_couleur {
        peintre := ctx.peintre
        entropie := graine_aléatoire(noeud.id + 1)

        couleur: CouleurRVBA
        couleur.r = aléatoire_unilatérale(entropie)
        couleur.v = aléatoire_unilatérale(entropie)
        couleur.b = aléatoire_unilatérale(entropie)
        couleur.a = 1.0
        peintre.remplis_rectangle(rect, couleur)
        retourne
    }

    IGUMI.commence_immédiat_id(IGUMI.ModeImmédiat.TRIANGLES, IGUMI.nuanceur_id)

    // imprime("[%] (% % % %) : %\n", #nom_de_cette_fonction, rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)

    IGUMI.quad_immédiat_id(rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)
}

remplis_rectangle_id :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDisposition, rect: RectanglePosDim(r32))
{
    rect = ctx.donne_rectangle_corrigé(rect)

    si ctx.tampon_id_couleur {
        peintre := ctx.peintre
        entropie := graine_aléatoire(noeud.id + 1)

        couleur: CouleurRVBA
        couleur.r = aléatoire_unilatérale(entropie)
        couleur.v = aléatoire_unilatérale(entropie)
        couleur.b = aléatoire_unilatérale(entropie)
        couleur.a = 1.0
        peintre.remplis_rectangle(rect, couleur)
        retourne
    }

    IGUMI.commence_immédiat_id(IGUMI.ModeImmédiat.TRIANGLES, IGUMI.nuanceur_id)

    // imprime("[%] (% % % %) : %\n", #nom_de_cette_fonction, rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)

    IGUMI.quad_immédiat_id(rect.x, rect.y, rect.largeur, rect.hauteur, noeud.id)
}

dessine_image :: fonc (ctx: *ContexteDessin, image: ImageIO, rect: RectanglePosDim(r32))
{
    rect = ctx.donne_rectangle_corrigé(rect)
    peintre := ctx.peintre

    si image.donnees {
        peintre.dessine_image(image, rect)
    }
    sinon {
        /* À FAIRE : image nulle */
        peintre.remplis_rectangle(rect, CouleurRVBA(1.0, 0.0, 1.0, 1.0))
    }
}

donne_image_pour_url :: fonc (ctx: *ContexteDessin, url_string: chaine) -> Optionnel(ImageIO)
{
    données_image := HTML.donne_image_pour_url(ctx.document, url_string)

    si données_image.état == HTML.ÉtatRequêteImage.Succès {
        retourne données_image.données
    }

    si données_image.état == HTML.ÉtatRequêteImage.Échec {
        retourne
    }

    si données_image.état == HTML.ÉtatRequêteImage.EnCours {
        retourne
    }

    url_src := HTML.basic_url_parser(url_string)

    HTML.définis_état_requête_image(ctx.document, url_string, HTML.ÉtatRequêteImage.EnCours)

    RappelProcessResponse :: struct {
        empl base: HTML.BaseProcessResponse

        document: *HTML.Document
        request: *HTML.Request
        url_string: chaine
    }

    sur_rappel_process_response :: fonc (base: *HTML.BaseProcessResponse, réponse: *HTML.Response)
    {
        diffère HTML.détruit_response(réponse)

        rappel := base comme *RappelProcessResponse

        saufsi réponse.body_info {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        content_type := réponse.body_info.content_type
        saufsi est_type_mime_image(content_type) {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        contenu := réponse.body.Quelque

        image_data: ImageIO
        saufsi HTML.décode_image(contenu, content_type, *image_data) {
            HTML.définis_état_requête_image(rappel.document, rappel.url_string, HTML.ÉtatRequêteImage.Échec)
            retourne
        }

        HTML.définis_données_image(rappel.document, rappel.url_string, image_data)
        HTML.invalide_style_pour_image(rappel.document)
    }

    sur_destruction_process_response :: fonc (base: *HTML.BaseProcessResponse)
    {
        rappel := base comme *RappelProcessResponse
        HTML.détruit_request(rappel.request)
        déloge(rappel)
    }

    process_response := loge(RappelProcessResponse)
    process_response.document = ctx.document
    process_response.url_string = url_string
    process_response.sur_rappel = sur_rappel_process_response
    process_response.sur_destruction = sur_destruction_process_response

    request := loge(HTML.Request)
    request.destination = "image"
    request.mode = "no-cors"
    request.redirect_mode = "follow"
    HTML.définis_url(request, url_src)
    process_response.request = request

    user_agent := HTML.donne_user_agent(ctx.document)
    fetch_controller := HTML.fetch(user_agent, request, process_response = process_response)
    diffère HTML.détruit_fetch_controller(fetch_controller)
}

dessine_arrière_plan :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDispositionBloc)
{
    peintre := ctx.peintre

    background_color := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BackgroundColor).couleur
    si background_color.a != 0 {
        rect_noeud: RectanglePosDim(r32)
        si noeud.parent == nul {
            /* L'arrière plan de l'élément racine doit s'appliquer au cannevas. */
            rect_noeud.hauteur = ctx.hauteur_vue comme r32
            rect_noeud.largeur = ctx.largeur_vue comme r32
        }
        sinon {
            rect_noeud = ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())
        }

        border_radius_top_left := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BorderTopLeftRadius).pixels comme r32
        border_radius_top_right := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BorderTopRightRadius).pixels comme r32
        border_radius_bottom_right := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BorderBottomRightRadius).pixels comme r32
        border_radius_bottom_left := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BorderBottomLeftRadius).pixels comme r32

        rayons := [ border_radius_top_left, border_radius_top_right, border_radius_bottom_right, border_radius_bottom_left ]
        peintre.remplis_rectangle_arrondi(rect_noeud, rayons, background_color)
    }
    sinon si noeud.parent == nul && noeud.élément && noeud.élément.local_name == HTML.TAG_html {
        /* Hérite de l'arrière-plan du "body".
            * https://www.w3.org/TR/CSS21/colors.html#background */
        body := HTML.donne_élément_body(noeud.élément.document)
        si body && body.noeud_de_disposition {
            background_color = body.noeud_de_disposition.propriétés.donne_valeur_utilisée(IDPropriété.BackgroundColor).couleur
            si background_color.a != 0 {
                /* À FAIRE : ne peint pas l'arrière-plan du "body" dans ce cas. */
                rect_noeud: RectanglePosDim(r32)
                rect_noeud.hauteur = ctx.hauteur_vue comme r32
                rect_noeud.largeur = ctx.largeur_vue comme r32
                peintre.remplis_rectangle(rect_noeud, background_color)
            }
        }
    }

    image := noeud.propriétés.donne_valeur_utilisée(IDPropriété.BackgroundImage)
    si image.est_none() {
        retourne
    }

    opt_image_data := ctx.donne_image_pour_url(image.url_absolu)
    saufsi opt_image_data.possède_valeur() {
        retourne
    }

    /* À FAIRE : parse la propriété "background-repeat". Ceci est la valeur défaut. */
    répétition: RépétitionImage
    répétition.x = GenreRépétitionImage.Normale
    répétition.y = GenreRépétitionImage.Normale

    image_data := opt_image_data.Quelque
    /* À FAIRE : background-clip */
    rect := noeud.donne_rectangle_bordure()

    rect_image := rect
    rect_image.largeur = image_data.largeur comme r32
    rect_image.hauteur = image_data.hauteur comme r32

    rect = ctx.donne_rectangle_corrigé(rect)
    rect_image = ctx.donne_rectangle_corrigé(rect_image)
    peintre.dessine_image(image_data, rect_image, rect, répétition)
}

dessine_bordure :: fonc (ctx: *ContexteDessin, noeud: *NoeudDeDispositionBloc)
{
    bordure := donne_bordure(*noeud.propriétés.style)
    peintre := ctx.peintre

    pour bordure.style {
        si it == LineStyle.None || it == LineStyle.Hidden {
            continue
        }

        coté := index_it comme z32 comme BorderEdge

        couleur: CouleurRVBAN8

        si it == LineStyle.Inset {
            si coté == BorderEdge.Top || coté == BorderEdge.Left {
                couleur = CouleurRVBAN8(154, 154, 154, 255)
            }
            sinon {
                couleur = CouleurRVBAN8(238, 238, 238, 255)
            }
        }
        sinon si it == LineStyle.Outset {
            si coté == BorderEdge.Top || coté == BorderEdge.Left {
                couleur = CouleurRVBAN8(238, 238, 238, 255)
            }
            sinon {
                couleur = CouleurRVBAN8(154, 154, 154, 255)
            }
        }
        sinon {
            border_color := bordure.color[coté]
            /* À FAIRE : calcule la valeur même s'il n'y a pas de propriété. */
            si border_color.est_mot_clé(PROP_currentColor) {
                couleur = noeud.propriétés.donne_valeur_utilisée(IDPropriété.Color)
            }
            sinon {
                couleur = bordure.color[coté].couleur
            }
        }

        largeur := bordure.width[coté].pixels comme r32

        rect: RectanglePosDim(r32)
        rect_bordure := noeud.donne_rectangle_bordure()

        si coté == BorderEdge.Top {
            rect.x = rect_bordure.x
            rect.y = rect_bordure.y
            rect.largeur = rect_bordure.largeur
            rect.hauteur = largeur
        }
        sinon si coté == BorderEdge.Bottom {
            rect.x = rect_bordure.x
            rect.y = rect_bordure.y + rect_bordure.hauteur - largeur
            rect.largeur = rect_bordure.largeur
            rect.hauteur = largeur
        }
        sinon si coté == BorderEdge.Left {
            rect.x = rect_bordure.x
            rect.y = rect_bordure.y
            rect.largeur = largeur
            rect.hauteur = rect_bordure.hauteur
        }
        sinon /* si coté == BorderEdge.Right */ {
            rect.x = rect_bordure.x + rect_bordure.largeur - largeur
            rect.y = rect_bordure.y
            rect.largeur = largeur
            rect.hauteur = rect_bordure.hauteur
        }

        rect = ctx.donne_rectangle_corrigé(rect)
        peintre.remplis_rectangle(rect, couleur)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDisposition
 * \{ */

ModèleDeBoite :: struct {
    marge: RectanglePosDim(r32)
    bordure: RectanglePosDim(r32)
    rembourrage: RectanglePosDim(r32)
    contenu: RectanglePosDim(r32)
}

décale_horizontalement :: fonc (modèle: &ModèleDeBoite, delta: r32)
{
    modèle.marge.x += delta
    modèle.bordure.x += delta
    modèle.rembourrage.x += delta
    modèle.contenu.x += delta
}

décale_verticalement :: fonc (modèle: &ModèleDeBoite, delta: r32)
{
    modèle.marge.y += delta
    modèle.bordure.y += delta
    modèle.rembourrage.y += delta
    modèle.contenu.y += delta
}

définis_hauteur_contenu :: fonc (modèle: &ModèleDeBoite, hauteur: r32)
{
    modèle.marge.hauteur = hauteur
    modèle.bordure.hauteur = hauteur
    modèle.contenu.hauteur = hauteur
    modèle.rembourrage.hauteur = hauteur
}

incrémente_hauteur_contenu :: fonc (modèle: &ModèleDeBoite, hauteur: r32)
{
    modèle.marge.hauteur += hauteur
    modèle.bordure.hauteur += hauteur
    modèle.contenu.hauteur += hauteur
    modèle.rembourrage.hauteur += hauteur
}

définis_largeur_contenu :: fonc (modèle: &ModèleDeBoite, largeur: r32)
{
    modèle.marge.largeur = largeur
    modèle.bordure.largeur = largeur
    modèle.contenu.largeur = largeur
    modèle.rembourrage.largeur = largeur
}

définis_largeur :: fonc (modèle: &ModèleDeBoite, largeur: r32)
{
    modèle.contenu.largeur = largeur
}

définis_hauteur :: fonc (modèle: &ModèleDeBoite, hauteur: r32)
{
    modèle.contenu.hauteur = hauteur
}

/* Représente les cotations pour un axe donné du modèle de boite. */
CotationModèleDeBoite :: struct {
    marge_début: r32
    bordure_début: r32
    rembourrage_début: r32
    contenu: r32
    rembourrage_fin: r32
    bordure_fin: r32
    marge_fin: r32
}

donne_cotations_horizontales :: fonc (modèle: &ModèleDeBoite) -> CotationModèleDeBoite
{
    résultat: CotationModèleDeBoite
    résultat.marge_début = modèle.bordure.x - modèle.marge.x
    résultat.bordure_début = modèle.rembourrage.x - modèle.bordure.x
    résultat.rembourrage_début = modèle.contenu.x - modèle.rembourrage.x
    résultat.contenu = modèle.contenu.largeur
    résultat.rembourrage_fin = (modèle.rembourrage.x + modèle.rembourrage.largeur) - (modèle.contenu.x + modèle.contenu.largeur)
    résultat.bordure_fin = (modèle.bordure.x + modèle.bordure.largeur) - (modèle.rembourrage.x + modèle.rembourrage.largeur)
    résultat.marge_fin = (modèle.marge.x + modèle.marge.largeur) - (modèle.bordure.x + modèle.bordure.largeur)
    retourne résultat
}

donne_cotations_verticales :: fonc (modèle: &ModèleDeBoite) -> CotationModèleDeBoite
{
    résultat: CotationModèleDeBoite
    résultat.marge_début = modèle.bordure.y - modèle.marge.y
    résultat.bordure_début = modèle.rembourrage.y - modèle.bordure.y
    résultat.rembourrage_début = modèle.contenu.y - modèle.rembourrage.y
    résultat.contenu = modèle.contenu.hauteur
    résultat.rembourrage_fin = (modèle.rembourrage.y + modèle.rembourrage.hauteur) - (modèle.contenu.y + modèle.contenu.hauteur)
    résultat.bordure_fin = (modèle.bordure.y + modèle.bordure.hauteur) - (modèle.rembourrage.y + modèle.rembourrage.hauteur)
    résultat.marge_fin = (modèle.marge.y + modèle.marge.hauteur) - (modèle.bordure.y + modèle.bordure.hauteur)
    retourne résultat
}

imprime_cotation :: fonc (enchaineuse: *Enchaineuse, cotation: *CotationModèleDeBoite)
{
    imprime_dans_enchaineuse(enchaineuse, "[% % %] % [% % %]", cotation.marge_début, cotation.bordure_début, cotation.rembourrage_début, cotation.contenu, cotation.rembourrage_fin, cotation.bordure_fin, cotation.marge_fin)
}

NoeudDeDisposition :: struct {
    type := #type_de_cette_structure

    id: n32
    id_stacking_context: n32
    propriétés: *PropriétésDeStyle

    parent: *NoeudDeDisposition
    enfants: [..]*NoeudDeDisposition

    modèle_de_boite: ModèleDeBoite
    traite_comme_display_none: bool
    est_pseudo_element: bool

    sur_mesure: fonc (*NoeudDeDisposition, *ContexteDeFormattage, ModeDeFormattage, *ValeurDeStyle)(rien)
    sur_dessin: fonc (*NoeudDeDisposition, *ContexteDessin)(rien)
    sur_dessin_fragment: fonc (*NoeudDeDisposition, *Fragment, *LignesTenante, *ContexteDessin)(rien)
    sur_dessin_tampon_id: fonc (*NoeudDeDisposition, *ContexteDessin)(rien)
    sur_dessin_fragment_tampon_id: fonc (*NoeudDeDisposition, *Fragment, *ContexteDessin)(rien)
    sur_divise_en_lignes: fonc (*NoeudDeDisposition, *Enligneuse, *ContexteDeFormattage, ModeDeFormattage)(rien)
    sur_correction_hiérarchie: fonc(*NoeudDeDisposition, *CréatriceArbre)(rien)
}

donne_propriétés :: fonc (noeud: *NoeudDeDisposition) -> *PropriétésDeStyle
{
    si noeud.type == NoeudDeDispositionTexte {
        retourne donne_propriété_parent(noeud comme *NoeudDeDispositionTexte)
    }
    retourne noeud.propriétés
}

détermine_fonte :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage) -> *Fonte
{
    globales := ctx.globales
    assert(globales != nul)
    famille_fonte := "sérif"
    style := "normal"
    graisse := GraisseFonte.Normal
    propriétés := noeud.donne_propriétés()
    taille_fonte := propriétés.donne_valeur_utilisée(IDPropriété.FontSize).pixels comme r32

    font_family := propriétés.donne_valeur_utilisée(IDPropriété.FontFamily).mot_clé
    si font_family == PROP_monospace {
        famille_fonte = "monospace"
    }
    sinon si font_family == PROP_sans_serif {
        famille_fonte = "sans-serif"
    }

    font_weight := propriétés.donne_valeur_utilisée(IDPropriété.FontWeight).number
    si font_weight >= 700.0 {
        graisse = GraisseFonte.Gras
    }

    font_style := propriétés.donne_valeur_utilisée(IDPropriété.FontStyle).mot_clé
    si font_style == PROP_italic {
        style = "italic"
    }

    résultat := globales.donne_fonte(famille_fonte, style, graisse, taille_fonte)
    si noeud.type == NoeudDeDispositionTexte {
        noeud_texte := noeud comme *NoeudDeDispositionTexte
        noeud_texte.fonte = résultat
    }
    sinon si noeud.type == NoeudDeDispositionSelect {
        noeud_select := noeud comme *NoeudDeDispositionSelect
        noeud_select.fonte = résultat
    }
    retourne résultat
}

donne_rectangle_rembourrage :: fonc (noeud: *NoeudDeDisposition) -> RectanglePosDim(r32)
{
    retourne noeud.modèle_de_boite.rembourrage
}

donne_rectangle_bordure :: fonc (noeud: *NoeudDeDisposition) -> RectanglePosDim(r32)
{
    retourne noeud.modèle_de_boite.bordure
}

donne_rectangle_contenu :: fonc (noeud: *NoeudDeDisposition) -> RectanglePosDim(r32)
{
    retourne noeud.modèle_de_boite.contenu
}

donne_propriété_parent :: fonc (noeud_texte: *NoeudDeDispositionTexte) -> *PropriétésDeStyle
{
    parent := noeud_texte.parent
    tantque est_noeud_anonyme(parent) {
        parent = parent.parent
    }
    retourne parent.propriétés
}

est_noeud_anonyme :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    si noeud.type != NoeudDeDispositionBloc && noeud.type != NoeudDeDispositionInline {
        retourne faux
    }

    noeud_bloc := noeud comme *NoeudDeDispositionBloc
    retourne noeud_bloc.élément == nul
}

est_noeud_non_anonyme :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    retourne !est_noeud_anonyme(noeud)
}

est_block_level :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    assert(noeud.propriétés != nul)
    retourne donne_display(noeud).outer_type == Display.Type.Block
}

est_inline_level :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    assert(noeud.propriétés != nul)
    retourne donne_display(noeud).outer_type == Display.Type.Inline
}

donne_display :: fonc (noeud: *NoeudDeDisposition) -> Display
{
    assert(noeud.propriétés != nul)
    retourne noeud.propriétés.donne_valeur_utilisée(IDPropriété.Display).display
}

donne_line_height :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage) -> r32
{
    fonte := détermine_fonte(noeud, ctx)
    retourne donne_line_height(noeud.propriétés, fonte)
}

donne_line_height :: fonc (propriétés: *PropriétésDeStyle, fonte: *Fonte) -> r32
{
    line_height := propriétés.donne_valeur_utilisée(IDPropriété.LineHeight)
    si line_height.est_pixels() {
        retourne line_height.pixels comme r32
    }

    métriques := fonte.donne_métriques_fonte()

    si line_height.est_nombre() {
        retourne line_height.number comme r32 * métriques.donne_hauteur_ligne()
    }

    assert(line_height.est_mot_clé(PROP_normal))
    retourne métriques.donne_hauteur_ligne()
}

donne_élément :: fonc (noeud: *NoeudDeDisposition) -> *HTML.Élément
{
    si noeud.type == NoeudDeDispositionInline {
        retourne (noeud comme *NoeudDeDispositionBloc).élément
    }
    si noeud.type == NoeudDeDispositionRemplacement {
        retourne (noeud comme *NoeudDeDispositionBloc).élément
    }
    si noeud.type == NoeudDeDispositionBloc {
        retourne (noeud comme *NoeudDeDispositionBloc).élément
    }
    si noeud.type == NoeudDeDispositionNouvelleLigne {
        retourne (noeud comme *NoeudDeDispositionNouvelleLigne).élément
    }
    si noeud.type == TableWrapperBox {
        retourne (noeud comme *TableWrapperBox).élément
    }
    si noeud.type == TableBox {
        retourne (noeud comme *TableBox).élément
    }
    si noeud.type == TableColumnGroupBox {
        retourne (noeud comme *TableColumnGroupBox).élément
    }
    si noeud.type == TableColumnBox {
        retourne (noeud comme *TableColumnBox).élément
    }
    si noeud.type == TableFooterGroupBox {
        retourne (noeud comme *TableFooterGroupBox).élément
    }
    si noeud.type == TableHeaderGroupBox {
        retourne (noeud comme *TableHeaderGroupBox).élément
    }
    si noeud.type == TableRowGroupBox {
        retourne (noeud comme *TableRowGroupBox).élément
    }
    si noeud.type == TableRowBox {
        retourne (noeud comme *TableRowBox).élément
    }
    si noeud.type == TableCellBox {
        retourne (noeud comme *TableCellBox).élément
    }
    si noeud.type == CaptionBox {
        retourne (noeud comme *CaptionBox).élément
    }
    retourne nul
}

donne_élément_récursif :: fonc (noeud: *NoeudDeDisposition) -> *HTML.Élément
{
    noeud_courant := noeud

    tantque noeud_courant != nul {
        élément := noeud_courant.donne_élément()
        si élément {
            retourne élément
        }

        noeud_courant = noeud_courant.parent
    }

    retourne nul
}

imprime_élément :: fonc (noeud: *NoeudDeDisposition, message: chaine, récursif := faux)
{
    enchaineuse: Enchaineuse
    initialise_enchaineuse(*enchaineuse)
    diffère détruit_tampons(*enchaineuse)

    imprime_élément(*enchaineuse, noeud, message, récursif)
    copie_dans_sortie_standarde(*enchaineuse)
}

imprime_élément :: fonc (enchaineuse: *Enchaineuse, noeud: *NoeudDeDisposition, message: chaine, récursif := faux)
{
    élément := si récursif {
        noeud.donne_élément_récursif()
    }
    sinon {
        noeud.donne_élément()
    }

    ajoute_au_tampon(enchaineuse, message, " : ")

    saufsi élément {
        ajoute_au_tampon(enchaineuse, "<anonyme>\n")
        retourne
    }

    ajoute_au_tampon(enchaineuse, élément.local_name)

    id := élément.id
    saufsi id.est_vide() {
        ajoute_au_tampon(enchaineuse, "#", id)
    }

    classes := HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_class)
    saufsi classes.est_vide() {
        ajoute_au_tampon(enchaineuse, ".", classes)
    }

    ajoute_au_tampon(enchaineuse, "\n")
}

définis_position :: fonc (noeud: *NoeudDeDisposition, x: r32, y: r32)
{
    dx := x - noeud.modèle_de_boite.marge.x
    dy := y - noeud.modèle_de_boite.marge.y
    décale_horizontalement(noeud.modèle_de_boite, dx)
    décale_verticalement(noeud.modèle_de_boite, dy)
}

décale_vers_position :: fonc (noeud: *NoeudDeDisposition, x: r32, y: r32)
{
    dx := x - noeud.modèle_de_boite.marge.x
    dy := y - noeud.modèle_de_boite.marge.y
    décale(noeud, dx, dy)
}

décale :: fonc (noeud: *NoeudDeDisposition, x: r32, y: r32)
{
    décale_horizontalement(noeud.modèle_de_boite, x)
    décale_verticalement(noeud.modèle_de_boite, y)

    pour noeud.enfants {
        it.décale(x, y)
    }

    si noeud.type == NoeudDeDispositionBloc || noeud.type == NoeudDeDispositionInline || noeud.type == TableCellBox {
        bloc := noeud comme *NoeudDeDispositionBloc
        pour * bloc.lignes_tenante.fragments {
            it.x += x
            it.y += y
        }
    }
}

donne_nom_classe :: fonc (noeud: *NoeudDeDisposition) -> chaine
{
    infos := __table_des_types[noeud.type] comme *InfoTypeStructure
    retourne infos.nom
}

donne_nom_classe_sans_préfixe :: fonc (noeud: *NoeudDeDisposition) -> chaine
{
    résultat := noeud.donne_nom_classe()
    préfixe := "NoeudDeDisposition"
    si résultat.commence_par(préfixe) {
        résultat = avance(résultat, préfixe.taille)
    }
    retourne résultat
}

mesure :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage, outrepas_largeur: *ValeurDeStyle)
{
    si noeud.sur_mesure {
        noeud.sur_mesure(noeud, ctx, mode, outrepas_largeur)
    }
}

dessine :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    si noeud.id_stacking_context != ctx.id_stacking_context {
        retourne
    }

    si noeud.sur_dessin {
        noeud.sur_dessin(noeud, ctx)
    }
}

dessine_fragment :: fonc (noeud: *NoeudDeDisposition, fragment: *Fragment, lignes_tenante: *LignesTenante, ctx: *ContexteDessin)
{
    si noeud.sur_dessin_fragment {
        noeud.sur_dessin_fragment(noeud, fragment, lignes_tenante, ctx)
    }
    sinon {
        noeud.dessine(ctx)
    }
}

dessine_tampon_id :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    si noeud.id_stacking_context != ctx.id_stacking_context {
        retourne
    }

    si noeud.sur_dessin_tampon_id {
        noeud.sur_dessin_tampon_id(noeud, ctx)
    }
}

dessine_fragment_tampon_id :: fonc (noeud: *NoeudDeDisposition, fragment: *Fragment, ctx: *ContexteDessin)
{
    saufsi noeud.sur_dessin_fragment_tampon_id {
        ctx.remplis_rectangle_id(noeud, fragment.donne_rectangle())

        pour noeud.enfants {
            noeud.dessine_tampon_id(ctx)
        }

        retourne
    }

    noeud.sur_dessin_fragment_tampon_id(noeud, fragment, ctx)
}

ajoute_enfant :: fonc (parent: *NoeudDeDisposition, enfant: *NoeudDeDisposition)
{
    enfant.parent = parent
    tableau_ajoute(*parent.enfants, enfant)
}

donne_curseur :: fonc (noeud: *NoeudDeDisposition) -> CursorType
{
    saufsi noeud.propriétés {
        retourne CursorType.auto
    }
    retourne noeud.propriétés.donne_valeur_utilisée(IDPropriété.Cursor).cursor
}

divise_en_lignes :: fonc (noeud: *NoeudDeDisposition, enligneuse: *Enligneuse, ctx: *ContexteDeFormattage, mode: ModeDeFormattage)
{
    si (noeud.type == NoeudDeDispositionTexte || noeud.donne_display().inner_type == Display.Type.Flow) {
        si noeud.sur_divise_en_lignes {
            noeud.sur_divise_en_lignes(noeud, enligneuse, ctx, mode)
            retourne
        }

        pour noeud.enfants {
            divise_en_lignes(it, enligneuse, ctx, mode)
        }

        retourne
    }

    noeud.mesure(ctx, mode, nul)
    _ := enligneuse.ajoute_fragment(noeud, noeud.modèle_de_boite.marge.hauteur, noeud.modèle_de_boite.marge.largeur, Fragment.Type.InlineBlock)
}

corrige_hiérarchie :: fonc (noeud: *NoeudDeDisposition, créatrice: *CréatriceArbre)
{
    si noeud.sur_correction_hiérarchie {
        noeud.sur_correction_hiérarchie(noeud, créatrice)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ContainingBlock
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#containing-block-details
 * https://drafts.csswg.org/css-display/#containing-block
 * https://www.w3.org/TR/css-position-3/#def-cb
 * \{ */

ContainingBlock :: struct {
    rect: RectanglePosDim(r32)
    noeud: *NoeudDeDisposition
}

crée_containing_block_pour_contenu :: fonc (noeud: *NoeudDeDisposition) -> ContainingBlock
{
    résultat: ContainingBlock
    résultat.rect = noeud.modèle_de_boite.contenu
    résultat.noeud = noeud
    retourne résultat
}

crée_containing_block_pour_rembourrage :: fonc (noeud: *NoeudDeDisposition) -> ContainingBlock
{
    résultat: ContainingBlock
    résultat.rect = noeud.modèle_de_boite.rembourrage
    résultat.noeud = noeud
    retourne résultat
}

donne_x :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.x
}

donne_y :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.y
}

donne_largeur :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.largeur
}

donne_hauteur :: fonc (bloc: &ContainingBlock) -> r32
{
    retourne bloc.rect.hauteur
}

donne_containing_block :: fonc (noeud: *NoeudDeDisposition, ctx: *ContexteDeFormattage) -> ContainingBlock
{
    // 1. The containing block in which the root element lives is a rectangle called the initial containing block.
    // For continuous media, it has the dimensions of the viewport and is anchored at the canvas origin;
    // it is the page area for paged media.
    // The 'direction' property of the initial containing block is the same as for the root element.
    si noeud.parent == nul {
        retourne ctx.donne_initial_containing_block()
    }

    // 2. For other elements, if the element's position is 'relative' or 'static',
    // the containing block is formed by the content edge of the nearest block
    // container ancestor box.
    position := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Position)
    si position.est_mot_clé(PROP_relative) || position.est_mot_clé(PROP_static) {
        parent := noeud.parent

        tantque parent != nul {
            si parent.type == NoeudDeDispositionBloc || parent.type == TableWrapperBox || parent.type == TableCellBox {
                retourne crée_containing_block_pour_contenu(parent)
            }

            parent = parent.parent
        }
    }
    // 3. If the element has 'position: fixed', the containing block is established by the viewport
    // in the case of continuous media or the page area in the case of paged media.
    sinon si position.est_mot_clé(PROP_fixed) {
        retourne ctx.donne_initial_containing_block()
    }
    // 4. If the element has 'position: absolute', the containing block is established by the nearest
    // ancestor with a 'position' of 'absolute', 'relative' or 'fixed', in the following way:
    sinon si position.est_mot_clé(PROP_absolute) {
        ancêtre := noeud.parent

        tantque ancêtre != nul {
            position_parent := ancêtre.propriétés.donne_valeur_utilisée(IDPropriété.Position)
            si position_parent.est_mot_clé(PROP_relative) || position_parent.est_mot_clé(PROP_absolute) || position_parent.est_mot_clé(PROP_fixed) {
                arrête
            }

            ancêtre = ancêtre.parent
        }

        si ancêtre {
            // À FAIRE : 1. In the case that the ancestor is an inline element, the containing block is the bounding
            // box around the padding boxes of the first and the last inline boxes generated for that element.
            // In CSS 2.1, if the inline element is split across multiple lines, the containing block is undefined.

            // À FAIRE : 2. Otherwise, the containing block is formed by the padding edge of the ancestor.
            retourne crée_containing_block_pour_rembourrage(ancêtre)
        }
    }

    // If there is no such ancestor, the containing block is the initial containing block.
    retourne ctx.donne_initial_containing_block()
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom BoiteDeLigne
 * Boite pour tenir les fragments de chaque ligne pour les dispositions
 * « inline ».
 * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#line-box
 * \{ */

Fragment :: struct {
    Type :: énum {
        Normal
        MargeDébut
        MargeFin
        RembourrageDébut
        RembourrageFin
        InlineBlock
    }

    noeud: *NoeudDeDisposition
    index_début: z64
    index_fin: z64
    type: Type

    x: r32
    y: r32
    hauteur: r32
    largeur: r32
}

donne_rectangle :: fonc (fragment: *Fragment) -> RectanglePosDim(r32)
{
    rect: RectanglePosDim(r32)
    rect.x = fragment.x
    rect.y = fragment.y
    rect.largeur = fragment.largeur
    rect.hauteur = fragment.hauteur
    retourne rect
}

BoiteDeLigne :: struct {
    largeur_max: r32
    position_x: r32
    position_y: r32

    premier_fragment: z64
    dernier_fragment: z64
    largeur_courante: r32
    hauteur_contenu: r32
}

peut_ajouter_fragment :: fonc (boite: *BoiteDeLigne, largeur: r32) -> bool
{
    retourne (boite.largeur_courante + largeur) <= boite.largeur_max
}

garantis_hauteur_minimale :: fonc (boite: *BoiteDeLigne, hauteur: r32)
{
    boite.hauteur_contenu = max(boite.hauteur_contenu, hauteur)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom LignesTenante
 * Structure pour tenir les BoiteDeLignes et leurs fragments d'un bloc.
 * \{ */

LignesTenante :: struct {
    ctx: *ContexteDeFormattageInline
    bloc: *NoeudDeDispositionBloc
    lignes: [..]BoiteDeLigne
    fragments: [..]Fragment

    position_y_courante: r32
}

réinitialise :: fonc (lignes_tenante: *LignesTenante)
{
    lignes_tenante.lignes.taille = 0
    lignes_tenante.fragments.taille = 0
}

détruit_données :: fonc (lignes_tenante: *LignesTenante)
{
    déloge(lignes_tenante.lignes)
    déloge(lignes_tenante.fragments)
}

donne_ligne_courante :: fonc (lignes_tenante: *LignesTenante) -> *BoiteDeLigne
{
    si lignes_tenante.lignes.taille == 0 {
        retourne ajoute_ligne(lignes_tenante)
    }

    retourne *lignes_tenante.lignes[lignes_tenante.lignes.taille - 1]
}

ajoute_ligne :: fonc (lignes_tenante: *LignesTenante) -> *BoiteDeLigne
{
    si lignes_tenante.lignes.taille {
        lignes_tenante.position_y_courante += lignes_tenante.lignes[lignes_tenante.lignes.taille - 1].hauteur_contenu
    }

    décalage_x := lignes_tenante.bloc.modèle_de_boite.contenu.x
    largeur_contenu := lignes_tenante.bloc.modèle_de_boite.contenu.largeur

    décalage_ligne, largeur_ligne := donne_position_et_largeur_ligne(lignes_tenante.ctx, décalage_x, décalage_x + largeur_contenu, lignes_tenante.position_y_courante)

    résultat := tableau_ajoute_élément(*lignes_tenante.lignes)
    résultat.position_x = décalage_ligne
    résultat.position_y = lignes_tenante.position_y_courante
    résultat.largeur_max = largeur_ligne

    résultat.premier_fragment = lignes_tenante.fragments.taille
    /* -1 pour que le calcul du nombre de fragments se fasse via "fin - début + 1", et
     * avoir un résultat correcte quand il n'y a aucun fragment dans la ligne. */
    résultat.dernier_fragment = lignes_tenante.fragments.taille - 1
    retourne résultat
}

ajoute_fragment :: fonc (lignes_tenante: *LignesTenante, fragment: &Fragment) -> z64
{
    résultat := lignes_tenante.fragments.taille
    tableau_ajoute(*lignes_tenante.fragments, fragment)
    retourne résultat
}

donne_fragments_ligne :: fonc (lignes_tenante: *LignesTenante, ligne: *BoiteDeLigne) -> []Fragment
{
    résultat: []Fragment
    résultat.pointeur = lignes_tenante.fragments.pointeur + ligne.premier_fragment
    résultat.taille = ligne.dernier_fragment - ligne.premier_fragment + 1
    retourne résultat
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Enligneuse
 * \{ */

Enligneuse :: struct {
    bloc_contenant: *NoeudDeDispositionBloc
    ligne_courante: *BoiteDeLigne
    mode_de_formattage: ModeDeFormattage
}

divise_en_lignes :: fonc (enligneuse: *Enligneuse, bloc_contenant: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattageInline, mode: ModeDeFormattage)
{
    réinitialise_lignes(bloc_contenant)
    enligneuse.bloc_contenant = bloc_contenant
    enligneuse.mode_de_formattage = mode
    bloc_contenant.lignes_tenante.bloc = bloc_contenant
    bloc_contenant.lignes_tenante.position_y_courante = bloc_contenant.modèle_de_boite.contenu.y
    bloc_contenant.lignes_tenante.ctx = ctx

    pour bloc_contenant.enfants {
        divise_en_lignes(it, enligneuse, ctx, mode)
    }
}

donne_lignes_tenante :: fonc (empl enligneuse: *Enligneuse) -> *LignesTenante
{
    retourne *bloc_contenant.lignes_tenante
}

ajoute_ligne :: fonc (empl enligneuse: *Enligneuse) -> *BoiteDeLigne
{
    ligne_courante = ajoute_ligne(*enligneuse.bloc_contenant.lignes_tenante)
    retourne ligne_courante
}

peut_ajouter_fragment :: fonc (empl enligneuse: *Enligneuse, largeur: r32) -> bool
{
    discr mode_de_formattage {
        Normal {
            retourne ligne_courante.peut_ajouter_fragment(largeur)
        }
        ToutesLesBrisuresDeLigne {
            retourne faux
        }
        BrisureDeLigneExplicite {
            /* Les <br> ajouterons une ligne explicitement. */
            retourne vrai
        }
    }
    retourne faux
}

ajoute_fragment :: fonc (empl enligneuse: *Enligneuse, noeud: *NoeudDeDisposition, index_début: z64, index_fin: z64, hauteur: r32, largeur: r32, type := Fragment.Type.Normal) -> z64
{
    lignes_tenante := *enligneuse.bloc_contenant.lignes_tenante

    ligne_courante = lignes_tenante.donne_ligne_courante()
    si mode_de_formattage == ModeDeFormattage.Normal && !ligne_courante.peut_ajouter_fragment(largeur) {
        ligne_courante = lignes_tenante.ajoute_ligne()
    }

    fragment: Fragment
    fragment.noeud = noeud
    fragment.index_début = index_début
    fragment.index_fin = index_fin
    fragment.hauteur = hauteur
    fragment.largeur = largeur
    fragment.x = ligne_courante.position_x + ligne_courante.largeur_courante
    fragment.y = ligne_courante.position_y
    fragment.type = type

    ligne_courante.largeur_courante += largeur
    ligne_courante.garantis_hauteur_minimale(hauteur)

    ligne_courante.dernier_fragment = ajoute_fragment(lignes_tenante, fragment)
    retourne ligne_courante.dernier_fragment
}

ajoute_fragment :: fonc (enligneuse: *Enligneuse, noeud: *NoeudDeDisposition, hauteur: r32, largeur: r32, type: Fragment.Type) -> z64
{
    retourne ajoute_fragment(enligneuse, noeud, 0, 0, hauteur, largeur, type)
}

garantis_hauteur_minimale :: fonc (enligneuse: *Enligneuse, hauteur: r32)
{
    enligneuse.ligne_courante.garantis_hauteur_minimale(hauteur)
}

peut_ignorer_espace_blanche_en_début_de_ligne :: fonc (empl enligneuse: *Enligneuse) -> bool
{
    retourne ligne_courante.largeur_courante == 0.0
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionBloc
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionBloc :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_bloc_sur_mesure
    sur_dessin = noeud_disposition_bloc_sur_dessin
    sur_dessin_tampon_id = noeud_disposition_bloc_sur_dessin_tampon_id
    sur_correction_hiérarchie = noeud_disposition_bloc_corrige_hiérarchie

    élément: *HTML.Élément

    lignes_tenante: LignesTenante

    est_disposition_inline: bool
}

détruit_lignes :: fonc (noeud: *NoeudDeDispositionBloc)
{
    détruit_données(*noeud.lignes_tenante)
}

réinitialise_lignes :: fonc (noeud: *NoeudDeDispositionBloc)
{
    réinitialise(*noeud.lignes_tenante)
}

détermine_disposition_interne :: fonc (noeud: *NoeudDeDispositionBloc)
{
    si noeud.enfants.taille == 0 {
        retourne
    }

    /* Crée des blocs anonymes pour envelopper les blocs inlines si au moins un enfant est un bloc.
     * Voir définition de "block container box" :
     * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#block-boxes */
    doit_formatter_en_blocs := faux

    pour noeud.enfants {
        si it.est_block_level() {
            doit_formatter_en_blocs = vrai
            arrête
        }
    }

    noeud.est_disposition_inline = doit_formatter_en_blocs == faux
}

MéthodeDeFormattage :: énum {
    Flex
    Bloc
    Inline
    DansContexteCourant
}

donne_méthode_de_formattage_intérieur :: fonc (noeud: *NoeudDeDisposition) -> MéthodeDeFormattage
{
    si noeud.type == NoeudDeDispositionInline {
        retourne MéthodeDeFormattage.Inline
    }

    si (noeud.type == NoeudDeDispositionBloc || noeud.type == TableCellBox) {
        noeud_bloc := noeud comme *NoeudDeDispositionBloc
        si noeud_bloc.est_disposition_inline {
            retourne MéthodeDeFormattage.Inline
        }
    }

    si noeud.donne_display().inner_type == Display.Type.Flex {
        retourne MéthodeDeFormattage.Flex
    }

    si noeud.requiers_contexte_de_formattage_indépendant() {
        retourne MéthodeDeFormattage.Bloc
    }

    retourne MéthodeDeFormattage.DansContexteCourant
}

formatte_intérieur :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDeFormattage, mode: ModeDeFormattage)
{
    discr donne_méthode_de_formattage_intérieur(noeud) {
        Inline {
            ctx_local: ContexteDeFormattageInline
            initialise_depuis_parent(*ctx_local, ctx)

            /* Si nous devons générer un contexte indépendant, nous ignorons le parent 
             * afin que les boites flottantes de ce dernier n'influencent pas nos lignes.
             * (Ceci peut arriver quand un boite flottante établis un contexte de 
             * formattage inline.) */
            si noeud.requiers_contexte_de_formattage_indépendant() {
                ctx_local.parent = nul
            }

            ctx_local.racine = noeud
            ctx_local.doit_aligner_les_descendants = ctx.doit_aligner_les_descendants

            formatte(*ctx_local, noeud, mode)
        }
        Flex {
            ctx_local: ContexteDeFormattageFlex
            initialise_depuis_parent(*ctx_local, ctx)
            formatte(*ctx_local, noeud, mode)
        }
        Bloc {
            ctx_local: ContexteDeFormattageBloc
            initialise_depuis_parent(*ctx_local, ctx)
            diffère détruit_données_contexte(*ctx_local)
            ctx_local.racine = noeud
            ctx_local.doit_aligner_les_descendants = ctx.doit_aligner_les_descendants

            formatte(*ctx_local, noeud, mode)
        }
        DansContexteCourant {
            formatte(ctx, noeud, mode)
        }
    }
}

est_élément_avec_attribut_alignement_spécial :: fonc (élément: *HTML.Élément) -> bool
{
    tags := [HTML.TAG_div, HTML.TAG_thead, HTML.TAG_tbody, HTML.TAG_tfoot, HTML.TAG_tr, HTML.TAG_td, HTML.TAG_th]

    pour tags {
        si élément.local_name == it {
            retourne vrai
        }
    }

    retourne faux
}

donne_alignement_descendants :: fonc (noeud: *NoeudDeDispositionBloc) -> Optionnel(AlignementTexte)
{
    saufsi noeud.élément {
        retourne
    }

    si noeud.élément.local_name == HTML.TAG_center {
        retourne AlignementTexte.Centre
    }

    si est_élément_avec_attribut_alignement_spécial(noeud.élément) {
        attribut_align := HTML.donne_attribut(noeud.élément, HTML.ATTR_align)
        saufsi attribut_align.possède_valeur() {
            retourne
        }

        alignement := attribut_align.Quelque
        si compare_ascii_insensible_à_la_casse(alignement, PROP_center) || compare_ascii_insensible_à_la_casse(alignement, PROP_middle) {
            retourne AlignementTexte.Centre
        }

        si compare_ascii_insensible_à_la_casse(alignement, PROP_right) {
            retourne AlignementTexte.Droite
        }

        si compare_ascii_insensible_à_la_casse(alignement, PROP_left) {
            retourne AlignementTexte.Gauche
        }

        si compare_ascii_insensible_à_la_casse(alignement, PROP_justify) {
            retourne AlignementTexte.Justifié
        }

        retourne
    }
}

noeud_disposition_bloc_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage, outrepas_largeur: *ValeurDeStyle)
{
    noeud := base comme *NoeudDeDispositionBloc

    containing_block := donne_containing_block(base, ctx)

    /* À FAIRE : utilise les bonnes formules.
     * https://www.w3.org/TR/2011/REC-CSS2-20110607/visudet.html#Computing_widths_and_margins */
    args := crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, outrepas_largeur, mode)
    calcule_largeur_et_marges(*args)
    état_calcul_hauteur := calcule_hauteur_et_marges(*args)

    doit_aligner_les_descendants := ctx.doit_aligner_les_descendants
    alignement := donne_alignement_descendants(noeud)
    si alignement.possède_valeur() {
        ctx.doit_aligner_les_descendants = alignement
    }

    formatte_intérieur(noeud, ctx, mode)

    si état_calcul_hauteur == ÉtatCalculHauteur.HauteurDépendSurContenu {
        args.calcul_hauteur_après_contenu = vrai
        _ := calcule_hauteur_et_marges(*args)
    }

    ctx.doit_aligner_les_descendants = doit_aligner_les_descendants

    si mode == ModeDeFormattage.Normal {
        ctx.dispose_les_boites_absolues(noeud)
    }
}

noeud_disposition_bloc_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionBloc

    si noeud.est_noeud_non_anonyme() || noeud.est_pseudo_element {
        dessine_arrière_plan(ctx, noeud)
        dessine_bordure(ctx, noeud)
    }

    saufsi noeud.est_disposition_inline {
        dessine_enfants_block(noeud, ctx)
    }
    sinon {
        dessine_enfants_inline(noeud, ctx)
    }
}

noeud_disposition_bloc_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionBloc

    ctx.remplis_rectangle_id(noeud)

    saufsi noeud.est_disposition_inline {
        dessine_tampon_id_enfants_block(noeud, ctx)
    }
    sinon {
        dessine_tampon_id_enfants_inline(noeud, ctx)
    }
}

dessine_enfants_block :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    // rect := ctx.donne_rectangle_corrigé(noeud)

    // couleur := CouleurRVBA(0.5, 0.0, 0.5, 1.0)

    // peintre := ctx.peintre
    // peintre.dessine_rectangle(rect, couleur)

    pour noeud.enfants {
        position := it.propriétés.donne_valeur_utilisée(IDPropriété.Position)
        saufsi position.est_mot_clé(PROP_static) {
            continue
        }
        float := it.propriétés.donne_valeur_utilisée(IDPropriété.Float)
        saufsi float.est_none() {
            continue
        }
        dessine(it, ctx)
    }

    pour noeud.enfants {
        position := it.propriétés.donne_valeur_utilisée(IDPropriété.Position)
        si position.est_mot_clé(PROP_static) {
            continue
        }
        float := it.propriétés.donne_valeur_utilisée(IDPropriété.Float)
        saufsi float.est_none() {
            continue
        }
        dessine(it, ctx)
    }

    pour noeud.enfants {
        float := it.propriétés.donne_valeur_utilisée(IDPropriété.Float)
        si float.est_none() {
            continue
        }
        dessine(it, ctx)
    }
}

dessine_tampon_id_enfants_block :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    pour noeud.enfants {
        dessine_tampon_id(it, ctx)
    }
}

dessine_enfants_inline :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    lignes_tenante := *noeud.lignes_tenante
    pour * lignes_tenante.fragments {
        dessine_fragment(it.noeud, it, lignes_tenante, ctx)
    }
}

dessine_tampon_id_enfants_inline :: fonc (noeud: *NoeudDeDispositionBloc, ctx: *ContexteDessin)
{
    pour * noeud.lignes_tenante.fragments {
        dessine_fragment_tampon_id(it.noeud, it, ctx)
    }
}

noeud_disposition_bloc_corrige_hiérarchie :: fonc (base: *NoeudDeDisposition, créatrice: *CréatriceArbre)
{
    noeud := base comme *NoeudDeDispositionBloc
    détermine_disposition_interne(noeud)

    si noeud.est_disposition_inline {
        retourne
    }

    nouveaux_enfants: [..]*NoeudDeDisposition

    bloc_anonyme_courant: *NoeudDeDispositionBloc
    pour noeud.enfants {
        si it.est_block_level() {
            tableau_ajoute(*nouveaux_enfants, it)
            bloc_anonyme_courant = nul
            continue
        }

        si bloc_anonyme_courant == nul {
            bloc_anonyme_courant = créatrice.arbre.crée_noeud(NoeudDeDispositionBloc)
            bloc_anonyme_courant.parent = noeud
            bloc_anonyme_courant.est_disposition_inline = vrai
            crée_style_pour_bloc_anonyme(créatrice.résolveuse, bloc_anonyme_courant, noeud, Display.Type.Block)
            tableau_ajoute(*nouveaux_enfants, bloc_anonyme_courant)
        }

        bloc_anonyme_courant.ajoute_enfant(it)
    }

    déloge(noeud.enfants)
    noeud.enfants = nouveaux_enfants
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionInline
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionInline :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_inline_sur_mesure
    sur_dessin = noeud_disposition_inline_sur_dessin
    sur_dessin_fragment = noeud_disposition_inline_sur_dessin_fragment
    sur_divise_en_lignes = noeud_disposition_inline_sur_divise_en_lignes
    sur_dessin_tampon_id = noeud_disposition_inline_sur_dessin_tampon_id
    sur_correction_hiérarchie = noeud_disposition_inline_corrige_hiérarchie
}

noeud_disposition_inline_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage, outrepas_largeur: *ValeurDeStyle)
{
    noeud := base comme *NoeudDeDispositionInline
    containing_block := donne_containing_block(base, ctx)
    args := crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, outrepas_largeur, mode)
    calcule_largeur_et_marges(*args)
    _ := calcule_hauteur_et_marges(*args)
    formatte_intérieur(noeud, ctx, mode)
}

noeud_disposition_inline_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, enligneuse: *Enligneuse, ctx: *ContexteDeFormattage, mode: ModeDeFormattage)
{
    noeud := base comme *NoeudDeDispositionInline
    containing_block := donne_containing_block(noeud, ctx)
    args := crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, nul, mode)
    calcule_largeur_et_marges(*args)
    _ := calcule_hauteur_et_marges(*args)

    propriétés := noeud.propriétés

    margin_left := propriétés.donne_valeur_utilisée(IDPropriété.MarginLeft).pixels comme r32
    padding_left := propriétés.donne_valeur_utilisée(IDPropriété.PaddingLeft).pixels comme r32
    padding_right := propriétés.donne_valeur_utilisée(IDPropriété.PaddingRight).pixels comme r32
    margin_right := propriétés.donne_valeur_utilisée(IDPropriété.MarginRight).pixels comme r32

    fragment_marge_début := enligneuse.ajoute_fragment(noeud, 0.0, margin_left, Fragment.Type.MargeDébut)
    fragment_rembourrage_début := enligneuse.ajoute_fragment(noeud, 0.0, padding_left, Fragment.Type.RembourrageDébut)

    pour noeud.enfants {
        divise_en_lignes(it, enligneuse, ctx, mode)
    }

    fragment_rembourrage_fin := enligneuse.ajoute_fragment(noeud, 0.0, padding_right, Fragment.Type.RembourrageFin)
    fragment_marge_fin := enligneuse.ajoute_fragment(noeud, 0.0, margin_right, Fragment.Type.MargeFin)

    lignes_tenante := enligneuse.donne_lignes_tenante()

    hauteur := 0.0
    pour fragment_rembourrage_début + 1 ... fragment_rembourrage_fin - 1 {
        fragment := lignes_tenante.fragments[it]
        hauteur = max(fragment.hauteur, hauteur)
    }

    index_fragments := [fragment_marge_début, fragment_rembourrage_début, fragment_rembourrage_fin, fragment_marge_fin]
    pour index_fragments {
        fragment := *lignes_tenante.fragments[it]
        fragment.hauteur = hauteur
    }
}

noeud_disposition_inline_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionInline
    dessine_enfants_inline(noeud, ctx)
}

noeud_disposition_inline_sur_dessin_fragment :: fonc (base: *NoeudDeDisposition, fragment: *Fragment, lignes_tenante: *LignesTenante, ctx: *ContexteDessin)
{
    si fragment.type == Fragment.Type.InlineBlock {
        noeud_disposition_bloc_sur_dessin(base, ctx)
        retourne
    }

    si fragment.type == Fragment.Type.MargeDébut || fragment.type == Fragment.Type.MargeFin {
        retourne
    }

    si fragment.type == Fragment.Type.RembourrageDébut {
        propriétés := base.propriétés

        background_color := propriétés.donne_valeur_utilisée(IDPropriété.BackgroundColor).couleur

        /* Le rembourrage vertical n'affecte pas la taille mais doit être inclus dans le dessin. */
        padding_top := propriétés.donne_valeur_utilisée(IDPropriété.PaddingTop).pixels comme r32
        padding_bottom := propriétés.donne_valeur_utilisée(IDPropriété.PaddingBottom).pixels comme r32

        si background_color.a != 0 {
            border_radius_top_left := propriétés.donne_valeur_utilisée(IDPropriété.BorderTopLeftRadius).pixels comme r32
            border_radius_top_right := propriétés.donne_valeur_utilisée(IDPropriété.BorderTopRightRadius).pixels comme r32
            border_radius_bottom_right := propriétés.donne_valeur_utilisée(IDPropriété.BorderBottomRightRadius).pixels comme r32
            border_radius_bottom_left := propriétés.donne_valeur_utilisée(IDPropriété.BorderBottomLeftRadius).pixels comme r32

            premier_fragment := fragment
            dernier_fragment := lignes_tenante.fragments.pointeur + lignes_tenante.fragments.taille
            tantque fragment != dernier_fragment {
                rect_noeud := ctx.donne_rectangle_corrigé(fragment.donne_rectangle())
                rect_noeud.y -= padding_top
                rect_noeud.hauteur += padding_top + padding_bottom

                est_premier_fragment := fragment == premier_fragment
                est_dernier_fragment := fragment.type == Fragment.Type.RembourrageFin && fragment.noeud == base

                si est_premier_fragment {
                    rayons := [border_radius_top_left, 0.0, 0.0, border_radius_bottom_left]
                    ctx.peintre.remplis_rectangle_arrondi(rect_noeud, rayons, background_color)
                }
                sinon si est_dernier_fragment {
                    rayons := [0.0, border_radius_top_right, border_radius_bottom_right, 0.0]
                    ctx.peintre.remplis_rectangle_arrondi(rect_noeud, rayons, background_color)
                }
                sinon {
                    ctx.peintre.remplis_rectangle(rect_noeud, background_color)
                }

                si est_dernier_fragment {
                    arrête
                }

                fragment += 1
            }
        }
    }
}

noeud_disposition_inline_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionInline
    ctx.remplis_rectangle_id(noeud)
    dessine_tampon_id_enfants_inline(noeud, ctx)
}

noeud_disposition_inline_corrige_hiérarchie :: fonc (base: *NoeudDeDisposition, créatrice: *CréatriceArbre)
{
    noeud := base comme *NoeudDeDispositionInline
    détermine_disposition_interne(noeud)

    si noeud.est_disposition_inline {
        retourne
    }

    assert(noeud.parent != nul)

    /* When an inline box contains an in-flow block-level box, the inline box (and its
     * inline ancestors within the same line box) are broken around the block-level
     * box (and any block-level siblings that are consecutive or separated only by
     * collapsible whitespace and/or out-of-flow elements), splitting the inline box
     * into two boxes (even if either side is empty), one on each side of the
     * block-level box(es). The line boxes before the break and after the break are
     * enclosed in anonymous block boxes, and the block-level box becomes a sibling of
     * those anonymous boxes. When such an inline box is affected by relative
     * positioning, any resulting translation also affects the block-level box
     * contained in the inline box.
     *
     * À FAIRE : récursion
     *
     * https://www.w3.org/TR/2011/REC-CSS2-20110607/visuren.html#anonymous-block-level */
    anciens_enfants := noeud.enfants
    diffère déloge(anciens_enfants)

    noeud_inline_courant := noeud
    noeud_inline_courant.enfants.pointeur = nul
    noeud_inline_courant.enfants.taille = 0
    noeud_inline_courant.enfants.capacité = 0

    pour anciens_enfants {
        si it.est_block_level() {
            noeud.parent.ajoute_enfant(it)
            noeud_inline_courant = nul
            continue
        }

        si noeud_inline_courant == nul {
            noeud_inline_courant = créatrice.arbre.crée_noeud(NoeudDeDispositionInline)
            noeud_inline_courant.parent = noeud
            noeud_inline_courant.est_disposition_inline = vrai
            crée_style_pour_bloc_anonyme(créatrice.résolveuse, noeud_inline_courant, noeud, Display.Type.Inline)
            noeud.parent.ajoute_enfant(noeud_inline_courant)
        }

        noeud_inline_courant.ajoute_enfant(it)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionNouvelleLigne
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionNouvelleLigne :: struct {
    empl base_bloc: NoeudDeDisposition
    type = #type_de_cette_structure
    élément: *HTML.Élément
    sur_divise_en_lignes = noeud_disposition_nouvelle_ligne_sur_divise_en_lignes
}

noeud_disposition_nouvelle_ligne_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, enligneuse: *Enligneuse, ctx: *ContexteDeFormattage, mode: ModeDeFormattage @inutilisée)
{
    ligne := enligneuse.ajoute_ligne()
    line_height := donne_line_height(base, ctx)
    ligne.garantis_hauteur_minimale(line_height)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionTexte
 * \{ */

/* Créé pour chaque élément contenant du texte. */
NoeudDeDispositionTexte :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    sur_dessin_fragment = noeud_disposition_texte_sur_dessin_fragment
    sur_divise_en_lignes = noeud_disposition_texte_sur_divise_en_lignes

    noeud_texte: *HTML.NoeudTexte
    texte: ChaineUTF16
    fonte: *Fonte

    fragments: [..]FragmentTexte
}

noeud_disposition_texte_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, enligneuse: *Enligneuse, ctx: *ContexteDeFormattage, mode: ModeDeFormattage)
{
    noeud := base comme *NoeudDeDispositionTexte

    propriétés := donne_propriété_parent(noeud)

    text_transform := propriétés.donne_valeur_utilisée(IDPropriété.TextTransform)
    si text_transform.est_mot_clé(PROP_uppercase) {
        noeud.texte = transforme_texte_en_majuscule(noeud.noeud_texte.données)
    }
    sinon si text_transform.est_mot_clé(PROP_lowercase) {
        noeud.texte = transforme_texte_en_minuscule(noeud.noeud_texte.données)
    }
    sinon {
        noeud.texte = copie_chaine(noeud.noeud_texte.données)
    }

    traite_espaces_blanches(noeud)

    fonte := détermine_fonte(noeud, ctx)
    métriques := fonte.donne_métriques_fonte()

    définis_hauteur_contenu(noeud.modèle_de_boite, métriques.donne_hauteur_ligne())

    hauteur_texte := métriques.donne_hauteur_ligne()

    line_height := donne_line_height(propriétés, fonte)
    /* line-height définis la hauteur minimale. */
    hauteur_texte = max(hauteur_texte, line_height)

    // À FAIRE : traite proprement la hauteur de ligne pour les noeuds interéléments.
    // La taille de fonte peut avoir été héritée de l'élément parent des noeuds adelphes.
    si est_vide_ou_uniquement_espaces_blanches(noeud.texte) {
        hauteur_texte = 0.0
    }

    pour noeud.fragments {
        si it.ignore_mais_préserve_soft_wrap_opportunity {
            continue
        }

        si it.est_espace_blanche && it.texte.unités[0] == 0x0A {
            ligne := enligneuse.ajoute_ligne()
            ligne.garantis_hauteur_minimale(hauteur_texte)
            continue
        }

        largeur_morceau := fonte.donne_largeur_texte(it.texte)

        discr mode {
            Normal {
                si it.est_espace_blanche && enligneuse.peut_ignorer_espace_blanche_en_début_de_ligne() {
                    continue
                }

                _ := enligneuse.ajoute_fragment(noeud, index_it, index_it + 1, hauteur_texte, largeur_morceau comme r32)
            }
            ToutesLesBrisuresDeLigne {
                saufsi it.est_espace_blanche {
                    _ := enligneuse.ajoute_fragment(noeud, index_it, index_it + 1, hauteur_texte, largeur_morceau comme r32)
                }

                ligne := enligneuse.ajoute_ligne()
                ligne.garantis_hauteur_minimale(hauteur_texte)
            }
            BrisureDeLigneExplicite {
                _ := enligneuse.ajoute_fragment(noeud, index_it, index_it + 1, hauteur_texte, largeur_morceau comme r32)
            }
        }
    }
}

/* https://drafts.csswg.org/css-text/#white-space-phase-1 */
traite_espaces_blanches :: fonc (noeud: *NoeudDeDispositionTexte)
{
    noeud.fragments.taille = 0
    fragmente_texte_avec_espaces_blanches_ascii(noeud.texte, *noeud.fragments)

    propriétés := donne_propriété_parent(noeud)
    white_space := propriétés.donne_valeur_utilisée(IDPropriété.WhiteSpace).mot_clé

    si white_space == PROP_normal || white_space == PROP_nowrap || white_space == PROP_pre_line {
        // 1. Any sequence of collapsible spaces and tabs immediately preceding or following a segment
        //    break is removed.
        collapse_séquences_d_espaces_blanches_autour_bris_segments(noeud.fragments)

        // 2. Collapsible segment breaks are transformed for rendering according to the segment break
        //    transformation rules.
        collapse_bris_segments(noeud.fragments, white_space)

        // 3. Every collapsible tab is converted to a collapsible space (U+0020).
        convertis_tabulations_en_espaces(noeud.fragments)

        // 4. Any collapsible space immediately following another collapsible space—​even one outside
        //    the boundary of the inline containing that space, provided both spaces are within the
        //    same inline formatting context—​is collapsed to have zero advance width. (It is invisible,
        //    but retains its soft wrap opportunity, if any.)
        // À FAIRE : collapse également (entre les noeuds) lors de la disposition
        collapse_espaces_consécutives(noeud.fragments)
    }
    sinon {
        assert(white_space == PROP_wrap || white_space == PROP_pre || white_space == PROP_pre_wrap || white_space == PROP_break_spaces)
    }
}

FragmentTexte :: struct {
    texte: ChaineUTF16
    est_espace_blanche: bool
    ignore_mais_préserve_soft_wrap_opportunity: bool
}

/* Similaire à Chaine.divise_sur_espaces_blanches_ascii mais nous préservons les espaces. */
fragmente_texte_avec_espaces_blanches_ascii :: fonc (chn: ChaineUTF16, fragments: *[..]FragmentTexte)
{
    position := 0

    texte: ChaineUTF16

    tantque position < chn.taille() && est_espace_blanche_ascii(chn.unités.pointeur[position]) {
        texte.unités.pointeur = chn.unités.pointeur + position
        texte.unités.taille = 1
        tableau_ajoute(fragments, FragmentTexte(texte, vrai))

        position += 1
    }

    tantque position < chn.taille() {
        texte.unités.pointeur = chn.unités.pointeur + position
        texte.unités.taille = 0

        tantque position < chn.taille() && !est_espace_blanche_ascii(chn.unités.pointeur[position]) {
            texte.unités.taille += 1
            position += 1
        }

        si texte.taille() != 0 {
            tableau_ajoute(fragments, FragmentTexte(texte, faux))
        }

        texte.unités.pointeur = chn.unités.pointeur + position
        texte.unités.taille = 0

        tantque position < chn.taille() && est_espace_blanche_ascii(chn.unités.pointeur[position]) {
            texte.unités.pointeur = chn.unités.pointeur + position
            texte.unités.taille = 1
            tableau_ajoute(fragments, FragmentTexte(texte, vrai))

            position += 1
        }
    }
}

collapse_séquences_d_espaces_blanches_autour_bris_segments :: fonc (fragments: &[..]FragmentTexte)
{
    début := fragments.pointeur
    fin := fragments.pointeur + fragments.taille

    entrée := début
    sortie := début

    dernier_fragment_non_collapsible := entrée - 1

    tantque début < fin {
        si début.texte.unités[0] == 0x0A {
            sortie = dernier_fragment_non_collapsible + 1
            assert(sortie < fin)

            mémoire(sortie) = mémoire(début)
            dernier_fragment_non_collapsible = sortie
            début += 1
            sortie += 1

            tantque début < fin && début.est_espace_blanche {
                si début.texte.unités[0] == 0x0A {
                    arrête
                }
                début += 1
            }

            continue
        }

        saufsi début.est_espace_blanche {
            dernier_fragment_non_collapsible = sortie
        }

        mémoire(sortie) = mémoire(début)
        début += 1
        sortie += 1
    }

    fragments.taille = (sortie - entrée)
}

/* https://drafts.csswg.org/css-text/#line-break-transform */
collapse_bris_segments :: fonc (fragments: &[..]FragmentTexte, white_space: CSSOMString)
{
    // When white-space is pre, pre-wrap, break-spaces, or pre-line, segment breaks are
    // not collapsible and are instead transformed into a preserved line feed (U+000A).
    si white_space == PROP_pre || white_space == PROP_pre_wrap || white_space == PROP_break_spaces || white_space == PROP_pre_line {
        retourne
    }

    // 1. First, any collapsible segment break immediately following another collapsible
    // segment break is removed.
    début := fragments.pointeur
    fin := fragments.pointeur + fragments.taille

    entrée := début
    sortie := début

    dernier_fut_bris_segment := faux

    tantque début < fin {
        si début.texte.unités[0] == 0x0A {
            si dernier_fut_bris_segment {
                sortie -= 1
            }
            dernier_fut_bris_segment = vrai
        }
        sinon {
            dernier_fut_bris_segment = faux
        }

        mémoire(sortie) = mémoire(début)
        début += 1
        sortie += 1
    }

    fragments.taille = (sortie - entrée)

    // 2. Then any remaining segment break is either transformed into a space (U+0020)
    // or removed depending on the context before and after the break.
    // The rules for this operation are UA-defined in this level.
    chn_espace := crée_chaine_utf16_unique(" ").donne_chaine_utf16()

    début = fragments.pointeur
    tantque début < fin {
        si début.texte.unités[0] == 0x0A {
            début.texte = chn_espace
        }
        début += 1
    }
}

convertis_tabulations_en_espaces :: fonc (fragments: &[..]FragmentTexte)
{
    chn_espace := crée_chaine_utf16_unique(" ").donne_chaine_utf16()
    début := fragments.pointeur
    fin := fragments.pointeur + fragments.taille

    tantque début < fin {
        si début.texte.unités[0] == '\t' comme n32 {
            début.texte = chn_espace
        }
        début += 1
    }
}

collapse_espaces_consécutives :: fonc (fragments: &[..]FragmentTexte)
{
    début := fragments.pointeur
    fin := fragments.pointeur + fragments.taille

    dernier_fut_espace_blanche := faux

    tantque début < fin {
        si début.est_espace_blanche {
            si dernier_fut_espace_blanche {
                début.ignore_mais_préserve_soft_wrap_opportunity = vrai
            }
            dernier_fut_espace_blanche = vrai
        }
        sinon {
            dernier_fut_espace_blanche = faux
        }
        début += 1
    }
}

noeud_disposition_texte_sur_dessin_fragment :: fonc (base: *NoeudDeDisposition, fragment: *Fragment, lignes_tenante: *LignesTenante @inutilisée, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionTexte

    propriétés := donne_propriété_parent(noeud)

    effets: EffetsTexte

    line := propriétés.donne_valeur_utilisée(IDPropriété.TextDecorationLine)
    saufsi line.est_mot_clé(PROP_none) {
        decoration := line.text_decoration_line
        couleur := vers_couleur_rvba(propriétés.donne_valeur_utilisée(IDPropriété.TextDecorationColor).couleur)

        si decoration.UNDERLINE {
            effets.souslignage = vrai
            effets.couleur_souslignage = couleur
        }
        si decoration.OVERLINE {
            effets.surlignage = vrai
            effets.couleur_surlignage = couleur
        }
        si decoration.LINE_THROUGH {
            effets.barrage = vrai
            effets.couleur_barrage = couleur
        }
    }

    peintre := ctx.peintre
    fonte := noeud.fonte
    assert(fonte != nul)

    métriques := donne_métriques_fonte(fonte)
    couleur := propriétés.donne_valeur_utilisée(IDPropriété.Color).couleur

    rect := ctx.donne_rectangle_corrigé(fragment.donne_rectangle())
    rect.y -= métriques.jambage_inférieur

    // peintre.dessine_rectangle(rect, couleur)

    texte := noeud.fragments[fragment.index_début].texte

    peintre.dessine_texte(fonte, texte, rect, couleur, *effets)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement
 * \{ */

/* Créé pour chaque noeud hors texte */
NoeudDeDispositionRemplacement :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = noeud_disposition_remplacement_sur_mesure
    sur_divise_en_lignes = noeud_disposition_remplacement_sur_divise_en_lignes
    sur_dessin_tampon_id = noeud_disposition_remplacement_sur_dessin_tampon_id
    sur_dessin_fragment = noeud_disposition_remplacement_sur_dessin_fragment
    sur_correction_hiérarchie = nul

    /* Rappels spécifiques. */
    sur_calcul_dimensions_intrinsèques: fonc(*NoeudDeDispositionRemplacement,*ContexteDeFormattage)(rien)
    
    ratio_intrinsèque: Optionnel(r32)
    largeur_intrinsèque: Optionnel(r32)
    hauteur_intrinsèque: Optionnel(r32)

    fonte: *Fonte
}

possède_ratio_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    retourne noeud.ratio_intrinsèque.possède_valeur()
}

possède_largeur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    retourne noeud.largeur_intrinsèque.possède_valeur()
}

possède_hauteur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> bool
{
    retourne noeud.hauteur_intrinsèque.possède_valeur()
}

donne_ratio_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    retourne noeud.ratio_intrinsèque.Quelque
}

donne_largeur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    retourne noeud.largeur_intrinsèque.Quelque
}

donne_hauteur_intrinsèque :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    retourne noeud.hauteur_intrinsèque.Quelque
}

noeud_disposition_remplacement_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage, outrepas_largeur: *ValeurDeStyle @inutilisée)
{
    noeud := base comme *NoeudDeDispositionRemplacement

    noeud.sur_calcul_dimensions_intrinsèques(noeud, ctx)

    containing_block := donne_containing_block(noeud, ctx)

    args := crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, nul, mode)
    calcule_largeur_et_marges(*args)
    _ := calcule_hauteur_et_marges(*args)
}

noeud_disposition_remplacement_sur_divise_en_lignes :: fonc (base: *NoeudDeDisposition, enligneuse: *Enligneuse, ctx: *ContexteDeFormattage, mode: ModeDeFormattage)
{
    noeud_disposition_remplacement_sur_mesure(base, ctx, mode, nul)

    noeud := base comme *NoeudDeDispositionRemplacement
    largeur := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Width).pixels comme r32
    hauteur := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Height).pixels comme r32

    _ := enligneuse.ajoute_fragment(noeud, 0, 0, hauteur, largeur, Fragment.Type.InlineBlock)
}

noeud_disposition_remplacement_sur_dessin_tampon_id :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    ctx.remplis_rectangle_id(noeud)
}

noeud_disposition_remplacement_sur_dessin_fragment :: fonc (base: *NoeudDeDisposition, fragment: *Fragment @inutilisée, lignes_tenante: *LignesTenante @inutilisée, ctx: *ContexteDessin)
{
    base.sur_dessin(base, ctx)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement pour image
 * \{ */

#portée_fichier

crée_noeud_de_remplacement_image :: fonc (arbre: *ArbreDeDisposition, élément: *HTML.HTMLImageElement) -> *NoeudDeDispositionRemplacement
{
    résultat := arbre.crée_noeud(NoeudDeDispositionRemplacement)
    résultat.élément = élément
    résultat.sur_dessin = noeud_image_sur_dessin
    résultat.sur_dessin_fragment = noeud_image_sur_dessin_fragment
    résultat.sur_calcul_dimensions_intrinsèques = noeud_image_calcul_dimensions_intrinsèques
    retourne résultat
}

noeud_image_calcul_dimensions_intrinsèques :: fonc (noeud: *NoeudDeDispositionRemplacement, ctx: *ContexteDeFormattage @inutilisée)
{
    élément := noeud.élément comme *HTML.HTMLImageElement
    assert(élément.local_name == HTML.TAG_img)

    si élément.image_data.donnees {
        noeud.ratio_intrinsèque = HTML.donne_ratio_intrinsèque(élément)
        noeud.largeur_intrinsèque = HTML.donne_largeur_intrinsèque(élément) comme r32
        noeud.hauteur_intrinsèque = HTML.donne_hauteur_intrinsèque(élément) comme r32
    }
}

noeud_image_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    assert(noeud.élément.local_name == HTML.TAG_img)
    image := noeud.élément comme *HTML.HTMLImageElement
    dessine_image(ctx, image.image_data, noeud.donne_rectangle_bordure())
}

noeud_image_sur_dessin_fragment :: fonc (base: *NoeudDeDisposition, fragment: *Fragment, lignes_tenante: *LignesTenante @inutilisée, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    assert(noeud.élément.local_name == HTML.TAG_img)
    image := noeud.élément comme *HTML.HTMLImageElement
    dessine_image(ctx, image.image_data, fragment.donne_rectangle())
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement pour input
 * \{ */

#portée_fichier

crée_noeud_de_remplacement_text_input :: fonc (arbre: *ArbreDeDisposition, élément: *HTML.HTMLInputElement) -> *NoeudDeDispositionRemplacement
{
    résultat := arbre.crée_noeud(NoeudDeDispositionRemplacement)
    résultat.élément = élément
    résultat.sur_dessin = noeud_text_input_sur_dessin
    résultat.sur_calcul_dimensions_intrinsèques = noeud_text_input_dimensions_intrinsèques
    retourne résultat
}

noeud_text_input_dimensions_intrinsèques :: fonc (noeud: *NoeudDeDispositionRemplacement, ctx: *ContexteDeFormattage)
{
    assert(noeud.élément.local_name == HTML.TAG_input)

    noeud.fonte = détermine_fonte(noeud, ctx)
    métriques := noeud.fonte.donne_métriques_fonte()
    hauteur_ligne := métriques.donne_hauteur_ligne()

    line_height := donne_line_height(noeud, ctx)

    /* For input elements whose type attribute is in one of the above states, the used value of the 'line-height'
     * property must be a length value that is no smaller than what the used value would be for 'line-height: normal'. */
    si line_height < hauteur_ligne {
        noeud.propriétés.définis_valeur_utilisée(IDPropriété.LineHeight, ValeurDeStyle(pixels = hauteur_ligne))
        line_height = hauteur_ligne
    }

    noeud.largeur_intrinsèque = donne_inline_size_pour_text_input(noeud)
    noeud.hauteur_intrinsèque = line_height
}

/* https://html.spec.whatwg.org/multipage/rendering.html#the-input-element-as-a-text-entry-widget */
donne_inline_size_pour_text_input :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    métriques := noeud.fonte.donne_métriques_fonte()

    // À FAIRE : 1. If the 'field-sizing' property on the element has a computed value of 'content',
    // the inline size is determined by the text which the element shows. The text is either
    // a value or a short hint specified by the placeholder attribute. User agents may take
    // the text caret size into account in the inline size.

    // 2. If the element has a size attribute, and parsing that attribute's value using the
    // rules for parsing non-negative integers doesn't generate an error, return the value
    // obtained from applying the converting a character width to pixels algorithm to the value
    // of the attribute.
    size := HTML.donne_attribut_ou_chaine_vide(noeud.élément, HTML.ATTR_size)
    saufsi size.est_vide() {
        opt_value := HTML.parse_a_non_negative_integer(size)
        si opt_value.possède_valeur() {
            retourne convert_a_character_width_to_pixels(opt_value.Quelque comme r32, métriques)
        }
    }

    // 3. Otherwise, return the value obtained from applying the converting a character width to
    // pixels algorithm to the number 20.
    retourne convert_a_character_width_to_pixels(20.0, métriques)
}

/* https://html.spec.whatwg.org/multipage/rendering.html#converting-a-character-width-to-pixels */
convert_a_character_width_to_pixels :: fonc (size: r32, métriques: &MétriquesFonte) -> r32
{
    retourne (size - 1.0) * métriques.largeur_moyenne + métriques.largeur_maximale
}

noeud_text_input_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    assert(noeud.élément.local_name == HTML.TAG_input)

    dessine_arrière_plan(ctx, noeud)

    /* À FAIRE : considère toutes les propriétés de bordure. */
    border_declaration := donne_déclaration(noeud.propriétés, PROP_border)
    si border_declaration {
        dessine_bordure(ctx, noeud)
    }
    sinon {
        /* Dessine une bordure par défaut. */
        rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())
        couleur := CouleurRVBAN8(0, 0, 0, 255)
        ctx.peintre.dessine_rectangle(rect, couleur)
    }

    élément := noeud.élément comme *HTML.HTMLInputElement
    couleur := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Color).couleur
    value := HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_value).donne_chaine_utf16()

    si value.est_vide() {
        value = HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_placeholder).donne_chaine_utf16()
        couleur.a = 128
    }

    saufsi value.est_vide() {
        métriques := donne_métriques_fonte(noeud.fonte)

        rect_texte := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
        rect_texte.y -= métriques.jambage_inférieur

        ctx.peintre.dessine_texte(noeud.fonte, value, rect_texte, couleur, nul)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement pour textarea
 * \{ */

#portée_fichier

crée_noeud_de_remplacement_textarea :: fonc (arbre: *ArbreDeDisposition, élément: *HTML.HTMLTextAreaElement) -> *NoeudDeDispositionRemplacement
{
    résultat := arbre.crée_noeud(NoeudDeDispositionRemplacement)
    résultat.élément = élément
    résultat.sur_dessin = noeud_textarea_sur_dessin
    résultat.sur_calcul_dimensions_intrinsèques = noeud_textarea_dimensions_intrinsèques
    retourne résultat
}

noeud_textarea_dimensions_intrinsèques :: fonc (noeud: *NoeudDeDispositionRemplacement, ctx: *ContexteDeFormattage)
{
    assert(noeud.élément.local_name == HTML.TAG_textarea)
    noeud.fonte = détermine_fonte(noeud, ctx)
    noeud.largeur_intrinsèque = donne_textarea_effective_width(noeud)
    noeud.hauteur_intrinsèque = donne_textarea_effective_height(noeud, ctx)
}

LARGEUR_BARRE_DE_DÉFILEMENT :: 16.0

/* https://html.spec.whatwg.org/multipage/rendering.html#textarea-effective-width */
donne_textarea_effective_width :: fonc (noeud: *NoeudDeDispositionRemplacement) -> r32
{
    élément := noeud.élément comme *HTML.HTMLTextAreaElement
    métriques := noeud.fonte.donne_métriques_fonte()
    character_width := HTML.donne_character_width(élément)
    retourne (character_width comme r32) * métriques.largeur_moyenne + LARGEUR_BARRE_DE_DÉFILEMENT
}

/* https://html.spec.whatwg.org/multipage/rendering.html#textarea-effective-height */
donne_textarea_effective_height :: fonc (noeud: *NoeudDeDispositionRemplacement, ctx: *ContexteDeFormattage) -> r32
{
    élément := noeud.élément comme *HTML.HTMLTextAreaElement
    character_height := HTML.donne_character_height(élément)
    line_height := donne_line_height(noeud, ctx)
    retourne character_height comme r32 * line_height
}

noeud_textarea_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    assert(noeud.élément.local_name == HTML.TAG_textarea)

    dessine_arrière_plan(ctx, noeud)

    /* À FAIRE : considère toutes les propriétés de bordure. */
    border_declaration := donne_déclaration(noeud.propriétés, PROP_border)
    si border_declaration {
        dessine_bordure(ctx, noeud)
    }
    sinon {
        /* Dessine une bordure par défaut. */
        rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_bordure())
        couleur := CouleurRVBAN8(0, 0, 0, 255)
        ctx.peintre.dessine_rectangle(rect, couleur)
    }

    élément := noeud.élément comme *HTML.HTMLTextAreaElement
    couleur := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Color).couleur
    value := HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_value).donne_chaine_utf16()

    si value.est_vide() {
        value = HTML.donne_attribut_ou_chaine_vide(élément, HTML.ATTR_placeholder).donne_chaine_utf16()
        couleur.a = 128
    }

    saufsi value.est_vide() {
        métriques := donne_métriques_fonte(noeud.fonte)

        rect_texte := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
        rect_texte.y -= métriques.jambage_inférieur

        ctx.peintre.dessine_texte(noeud.fonte, value, rect_texte, couleur, nul)
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement pour canvas
 * \{ */

#portée_fichier

crée_noeud_de_remplacement_canvas :: fonc (arbre: *ArbreDeDisposition, élément: *HTML.HTMLCanvasElement) -> *NoeudDeDispositionRemplacement
{
    résultat := arbre.crée_noeud(NoeudDeDispositionRemplacement)
    résultat.élément = élément
    résultat.sur_dessin = noeud_canvas_sur_dessin
    résultat.sur_calcul_dimensions_intrinsèques = noeud_canvas_calcul_dimensions_intrinsèques
    retourne résultat
}

noeud_canvas_calcul_dimensions_intrinsèques :: fonc (noeud: *NoeudDeDispositionRemplacement, ctx: *ContexteDeFormattage @inutilisée)
{
    élément := noeud.élément comme *HTML.HTMLCanvasElement
    assert(élément.local_name == HTML.TAG_canvas)
    noeud.largeur_intrinsèque = HTML.donne_largeur_intrinsèque(élément) comme r32
    noeud.hauteur_intrinsèque = HTML.donne_hauteur_intrinsèque(élément) comme r32
}

noeud_canvas_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    noeud := base comme *NoeudDeDispositionRemplacement
    élément := noeud.élément comme *HTML.HTMLCanvasElement
    assert(noeud.élément.local_name == HTML.TAG_canvas)

    dessine_arrière_plan(ctx, noeud)
    dessine_bordure(ctx, noeud)

    tampon_de_rendu := HTML.donne_tampon_de_rendu(élément)

    image: TamponImageRVBA8Bit
    image.données = tampon_de_rendu.données.pointeur
    image.largeur = tampon_de_rendu.rect.largeur
    image.hauteur = tampon_de_rendu.rect.hauteur
    
    rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
    ctx.peintre.dessine_image(image, rect)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom NoeudDeDispositionRemplacement pour select
 * \{ */

#portée_fichier

NoeudDeDispositionSelect :: struct {
    empl noeud_rempl: NoeudDeDispositionRemplacement
    type = #type_de_cette_structure

    texte: ChaineUTF16
    fragments: [..]FragmentTexte
}

réinitialise :: fonc (noeud: *NoeudDeDispositionSelect)
{
    détruit_chaine(noeud.texte)
    noeud.fragments.taille = 0
}

crée_noeud_de_remplacement_select :: fonc (arbre: *ArbreDeDisposition, élément: *HTML.HTMLSelectElement) -> *NoeudDeDispositionRemplacement
{
    résultat := arbre.crée_noeud(NoeudDeDispositionSelect)
    résultat.élément = élément
    résultat.sur_dessin = noeud_select_sur_dessin
    résultat.sur_calcul_dimensions_intrinsèques = noeud_select_calcul_dimensions_intrinsèques
    retourne résultat
}

// À FAIRE : standardise (https://html.spec.whatwg.org/multipage/rendering.html#the-select-element-2)
donne_select_labels_width :: fonc (noeud: *NoeudDeDispositionSelect, ctx: *ContexteDeFormattage) -> r32
{
    réinitialise(noeud)

    select := noeud.élément comme *HTML.HTMLSelectElement

    option := donne_première_option(select)
    saufsi option {
        retourne 0.0
    }

    noeud.texte = HTML.donne_contenu_texte_descandants(option)
    si noeud.texte.taille() == 0 {
        retourne 0.0
    }

    fragmente_texte_avec_espaces_blanches_ascii(noeud.texte, *noeud.fragments)
    collapse_séquences_d_espaces_blanches_autour_bris_segments(noeud.fragments)
    collapse_bris_segments(noeud.fragments, PROP_normal)
    convertis_tabulations_en_espaces(noeud.fragments)
    collapse_espaces_consécutives(noeud.fragments)

    fonte := détermine_fonte(noeud, ctx)

    résultat := 0.0

    pour noeud.fragments {
        résultat += donne_largeur_texte(fonte, it.texte) comme r32
    }

    retourne résultat
}

donne_première_option :: fonc (select: *HTML.HTMLSelectElement) -> *HTML.HTMLOptionElement
{
    pour select.enfants {
        saufsi HTML.est_élément(it) {
            continue
        }

        élément := it comme *HTML.Élément
        si élément.local_name != HTML.TAG_option {
            continue
        }

        retourne élément comme *HTML.HTMLOptionElement
    }

    retourne nul
}

noeud_select_calcul_dimensions_intrinsèques :: fonc (noeud: *NoeudDeDispositionSelect, ctx: *ContexteDeFormattage)
{
    élément := noeud.élément comme *HTML.HTMLSelectElement
    assert(élément.local_name == HTML.TAG_select)
    noeud.largeur_intrinsèque = donne_select_labels_width(noeud, ctx)
    noeud.hauteur_intrinsèque = donne_line_height(noeud, ctx)
}

noeud_select_sur_dessin :: fonc (noeud: *NoeudDeDispositionSelect, ctx: *ContexteDessin)
{
    dessine_arrière_plan(ctx, noeud)

    peintre := ctx.peintre
    fonte := noeud.fonte
    assert(fonte != nul)

    métriques := donne_métriques_fonte(fonte)
    couleur := noeud.propriétés.donne_valeur_utilisée(IDPropriété.Color).couleur

    rect := ctx.donne_rectangle_corrigé(noeud.donne_rectangle_contenu())
    rect.y -= métriques.jambage_inférieur

    pour noeud.fragments {
        peintre.dessine_texte(fonte, it.texte, rect, couleur, nul)

        rect.x += donne_largeur_texte(fonte, it.texte) comme r32
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom ItératriceArbre
 * \{ */

ItératriceArbre :: struct {
    racine: *NoeudDeDisposition
}

opérateur pour :: fonc (itératrice: ItératriceArbre) -> *NoeudDeDisposition
{
    pile: Pile(*NoeudDeDisposition)
    diffère détruit(pile)

    pile.empile(itératrice.racine)

    index: z64

    tantque !pile.est_vide() {
        enfant := pile.dépile()

        `it = enfant
        `index_it = index

        #corps_boucle

        pour > enfant.enfants {
            pile.empile(it)
        }

        index += 1
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableWrapperBox
 * \{ */

 #portée_module

TableWrapperBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = table_wrapper_box_sur_mesure
    sur_correction_hiérarchie = noeud_table_wrapper_box_corrige_hiérarchie

    table: *TableBox
}

table_wrapper_box_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage, outrepas_largeur: *ValeurDeStyle)
{
    noeud := base comme *TableWrapperBox

    containing_block := donne_containing_block(base, ctx)

    args := crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, outrepas_largeur, mode)
    calcule_largeur_et_marges(*args)
    _ := calcule_hauteur_et_marges(*args)

    ctx_local: ContexteDeFormattageBloc
    initialise_depuis_parent(*ctx_local, ctx)
    diffère détruit_données_contexte(*ctx_local)

    ctx_local.racine = noeud

    formatte(*ctx_local, noeud, mode)

    width := noeud.propriétés.donne_valeur_calculée(IDPropriété.Width)
    si width.est_auto() {
        largeur_final := ValeurDeStyle(pixels = noeud.table.modèle_de_boite.contenu.largeur)
        args = crée_arguments_dimensionnement_largeur(ctx, noeud, containing_block, *largeur_final, mode)
        calcule_largeur_et_marges(*args)
    }

    height := noeud.propriétés.donne_valeur_calculée(IDPropriété.Height)
    si height.est_auto() {
        hauteur_finale := noeud.table.propriétés.donne_valeur_utilisée(IDPropriété.Height)
        args = crée_arguments_dimensionnement_hauteur(ctx, noeud, containing_block, *hauteur_finale)
        _ := calcule_hauteur_et_marges(*args)
    }
}

est_row_group_box :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    // A 'table-row-group', 'table-header-group', or 'table-footer-group' 
    retourne noeud.type == TableRowGroupBox || noeud.type == TableHeaderGroupBox || noeud.type == TableFooterGroupBox
}

est_proper_table_child :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    // A 'table-row' box, row group box, 'table-column' box, 'table-column-group' box, or 'table-caption' box. 
    retourne noeud.type == TableRowBox || est_row_group_box(noeud) || noeud.type == TableColumnBox || noeud.type == TableColumnGroupBox || noeud.type == CaptionBox
}

est_proper_table_row_parent :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    // A 'table' or 'inline-table' box or row group box 
    retourne noeud.type == TableBox || est_row_group_box(noeud)
}

est_internal_table_box :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    // A 'table-cell' box, 'table-row' box, row group box, 'table-column' box, or 'table-column-group' box. 
    retourne noeud.type == TableCellBox || noeud.type == TableRowBox || est_row_group_box(noeud) || noeud.type == TableColumnBox || noeud.type == TableColumnGroupBox
}

est_tabular_container :: fonc (noeud: *NoeudDeDisposition) -> bool
{
    // A 'table-row' box or proper table row parent 
    retourne noeud.type == TableRowBox || est_proper_table_row_parent(noeud)
}

sont_consecutifs :: fonc (a: *NoeudDeDisposition, b: *NoeudDeDisposition) -> bool
{
    // Two sibling boxes are consecutive if they have no intervening siblings
    // other than, optionally, an anonymous inline containing only white spaces.
    // A sequence of sibling boxes is consecutive if each box in the sequence is
    // consecutive to the one before it in the sequence. 
    si a.parent != b.parent {
        retourne faux
    }
}

noeud_table_wrapper_box_corrige_hiérarchie :: fonc (base: *NoeudDeDisposition, créatrice: *CréatriceArbre)
{
    // 1. Remove irrelevant boxes
    pour ItératriceArbre(base) {
        remove_irrelevant_boxes(it)
    }

    // 2. Generate missing child wrappers
    pour ItératriceArbre(base) {
        generate_missing_child_wrappers(it, créatrice)
    }

    // 3. Generate missing parents
    pour ItératriceArbre(base) {
        generate_missing_parents(it, créatrice)
    }
}

remove_irrelevant_boxes :: fonc (noeud: *NoeudDeDisposition)
{
    // 1. All child boxes of a 'table-column' parent are treated as if they had 'display: none'.
    si noeud.type == TableColumnBox {
        pour noeud.enfants {
            it.traite_comme_display_none = vrai
        }
    }

    // 2. If a child C of a 'table-column-group' parent is not a 'table-column' box,
    // then it is treated as if it had 'display: none'.
    si noeud.type == TableColumnGroupBox {
        si noeud.parent == nul || noeud.parent.type != TableColumnBox {
            noeud.traite_comme_display_none = vrai
        }
    }

    // 3. If a child C of a tabular container P is an anonymous inline box that
    // contains only white space, and its immediately preceding and following
    // siblings, if any, are proper table descendants of P and are either
    // 'table-caption' or internal table boxes, then it is treated as if it had
    // 'display: none'.
    si est_tabular_container(noeud) {
        pour noeud.enfants {
            saufsi est_espace_blanche_document(it) {
                continue
            }

            si index_it > 0 {
                précédent := noeud.enfants[index_it - 1]
                saufsi est_proper_table_descendant(noeud, précédent) {
                    continue
                }
            }

            si index_it < noeud.enfants.taille - 1 {
                suivant := noeud.enfants[index_it + 1]
                saufsi est_proper_table_descendant(noeud, suivant) {
                    continue
                }
            }

            it.traite_comme_display_none = vrai
        }
    }

    // 4. If a box B is an anonymous inline containing only white space, and is
    // between two immediate siblings each of which is either an internal table
    // box or a 'table-caption' box then B is treated as if it had 'display: none'.
    // À FAIRE
}

// A box D is a proper table descendant of A if D can be a descendant of A
// without causing the generation of any intervening 'table' or 'inline-table' boxes.
est_proper_table_descendant :: fonc (A: *NoeudDeDisposition, D: *NoeudDeDisposition) -> bool
{
    si A.type == TableRowBox {
        retourne D.type == TableCellBox
    }

    si A.type == TableBox {
        retourne est_proper_table_child(D)
    }

    assert(est_row_group_box(A))
    retourne D.type == TableRowBox
}

generate_missing_child_wrappers :: fonc (noeud: *NoeudDeDisposition, créatrice: *CréatriceArbre)
{
    // 1. If a child C of a 'table' or 'inline-table' box is not a proper table
    // child, then generate an anonymous 'table-row' box around C and all
    // consecutive siblings of C that are not proper table children.
    si noeud.type == TableBox {
        table := noeud comme *TableBox

        pour noeud.enfants {
            si it.est_proper_table_child() {
                continue
            }

            table_row_box := créatrice.arbre.crée_noeud(TableRowBox)
            crée_style_pour_bloc_anonyme(créatrice.résolveuse, table_row_box, table, Display.Type.TableRow)
            table_row_box.est_pseudo_element = it.est_pseudo_element
            ajoute_enfant(table_row_box, it)

            table.enfants[index_it] = table_row_box
        }
    }

    // 2. If a child C of a row group box is not a 'table-row' box, then
    // generate an anonymous 'table-row' box around C and all consecutive
    // siblings of C that are not 'table-row' boxes.
    si noeud.type == TableRowGroupBox {
        table_row_group := noeud comme *TableRowGroupBox

        pour table_row_group.enfants {
            si it.type != TableRowBox {
                table_row_box := créatrice.arbre.crée_noeud(TableRowBox)
                crée_style_pour_bloc_anonyme(créatrice.résolveuse, table_row_box, table_row_group, Display.Type.TableRow)
                table_row_box.est_pseudo_element = it.est_pseudo_element
                ajoute_enfant(table_row_box, it)

                table_row_group.enfants[index_it] = table_row_box
            }
        }
    }

    // 3. If a child C of a 'table-row' box is not a 'table-cell', then
    // generate an anonymous 'table-cell' box around C and all consecutive
    // siblings of C that are not 'table-cell' boxes. 
    si noeud.type == TableRowBox {
        table_row_box := noeud comme *TableRowBox

        pour table_row_box.enfants {
            si it.type != TableCellBox {
                table_cell := créatrice.arbre.crée_noeud(TableCellBox)
                crée_style_pour_bloc_anonyme(créatrice.résolveuse, table_cell, table_row_box, Display.Type.TableCell)
                table_cell.est_pseudo_element = it.est_pseudo_element
                ajoute_enfant(table_cell, it)

                table_row_box.enfants[index_it] = table_cell
            }
        }
    }
}

generate_missing_parents :: fonc (noeud: *NoeudDeDisposition @inutilisée, créatrice: *CréatriceArbre @inutilisée)
{
    // À FAIRE
    // For each 'table-cell' box C in a sequence of consecutive internal table and 'table-caption' siblings, if C's parent is not a 'table-row' then generate an anonymous 'table-row' box around C and all consecutive siblings of C that are 'table-cell' boxes.
    // For each proper table child C in a sequence of consecutive proper table children, if C is misparented then generate an anonymous 'table' or 'inline-table' box T around C and all consecutive siblings of C that are proper table children. (If C's parent is an 'inline' box, then T must be an 'inline-table' box; otherwise it must be a 'table' box.)
        // A 'table-row' is misparented if its parent is neither a row group box nor a 'table' or 'inline-table' box.
        // A 'table-column' box is misparented if its parent is neither a 'table-column-group' box nor a 'table' or 'inline-table' box.
        // A row group box, 'table-column-group' box, or 'table-caption' box is misparented if its parent is neither a 'table' box nor an 'inline-table' box. 
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableBox
 * \{ */

TableBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
    sur_mesure = table_box_sur_mesure
    sur_dessin = table_box_sur_dessin
}

table_box_sur_mesure :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDeFormattage, mode: ModeDeFormattage @inutilisée, outrepas_largeur: *ValeurDeStyle @inutilisée)
{
    noeud := base comme *TableBox

    ctx_local: ContexteDeFormattageTable
    initialise_depuis_parent(*ctx_local, ctx)

    formatte_table(*ctx_local, noeud)
}

table_box_sur_dessin :: fonc (base: *NoeudDeDisposition, ctx: *ContexteDessin)
{
    table := base comme *TableBox
    pour table.enfants {
        saufsi est_row_group_box(it) {
            continue
        }

        group_box := it comme *NoeudDeDispositionBloc
        dessine_arrière_plan(ctx, group_box)

        pour enfant_row_group dans it.enfants {
            saufsi enfant_row_group.type == TableRowBox {
                continue
            }

            row := enfant_row_group comme *TableRowBox

            dessine_arrière_plan(ctx, row)

            pour enfant_row dans row.enfants {
                saufsi enfant_row.type == TableCellBox {
                    continue
                }

                dessine(enfant_row, ctx)
            }
        }
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableColumnGroupBox
 * \{ */

TableColumnGroupBox :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    // sur_mesure = noeud_disposition_bloc_sur_mesure
    // sur_dessin = noeud_disposition_bloc_sur_dessin
    // sur_dessin_tampon_id = noeud_disposition_bloc_sur_dessin_tampon_id
    // sur_correction_hiérarchie = noeud_disposition_bloc_corrige_hiérarchie

    élément: *HTML.Élément
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableColumnBox
 * \{ */

TableColumnBox :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    // sur_mesure = noeud_disposition_bloc_sur_mesure
    // sur_dessin = noeud_disposition_bloc_sur_dessin
    // sur_dessin_tampon_id = noeud_disposition_bloc_sur_dessin_tampon_id
    // sur_correction_hiérarchie = noeud_disposition_bloc_corrige_hiérarchie

    élément: *HTML.Élément
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableFooterGroupBox
 * \{ */

TableFooterGroupBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableHeaderGroupBox
 * \{ */

TableHeaderGroupBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableRowGroupBox
 * \{ */

TableRowGroupBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableRowBox
 * \{ */

TableRowBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom TableCellBox
 * \{ */

TableCellBox :: struct {
    empl base_bloc: NoeudDeDispositionBloc
    type = #type_de_cette_structure
}

/* Retourne la largeur pour le contenu de la cellule étant donné la largeur disponible. */
donne_largeur_contenu :: fonc (box: *TableCellBox, largeur_disponible: r64) -> r64
{
    propriétés := box.propriétés

    ids_propriétés := [IDPropriété.BorderWidthLeft, IDPropriété.BorderWidthRight, IDPropriété.PaddingLeft, IDPropriété.PaddingRight]

    pour ids_propriétés {
        prop := propriétés.donne_valeur_calculée(it)

        si prop.est_pixels() {
            largeur_disponible -= prop.pixels
        }
    }

    retourne largeur_disponible
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom CaptionBox
 * \{ */

CaptionBox :: struct {
    empl base: NoeudDeDisposition
    type = #type_de_cette_structure
    // sur_mesure = noeud_disposition_bloc_sur_mesure
    // sur_dessin = noeud_disposition_bloc_sur_dessin
    // sur_dessin_tampon_id = noeud_disposition_bloc_sur_dessin_tampon_id
    // sur_correction_hiérarchie = noeud_disposition_bloc_corrige_hiérarchie

    élément: *HTML.Élément
}

/** \} */
