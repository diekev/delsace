/* ------------------------------------------------------------------------- */
/** \nom Périphériques
 * \{ */

importe Périphériques

donne_cursor_système :: fonc (type: CursorType) -> TypeCurseurSystème
{
    discr type {
        auto,
        default {
            retourne TypeCurseurSystème.FLÈCHE
        }
        none {
            retourne TypeCurseurSystème.VIERGE
        }
        context_menu {
            retourne TypeCurseurSystème.MENU_CONTEXTUEL
        }
        help {
            retourne TypeCurseurSystème.POINT_D_INTERROGATION
        }
        pointer {
            retourne TypeCurseurSystème.MAIN_POINTANT
        }
        progress {
            retourne TypeCurseurSystème.PROGRÈS
        }
        wait {
            retourne TypeCurseurSystème.ATTENTE
        }
        cell {
            retourne TypeCurseurSystème.CELLULE
        }
        crosshair {
            retourne TypeCurseurSystème.CROIX
        }
        text {
            retourne TypeCurseurSystème.TEXTE
        }
        vertical_text {
            retourne TypeCurseurSystème.TEXTE_VERTICAL
        }
        alias {
            retourne TypeCurseurSystème.ALIAS
        }
        copy {
            retourne TypeCurseurSystème.COPIE
        }
        move {
            retourne TypeCurseurSystème.DÉPLACEMENT
        }
        no_drop {
            retourne TypeCurseurSystème.DÉPOSAGE_INTERDIT
        }
        not_allowed {
            retourne TypeCurseurSystème.INTERDIT
        }
        grab {
            retourne TypeCurseurSystème.MAIN_OUVERTE
        }
        grabbing {
            retourne TypeCurseurSystème.MAIN_FERMÉE
        }
        e_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_EST
        }
        n_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD
        }
        ne_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD_EST
        }
        nw_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD_OUEST
        }
        s_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_SUD
        }
        se_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_SUD_EST
        }
        sw_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_SUD_OUEST
        }
        w_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_OUEST
        }
        ew_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_EST_OUEST
        }
        ns_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD_SUD
        }
        nesw_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD_EST_SUD_OUEST
        }
        nwse_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_NORD_OUEST_SUD_EST
        }
        col_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_COLONNE
        }
        row_resize {
            retourne TypeCurseurSystème.REDIMENSIONNEMENT_LIGNE
        }
        all_scroll {
            retourne TypeCurseurSystème.DÉFILAGE_OMNI
        }
        zoom_in {
            retourne TypeCurseurSystème.ZOOM
        }
        zoom_out {
            retourne TypeCurseurSystème.DÉZOOM
        }
    }

    retourne TypeCurseurSystème.FLÈCHE
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom PeintureInterface
 * \{ */

importe IGUMI
importe OpenGL
importe PeintureInterface

dessine_commandes :: fonc (peintre: *Peintre, commandes: *CommandesDeRendu)
{
    peintre.commence_dessin()

    // active_gl_debug()

    début := commandes.commandes.pointeur
    fin := début + commandes.commandes.taille

    tantque début < fin {
        base := début comme *BaseCommande
        discr base.type {
            REMPLIS_RECTANGLE_ID {}
            REMPLIS_RECTANGLE {
                commande := base comme *CommandeRemplisRectangle
                peintre.remplis_rectangle(commande.rect, commande.couleur)
            }
            REMPLIS_RECTANGLE_ARRONDI {
                commande := base comme *CommandeRemplisRectangleArrondi
                peintre.remplis_rectangle_arrondi(commande.rect, commande.rayons, commande.couleur)
            }
            DESSINE_RECTANGLE {
                commande := base comme *CommandeDessineRectangle
                peintre.dessine_rectangle(commande.rect, commande.couleur)
            }
            DESSINE_RECTANGLE_ARRONDI {
                // commande := base comme *CommandeDessineRectangleArrondi
                // peintre.dessine_rectangle_arrondi(commande.rect, commande.rayons, commande.couleur)
            }
            DESSINE_BORDURE {
                commande := base comme *CommandeDessineBordure

                couleurs_peintre: [4]CouleurPeintre = ---
                pour commande.couleurs {
                    couleurs_peintre[indice_it] = it
                }

                peintre.dessine_bordure(commande.rect, commande.largeurs, couleurs_peintre)
            }
            DESSINE_IMAGE {
                commande := base comme *CommandeDessineImage
                peintre.dessine_image(*commande.image_data, commande.rect_image, commande.rect, commande.paramètres)
            }
            DESSINE_TAMPON_IMAGE {
                commande := base comme *CommandeDessineImageTampon
                peintre.dessine_image(commande.image_data, commande.rect)
            }
            DESSINE_TEXTE {
                commande := base comme *CommandeDessineTexte
                peintre.dessine_texte(commande.fonte, commande.texte, commande.rect, commande.couleur, *commande.effets)
            }
            DESSINE_CERCLE {
                commande := base comme *CommandeDessineCercle

                cercle: Cercle(r32)
                cercle.centre_x = commande.rect.x + commande.rect.largeur / 2
                cercle.centre_y = commande.rect.y + commande.rect.hauteur / 2
                cercle.rayon = max(commande.rect.largeur, commande.rect.hauteur) / 2.0

                peintre.dessine_cercle(cercle, commande.couleur)
            }
            REMPLIS_CERCLE {
                commande := base comme *CommandeRemplisCercle

                cercle: Cercle(r32)
                cercle.centre_x = commande.rect.x + commande.rect.largeur / 2
                cercle.centre_y = commande.rect.y + commande.rect.hauteur / 2
                cercle.rayon = max(commande.rect.largeur, commande.rect.hauteur) / 2.0

                peintre.remplis_cercle(cercle, commande.couleur)
            }
            DÉBUTE_STACKING_CONTEXT {
                // Fondation.imprimeln("%", base.type)
                // IGUMI.commence_frame_buffer()
            }
            TERMINE_STACKING_CONTEXT {
                // commande := base comme *CommandeTermineStackingContexte
                // Fondation.imprimeln("%", base.type)
                // IGUMI.termine_frame_buffer(commande.opacité)
            }
            DÉBUTE_CISELAGE {
                commande := base comme *CommandeDébuteCiselage
                peintre.définis_ciseau(commande.rect)
            }
            TERMINE_CISELAGE {
                peintre.restaure_ciseau()
            }
        }

        début += base.taille_type
    }

    peintre.termine_dessin()
}

dessine_commandes_id :: fonc (peintre: *Peintre @inutilisée, commandes: *CommandesDeRendu, tampon_id: *TamponID)
{
    IGUMI.réinitialise_données_id()

    fbo: GLuint
    glGenFramebuffers(1, *fbo)
    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, fbo)

    render_buf: GLuint
    glGenRenderbuffers(1, *render_buf)
    glBindRenderbuffer(GL_RENDERBUFFER, render_buf)
    glRenderbufferStorage(GL_RENDERBUFFER, GL_R32UI, tampon_id.largeur, tampon_id.hauteur)
    vérifie_erreur("glRenderbufferStorage")
    glFramebufferRenderbuffer(GL_FRAMEBUFFER, GL_COLOR_ATTACHMENT0, GL_RENDERBUFFER, render_buf)
    vérifie_erreur("glFramebufferRenderbuffer")

    début := commandes.commandes.pointeur
    fin := début + commandes.commandes.taille

    IGUMI.commence_immédiat_id(IGUMI.ModeImmédiat.TRIANGLES)
    vérifie_erreur("commence_immédiat_id")

    /* Remets à zéro au cas la mémoire est réutilisée par le pilote. */
    IGUMI.quad_immédiat_id(0.0, 0.0, tampon_id.largeur comme r32, tampon_id.hauteur comme r32, 0)

    tantque début < fin {
        base := début comme *BaseCommande

        si base.type == TypeCommandeDeRendu.REMPLIS_RECTANGLE_ID {
            commande := base comme *CommandeRemplisRectangleID
            rect := commande.rect
            IGUMI.quad_immédiat_id(rect.x, rect.y, rect.largeur, rect.hauteur, commande.id)
        }

        début += base.taille_type
    }

    IGUMI.termine_immédiat_id()
    vérifie_erreur("termine_immédiat_id")

    glBindFramebuffer(GL_DRAW_FRAMEBUFFER, 0)
    vérifie_erreur("glBindFramebuffer")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, fbo)
    vérifie_erreur("glBindFramebuffer")

    glReadBuffer(GL_COLOR_ATTACHMENT0)
    vérifie_erreur("glReadBuffer")
    glReadPixels(0, 0, tampon_id.largeur, tampon_id.hauteur, GL_RED_INTEGER, GL_UNSIGNED_INT, tampon_id.données.pointeur)
    vérifie_erreur("glReadPixels")

    glBindFramebuffer(GL_READ_FRAMEBUFFER, 0)

    glBindRenderbuffer(GL_RENDERBUFFER, 0)

    glDeleteFramebuffers(1, *fbo)
    glDeleteRenderbuffers(1, *render_buf)

    tampon_id.fut_dessiné = vrai
}

/** \} */
