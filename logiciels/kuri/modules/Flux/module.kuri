importe Fondation
importe Ordinatrice
importe SysFichier

/* ------------------------------------------------------------------------- */
/** \nom FluxOctetsMémoire
 * \{ */

FluxOctets :: struct {
    sur_positionne: fonc(*FluxOctets, PositionFichier)(rien)
    sur_lis: fonc(*FluxOctets, []octet)(z64)
    sur_ferme: fonc(*FluxOctets)(rien)
    sur_est_à_la_fin: fonc(*FluxOctets)(bool)
}

ferme :: fonc (flux: *FluxOctets)
{
    si flux.sur_ferme {
        flux.sur_ferme(flux)
    }
}

est_valide :: fonc (flux: *FluxOctets) -> bool
{
    retourne flux.sur_est_à_la_fin(flux) == faux
}

est_à_la_fin :: fonc (flux: *FluxOctets) -> bool
{
    retourne flux.sur_est_à_la_fin(flux)
}

positionne :: fonc (flux: *FluxOctets, position: PositionFichier)
{
    si flux.sur_positionne {
        flux.sur_positionne(flux, position)
    }
}

lis :: fonc (flux: *FluxOctets, tampon: []octet) -> z64
{
    retourne flux.sur_lis(flux, tampon)
}

lis :: fonc (flux: *FluxOctets, position: PositionFichier, tampon: []octet) -> z64
{
    positionne(flux, position)
    retourne lis(flux, tampon)
}

lis_grand_boutisme :: fonc (flux: *FluxOctets, $T: type_de_données) -> T, z64
{
    résultat: T
    taille_lue := flux.lis(résultat)
    commute_boutisme(*résultat)
    retourne résultat, taille_lue
}

lis_grand_boutisme :: fonc (flux: *FluxOctets, position: PositionFichier, $T: type_de_données) -> T, z64
{
    positionne(flux, position)
    retourne lis_grand_boutisme(flux, T)
}

lis_petit_boutisme :: fonc (flux: *FluxOctets, $T: type_de_données) -> T, z64
{
    résultat: T
    taille_lue := flux.lis(résultat)
    retourne résultat, taille_lue
}

lis_petit_boutisme :: fonc (flux: *FluxOctets, position: PositionFichier, $T: type_de_données) -> T, z64
{
    positionne(flux, position)
    retourne lis_petit_boutisme(flux, T)
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom FluxOctetsMémoire
 * \{ */

FluxOctetsMémoire :: struct {
    empl base: FluxOctets

    octets: []octet
    curseur: []octet
}

crée_flux_octets_mémoire :: fonc (octets: []octet) -> FluxOctetsMémoire
{
    résultat: FluxOctetsMémoire
    résultat.sur_lis = flux_octets_mémoire_lis
    résultat.sur_positionne = flux_octets_mémoire_positionne
    résultat.sur_est_à_la_fin = flux_octets_mémoire_est_à_la_fin
    résultat.octets = octets
    résultat.curseur = octets
    retourne résultat
}

flux_octets_mémoire_est_à_la_fin :: fonc (flux: *FluxOctetsMémoire) -> bool
{
    retourne flux.curseur.taille == 0
}

flux_octets_mémoire_positionne :: fonc (flux: *FluxOctetsMémoire, position: PositionFichier)
{
    discr position {
        Début(pos) {
            flux.curseur = avance(flux.octets, pos comme z64)
        }
        Fin(pos) {
            flux.curseur = avance(flux.octets, flux.octest.taille + pos comme z64)
        }
        Décalage(pos) {
            flux.curseur = avance(flux.curseur, pos comme z64)
        }
        sinon {}
    }

    si flux.curseur.taille <= 0 {
        flux.curseur.pointeur = nul
        flux.curseur.taille = 0
    }
}

flux_octets_mémoire_lis :: fonc (base: *FluxOctets, tampon: []octet) -> z64
{
    flux := base comme *FluxOctetsMémoire

    taille_à_lire := tampon.taille
    si taille_à_lire > flux.curseur.taille {
        taille_à_lire = flux.curseur.taille
    }

    copie_mem_nonsur(src = flux.curseur.pointeur, dst = tampon.pointeur, taille = taille_à_lire)

    flux.curseur = avance(flux.curseur, taille_à_lire)

    retourne taille_à_lire
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom FluxOctetsFichier
 * \{ */

FluxOctetsFichier :: struct {
    empl base: FluxOctets

    fichier: Fichier
    taille_fichier: z64
    curseur: z64
}

crée_flux_octets_fichier :: fonc (chemin: chaine) -> FluxOctetsFichier
{
    retourne crée_flux_octets_fichier(CheminFichier(chemin))
}

crée_flux_octets_fichier :: fonc (chemin: CheminFichier) -> FluxOctetsFichier
{
    résultat: FluxOctetsFichier
    résultat.sur_lis = flux_octets_fichier_lis
    résultat.sur_ferme = flux_octets_fichier_ferme
    résultat.sur_positionne = flux_octets_fichier_positionne
    résultat.sur_est_à_la_fin = flux_octets_fichier_est_à_la_fin

    résultat.fichier = tente ouvre_fichier(chemin, pour_lecture, 0o644) piège err {
        imprimeln("Impossible d'ouvrir le fichier '%' : %", chemin.chn, err)
        retourne résultat
    }

    résultat.taille_fichier = détermine_taille_fichier(*résultat.fichier)

    retourne résultat
}

flux_octets_fichier_lis :: fonc (base: *FluxOctets, tampon: []octet) -> z64
{
    flux := base comme *FluxOctetsFichier

    octets_lus := tente lis(*flux.fichier, tampon) piège err {
        imprimeln("Impossible de lire le fichier : %", err)
        retourne 0
    }

    flux.curseur += octets_lus

    retourne octets_lus comme z64
}

flux_octets_fichier_positionne :: fonc (flux: *FluxOctetsFichier, position: PositionFichier)
{
    flux.curseur = tente positionne_fichier(*flux.fichier, position) piège _ {
        panique("Impossible de chercher le point dans le fichier !")
    }
}

flux_octets_fichier_ferme :: fonc (base: *FluxOctets)
{
    flux := base comme *FluxOctetsFichier
    _ := ferme(*flux.fichier)
}

flux_octets_fichier_est_à_la_fin :: fonc (flux: *FluxOctetsFichier) -> bool
{
    retourne flux.curseur >= flux.taille_fichier
}

/** \} */

