importe Fondation

Syntaxeuse :: struct {
    lexèmes : []Lexème
    assembleuse : AssembleuseArbre

    position := 0

	erreurs : []chaine

	est_extension := faux
	fichier_étendu := ""

	inclusions : []*NoeudSyntaxique
}

construit_syntaxeuse :: fonc(lexèmes : &[]Lexème) -> Syntaxeuse
{
    syntaxeuse : Syntaxeuse
    syntaxeuse.lexèmes = lexèmes
	syntaxeuse.assembleuse = construit_assembleuse()

    retourne syntaxeuse
}

détruit_syntaxeuse :: fonc (syntaxeuse: &Syntaxeuse)
{
	déloge syntaxeuse.erreurs
	déloge syntaxeuse.inclusions
}

lexème_courant :: fonc(syntaxeuse : &Syntaxeuse) -> TypeLexème
{
	retourne syntaxeuse.lexèmes[syntaxeuse.position].type_lexème
}

avance :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	syntaxeuse.position += 1
}

recule :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	syntaxeuse.position -= 1
}

requiers_lexème :: fonc(syntaxeuse : &Syntaxeuse, type : TypeLexème) -> bool
{
	t := syntaxeuse.lexème_courant()
	syntaxeuse.avance()
	retourne t == type
}

est_lexème :: fonc(syntaxeuse : &Syntaxeuse, type : TypeLexème) -> bool
{
	retourne syntaxeuse.lexème_courant() == type
}

données :: fonc(syntaxeuse : &Syntaxeuse) -> &Lexème
{
	retourne syntaxeuse.lexèmes[syntaxeuse.position - 1]
}

position :: fonc(syntaxeuse : &Syntaxeuse) -> z32
{
    retourne syntaxeuse.position - 1
}

fini :: fonc(syntaxeuse : &Syntaxeuse) -> bool
{
    retourne syntaxeuse.position >= syntaxeuse.lexèmes.taille
}

performe_syntaxage :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	syntaxeuse.position = 0

	si (syntaxeuse.lexèmes.taille == 0) {
		retourne
	}

    données_lexème : Lexème
	_ := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, données_lexème)

	syntaxeuse.analyse_page()

	pour syntaxeuse.erreurs {
		imprime("%\n", it)
	}

	syntaxeuse.assembleuse.attend_type(TypeNoeud.BLOC)
}

analyse_page :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	tantque !syntaxeuse.fini() {
		si (syntaxeuse.est_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
			syntaxeuse.avance()
			syntaxeuse.assembleuse.ajoute_noeud(TypeNoeud.CHAINE_CARACTÈRE, syntaxeuse.données())
		}
		sinon si (syntaxeuse.est_lexème(TypeLexème.DÉBUT_EXPRESSION)) {
			syntaxeuse.analyse_expression()
		}
		sinon si (syntaxeuse.est_lexème(TypeLexème.DÉBUT_VARIABLE)) {
			syntaxeuse.avance()

			si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
				syntaxeuse.rapporte_erreur("Attendu identifiant de la variable après '{{'")
			}

			syntaxeuse.assembleuse.ajoute_noeud(TypeNoeud.VARIABLE, syntaxeuse.données())

			si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_VARIABLE)) {
				syntaxeuse.rapporte_erreur("Attendu '}}' à la fin de la déclaration d'une variable")
			}
		}
		sinon {
			syntaxeuse.rapporte_erreur("Lexème inattendu")
		}
	}
}

analyse_expression :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	si (!syntaxeuse.requiers_lexème(TypeLexème.DÉBUT_EXPRESSION)) {
		syntaxeuse.rapporte_erreur("Attendu '{%'")
	}

	si (syntaxeuse.est_lexème(TypeLexème.SI)) {
		syntaxeuse.analyse_si()
	}
	sinon si (syntaxeuse.est_lexème(TypeLexème.POUR)) {
		syntaxeuse.analyse_pour()
	}
	sinon si syntaxeuse.est_lexème(TypeLexème.ÉTEND) {
		syntaxeuse.avance()

		si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
			syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'étend'")
		}

		nom_fichier := syntaxeuse.données().chn

		syntaxeuse.est_extension = vrai
		syntaxeuse.fichier_étendu = nom_fichier

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}
	}
	sinon si syntaxeuse.est_lexème(TypeLexème.INCLUS) {
		syntaxeuse.avance()

		si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
			syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'inclus'")
		}

		noeud := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, syntaxeuse.données())
		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.BLOC)

		nom_fichier := syntaxeuse.données().chn
		noeud.nom = nom_fichier

		tableau_ajoute(*syntaxeuse.inclusions, noeud)

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}

	}
	sinon si syntaxeuse.est_lexème(TypeLexème.BLOC) {
		syntaxeuse.avance()
		noeud := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, syntaxeuse.données())

		si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
			syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'bloc'")
		}

		nom_bloc := syntaxeuse.données().chn
		noeud.nom = nom_bloc

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}
	}
	sinon si (syntaxeuse.est_lexème(TypeLexème.SINON)) {
		//assert(syntaxeuse.assembleuse.noeud_courant() == "SI")
		syntaxeuse.avance()

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.BLOC)

		syntaxeuse.assembleuse.attend_type(TypeNoeud.SI)

		_ := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, syntaxeuse.données())

		syntaxeuse.analyse_page()
	}
	sinon si (syntaxeuse.est_lexème(TypeLexème.FINSI)) {
		syntaxeuse.avance()

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.BLOC)

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.SI)

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}
	}
	sinon si (syntaxeuse.est_lexème(TypeLexème.FINPOUR)) {
		syntaxeuse.avance()

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.BLOC)

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.POUR)

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}
	}
	sinon si syntaxeuse.est_lexème(TypeLexème.FINBLOC) {
		syntaxeuse.avance()

		syntaxeuse.assembleuse.dépile_noeud(TypeNoeud.BLOC)

		si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
			syntaxeuse.rapporte_erreur("Attendu '%}'")
		}
	}
	sinon {
		syntaxeuse.rapporte_erreur("Identifiant inconnu")
	}
}

analyse_si :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	si (!syntaxeuse.requiers_lexème(TypeLexème.SI)) {
		syntaxeuse.rapporte_erreur("Attendu 'si'")
	}

	_ := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.SI, syntaxeuse.données())

	si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
		syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'si'")
	}

	syntaxeuse.assembleuse.ajoute_noeud(TypeNoeud.VARIABLE, syntaxeuse.données())

	si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
		syntaxeuse.rapporte_erreur("Attendu '%}'")
	}

	_ = syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, syntaxeuse.données())

	syntaxeuse.analyse_page()
}

analyse_pour :: fonc(syntaxeuse : &Syntaxeuse) -> rien
{
	si (!syntaxeuse.requiers_lexème(TypeLexème.POUR)) {
		syntaxeuse.rapporte_erreur("Attendu 'pour'")
	}

	_ := syntaxeuse.assembleuse.empile_noeud(TypeNoeud.POUR, syntaxeuse.données())

	si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
		syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'pour'")
	}

	syntaxeuse.assembleuse.ajoute_noeud(TypeNoeud.VARIABLE, syntaxeuse.données())

	si (!syntaxeuse.requiers_lexème(TypeLexème.DANS)) {
		syntaxeuse.rapporte_erreur("Attendu 'dans'")
	}

	si (!syntaxeuse.requiers_lexème(TypeLexème.CHAINE_CARACTÈRE)) {
		syntaxeuse.rapporte_erreur("Attendu une chaîne de caractère après 'dans'")
	}

	syntaxeuse.assembleuse.ajoute_noeud(TypeNoeud.VARIABLE, syntaxeuse.données())

	si (!syntaxeuse.requiers_lexème(TypeLexème.FIN_EXPRESSION)) {
		syntaxeuse.rapporte_erreur("Attendu '%}'")
	}

	_ = syntaxeuse.assembleuse.empile_noeud(TypeNoeud.BLOC, syntaxeuse.données())

	syntaxeuse.analyse_page()
}

imprime_arbre :: fonc(racine : *NoeudSyntaxique, tab : z32) -> rien
{
	pour i dans 0 ... tab - 1 {
		imprime("  ")
	}

	imprime("% (%)\n", racine.type, racine.données.chn)

	pour noeud dans racine.enfants {
		imprime_arbre(noeud, tab + 1)
	}
}

rapporte_erreur :: fonc (syntaxeuse: &Syntaxeuse, message: chaine)
{
	syntaxeuse.avance()

	enchaineuse: Enchaineuse
	initialise_enchaineuse(*enchaineuse)

	lexème := syntaxeuse.données()

	imprime_dans_enchaineuse(*enchaineuse, "ligne %, % : %", lexème.ligne, message, lexème.chn)

	message_erreur := chaine_depuis_enchaineuse(*enchaineuse)

	tableau_ajoute(*syntaxeuse.erreurs, message_erreur)
}
