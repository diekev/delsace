importe Couleur
importe Fondation
importe Géométrie
importe Périphériques
importe PeintureInterface
importe Typographie

/* ------------------------------------------------------------------------- */
/** \nom Panneau barre de menu
 * \{ */

PanneauBarreDeMenu :: struct {
    empl base: Panneau

    Élément :: struct {
        décalage: z32
        largeur: z32
        animation: Animation
    }

    État :: struct {
        élément_pressé := -1
    }

    barre_de_menu: *BarreDeMenu

    éléments: [..]Élément

    hauteur_élément: z32
    espaçage_élément: z32
    décalage_élément_x: z32
    décalage_élément_y: z32

    état: État
}

crée_panneau_barre_de_menu :: fonc (ctx: *ContexteInterface, barre_de_menu: *BarreDeMenu) -> *PanneauBarreDeMenu
{
    sur_déplacement_souris :: fonc (panneau_menu: *PanneauBarreDeMenu, souris: ÉtatSouris)
    {
        observe_souris(panneau_menu, souris)
    }

    sur_clique_souris :: fonc (panneau_menu: *PanneauBarreDeMenu, clique: CliqueSouris) -> bool
    {
        retourne observe_clique(panneau_menu, clique)
    }

    sur_dessin :: fonc (panneau_menu: *PanneauBarreDeMenu, contexte_affichage: *ContexteAffichage)
    {
        dessine(panneau_menu, contexte_affichage)
    }

    sur_destruction :: fonc (panneau: *Panneau)
    {
        panneau_menu := panneau comme *PanneauBarreDeMenu
        détruit_panneau_barre_de_menu(panneau_menu)
    }

    résultat := ctx.crée_panneau(PanneauBarreDeMenu)

    /* Données spécifiques. */
    résultat.barre_de_menu = barre_de_menu

    /* Rappels. */
    résultat.sur_clique_souris = enveloppe_rappel(sur_clique_souris)
    résultat.sur_déplacement_souris = enveloppe_rappel(sur_déplacement_souris)
    résultat.sur_dessin = enveloppe_rappel(sur_dessin)
    résultat.sur_destruction = sur_destruction

    retourne résultat
}

détruit_panneau_barre_de_menu :: fonc (panneau: &*PanneauBarreDeMenu)
{
    déloge(panneau.éléments)
    déloge(panneau)
    panneau = nul
}

ajourne_disposition :: fonc (empl panneau: *PanneauBarreDeMenu)
{
    saufsi besoin_ajournement_disposition {
        retourne
    }
    diffère besoin_ajournement_disposition = faux

    fonte := contexte.fonte
    métriques := donne_métriques_fonte(fonte)

    /* Calcul rect. */
    hauteur_de_ligne := métriques.donne_hauteur_ligne() comme z32
    hauteur_menu := hauteur_de_ligne + 10

    rect_parent := parent.rect

    rect.x = 0
    rect.y = rect_parent.hauteur - hauteur_menu
    rect.largeur = rect_parent.largeur
    rect.hauteur = hauteur_menu

    /* Calcul taille éléments. */
    hauteur_élément = métriques.donne_hauteur_caractère() comme z32
    espaçage_élément = donne_largeur_texte(fonte, "0")
    décalage_élément_x = espaçage_élément * 2
    décalage_élément_y = (rect.hauteur - hauteur_élément) / 2

    tableau_redimensionne(éléments, barre_de_menu.menus.taille)

    décalage_élément := décalage_élément_x

    pour barre_de_menu.menus {
        élément: PanneauBarreDeMenu.Élément
        élément.décalage = décalage_élément
        élément.largeur = donne_largeur_texte(fonte, it.texte)

        éléments[index_it] = élément

        décalage_élément += (élément.largeur + décalage_élément_x * 2)
    }
}

dessine :: fonc (empl panneau: *PanneauBarreDeMenu, ctx: *ContexteAffichage)
{
    ajourne_disposition(panneau)

    thème := ctx.thème

    remplis_rectangle(ctx.peintre, rect, thème.couleur_arrière_plan)

    rect_élément: RectanglePosDim(z32)
    rect_élément.hauteur = hauteur_élément

    pour * éléments {
        saufsi animation_est_fini(it.animation) {
            ajourne_animation(*it.animation)
            rect_pour_animation := donne_rect_élément_pour_clique(panneau, index_it comme z32)
            valeur := it.animation.temps comme n8

            poids := 1.0 - (valeur comme r32 / 64.0)
            couleur := interpole_linéaire(thème.couleur_menu_pressé, poids, thème.couleur_arrière_plan)
            remplis_rectangle(ctx.peintre, rect_pour_animation, couleur)
        }
        sinon si index_it == état.élément_pressé {
            couleur := thème.couleur_menu_pressé
            rect_pour_animation := donne_rect_élément_pour_clique(panneau, index_it comme z32)
            remplis_rectangle(ctx.peintre, rect_pour_animation, couleur)
        }

        rect_élément.x = rect.x + it.décalage
        rect_élément.y = rect.y + décalage_élément_y
        rect_élément.largeur = it.largeur

        menu := barre_de_menu.menus[index_it]
        dessine_texte(ctx.peintre, ctx.fonte, menu.texte, rect_élément, thème.couleur_texte)
    }
}

observe_souris :: fonc (empl panneau: *PanneauBarreDeMenu, souris: ÉtatSouris)
{
    saufsi rect.contient(souris.où) {
        retourne
    }

    si état.élément_pressé == -1 {
        retourne
    }

    pour éléments {
        rect_élément := donne_rect_élément_pour_clique(panneau, index_it)
        saufsi rect_élément.contient(souris.où) {
            continue
        }

        définis_élément_pressé(panneau, index_it, faux)
    }
}

observe_clique :: fonc (empl panneau: *PanneauBarreDeMenu, souris: CliqueSouris) -> bool
{
    saufsi rect.contient(souris.état.où) {
        retourne faux
    }

    si souris.action != ActionSouris.PRESSÉE {
        retourne vrai
    }

    pour éléments {
        rect_élément := donne_rect_élément_pour_clique(panneau, index_it)
        saufsi rect_élément.contient(souris.état.où) {
            continue
        }

        définis_élément_pressé(panneau, index_it, vrai)
        arrête
    }

    retourne vrai
}

définis_élément_pressé :: fonc (empl panneau: *PanneauBarreDeMenu, index_élément: z64, indéfinis_élément: bool)
{
    assert(index_élément >= -1)
    assert(index_élément < éléments.taille)

    si état.élément_pressé == index_élément {
        si indéfinis_élément {
            anime_élément_pour_dépression(panneau)
            état.élément_pressé = -1
            contexte.détruit_panneau_menu()
        }
    }
    sinon {
        anime_élément_pour_dépression(panneau)

        état.élément_pressé = index_élément comme z32
        contexte.détruit_panneau_menu()

        si index_élément != -1 {
            anime_élément_pour_pression(panneau)
            rect_élément := donne_rect_élément_pour_clique(panneau, index_élément)
            panneau_menu := crée_panneau_menu(contexte, barre_de_menu.menus[index_élément], Point2D(z32)(rect_élément.x, rect_élément.y))
            panneau_menu.panneau_barre_de_menu = panneau
            contexte.affiche_panneau_menu(panneau_menu)
        }
    }
}

/* Retourne les limites de l'élément pour les cliques. */
donne_rect_élément_pour_clique :: fonc (empl panneau: *PanneauBarreDeMenu, index_élément: z64) -> RectanglePosDim(z32)
{
    assert(index_élément >= 0)
    assert(index_élément < éléments.taille)

    élément := éléments[index_élément]

    résultat : RectanglePosDim(z32) = ---
    résultat.x = rect.x + élément.décalage - décalage_élément_x
    résultat.y = rect.y
    résultat.hauteur = rect.hauteur
    résultat.largeur = élément.largeur + décalage_élément_x * 2
    retourne résultat
}

/* Appelé par PanneauInterface quand le menu actif est fermé. */
sur_menu_fermé :: fonc (empl panneau: *PanneauBarreDeMenu)
{
    anime_élément_pour_dépression(panneau)
    état.élément_pressé = -1
}

anime_élément_pour_pression :: fonc (empl panneau: *PanneauBarreDeMenu)
{
    si état.élément_pressé != -1 {
        élément := *éléments[état.élément_pressé]
        élément.animation.temps = 0
        élément.animation.cible = 64
        élément.animation.vitesse = 1.0 / 8.0
    }
}

anime_élément_pour_dépression :: fonc (empl panneau: *PanneauBarreDeMenu)
{
    si état.élément_pressé != -1 {
        élément := *éléments[état.élément_pressé]
        élément.animation.temps = 64
        élément.animation.cible = 0
        élément.animation.vitesse = 1.0 / 8.0
    }
}

/** \} */

/* ------------------------------------------------------------------------- */
/** \nom Panneau Menu
 * \{ */

PanneauMenu :: struct {
    empl base: Panneau

    Élément :: struct {
        décalage: z32
        hauteur: z32
        largeur: z32
        est_séparateur: bool
        est_sous_menu: bool
    }

    État :: struct {
        élément_pressé : z32 = -1
        élément_survolé : z32 = -1
    }

    panneau_barre_de_menu: *PanneauBarreDeMenu
    menu: *Menu
    éléments: [..]Élément
    position: Point2D(z32)

    hauteur_élément: z32
    espaçage_élément: z32
    décalage_élément_x: z32
    décalage_élément_y: z32

    état: État

    menu_fut_affiché: *PanneauMenu

    /* Largeur requise par le parent. Si non-nulle, elle sera utilisée pour
     * définir la largeur du menu. */
    largeur_requise := 0
}

crée_panneau_menu :: fonc (ctx: *ContexteInterface, menu: *Menu, position: Point2D(z32)) -> *PanneauMenu
{
    sur_déplacement_souris :: fonc (panneau_menu: *PanneauMenu, souris: ÉtatSouris)
    {
        observe_souris(panneau_menu, souris)
    }

    sur_clique_souris :: fonc (empl panneau_menu: *PanneauMenu, clique: CliqueSouris) -> bool
    {
        saufsi rect.contient(clique.état.où) {
            retourne faux
        }

        si clique.action == ActionSouris.PRESSÉE {
            état.élément_pressé = état.élément_survolé
            retourne vrai
        }

        si clique.action == ActionSouris.RELACHÉE {
            élément := menu.éléments[état.élément_pressé]
            gère_clique_souris(élément)
            si panneau_barre_de_menu {
                panneau_barre_de_menu.définis_élément_pressé(-1, vrai)
            }
            sinon {
                contexte.détruit_panneau_menu()
            }
            retourne vrai
        }

        retourne faux
    }

    sur_dessin :: fonc (panneau_menu: *PanneauMenu, contexte_affichage: *ContexteAffichage)
    {
        dessine(panneau_menu, contexte_affichage)
    }

    sur_destruction :: fonc (panneau: *Panneau)
    {
        panneau_menu := panneau comme *PanneauMenu
        détruit_panneau_menu(panneau_menu)
    }

    résultat := ctx.crée_panneau(PanneauMenu)

    /* Données spécifiques. */
    résultat.menu = menu
    résultat.position = position

    /* Rappels. */
    résultat.sur_clique_souris = enveloppe_rappel(sur_clique_souris)
    résultat.sur_déplacement_souris = enveloppe_rappel(sur_déplacement_souris)
    résultat.sur_dessin = enveloppe_rappel(sur_dessin)
    résultat.sur_destruction = sur_destruction

    retourne résultat
}

détruit_panneau_menu :: fonc (panneau: &*PanneauMenu)
{
    déloge(panneau.éléments)
    déloge(panneau)
    panneau = nul
}

ajourne_disposition :: fonc (empl panneau: *PanneauMenu, ctx: *ContexteAffichage)
{
    saufsi besoin_ajournement_disposition {
        retourne
    }
    diffère besoin_ajournement_disposition = faux

    métriques := donne_métriques_fonte(ctx.fonte)

    hauteur_de_ligne := métriques.donne_hauteur_ligne() comme z32

    /* Calcul taille éléments. */
    hauteur_élément_texte := hauteur_de_ligne + 10
    hauteur_élément = hauteur_élément_texte
    hauteur_élément_séparateur := 10
    espaçage_élément = donne_largeur_texte(ctx.fonte, "0")
    décalage_élément_x = espaçage_élément * 2

    tableau_redimensionne(éléments, menu.éléments.taille)

    largeur_max_élément := 0

    hauteur_panneau := 0

    /* Calcule la largeur du panneau et des éléments textes. */
    pour menu.éléments {
        élément: PanneauMenu.Élément
        élément.largeur = donne_largeur_texte(ctx.fonte, it.texte)
        si élément.largeur < largeur_requise {
            élément.largeur = largeur_requise
        }

        si élément.largeur > largeur_max_élément {
            largeur_max_élément = élément.largeur
        }

        discr it.données {
            Action {
                hauteur_panneau += hauteur_élément_texte
            }
            SousMenu {
                hauteur_panneau += hauteur_élément_texte
                élément.est_sous_menu = vrai
            }
            Séparateur {
                hauteur_panneau += hauteur_élément_séparateur
                élément.est_séparateur = vrai
            }
            sinon {}
        }

        éléments[index_it] = élément
    }

    largeur_panneau := largeur_max_élément + décalage_élément_x * 2
    si largeur_panneau > largeur_requise && largeur_requise != 0 {
        largeur_panneau = largeur_requise
    }
    décalage_élément := hauteur_panneau

    pour menu.éléments {
        élément := *éléments[index_it]

        discr it.données {
            Action {
                décalage_élément -= hauteur_élément_texte

                élément.décalage = décalage_élément + 5
                élément.hauteur = hauteur_élément
            }
            SousMenu {
                décalage_élément -= hauteur_élément_texte

                élément.décalage = décalage_élément + 5
                élément.hauteur = hauteur_élément
            }
            Séparateur {
                décalage_élément -= hauteur_élément_séparateur

                élément.décalage = décalage_élément + 5
                élément.hauteur = hauteur_élément_séparateur
                élément.largeur = largeur_panneau
            }
            sinon {}
        }
    }

    rect.x = position.x
    rect.largeur = largeur_panneau
    rect.hauteur = hauteur_panneau
    rect.y = position.y - rect.hauteur
}

dessine :: fonc (empl panneau: *PanneauMenu, ctx: *ContexteAffichage)
{
    ajourne_disposition(panneau, ctx)

    thème := ctx.thème
    remplis_rectangle(ctx.peintre, rect, thème.couleur_arrière_plan)

    si état.élément_survolé != -1 && !éléments[état.élément_survolé].est_séparateur {
        rect_élément := donne_rect_élément_pour_clique(panneau, état.élément_survolé)
        remplis_rectangle(ctx.peintre, rect_élément, thème.couleur_menu_survolé)
    }

    métriques := donne_métriques_fonte(ctx.fonte)

    rect_élément: RectanglePosDim(z32)
    rect_élément.x = rect.x + décalage_élément_x
    rect_élément.hauteur = métriques.donne_hauteur_ligne() comme z32

    pour éléments {
        rect_élément.largeur = it.largeur
        rect_élément.y = rect.y + it.décalage

        si it.est_séparateur {
            p0 := Point2D(z32)(rect.x, rect_élément.y)
            p1 := Point2D(z32)(rect.x + rect.largeur, rect_élément.y)
            dessine_segment(ctx.peintre, Ligne(z32)(p0, p1), thème.couleur_contour)
        }
        sinon {
            texte := menu.éléments[index_it].texte
            dessine_texte(ctx.peintre, ctx.fonte, texte, rect_élément, thème.couleur_texte)
        }
    }

    /* À FAIRE : les lignes du bas et du côté gauche sont manquantes. Peut-être à cause du ciseau ? */
    rect_corrigé := rect
    rect_corrigé.x += 1
    rect_corrigé.largeur -= 1
    rect_corrigé.y += 1
    rect_corrigé.hauteur -= 1
    dessine_rectangle(ctx.peintre, rect_corrigé, thème.couleur_contour)
}

observe_souris :: fonc (empl panneau: *PanneauMenu, souris: ÉtatSouris)
{
    saufsi rect.contient(souris.où) {
        saufsi menu_fut_affiché {
            définis_index_élément_survolé(panneau, -1)
        }
        retourne
    }

    pour éléments {
        rect_élément := donne_rect_élément_pour_clique(panneau, index_it)
        saufsi rect_élément.contient(souris.où) {
            continue
        }

        définis_index_élément_survolé(panneau, index_it comme z32)
        arrête
    }
}

donne_rect_élément_pour_clique :: fonc (empl panneau: *PanneauMenu, index_élément: z64) -> RectanglePosDim(z32)
{
    assert(index_élément >= 0)
    assert(index_élément < éléments.taille)

    élément := éléments[index_élément]

    résultat: RectanglePosDim(z32)
    résultat.x = rect.x
    résultat.y = rect.y + élément.décalage - 5
    résultat.hauteur = élément.hauteur
    résultat.largeur = rect.largeur
    retourne résultat
}

définis_index_élément_survolé :: fonc (empl panneau: *PanneauMenu, index_élément: z32)
{
    si index_élément == état.élément_survolé {
        retourne
    }

    // sors élément précédent
    si état.élément_survolé != -1 {
        élément := éléments[état.élément_survolé]
        si élément.est_sous_menu {
            contexte.dépile_panneau_menu()
            menu_fut_affiché = nul
        }
    }

    état.élément_survolé = index_élément

    // entre dans le nouvel élément
    si état.élément_survolé != -1 {
        élément := éléments[état.élément_survolé]
        si élément.est_sous_menu {
            rect_élément := donne_rect_élément_pour_clique(panneau, état.élément_survolé)

            panneau_menu := crée_panneau_menu(contexte, menu.éléments[état.élément_survolé].données.SousMenu, Point2D(z32)(rect_élément.x + rect_élément.largeur, rect_élément.y + rect_élément.hauteur))
            panneau_menu.panneau_barre_de_menu = panneau_barre_de_menu
            contexte.affiche_panneau_menu(panneau_menu)

            menu_fut_affiché = panneau_menu
        }
    }
}

/** \} */
